000100/*****************************************************************
000200*                                                                *
000300*                       IDENTIFICATION DIVISION                  *
000400*                                                                *
000500******************************************************************
000600*                                                                *
000700*          WISP - Wang Interchange Source Pre-processor          *
000800*           Copyright (c) 1988, 1989, 1990, 1991, 1992           *
000900*  An unpublished work of International Digital Scientific Inc.  *
001000*                     All rights reserved.                       *
001100*                                                                *
001200******************************************************************
001300*
001400 IDENTIFICATION DIVISION.
001500 PROGRAM-ID.
001600     TEXTMENU.
001700 AUTHOR.
001800     IDSI MIGRATION TEAM
001900 DATE-WRITTEN.
002000     08/02/89.
002100
002200/*****************************************************************
002300*                                                                *
002400*                         ENVIRONMENT DIVISION                   *
002500*                                                                *
002600******************************************************************
002700*
002800 ENVIRONMENT DIVISION.
002900 CONFIGURATION SECTION.
003000 SOURCE-COMPUTER.
003100     WANG-VS.
003200 OBJECT-COMPUTER.
003300     WANG-VS.
003400 FIGURATIVE-CONSTANTS.
003500     HIPRO      IS "84",
003600     LOPRO      IS "8C",
003700     SOLIDS     IS "0B",
003800     POS-CURSOR IS "A0",
003900     SOUNDALARM IS "E0".
004000
004100/*****************************************************************
004200*                                                                *
004300*                         INPUT-OUTPUT SECTION                   *
004400*                                                                *
004500******************************************************************
004600*
004700 INPUT-OUTPUT SECTION.
004800 FILE-CONTROL.
004900*
005000     SELECT CRT
005100         ASSIGN TO "CRT",      "DISPLAY",
005200         ORGANIZATION    IS SEQUENTIAL
005300         ACCESS MODE     IS RANDOM
005400         RELATIVE KEY    IS LINENUM
005500         PFKEY           IS PF-KEY
005600         CURSOR POSITION IS CURSOR-POS
005700         FILE STATUS     IS FILSTAT.
005800*
005900*$SEQLINE
006000     SELECT MENU-FILE
006100     ASSIGN TO "MENUFILE", "DISK", NODISPLAY
006200       ORGANIZATION IS SEQUENTIAL
006300       ACCESS MODE  IS SEQUENTIAL
006400       FILE STATUS  IS FILE-STATUS.
006500
006600*
006700/*****************************************************************
006800*                                                                *
006900*                      DATA DIVISION - FILE SECTION              *
007000*                                                                *
007100******************************************************************
007200*
007300 DATA DIVISION.
007400 FILE SECTION.
007500*
007600 FD  CRT
007700     LABEL RECORDS ARE OMITTED.
007800 01  CRTREC                             PIC X(1924).
007900*
008000 FD  MENU-FILE
008100     VALUE OF FILENAME IS CMD-FILE
008200     VALUE OF LIBRARY IS CMD-LIB
008300     VALUE OF VOLUME IS CMD-VOL
008400     DATA RECORD IS MENU-RECORD.
008500
008600 01  MENU-RECORD.
008700     05 MENU-PF.
008800        10  MENU-PF-KEY                 PIC X(1).
008900        10  MENU-PF-NUM                 PIC X(2).
009000        10  MENU-PF-N
009100              REDEFINES  MENU-PF-NUM    PIC 9(2).
009200        10  MENU-P-FILLER               PIC X(1).
009300        10  MENU-RUN-PROGRAM            PIC X(8).
009400        10  MENU-P1-FILLER              PIC X(1).
009500        10  MENU-RUN-TYPE               PIC X(1).
009600            88 MENU-TYPE-NONE           VALUE SPACE.
009700            88 MENU-TYPE-MENU           VALUE "M" "m".
009800            88 MENU-TYPE-PROGRAM        VALUE "P" "p".
009900        10  MENU-P2-FILLER              PIC X(1).
010000        10  MENU-RUN-LIBRARY            PIC X(8).
010100        10  MENU-P3-FILLER              PIC X(1).
010200        10  MENU-RUN-VOLUME             PIC X(6).
010300     05 MENU-FILLER                     PIC X(50).
010400*
010500/*****************************************************************
010600*                                                                *
010700*                         WORKING STORAGE                        *
010800*                                                                *
010900******************************************************************
011000*
011100 WORKING-STORAGE SECTION.
011200
011300/*****************************************************************
011400*                                                                *
011500*                   DEFINITIONS FOR LINK CALL(S)                 *
011600*                                                                *
011700******************************************************************
011800*
011900 01  LINK-TO-NAME                   PIC X(08) VALUE SPACES.
012000 01  LINK-TYPE                      PIC X(01) VALUE SPACE.
012100 01  LINK-LIBRARY                   PIC X(08) VALUE SPACES.
012200 01  LINK-VOLUME                    PIC X(06) VALUE SPACES.
012300 01  LINK-PCOUNT.
012400     03  FILLER                     BINARY    VALUE ZERO.
012500     03  LINK-PCOUNT-NO             BINARY    VALUE 1.
012600 01  LINK-CEXIT-FLAG                PIC X(01) VALUE " ".
012700 01  LINK-CEXIT-MSG                 PIC X(27) VALUE SPACES.
012800 01  LINK-CEXIT-MSG-LEN.
012900     03  FILLER                     BINARY    VALUE ZERO.
013000     03  FILLER                     BINARY    VALUE ZERO.
013100 01  LINK-HELP-FLAG                 PIC X(1)  VALUE SPACE.
013200 01  LINK-CODE.
013300     03  FILLER                     BINARY    VALUE ZERO.
013400     03  LINK-CODE-VAL              BINARY    VALUE ZERO.
013500 01  LINK-RETURN-CODE.
013600     03  FILLER                     BINARY    VALUE ZERO.
013700     03  LINK-RTN-CODE-VAL          BINARY    VALUE ZERO.
013800
013900******************************************************************
014000 01  DATE-INFO.
014100     03  DATEIN                     PIC 9(06).
014200     03  DATEGROUP                  REDEFINES DATEIN.
014300         05  YEAR-DIGITS            PIC 9(02).
014400         05  MONTH-DIGITS           PIC 9(02).
014500         05  DAY-DIGITS             PIC 9(02).
014600     03  RPT-DATE.
014700         05  RPT-MONTH              PIC 9(02).
014800         05  FILLER                 PIC X(01)  VALUE "/".
014900         05  RPT-DAY                PIC 9(02).
015000         05  FILLER                 PIC X(01)  VALUE "/".
015100         05  RPT-YEAR               PIC 9(02).
015200
015300******************************************************************
015400*                   FILE CONTROL STATUS SWITCHES                 *
015500******************************************************************
015600 01  FILSTAT.
015700     03  ERR-FLAG                   PIC X(01).
015800     03  PFK-BYTE                   PIC X(01).
015900
016000 77  LIST-STATUS                    PIC X(1)  VALUE "0".
016100     88 MORE-LIST                   VALUE "0".
016200     88 RECORD-FOUND-ON-LIST        VALUE "0".
016300     88 NO-MORE-LIST                VALUE "1".
016400     88 NO-RECORD-FOR-LIST          VALUE "2".
016500     88 DUPLICATE-ON-LIST           VALUE "2".
016600
016700******************************************************************
016800*               EXTERNAL FILE LOCATION DEFINITIONS               *
016900******************************************************************
017000 77  REC-COUNT                      PIC 9(4)  VALUE 500.
017100 77  FIL-FOR-LIST                   PIC X(8)  VALUE SPACES.
017200 77  LIB-FOR-LIST                   PIC X(8)  VALUE SPACES.
017300 77  VOL-FOR-LIST                   PIC X(6)  VALUE SPACES.
017400
017500******************************************************************
017600*             WORKSTATION FILE CONTROL DESCRIPTIONS              *
017700******************************************************************
017800 77  LINENUM                        PIC 9(02) VALUE 1.
017900 77  PF-KEY                         PIC 9(02) VALUE 1.
018000 01  CURSOR-POS.
018100     03  MOD-COL                    BINARY.
018200     03  MOD-ROW                    BINARY.
018300 01  REDEF-CURS-POS
018400        REDEFINES CURSOR-POS.
018500     03  FILLER                     PIC X(01).
018600     03  CURS-COL                   PIC X(01).
018700     03  FILLER                     PIC X(01).
018800     03  CURS-ROW                   PIC X(01).
018900
019000/*****************************************************************
019100*                                                                *
019200*                   DISPLAY DEFINITIONS                          *
019300*                                                                *
019400******************************************************************
019500*
019600 01  DISPLAY-REC USAGE IS DISPLAY-WS.
019700     05  ROW01-COL1              PIC X(3) ROW 1 COLUMN 1
019800         SOURCE WSA-USER-ID.
019900     05  ROW01-COL62             PIC X(6) ROW 1 COLUMN 62
020000         VALUE "MENU :"                          .
020100     05  ROW01-COL69             PIC X(8) ROW 1 COLUMN 69
020200         SOURCE WSA-PROGRAM.
020300     05  ROW02-COL20             PIC X(40) ROW 2 COLUMN 20
020400         SOURCE CMD-COMPANY-NAME.
020500     05  FILLER OCCURS 21 TIMES.
020600         10  ROW06-COL20         PIC X(80)  ROW 3 COLUMN 1
020700         SOURCE WSA-MENU-LINE.
020800     05  ROW20-COL20             PIC X(60) ROW 24 COLUMN 20
020900         SOURCE WSA-ERROR.
021000
021100******************************************************************
021200
021300 01  DISPLAY-REC-NOHEAD USAGE IS DISPLAY-WS.
021400     05  FILLER OCCURS 23 TIMES.
021500         10  NH-ROW06-COL20      PIC X(80)  ROW 1 COLUMN 1
021600         SOURCE WSA-MENU-LINE.
021700     05  NH-ROW20-COL20          PIC X(60) ROW 24 COLUMN 20
021800         SOURCE WSA-ERROR.
021900
022000******************************************************************
022100
022200 01  WSA-PROGRAM                    PIC X(8)    VALUE SPACES.
022300 01  WSA-USER-ID                    PIC X(3)    VALUE SPACES.
022400 01  ID                             PIC X(2)    VALUE "ID".
022500
022600 01  WSA-MENU.
022700     05  WSA-MENU-DATA              PIC X(1920) VALUE SPACES.
022800     05  WSA-MENU-TABLE
022900           REDEFINES WSA-MENU-DATA.
023000         10  WSA-MENU-LINE OCCURS 24 TIMES PIC X(80).
023100
023200 01  WSA-ERROR                      PIC X(60)  VALUE SPACES.
023300
023400 01  WSA-PROGRAM-TABLE.
023500     05  WSA-PROGRAM-AREA OCCURS 33 TIMES.
023600         10  WSA-RUN-PROGRAM        PIC X(8).
023700         10  WSA-RUN-TYPE           PIC X(1).
023800             88 WSA-TYPE-NONE       VALUE SPACE.
023900             88 WSA-TYPE-MENU       VALUE "M".
024000             88 WSA-TYPE-PROGRAM    VALUE "P".
024100         10  WSA-RUN-LIBRARY        PIC X(8).
024200         10  WSA-RUN-VOLUME         PIC X(6).
024300*
024400 01  FILE-STATUS.
024500     03  UFB-FLAG1                  PIC X(01).
024600         88  RECORD-FOUND           VALUE "0".
024700         88  DUPLICATE-RECORD       VALUE "2".
024800         88  END-OF-FILE            VALUE "F".
024900     03  UFB-FLAG2                  PIC X(01).
025000
025100 01  WSA-EXT                        PIC X(39)   VALUE "mnu".
025200 01  WSA-IND                        USAGE BINARY VALUE 0.
025300
025400 01  IND-001                        USAGE BINARY VALUE 0.
025500 01  IND-002                        USAGE BINARY VALUE 0.
025600 01  IND-003                        USAGE BINARY VALUE 0.
025700
025800 01  NUM-ROWS                       USAGE BINARY VALUE 0.
025900 01  BELL-TIMES.
026000     05  FILLER                     USAGE BINARY VALUE 0.
026100     05  BELL-NUM                   USAGE BINARY VALUE 1.
026200
026300
026400*****************************************************************
026500*                 MENU USAGE CONTROL TABLE
026600*
026700*  TABLE ALLOWS FOR 100 MENU DEEP RECURSIVE MENU CALLS
026800*****************************************************************
026900 01  MENU-USAGE-CTL                 PIC X(2400) VALUE SPACES.
027000 01  MENU-USAGE-CTL-R
027100         REDEFINES MENU-USAGE-CTL.
027200     03 MUCR-TABLE           OCCURS 100 TIMES.
027300        05 MUCR-MENU-NAME           PIC X(8).
027400        05 MUCR-MENU-LIBRARY        PIC X(8).
027500        05 MUCR-MENU-VOLUME         PIC X(8).
027600
027700 01  MENU-USAGE-STATUS              PIC 9(2) VALUE 00.
027800     88 MUS-NO-STATUS                        VALUE 00.
027900     88 MUS-MENU-REQUEST                     VALUE 10.
028000     88 MUS-MENU-EXIT                        VALUE 90.
028100     88 MUS-MENU-END                         VALUE 99.
028200/*****************************************************************
028300*                     lINKAGE DATA
028400******************************************************************
028500
028600 LINKAGE SECTION.
028700 01  CMD-FILE                       PIC X(8).
028800 01  CMD-LIB                        PIC X(8).
028900 01  CMD-VOL                        PIC X(6).
029000 01  CMD-COMPANY-NAME               PIC X(40).
029100*
029200/*****************************************************************
029300*                                                                *
029400*                    PROCEDURE DIVISION                          *
029500*                                                                *
029600******************************************************************
029700*
029800 PROCEDURE DIVISION USING CMD-FILE,
029900                          CMD-LIB,
030000                          CMD-VOL,
030100                          CMD-COMPANY-NAME.
030200
030300 START-PROGRAM-SECTION                SECTION.
030400 START-PROGRAM.
030500
030600     PERFORM INITIALIZATION-SECTION.
030700
030800     PERFORM MASTER-PROG-CTL-SECTION
030900          UNTIL MUS-MENU-END.
031000
031100     PERFORM TERMINATION-SECTION.
031200
031300 EXIT-PROGRAM.
031400     STOP RUN.
031500
031600******************************************************************
031700*                         INITIALIZATION                         *
031800******************************************************************
031900*
032000 INITIALIZATION-SECTION                             SECTION.
032100 INITIALIZATION.
032200
032300     CALL "EXTRACT" USING ID WSA-USER-ID.
032400     ACCEPT DATEIN FROM DATE.
032500     OPEN I-O    CRT.
032600
032700 INITIALIZATION-EXIT.
032800     EXIT.
032900
033000
033100****************************************************************
033200*         MASTER PROGRAM CONTROL
033300****************************************************************
033400 MASTER-PROG-CTL-SECTION                            SECTION.
033500 MASTER-PROG-CTL.
033600
033700     ADD 1 TO IND-003.
033800
033900     PERFORM A-PARSE-NAME-SECTION.
034000
034100     MOVE 1 TO IND-002.
034200
034300     MOVE CMD-FILE TO MUCR-MENU-NAME (IND-003).
034400     MOVE CMD-LIB TO MUCR-MENU-LIBRARY (IND-003).
034500     MOVE CMD-VOL TO MUCR-MENU-VOLUME (IND-003).
034600
034700     MOVE 00 TO MENU-USAGE-STATUS.
034800
034900     PERFORM DISPLAY-AND-READ-MENU-SECTION
035000       UNTIL  MUS-MENU-EXIT
035100             OR MUS-MENU-REQUEST.
035200
035300     IF IND-003 = 1
035400       AND MUS-MENU-EXIT
035500        MOVE 99 TO MENU-USAGE-STATUS
035600        GO TO MASTER-PROG-CTL-EXIT.
035700
035800     IF MUS-MENU-REQUEST
035900        MOVE WSA-RUN-PROGRAM (IND-002) TO CMD-FILE
036000        MOVE WSA-RUN-LIBRARY (IND-002) TO CMD-LIB
036100        MOVE WSA-RUN-VOLUME (IND-002) TO CMD-VOL
036200        GO TO MASTER-PROG-CTL-EXIT.
036300
036400
036500     MOVE SPACES TO MUCR-MENU-NAME (IND-003).
036600     SUBTRACT 1 FROM IND-003.
036700
036800     IF IND-003 > 0
036900       AND (MUCR-MENU-NAME (IND-003) NOT = SPACES)
037000        MOVE MUCR-MENU-NAME (IND-003) TO CMD-FILE
037100        MOVE MUCR-MENU-LIBRARY (IND-003) TO CMD-LIB
037200        MOVE MUCR-MENU-VOLUME (IND-003) TO CMD-VOL
037300        SUBTRACT 1 FROM IND-003.
037400
037500 MASTER-PROG-CTL-EXIT.
037600     EXIT.
037700
037800
037900*******************************************************************
038000*                    PARSE FOR THE FILE NAME
038100*******************************************************************
038200*
038300 A-PARSE-NAME-SECTION                              SECTION.
038400 A-PARSE-NAME.
038500     IF CMD-FILE NOT = SPACES
038600       STRING CMD-FILE DELIMITED BY SIZE
038700         INTO WSA-PROGRAM
038800     ELSE
038900         MOVE "SCRATCH" TO  WSA-PROGRAM.
039000     IF CMD-COMPANY-NAME = SPACES
039100         MOVE 24 TO NUM-ROWS
039200     ELSE
039300         MOVE 22 TO NUM-ROWS.
039400
039500     CALL "WSETFILEXT" USING WSA-EXT.
039600
039700     OPEN INPUT MENU-FILE.
039800     IF FILE-STATUS = "00"
039900        MOVE SPACES TO WSA-PROGRAM-TABLE
040000        MOVE SPACES TO WSA-MENU-DATA
040100        MOVE 1 TO IND-001
040200        MOVE 1 TO IND-002
040300        PERFORM A-READ-FILE-SECTION
040400           UNTIL END-OF-FILE.
040500
040600 A-PARSE-NAME-EXIT.
040700     EXIT.
040800
040900*****************************************************************
041000*                 READ THE MENU FILE
041100*****************************************************************
041200 A-READ-FILE-SECTION                               SECTION.
041300 A-READ-FILE.
041400
041500     MOVE SPACES TO  MENU-RECORD.
041600
041700     READ MENU-FILE
041800         AT END
041900            CLOSE MENU-FILE
042000            MOVE "FF" TO FILE-STATUS
042100            GO TO A-READ-EXIT.
042200
042300     IF MENU-PF-KEY = ">"
042400       AND MENU-PF-NUM = "NN"
042500          NEXT SENTENCE
042600      ELSE
042700        IF MENU-PF-KEY = "P"
042800          OR MENU-PF-KEY = "p"
042900           IF MENU-PF-NUM NOT < "00"
043000             AND MENU-PF-NUM < "33"
043100              MOVE MENU-PF-NUM TO IND-002
043200              ADD 1 TO IND-002
043300              MOVE MENU-RUN-PROGRAM TO WSA-RUN-PROGRAM(IND-002)
043400              MOVE MENU-RUN-TYPE TO WSA-RUN-TYPE(IND-002)
043500              MOVE MENU-RUN-LIBRARY TO WSA-RUN-LIBRARY(IND-002)
043600              MOVE MENU-RUN-VOLUME TO WSA-RUN-VOLUME(IND-002)
043700            ELSE
043800              IF IND-001 < NUM-ROWS
043900                 MOVE MENU-RECORD TO WSA-MENU-LINE(IND-001)
044000                 ADD 1 TO IND-001
044100               ELSE
044200                 NEXT SENTENCE
044300         ELSE
044400           IF IND-001 < NUM-ROWS
044500              MOVE MENU-RECORD TO WSA-MENU-LINE(IND-001)
044600              ADD 1 TO IND-001.
044700
044800 A-READ-EXIT.
044900        EXIT.
045000
045100
045200
045300******************************************************************
045400*                     DISPLAY AND READ DISPLAY REC               *
045500******************************************************************
045600*
045700 DISPLAY-AND-READ-MENU-SECTION                       SECTION.
045800 DISPLAY-AND-READ-MENU.
045900
046000     IF CMD-COMPANY-NAME = SPACES
046100       DISPLAY AND READ DISPLAY-REC-NOHEAD  ON CRT
046200       PFKEY 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16
046300         17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32
046400     ELSE
046500       DISPLAY AND READ DISPLAY-REC  ON CRT
046600       PFKEY 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16
046700         17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32.
046800
046900     MOVE SPACES TO WSA-ERROR.
047000
047100     MOVE PF-KEY TO IND-002.
047200     ADD 1 TO IND-002.
047300
047400     IF WSA-RUN-PROGRAM(IND-002) = SPACES
047500        CALL "BELL" USING BELL-TIMES
047600        MOVE "          INVALID KEY TRY AGAIN"
047700          TO WSA-ERROR
047800        GO TO DISPLAY-AND-READ-MENU-EXIT.
047900
048000     IF WSA-RUN-PROGRAM(IND-002) = "EXIT"
048100       OR  WSA-RUN-PROGRAM(IND-002) = "exit"
048200       OR WSA-RUN-PROGRAM(IND-002) = "LOGOFF"
048300        MOVE 90 TO MENU-USAGE-STATUS
048400        GO TO DISPLAY-AND-READ-MENU-EXIT.
048500
048600     IF WSA-TYPE-MENU (IND-002)
048700        MOVE 10 TO MENU-USAGE-STATUS
048800        GO TO DISPLAY-AND-READ-MENU-EXIT.
048900
049000     IF WSA-RUN-LIBRARY (IND-002) NOT = SPACES
049100       AND WSA-RUN-VOLUME (IND-002) NOT = SPACES
049200         PERFORM LINK-TO-PROGRAM-2-SECTION
049300      ELSE
049400        PERFORM LINK-TO-PROGRAM-SECTION.
049500
049600     MOVE PF-KEY TO RETURN-CODE.
049700
049800 DISPLAY-AND-READ-MENU-EXIT.
049900     EXIT.
050000
050100*****************************************************************
050200*           lINK TO PROGRAM REQUESTED BY PK-KEY
050300*****************************************************************
050400
050500 LINK-TO-PROGRAM-SECTION                           SECTION.
050600 LINK-TO-PROGRAM.
050700
050800     MOVE SPACES TO WSA-ERROR.
050900
051000     MOVE "S"                           TO  LINK-TYPE.
051100     MOVE WSA-RUN-PROGRAM(IND-002)      TO  LINK-TO-NAME.
051200     MOVE "        "                    TO  LINK-LIBRARY.
051300     MOVE "      "                      TO  LINK-VOLUME.
051400     MOVE  0                            TO  LINK-PCOUNT-NO.
051500     MOVE "C"                           TO  LINK-CEXIT-FLAG.
051600     MOVE "RETURN TO MENU             " TO  LINK-CEXIT-MSG.
051700     MOVE "H"                           TO  LINK-HELP-FLAG.
051800     CALL "LINK" USING LINK-TO-NAME,
051900                       LINK-TYPE, LINK-LIBRARY, LINK-VOLUME,
052000                       LINK-PCOUNT,
052100                       LINK-CODE, LINK-RETURN-CODE.
052200
052300     IF LINK-RTN-CODE-VAL > ZERO
052400         CALL "BELL" USING BELL-TIMES
052500         MOVE "          INVALID PROGRAM CALLED "
052600           TO WSA-ERROR.
052700
052800 LINK-TO-EXIT.
052900     EXIT.
053000
053100*****************************************************************
053200*           lINK TO PROGRAM 2, AS REQUESTED BY PK-KEY
053300*****************************************************************
053400
053500 LINK-TO-PROGRAM-2-SECTION                       SECTION.
053600 LINK-TO-PROGRAM-2.
053700
053800     MOVE SPACES TO WSA-ERROR.
053900
054000     MOVE "P"                           TO  LINK-TYPE.
054100     MOVE WSA-RUN-PROGRAM(IND-002)      TO  LINK-TO-NAME.
054200     MOVE WSA-RUN-LIBRARY (IND-002)     TO  LINK-LIBRARY.
054300     MOVE WSA-RUN-VOLUME (IND-002)      TO  LINK-VOLUME.
054400     MOVE  0                            TO  LINK-PCOUNT-NO.
054500     MOVE "C"                           TO  LINK-CEXIT-FLAG.
054600     MOVE "RETURN TO MENU             " TO  LINK-CEXIT-MSG.
054700     MOVE "H"                           TO  LINK-HELP-FLAG.
054800     CALL "LINK" USING LINK-TO-NAME,
054900                       LINK-TYPE, LINK-LIBRARY, LINK-VOLUME,
055000                       LINK-PCOUNT,
055100                       LINK-CODE, LINK-RETURN-CODE.
055200
055300     IF LINK-RTN-CODE-VAL > ZERO
055400         CALL "BELL" USING BELL-TIMES
055500         MOVE "          INVALID PROGRAM CALLED "
055600           TO WSA-ERROR.
055700
055800 LINK-TO-EXIT-2.
055900     EXIT.
056000
056100*****************************************************************
056200*              TERMINATE THE PROGRAM
056300*****************************************************************
056400 TERMINATION-SECTION                               SECTION.
056500 TERMINATION.
056600
056700     CLOSE CRT.
056800
056900 TERMINATION-EXIT.
057000     EXIT.
057100
057200
