000001 IDENTIFICATION DIVISION.
000200
000003 PROGRAM-ID. 
000003     PRINTOUT.
000400
000005 DATE-WRITTEN.       
000005     01-JUN-80   AKK
000006                     MODIFIED 25-FEB-82 JDH
000700
000008     THIS IS THE STANDARD  MCBA  SUBROUTINE  FOR HANDLING
000009     ALL  I/O  TO THE PRINTER  (OR  SPOOLED PRINTOUT DISK
000010     FILES), INCLUDING  OPENING  OR  CLOSING  THE PRINTER
000011     (OR DISK) FILES.
001200
000013     IT IS INTENDED FOR USE AS AN EXTERNAL SUBROUTINE, TO
000014     BE LINKED AS RE-ENTRANT CODE  WITH THE MAIN ROUTINE.
001500
000016     PROPRIETARY RIGHTS NOTICE: ALL RIGHTS RESERVED. 
000016     THIS
000017     MATERIAL CONTAINS THE VALUABLE PROPERTIES  AND TRADE
000018     SECRETS OF MINI-COMPUTER BUSINESS APPLICATIONS, INC.
000019     OF GLENDALE,  CALIFORNIA,   UNITED STATES OF AMERICA
000020     (MCBA)   EMBODYING  SUBSTANTIAL CREATIVE EFFORTS AND
000021     CONFIDENTIAL INFORMATION,  IDEAS AND EXPRESSIONS, NO
000022     PART OF  WHICH MAY BE  REPRODUCED OR  TRANSMITTED IN
000023     ANY FORM  OR BY ANY MEANS,  ELECTRONIC,  MECHANICAL,
000024     OR OTHERWISE,  INCLUDING  PHOTOCOPYING AND RECORDING
000025     OR IN CONNECTION  WITH  ANY  INFORMATION STORAGE  OR
000026     RETRIEVAL SYSTEM  WITHOUT THE PERMISSION  IN WRITING
000027     FROM MCBA.
002800
000029     COPYRIGHT NOTICE: COPYRIGHT (C) 1980, 1981, 1982 AN
000030     UNPUBLISHED WORK BY MINI-COMPUTER BUSINESS APPLICATIONS, INC.
003100
003200
003300/ *** ENVIRONMENT DIVISION ***
003400
000035 ENVIRONMENT DIVISION.
003600
000037 CONFIGURATION SECTION.
003800
000039 SOURCE-COMPUTER. 
000039     WANG-VS.
000040 OBJECT-COMPUTER. 
000040     WANG-VS.
004100
004200*    COPY FIGCONS.
      *COPY ../accopy/figcons.wcb
000100
000044 FIGURATIVE-CONSTANTS.
000045     ORDER-INIT IS "01A0",
000046     HEX00 IS "00",
000047     HEX01 IS "01", HEX02 IS "02", HEX03 IS "03", HEX04 IS "04",
000048     HEX05 IS "05", HEX06 IS "06", HEX07 IS "07", HEX08 IS "08",
000049     HEX09 IS "09", HEX10 IS "0A", HEX11 IS "0B", HEX12 IS "0C",
000050     HEX13 IS "0D", HEX14 IS "0E", HEX15 IS "0F", HEX16 IS "10",
000051     HEX17 IS "11", HEX18 IS "12", HEX19 IS "13", HEX20 IS "14",
000052     HEX21 IS "15", HEX22 IS "16", HEX23 IS "17", HEX24 IS "18",
000053     HEX25 IS "19", HEX26 IS "1A", HEX27 IS "1B", HEX28 IS "1C",
000054     HEX29 IS "1D", HEX30 IS "1E", HEX31 IS "1F", HEX32 IS "20",
000055     HEX33 IS "21", HEX34 IS "22", HEX35 IS "23", HEX36 IS "24",
000056     HEX37 IS "25", HEX38 IS "26", HEX39 IS "27", HEX40 IS "28",
000057     HEX41 IS "29", HEX42 IS "2A", HEX43 IS "2B", HEX44 IS "2C",
000058     HEX45 IS "2D", HEX46 IS "2E", HEX47 IS "2F", HEX48 IS "30",
000059     HEX49 IS "31", HEX50 IS "32", HEX51 IS "33", HEX52 IS "34",
000060     HEX53 IS "35", HEX54 IS "36", HEX55 IS "37", HEX56 IS "38",
000061     HEX57 IS "39", HEX58 IS "3A", HEX59 IS "3B", HEX60 IS "3C",
000062     HEX61 IS "3D", HEX62 IS "3E", HEX63 IS "3F", HEX64 IS "40",
000063     HEX65 IS "41", HEX66 IS "42", HEX67 IS "43", HEX68 IS "44",
000064     HEX69 IS "45", HEX70 IS "46", HEX71 IS "47", HEX72 IS "48",
000065     HEX73 IS "49", HEX74 IS "4A", HEX75 IS "4B", HEX76 IS "4C",
000066     HEX77 IS "4D", HEX78 IS "4E", HEX79 IS "4F", HEX80 IS "50",
000067     ALL-9-FIELD             IS  "39",
000068     ALL-X-FIELD             IS  "58",
000069     BLINKING-DISPLAY        IS  "94",
000070     DIM-NUMERIC-ENTRY-FAC   IS  "8A",
000071     DIM-PROTECT-NUMERIC-FAC IS  "8E",
000072     DIM-UPLOW-ENTRY-FAC     IS  "88",
000073     DIM-UPPER-ENTRY-FAC     IS  "89",
000074     DISPLAY-CODE            IS  "20",
000075     DISPLAY-HI-FAC          IS  "84",
000076     DISPLAY-LOW-FAC         IS  "8C",
000077     DOTS                    IS  "2E",
000078     ERROR-BIT               IS  "10",
000079     HI-NUMERIC-ENTRY-FAC    IS  "82",
000080     HIDE-FAC                IS  "9C",
000081     HYPHENS                 IS  "2D",
000082     NUMERIC-ENTRY-FAC       IS  "82",
000083     PROTECT-BIT             IS  "04",
000084     SECURITY-FAC            IS  "B8",
000085     UPLOW-ENTRY-FAC         IS  "80",
000086     UPPER-ENTRY-FAC         IS  "81".
004300/
000088 INPUT-OUTPUT SECTION.
004500
000090 FILE-CONTROL.
004700
004800*    COPY SLPRINT.
      *COPY ../accopy/slprint.wcb
000100
000094     SELECT PRINTER-FILE
000300
000096         ASSIGN TO "PRNTFILE", "PRINTER", NODISPLAY
000097         FILE STATUS IS PRINTER-FILE-STATUS
000098         BUFFER SIZE IS 5 BLOCKS.
004900*    COPY SLSCREEN.
      *COPY ../accopy/slscreen.wcb
000100
000101     SELECT CRT-FILE
000300
000103         ASSIGN TO "CRTFILE", "DISPLAY"
000104         ORGANIZATION    IS SEQUENTIAL
000105         ACCESS MODE     IS RANDOM
000106         RELATIVE KEY    IS CRT-KEY
000107         PFKEY           IS PFKEY-CODE
000108         CURSOR POSITION IS CRT-POSITION
000109         FILE STATUS     IS CRT-FILE-STATUS.
005000
005100/ *** DATA DIVISION ***
005200
000113 DATA DIVISION.
005400
000115 FILE SECTION.
005600
005700*    COPY FDPRINT.
      *COPY ../accopy/fdprint.wcb
000100
000119 FD  PRINTER-FILE
000300
000121         RECORD CONTAINS 132 CHARACTERS,
000122         LABEL RECORD IS OMITTED,
000123         VALUE OF
000124         FILENAME IS PRINTER-FILE-ASSIGNMENT
000125         SPACE    IS PRINTER-FILE-SPACE.
000900
000127 01  PRINT-RECORD-1-132.
000128     03  PRINT-RECORD-CHARS-1-1              PIC X(1).
000129     03  PRINT-RECORD-CHARS-2-12             PIC X(11).
000130     03  PRINT-RECORD-CHARS-13-24            PIC X(12).
000131     03  PRINT-RECORD-CHARS-25-36            PIC X(12).
000132     03  PRINT-RECORD-CHARS-37-48            PIC X(12).
000133     03  PRINT-RECORD-CHARS-49-60            PIC X(12).
000134     03  PRINT-RECORD-CHARS-61-72            PIC X(12).
000135     03  PRINT-RECORD-CHARS-73-84            PIC X(12).
000136     03  PRINT-RECORD-CHARS-85-96            PIC X(12).
000137     03  PRINT-RECORD-CHARS-97-108           PIC X(12).
000138     03  PRINT-RECORD-CHARS-109-120          PIC X(12).
000139     03  PRINT-RECORD-CHARS-121-132          PIC X(12).
002300
000141 01  PRINT-RECORD-1-120                      PIC X(120).
000142 01  PRINT-RECORD-1-108                      PIC X(108).
000143 01  PRINT-RECORD-1-96                       PIC X(96).
000144 01  PRINT-RECORD-1-84                       PIC X(84).
000145 01  PRINT-RECORD-1-72                       PIC X(72).
000146 01  PRINT-RECORD-1-60                       PIC X(60).
000147 01  PRINT-RECORD-1-48                       PIC X(48).
000148 01  PRINT-RECORD-1-36                       PIC X(36).
000149 01  PRINT-RECORD-1-24                       PIC X(24).
000150 01  PRINT-RECORD-1-12                       PIC X(12).
000151 01  PRINT-RECORD-1-1                        PIC X(1).
005800*    COPY FDSCREEN.
      *COPY ../accopy/fdscreen.wcb
000100
000154 FD  CRT-FILE
000300
000156     LABEL RECORDS ARE OMITTED.
000500
000158 01  CRT-RECORD.
000159     03  CRT-ORDER-AREA.
000160         05  CRT-ORDER-AREA-1                PIC X(1).
000161         05  CRT-ORDER-AREA-2                PIC X(1).
000162         05  CRT-ORDER-AREA-3                PIC X(1).
000163         05  CRT-ORDER-AREA-4                PIC X(1).
000164     03  CRT-SCREEN-AREA                     PIC X(1920).
005900
006000/ *** WORKING STORAGE SECTION ***
006100
000168 WORKING-STORAGE SECTION.
006300
000170 01  HEADER-LEGEND-TOTAL                 PIC 9(2) VALUE ZEROS.
006500
000172 01  MONTHS-VALUES.
000173     03  FILLER                          PIC X(3)  VALUE "JAN".
000174     03  FILLER                          PIC X(3)  VALUE "FEB".
000175     03  FILLER                          PIC X(3)  VALUE "MAR".
000176     03  FILLER                          PIC X(3)  VALUE "APR".
000177     03  FILLER                          PIC X(3)  VALUE "MAY".
000178     03  FILLER                          PIC X(3)  VALUE "JUN".
000179     03  FILLER                          PIC X(3)  VALUE "JUL".
000180     03  FILLER                          PIC X(3)  VALUE "AUG".
000181     03  FILLER                          PIC X(3)  VALUE "SEP".
000182     03  FILLER                          PIC X(3)  VALUE "OCT".
000183     03  FILLER                          PIC X(3)  VALUE "NOV".
000184     03  FILLER                          PIC X(3)  VALUE "DEC".
007900
000186 01  MONTHS-TABLE
000187         REDEFINES MONTHS-VALUES.
000188     03  MONTHS                          PIC X(3) OCCURS 12 TIMES.
008300
008400/
008500*    COPY WSFILERR.
      *COPY ../accopy/wsfilerr.wcb
000100
000193 01  FILE-ACCESS-ERROR-SCREEN USAGE DISPLAY-WS.
000194     05  FILLER                      PIC X(09)   ROW 11 COLUMN 20
000195         VALUE   "E R R O R".
000196     05  FILLER                      PIC X(24)   ROW 12 COLUMN 20
000197         VALUE   ". . . File Access Error:".
000198     05  FILLER                      PIC X(02)   ROW 12 COLUMN 46
000199         SOURCE  FILE-ERROR-STATUS.
000200     05  FILLER                      PIC X(04)   ROW 13 COLUMN 20
000201         VALUE   "From".
000202     05  FILLER                      PIC X(10)   ROW 13 COLUMN 26
000203         SOURCE  FILE-ERROR-NAME.
000204     05  FILLER                      PIC X(04)   ROW 13 COLUMN 38
000205         VALUE   "File".
001500
000207 01  NO-RECORD-ON-FILE-SCREEN USAGE DISPLAY-WS.
000208     05  FILLER                      PIC X(45)   ROW 01 COLUMN 01
000209         SOURCE  APPLICATION-NAME.
000210     05  FILLER                      PIC X(30)   ROW 01 COLUMN 51
000211         SOURCE  COMPANY-DISPLAY-NAME.
000212     05  NO-RECORD-ON-FILE-ITEM      PIC X(50)   ROW 12 COLUMN 20
000213         SOURCE  NO-RECORD-ON-FILE-LITERAL.
000214     05  NO-RECORD-MESSAGE-ITEM      PIC X(41)   ROW 24 COLUMN 02
000215         VALUE   "Press (PFK-16) to Return to Menu".
002500
000217 01  FILE-ERROR-NAME                     PIC X(10).
000218 01  FILE-ERROR-STATUS                   PIC X(02).
000219 01  NO-RECORD-ON-FILE-LITERAL           PIC X(50).
008600*    COPY WSFILES.
      *COPY ../accopy/wsfiles.wcb
000100
000222 01  FILE-IS-CLOSED                      PIC 9(1)  VALUE ZERO.
000223 01  FILE-IS-OPEN                        PIC 9(1)  VALUE 1.
000400
000225 01  FILE-STATUS-CODE-TABLE.
000226     03  NULL-STATUS                     PIC X(2)  VALUE SPACES.
000227     03  I-O-OK                          PIC X(2)  VALUE "00".
000228     03  RECORD-ON-FILE                  PIC X(2)  VALUE "00".
000229     03  DUP-RECORD-OK                   PIC X(2)  VALUE "02".
000230     03  AT-END                          PIC X(2)  VALUE "10".
000231     03  INVALID-KEY                     PIC X(2)  VALUE "21".
000232     03  DUPLICATE-KEY                   PIC X(2)  VALUE "22".
000233     03  RECORD-NOT-FOUND                PIC X(2)  VALUE "23".
000234     03  BOUNDARY-ERROR                  PIC X(2)  VALUE "24".
000235     03  HARDWARE-ERROR                  PIC X(2)  VALUE "30".
000236     03  BOUNDARY-SEQUENCE-ERROR         PIC X(2)  VALUE "34".
000237     03  INVALID-FUNCTION                PIC X(2)  VALUE "95".
001800
000239 01  OPENING-PROCEDURE-STATUS            PIC 9(1)  VALUE ZERO.
000240     88  OPENING-PROCEDURE-SUCCESSFUL        VALUE ZERO.
002100
000242 01  UNSUCCESSFUL                        PIC 9(1)  VALUE 1.
008700/
008800*    COPY WSPRINT.
      *COPY ../accopy/wsprint.wcb
000100
000246 01  PRINTER-FILE-ASSIGNMENT             PIC X(8)  VALUE SPACES.
000300
000400******************************************************************
000500*    SPACE FOR 999 PAGE PRINTOUT 60 LINES PER PAGE 13 EXTENTS    *
000600******************************************************************
000251 01  PRINTER-FILE-SPACE                  PIC 9(5)  VALUE 04616.
000800
000253 01  PRINTER-FILE-STATUS                 PIC X(2)  VALUE "00".
001000
000255 01  PRINTER-FILE-OPEN-STATUS            PIC 9(1)  VALUE ZERO.
000256     88  PRINTER-FILE-IS-OPEN                VALUE 1.
001300
000258 01  PRINT-OPERATION-VALUES.
000259     03  OPEN-PRINTER                   PIC S9(1) COMP VALUE 1.
000260     03  PRINT-A-LINE                   PIC S9(1) COMP VALUE 2.
000261     03  SKIP-A-LINE                    PIC S9(1) COMP VALUE 3.
000262     03  SKIP-A-PAGE                    PIC S9(1) COMP VALUE 4.
000263     03  CLOSE-PRINTER                  PIC S9(1) COMP VALUE 5.
002000
000265 01  SPOOLER-FILE-NAME.
000266     03  SPOOLER-PREFIX                  PIC X(5)  VALUE SPACES.
000267     03  SPOOLER-SUFFIX                  PIC X(3)  VALUE SPACES.
002400
000269 01  SPOOLER-TIME-STAMP.
000270     03  FILLER                          PIC 9(4)  VALUE ZEROES.
000271     03  SPOOLER-SECONDS                 PIC 9(4)  VALUE ZEROES.
008900/
009000*    COPY WSSCREEN.
      *COPY ../accopy/wsscreen.wcb
000100
000275 01  CRT-FILE-STATUS                     PIC X(2)  VALUE SPACES.
000300
000277 01  CRT-POSITION.
000278     03  CRT-COLUMN                      BINARY.
000279     03  CRT-ROW                         BINARY.
000700
000281 01  CRT-KEY                             PIC 9(2)  VALUE 1.
000900
000283 01  CRT-WCC-BYTE-SAVE                   PIC X(1).
001100
000285 01  CRT-ORDER-AREA-WS.
000286     03  CRT-LINE-NUMBER                 PIC X(1).
000287     03  CRT-WCC-BYTE                    PIC X(1).
000288     03  CRT-CURSOR-COLUMN-LOC           PIC X(1).
000289     03  CRT-CURSOR-ROW-LOC              PIC X(1).
001700
000291 01  CRT-ORDER-AREA-INIT.
000292     03  CRT-ORDER-AREA-INIT-1           PIC X(2).
000293     03  FILLER                          PIC X(2).
002100
000295 01  SCREEN-MODE                         PIC X(14) VALUE SPACES.
002300
000297 01  SCREEN-ALPHA-FIELD                  PIC X(80) VALUE SPACES.
000298 01  SCREEN-NUMERIC-FIELD                PIC S9(12)V9(3).
002600
000300 01  SCREEN-COMPLETION-STATUS            PIC X(1)  VALUE SPACE.
000301     88  SCREEN-ENTRY-COMPLETE               VALUE "C".
000302     88  SCREEN-NOT-COMPLETE                 VALUE SPACE.
003000
000304 01  SCREEN-ERROR-STATUS                 PIC X(1)  VALUE SPACE.
000305     88  SCREEN-ENTRY-OK                     VALUE SPACE.
000306     88  SCREEN-ENTRY-ERROR                  VALUE "E".
003400
000308 01  SCREEN-STATUSES.
000309     03  ENTRY-ERROR                     PIC X(1)  VALUE "E".
000310     03  SCREEN-COMPLETE                 PIC X(1)  VALUE "C".
003800
003900*01  ARE-YOU-SURE-ANSWER                 PIC X(1)  VALUE SPACE.
004000*    88  YES-SURE                            VALUE "Y".
004100*    88  NOT-SURE                            VALUE SPACE.
004200*
004300*01  OK-TO-DELETE-ANSWER                 PIC X(1)  VALUE SPACE.
004400*    88  OK-TO-DELETE                        VALUE "Y".
004500*    88  NOT-OK-TO-DELETE                    VALUE SPACE.
004600*
004700*01  OK-TO-POST-ANSWER                   PIC X(1)  VALUE SPACE.
004800*    88  OK-TO-POST                          VALUE "Y".
004900*    88  NOT-OK-TO-POST                      VALUE SPACE.
005000
000324 01  YES-NO-ANSWER-VALUES.
000325     03  ANSWER-SKIPPED                  PIC X(1)  VALUE SPACE.
000326     03  ANSWER-IS-YES                   PIC X(1)  VALUE "Y".
000327     03  ANSWER-IS-NO                    PIC X(1)  VALUE "N".
000328 01  YES-NO-ANSWER-TABLE
000329         REDEFINES YES-NO-ANSWER-VALUES.
000330     03  YES-NO-ANSWER-RANGE             PIC X(1)  OCCURS 3 TIMES.
005800
000332 01  MESSAGE-FIELD                       PIC X(79) VALUE SPACES.
006000
000334 01  PFKEY-CODE                          PIC 9(2)  VALUE ZEROES.
000335     88  ENTER-KEY-PRESSED                   VALUE 00.
000336     88  PFKEY-1-PRESSED                     VALUE 01.
000337     88  PFKEY-2-PRESSED                     VALUE 02.
000338     88  PFKEY-3-PRESSED                     VALUE 03.
000339     88  PFKEY-4-PRESSED                     VALUE 04.
000340     88  PFKEY-5-PRESSED                     VALUE 05.
000341     88  PFKEY-6-PRESSED                     VALUE 06.
000342     88  PFKEY-7-PRESSED                     VALUE 07.
000343     88  PFKEY-8-PRESSED                     VALUE 08.
000344     88  PFKEY-9-PRESSED                     VALUE 09.
000345     88  PFKEY-10-PRESSED                    VALUE 10.
000346     88  PFKEY-11-PRESSED                    VALUE 11.
000347     88  PFKEY-12-PRESSED                    VALUE 12.
000348     88  PFKEY-13-PRESSED                    VALUE 13.
000349     88  PFKEY-14-PRESSED                    VALUE 14.
000350     88  PFKEY-15-PRESSED                    VALUE 15.
000351     88  PFKEY-16-PRESSED                    VALUE 16.
000352     88  END-KEY-PRESSED                     VALUE 16.
000353     88  RESTART-KEY-PRESSED                 VALUE 17.
000354     88  PFKEY-32-PRESSED                    VALUE 32.
008200
000356 01  PFKEY-VALUES.
000357     03  END-KEY-VALUE                   PIC 9(2)  VALUE 16.
000358     03  RESTART-KEY-VALUE               PIC 9(2)  VALUE 17.
008600
008700
000361 01  WAIT-SCREEN USAGE DISPLAY-WS.
000362     03  FILLER                      PIC X(45)   ROW 01 COLUMN 01
000363         SOURCE  APPLICATION-NAME.
000364     03  FILLER                      PIC X(50)   ROW 02 COLUMN 01
000365         SOURCE  TYPE-OF-PROCESSING-FIELD.
000366     03  FILLER                      PIC X(30)   ROW 01 COLUMN 51
000367         SOURCE  COMPANY-DISPLAY-NAME.
000368     03  FILLER                      PIC X(61)   ROW 12 COLUMN 20
000369         SOURCE  WAIT-MESSAGE.
009700
000371 01  TYPE-OF-PROCESSING-FIELD        PIC X(50)   VALUE SPACES.
000372 01  WAIT-MESSAGE                    PIC X(61)   VALUE SPACES.
      *%IDSI  Added because no copy members.
009100*    COPY WSSYSDAT.
000375 01  WSA-DATE-AREA.
000376     05  PL-NDATE                     PIC 9(6).  
000377     05  PL-ADATE REDEFINES PL-NDATE  PIC X(6).
000378 01  SYSTEM-DATE REDEFINES WSA-DATE-AREA.
000379     05  SYSTEM-YEAR                  PIC 9(2).
000380     05  SYSTEM-MONTH                 PIC 9(2).
000381     05  SYSTEM-DAY                   PIC 9(2).
009200*    COPY WSSYSTIM.
000383 01  WSA-TIME-AREA.
000384     05  PL-NTIME                     PIC 9(8).
000385     05  PL-ATIME REDEFINES PL-NTIME  PIC X(8).
000386 01  SYSTEM-TIME REDEFINES WSA-TIME-AREA.
000387     05  SYSTEM-HOUR                  PIC 9(2).
000388     05  SYSTEM-MINUTE                PIC 9(2).
000389     05  SYSTEM-SECOND                PIC 9(2).
000390     05  SYSTEM-100-SECOND            PIC 9(2).
009300
009400/ *** LINKAGE SECTION ***
009500
000394 LINKAGE SECTION.
009700
000396 01  PASSED-VALUES.
009900
000398     03  PRINTER-STATUS                  PIC X(2).
000399     03  PRINT-OPERATION                 PIC 9(1).
010200
000401     03  REPORT-TITLE                    PIC X(132).
010400
000403     03  DETAIL-LINE                     PIC X(132).
010600
000405     03  SPOOLER-NAME                    PIC X(5).
010800
000407     03  TOP-LINE.
000408         05  RUN-DATE.
000409             07  FILLER                  PIC X(10).
000410             07  RUN-MONTH               PIC X(3).
000411             07  FILLER                  PIC X(1).
000412             07  RUN-DAY                 PIC 9(2).
000413             07  FILLER                  PIC X(4).
000414             07  RUN-YEAR                PIC 9(2).
000415             07  FILLER                  PIC X(3).
000416             07  RUN-HOUR                PIC 9(2).
000417             07  FILLER                  PIC X(1).
000418             07  RUN-MINUTE              PIC 9(2).
000419             07  HYPHEN-FILLER           PIC X(1).
000420             07  RUN-SECOND              PIC 9(2).
000421         05  FILLER                      PIC X(08).
000422         05  TOP-NAME                    PIC X(50).
000423         05  FILLER                      PIC X(32).
000424         05  FILLER                      PIC X(5).
000425         05  PAGE-NUMBER                 PIC 9(4).
012800
000427     03  LINE-COUNT                      PIC 9(2).
013000
000429     03  PASSING-SYSTEM-DATE             PIC 9(6).
000430     03  PASSING-SYSTEM-TIME             PIC 9(8).
013300
000432     03  HEADERS-TO-PRINT                PIC 9(1).
000433     03  LEGENDS-TO-PRINT                PIC 9(1).
000434     03  LEGENDS-FROM-PAGE-2-ONWARD      PIC 9(1).
013700
000436     03  HEADER-LEGEND-TABLE
000437             OCCURS 10 TIMES
000438             INDEXED BY HEADER-LEGEND-INDEX.
000439         05  HEADER-LEGEND-LINE          PIC X(132).
014200
014300
014400/ *** PROCEDURE DIVISION ***
014500
000444 PROCEDURE DIVISION USING PASSED-VALUES.
014700
000446 DECLARATIVES.
014900
015000
000449 PRINTER-ERROR SECTION.
000450     USE AFTER STANDARD ERROR PROCEDURE ON OUTPUT.
015300
000452 PRINTER-ERROR-PROCEDURE.
015500
000454     STOP RUN.
015700
000456 END DECLARATIVES.
015900
016000/ *** LEVEL 1 - PROGRAM MAIN LOGIC ***
016100
000460 LEVEL-1 SECTION.
      *%IDSI  Added to get the system date and time.
000462     ACCEPT PL-NDATE FROM DATE.         
000463     ACCEPT PL-NTIME FROM TIME.         
016300
000465 PRINT-OUT-PROCESSING.
016500
000467     IF PRINT-OPERATION IS = OPEN-PRINTER
000468         PERFORM OPEN-PRINTER-LOGIC,
000469         GO TO PRINT-OUT-PROCESSING-EXIT.
016900
000471     IF PRINT-OPERATION IS = PRINT-A-LINE
000472         PERFORM PRINT-A-LINE-LOGIC,
000473         GO TO PRINT-OUT-PROCESSING-EXIT.
017300
000475     IF PRINT-OPERATION IS = SKIP-A-LINE
000476         PERFORM LINE-FEED,
000477         GO TO PRINT-OUT-PROCESSING-EXIT.
017700
000479     IF PRINT-OPERATION IS = SKIP-A-PAGE
000480         PERFORM FORM-FEED,
000481         GO TO PRINT-OUT-PROCESSING-EXIT.
018100
000483     IF PRINT-OPERATION IS = CLOSE-PRINTER
000484         PERFORM CLOSE-PRINTER-LOGIC.
018400
000486 PRINT-OUT-PROCESSING-EXIT.
000487     EXIT PROGRAM.
018700
018800/ *** LEVEL-2 ROUTINES - IN ORDER OF APPEARANCE IN LEVEL-1 ***
018900
000491 LEVEL-2 SECTION.
019100
000493 OPEN-PRINTER-LOGIC.
019300
000495     PERFORM INITIALIZE-TOP-LINE.
000496     PERFORM SET-UP-SPOOLER-NAME.
000497     PERFORM OPEN-PRINTER-FILE.
000498     MOVE PRINTER-FILE-STATUS TO PRINTER-STATUS.
019800
000500 PRINT-A-LINE-LOGIC.
020000
000502     IF LINE-COUNT IS NOT < 60
000503         PERFORM BEGIN-NEW-PAGE.
020300
000505     MOVE DETAIL-LINE TO PRINT-RECORD-1-132.
000506     PERFORM PRINT-DETAIL-LINE.
020600
000508 CLOSE-PRINTER-LOGIC.
020800
000510     PERFORM CLOSE-PRINTER-FILE.
000511     MOVE PRINTER-FILE-STATUS TO PRINTER-STATUS.
021100
021200/ *** LEVEL-3 ROUTINES - IN ORDER OF APPEARANCE IN LEVEL-2 ***
021300
000515 LEVEL-3 SECTION.
021500
000517 INITIALIZE-TOP-LINE.
021700
000519     MOVE 60   TO LINE-COUNT.
000520     MOVE ZERO TO PAGE-NUMBER.
022000
000522     MOVE PASSING-SYSTEM-DATE TO SYSTEM-DATE.
000523     MOVE PASSING-SYSTEM-TIME TO SYSTEM-TIME.
022300
000525     MOVE MONTHS(SYSTEM-MONTH) TO RUN-MONTH.
000526     MOVE SYSTEM-DAY           TO RUN-DAY.
000527     MOVE SYSTEM-YEAR          TO RUN-YEAR.
000528     MOVE SYSTEM-HOUR          TO RUN-HOUR.
000529     MOVE SYSTEM-MINUTE        TO RUN-MINUTE.
000530     MOVE ":"                  TO HYPHEN-FILLER.
000531     MOVE SYSTEM-SECOND        TO RUN-SECOND.
023100
000533 SET-UP-SPOOLER-NAME.
023300
000535     MOVE SPOOLER-NAME        TO SPOOLER-PREFIX.
000536     MOVE PASSING-SYSTEM-TIME TO SPOOLER-TIME-STAMP.
000537     MOVE SPOOLER-SECONDS     TO SPOOLER-SUFFIX.
000538     MOVE SPOOLER-FILE-NAME   TO PRINTER-FILE-ASSIGNMENT.
023800
000540 BEGIN-NEW-PAGE.
024000
000542     PERFORM FORM-FEED.
000543     PERFORM PRINT-TOP-LINE.
000544     PERFORM PRINT-TITLE.
024400
000546     SET HEADER-LEGEND-INDEX TO 1.
000547     ADD LEGENDS-TO-PRINT, HEADERS-TO-PRINT GIVING
000548         HEADER-LEGEND-TOTAL.
000549     SET HEADER-LEGEND-INDEX UP BY HEADERS-TO-PRINT.
024900
000551     IF LEGENDS-TO-PRINT IS NOT < 1
000552         PERFORM PRINT-LEGEND
000553            UNTIL HEADER-LEGEND-INDEX IS > HEADER-LEGEND-TOTAL,
000554         PERFORM LINE-FEED,
000555         MOVE LEGENDS-FROM-PAGE-2-ONWARD TO LEGENDS-TO-PRINT.
025500
000557     SET HEADER-LEGEND-INDEX TO 1.
000558     IF HEADERS-TO-PRINT IS NOT < 1
000559         PERFORM PRINT-HEADERS
000560            UNTIL HEADER-LEGEND-INDEX IS > HEADERS-TO-PRINT.
026000
000562     PERFORM LINE-FEED.
026200
026300/ *** NEW-PAGE ROUTINES ***
026400
000566 NEW-PAGE-ROUTINES SECTION.
026600
000568 PRINT-TOP-LINE.
026800
000570     MOVE TOP-LINE TO PRINT-RECORD-1-132.
000571     PERFORM PRINT-DETAIL-LINE.
000572     PERFORM LINE-FEED.
027200
000574 PRINT-TITLE.
027400
000576     MOVE REPORT-TITLE TO PRINT-RECORD-1-132.
000577     PERFORM PRINT-DETAIL-LINE.
000578     PERFORM LINE-FEED.
027800
000580 PRINT-LEGEND.
028000
000582     MOVE HEADER-LEGEND-LINE (HEADER-LEGEND-INDEX)
000583         TO PRINT-RECORD-1-132.
000584     PERFORM PRINT-DETAIL-LINE.
000585     SET HEADER-LEGEND-INDEX UP BY 1.
028500
000587 PRINT-HEADERS.
028700
000589     MOVE HEADER-LEGEND-LINE (HEADER-LEGEND-INDEX)
000590         TO PRINT-RECORD-1-132.
000591     PERFORM PRINT-DETAIL-LINE.
000592     SET HEADER-LEGEND-INDEX UP BY 1.
029200
029300/ *** OUTPUT-TO-PRINTER ROUTINES ***
029400
000596 OUTPUT-TO-PRINTER SECTION.
029600
000598 PRINT-DETAIL-LINE.
029800
000600     PERFORM SCAN-AND-PRINT.
000601     ADD 1 TO LINE-COUNT.
000602     MOVE SPACES TO PRINT-RECORD-1-132.
030200
000604 LINE-FEED.
030400
000606     MOVE SPACES TO PRINT-RECORD-1-132.
000607     WRITE PRINT-RECORD-1-1 BEFORE ADVANCING 1 LINE.
000608     ADD 1 TO LINE-COUNT.
030800
000610 FORM-FEED.
031000
000612     MOVE SPACES TO PRINT-RECORD-1-132.
000613     WRITE PRINT-RECORD-1-1 BEFORE ADVANCING PAGE.
000614     MOVE ZERO TO LINE-COUNT.
000615     ADD 1 TO PAGE-NUMBER.
031500
031600/ *** SCAN-AND-PRINT ROUTINE ***
031700
000619 SCAN-AND-PRINT SECTION.
031900
000621 SCAN-LINE-AND-PRINT.
032100
000623     IF PRINT-RECORD-CHARS-121-132 IS NOT = SPACES
000624         WRITE PRINT-RECORD-1-132 BEFORE ADVANCING 1 LINE,
000625         GO TO SCAN-AND-PRINT-EXIT.
032500
000627     IF PRINT-RECORD-CHARS-109-120 IS NOT = SPACES
000628         WRITE PRINT-RECORD-1-120 BEFORE ADVANCING 1 LINE,
000629         GO TO SCAN-AND-PRINT-EXIT.
032900
000631     IF PRINT-RECORD-CHARS-97-108 IS NOT = SPACES
000632         WRITE PRINT-RECORD-1-108 BEFORE ADVANCING 1 LINE,
000633         GO TO SCAN-AND-PRINT-EXIT.
033300
000635     IF PRINT-RECORD-CHARS-85-96 IS NOT = SPACES
000636         WRITE PRINT-RECORD-1-96 BEFORE ADVANCING 1 LINE,
000637         GO TO SCAN-AND-PRINT-EXIT.
033700
000639     IF PRINT-RECORD-CHARS-73-84 IS NOT = SPACES
000640         WRITE PRINT-RECORD-1-84 BEFORE ADVANCING 1 LINE,
000641         GO TO SCAN-AND-PRINT-EXIT.
034100
000643     IF PRINT-RECORD-CHARS-61-72 IS NOT = SPACES
000644         WRITE PRINT-RECORD-1-72 BEFORE ADVANCING 1 LINE,
000645         GO TO SCAN-AND-PRINT-EXIT.
034500
000647     IF PRINT-RECORD-CHARS-49-60 IS NOT = SPACES
000648         WRITE PRINT-RECORD-1-60 BEFORE ADVANCING 1 LINE,
000649         GO TO SCAN-AND-PRINT-EXIT.
034900
000651     IF PRINT-RECORD-CHARS-37-48 IS NOT = SPACES
000652         WRITE PRINT-RECORD-1-48 BEFORE ADVANCING 1 LINE,
000653         GO TO SCAN-AND-PRINT-EXIT.
035300
000655     IF PRINT-RECORD-CHARS-25-36 IS NOT = SPACES
000656         WRITE PRINT-RECORD-1-36 BEFORE ADVANCING 1 LINE,
000657         GO TO SCAN-AND-PRINT-EXIT.
035700
000659     IF PRINT-RECORD-CHARS-13-24 IS NOT = SPACES
000660         WRITE PRINT-RECORD-1-24 BEFORE ADVANCING 1 LINE,
000661         GO TO SCAN-AND-PRINT-EXIT.
036100
000663     IF PRINT-RECORD-CHARS-2-12 IS NOT = SPACES
000664         WRITE PRINT-RECORD-1-12 BEFORE ADVANCING 1 LINE,
000665         GO TO SCAN-AND-PRINT-EXIT.
036500
000667     WRITE PRINT-RECORD-1-1 BEFORE ADVANCING 1 LINE.
036700
000669 SCAN-AND-PRINT-EXIT.
000670     EXIT.
037000
037100/ *** FILE I-O ROUTINES ***
037200
000674 FILE-I-O-ROUTINES SECTION.
037400
037500*** PRINTER FILE I-O ***
037600
000678 OPEN-PRINTER-FILE.
000679     OPEN OUTPUT PRINTER-FILE.
037900
000681 CLOSE-PRINTER-FILE.
000682     CLOSE PRINTER-FILE.
038200
038300
038400
