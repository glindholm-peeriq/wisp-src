
      /*****************************************************************
      *                                                                *
      *                       IDENTIFICATION DIVISION                  *
      *                                                                *
      ******************************************************************

       IDENTIFICATION DIVISION.
       PROGRAM-ID.
           QAFILEIO.
       AUTHOR.
           Greg Lindholm.
       DATE-WRITTEN.
           12/08/89.

      /*****************************************************************
      *                                                                *
      *                         ENVIRONMENT DIVISION                   *
      *                                                                *
      ******************************************************************

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.
           WANG-VS.
       OBJECT-COMPUTER.
           WANG-VS.
       FIGURATIVE-CONSTANTS.
           BLINK-BIT IS "10"
           PROTECT-BIT IS "04"
           DIM-BIT IS "08"
           TAB-STOP IS "86"
           BLINKING-DISPLAY IS "94"
           BRITE IS "84"
           LF IS "0A"
           NO-ECHO IS "98".

      /*****************************************************************
      *                                                                *
      *                         INPUT-OUTPUT SECTION                   *
      *                                                                *
      ******************************************************************
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *
           SELECT CRT
               ASSIGN TO "CRT",      "DISPLAY",
               ORGANIZATION    IS SEQUENTIAL
               ACCESS MODE     IS RANDOM
               RELATIVE KEY    IS LINENUM
               PFKEY           IS PF-KEY
               CURSOR POSITION IS CURSOR-POS
               FILE STATUS     IS FILSTAT.

      *$COMPRESSFILE
           SELECT FILE-A
               ASSIGN TO "AFILE",    "DISK",     NODISPLAY,
               ORGANIZATION    IS INDEXED
               ACCESS MODE     IS DYNAMIC
               RECORD KEY      IS FILE-A-KEY-X
               RESERVE         10 AREAS
               FILE STATUS     IS FILE-A-STAT.

           SELECT FILE-B
               ASSIGN TO "BFILE",    "DISK",     NODISPLAY,
               ORGANIZATION    IS INDEXED
               ACCESS MODE     IS DYNAMIC
               RESERVE         10 AREAS
               RECORD KEY      IS FILE-B-KEY
               ALTERNATE RECORD 01 IS FILE-B-ALTKEY-1 DUPLICATES
                                02 IS FILE-B-ALTKEY-2 DUPLICATES
               FILE STATUS     IS FILE-B-STAT.

           SELECT SEQUENTIAL-FILE
               ASSIGN TO "SEQFIL", "DISK",  NODISPLAY,
               FILE STATUS     IS FILSTAT.

      *$SORTFILE
           SELECT SORT-B-FILE
               ASSIGN TO "SORTFILE" "DISK"
               BUFFER SIZE IS 50 BLOCKS.

           SELECT FILE-B-SEQ
               ASSIGN TO "BFILESEQ",    "DISK",     NODISPLAY.

      /*****************************************************************
      *                                                                *
      *                      DATA DIVISION - FILE SECTION              *
      *                                                                *
      ******************************************************************
      *
       DATA DIVISION.
       FILE SECTION.
      *
       FD  CRT
           LABEL RECORDS ARE OMITTED.
       01  CRTREC                         PIC X(1924).

       FD  FILE-A
           RECORD CONTAINS 84 COMPRESSED CHARACTERS
           LABEL RECORDS ARE STANDARD
           VALUE OF FILENAME IS FIL-FOR-FILE-A
                    LIBRARY  IS LIB-FOR-FILE-A
                    VOLUME   IS VOL-FOR-FILE-A.

       01  FILE-A-RECORD.
           03  FILE-A-KEY-X                  PIC X(4).
           03  FILE-A-KEY REDEFINES FILE-A-KEY-X PIC 9(4).
           03  NAME-VALUE                    PIC X(40).
           03  ADDRESS-VALUE                 PIC X(40).

       FD  FILE-B
           RECORD CONTAINS 92 COMPRESSED CHARACTERS
           LABEL RECORDS ARE STANDARD
           VALUE OF FILENAME IS FIL-FOR-FILE-B
                    LIBRARY  IS LIB-FOR-FILE-B
                    VOLUME   IS VOL-FOR-FILE-B.

       01  FILE-B-RECORD.
           03  FILE-B-KEY                    PIC X(4).
           03  FILE-B-ALTKEY-1               PIC X(4).
           03  FILE-B-ALTKEY-2               PIC X(4).
           03  B-VALUE-1                     PIC X(40).
           03  B-VALUE-2                     PIC X(40).

       FD  SEQUENTIAL-FILE
           VALUE OF FILENAME IS SEQ-FILE-NAME
                    LIBRARY  IS SEQ-LIBRARY
                    VOLUME   IS SEQ-VOLUME.

       01  SEQ-RECORD   PIC X(20).


       SD  SORT-B-FILE.
       01  SORT-REC.
       	   05  SORT-B-KEY      	       	PIC X(4).
       	   05  SORT-B-ALT-1    	       	PIC X(4).
       	   05  SORT-B-ALT-2    	       	PIC X(4).
	   05  FILLER  	       	        PIC X(80).

       FD  FILE-B-SEQ
           RECORD CONTAINS 92 COMPRESSED CHARACTERS
           LABEL RECORDS ARE STANDARD
           VALUE OF FILENAME IS "BFILESEQ".

       01  FILE-B-RECORD-SEQ.
           03  FILE-B-SEQ-KEY                    PIC X(4).
           03  FILE-B-SEQ-ALTKEY-1               PIC X(4).
           03  FILE-B-SEQ-ALTKEY-2               PIC X(4).
           03  B-SEQ-VALUE-1                     PIC X(40).
           03  B-SEQ-VALUE-2                     PIC X(40).
	   
      /*****************************************************************
      *                                                                *
      *                         WORKING STORAGE                        *
      *                                                                *
      ******************************************************************

       WORKING-STORAGE SECTION.

       01  INDEX-COUNTER                  PIC 9(5).
       01  FILSTAT.
           03  ERR-FLAG                   PIC X(01).
           03  PFK-BYTE                   PIC X(01).

       01  FILE-A-STAT                    PIC X(2).
       01  FILE-B-STAT                    PIC X(2).
       01  FILE-PRT-STAT                  PIC X(2).

      ******************************************************************

       01  RETCOD.
           05  B4          BINARY VALUE 0.
           05  RET-CODE BINARY VALUE 0.
       01  RNUM   PIC 9(10).

       01  FIL-FOR-FILE-A                 PIC X(8) VALUE "AFILE".
       01  LIB-FOR-FILE-A                 PIC X(8) VALUE SPACES.
       01  VOL-FOR-FILE-A                 PIC X(6) VALUE SPACES.

       01  FIL-FOR-FILE-B                 PIC X(8) VALUE "BFILE".
       01  LIB-FOR-FILE-B                 PIC X(8) VALUE "LIBOUT".
       01  VOL-FOR-FILE-B                 PIC X(6) VALUE "VOLOUT".
       01  B-SCREEN-TITLE                 pic X(40) VALUE SPACES.

       01  FLV-TITLE                      PIC X(40) VALUE SPACES.
       01  FLV-FILE                       PIC X(8) VALUE SPACES.
       01  FLV-LIB                        PIC X(8) VALUE SPACES.
       01  FLV-VOL                        PIC X(6) VALUE SPACES.

       01  SEQ-FILE-NAME                  PIC X(8) VALUE "SEQFILE".
       01  SEQ-LIBRARY                    PIC X(8) VALUE SPACES.
       01  SEQ-VOLUME                     PIC X(6) VALUE SPACES.
       01  SEQ-SCREEN-TITLE               PIC X(40) VALUE SPACES.

       01  PRT-FILE-NAME                  PIC X(8) VALUE "PRTFILE".
       01  PRT-LIBRARY                    PIC X(8) VALUE SPACES.
       01  PRT-VOLUME                     PIC X(6) VALUE SPACES.

       01  REC-FLAG                       PIC X.
           88  NO-MORE-RECORDS            VALUE "N".
           88  MORE-RECORDS               VALUE "Y".

       01  SORT-FILE-NAME                 PIC X(8) VALUE "SFILE   ".

       01  EXT-PM                         PIC X(2) VALUE "PM".
       01  SAVE-PM                        PIC X.
       01  KEEP-MODE                      PIC X VALUE "K".

       77  LINENUM                        PIC 9(02) VALUE 1.
       77  PF-KEY                         PIC X(02) VALUE ZERO.

       01  HL                             PIC X(2) VALUE "HL".

       01  SAMPLE-PROG                    PIC X(8) VALUE "SAMPLE  ".
       01  DISPLAY-PROG                   PIC X(8) VALUE "DISPLAY ".

       01  CURSOR-POS.
           03  MOD-COL                    BINARY.
           03  MOD-ROW                    BINARY.

       01  REDEF-CURS-POS                 REDEFINES CURSOR-POS.
           03  FILLER                     PIC X(01).
           03  CURS-COL                   PIC X(01).
           03  FILLER                     PIC X(01).
           03  CURS-ROW                   PIC X(01).

       01  THE-PASS                       PIC X(6).
       01  MY-INDEX                       PIC 9(5).
       01  ACCEPT-FIELD                   PIC X(6).
       01  START-VAL                      PIC X(4) VALUE "0000".

       01  FILE-B-RECORD-ALT.
           03  FILE-B-KEYS-ALT            PIC X(12).
           03  B-VALUE-1-ALT              PIC X(40).
           03  B-VALUE-2-ALT              PIC X(40).

      ******************************************************************

006300 01  FSTAT                    PIC XX.
006400 01  SCREEN-FIELDS.
006500     05  W001-STATUS          PIC X       VALUE "C".
006600     05  W001-ADDRESS         PIC X       VALUE SPACE.
006700     05  W001-FUNCTION-TYPE   PIC X       VALUE "P".
006800     05  W001-LOCATION.
006900         10  W001-FILE        PIC X(8).
007000         10  W001-LIB         PIC X(8).
007100         10  W001-VOL         PIC X(6).
007200*
007300 01  EXTRACT-FIELDS.
007400     05  W002-PM              PIC X(2)     VALUE "PM".
007500     05  W002-PRINT-MODE      PIC X        VALUE SPACE.
007400     05  W002-ID              PIC X(2)     VALUE "ID".
007500     05  W002-USER-ID         PIC X(3)     VALUE SPACE.
007600
007700 01  SET-FIELDS.
007800     05  W003-PM              PIC XX       VALUE "PM".
007900     05  W003-PMH             PIC X        VALUE SPACE.

       01  W004-MODE                PIC X
                                    VALUE "S".
       01  W004-DISP                PIC X(2)
                                    VALUE "DX".

007700 01  DATE-FIELDS.
007800     05  W005-HL              PIC XX       VALUE "HL".
007900     05  W005-DATE-TIME       PIC X(45)    VALUE SPACE.

008000
       01 W-PRINT-LINE.
          05   FILLER               PIC X(05)    VALUE SPACES.
          05   FILLER               PIC X(07)    VALUE "User : ".
          05   W-PRINT-USER         PIC X(3).
          05   FILLER               PIC X(10)    VALUE SPACES.
          05   FILLER               PIC X(13)    VALUE "Printed on : ".
          05   W-PRINT-DATE         PIC X(45).

      /*****************************************************************
      *                                                                *
      *                   DISPLAY DEFINITIONS                          *
      *                                                                *
      ******************************************************************

       01  SEQ-FILE-SCREEN USAGE IS DISPLAY-WS.
           03  FILLER PIC X(48) ROW 5 COL 20
               VALUE 
               "THE SEQ FILE IS OPEN, PRESS (ENTER) TO CLOSE IT.".
           03  FILLER PIC X(26) ROW 24 COL 30 VALUE
               "PRESS (ENTER) TO CONTINUE.".

       01  FILE-LIB-VOL USAGE IS DISPLAY-WS.
           03  FILLER PIC X(40) ROW 5 COL 20
               SOURCE FLV-TITLE.

           03  FILLER PIC X(4) ROW 12 COL 15 VALUE "FILE".
           03  FILLER PIC X(8) ROW 12 COL 20 
               SOURCE FLV-FILE OBJECT FLV-FILE.

           03  FILLER PIC X(7) ROW 12 COL 30 VALUE "LIBRARY".
           03  FILLER PIC X(8) ROW 12 COL 38 
               SOURCE FLV-LIB OBJECT FLV-LIB.

           03  FILLER PIC X(6) ROW 12 COL 48 VALUE "VOLUME".
           03  FILLER PIC X(6) ROW 12 COL 55 
               SOURCE FLV-VOL OBJECT FLV-VOL.

           03  FILLER PIC X(40) ROW 24 COL 20 VALUE
               "(ENTER) to continue, (16) to EXIT".

       01  FILE-B-SCREEN USAGE IS DISPLAY-WS.
           03  FILLER PIC X(40) ROW 1 COL 20 VALUE
               "* * * FILE-B-SCREEN * * *".

           03  FILLER PIC X(40) ROW 3 COL 20
               SOURCE B-SCREEN-TITLE.

           03  FILLER PIC X(10) ROW  5 COL 20 VALUE "FILE-B-KEY".
           03  FILLER PIC X(04) ROW  5 COL 40 
               SOURCE FILE-B-KEY OBJECT FILE-B-KEY.

           03  FILLER PIC X(15) ROW  6 COL 20 VALUE "FILE-B-ALTKEY-1".
           03  FILLER PIC X(04) ROW  6 COL 40 
               SOURCE FILE-B-ALTKEY-1 OBJECT FILE-B-ALTKEY-1.

           03  FILLER PIC X(15) ROW  7 COL 20 VALUE "FILE-B-ALTKEY-2".
           03  FILLER PIC X(04) ROW  7 COL 40 
               SOURCE FILE-B-ALTKEY-2 OBJECT FILE-B-ALTKEY-2.

           03  FILLER PIC X(09) ROW  9 COL 10 VALUE "B-VALUE-1".
           03  FILLER PIC X(40) ROW  9 COL 20 
               SOURCE B-VALUE-1 OBJECT B-VALUE-1.

           03  FILLER PIC X(09) ROW 10 COL 10 VALUE "B-VALUE-2".
           03  FILLER PIC X(40) ROW 10 COL 20 
               SOURCE B-VALUE-2 OBJECT B-VALUE-2.

           03  FILLER PIC X(15) ROW 17 COL 20 VALUE "FILE-B-KEYS-ALT".
           03  FILLER PIC X(12) ROW 17 COL 40 
               SOURCE FILE-B-KEYS-ALT OBJECT FILE-B-KEYS-ALT.

           03  FILLER PIC X(13) ROW 19 COL 10 VALUE "B-VALUE-1-ALT".
           03  FILLER PIC X(40) ROW 19 COL 20 
               SOURCE B-VALUE-1-ALT OBJECT B-VALUE-1-ALT.

           03  FILLER PIC X(13) ROW 20 COL 10 VALUE "B-VALUE-2-ALT".
           03  FILLER PIC X(40) ROW 20 COL 20 
               SOURCE B-VALUE-2-ALT OBJECT B-VALUE-2-ALT.

           03  FILLER PIC X(11) ROW 22 COL 35 VALUE "FILE-B-STAT".
           03  FILLER PIC X(02) ROW 22 COL 47
               SOURCE FILE-B-STAT OBJECT FILE-B-STAT.

           03  FILLER SOURCE B-SCREEN-FOOTER PIC X(60) ROW 24 COL 20.

       01  B-SCREEN-FOOTER  PIC X(60) VALUE
               "PRESS (ENTER) TO CONTINUE.".


       01  SEQ-SCREEN USAGE IS DISPLAY-WS.
           03  FILLER PIC X(40) ROW 1 COL 20 VALUE
               "* * * SEQ-SCREEN * * *".

           03  FILLER PIC X(40) ROW 3 COL 20
               SOURCE SEQ-SCREEN-TITLE.

           03  FILLER PIC X(10) ROW  5 COL 20 VALUE "SEQ-RECORD".
           03  FILLER PIC X(20) ROW  5 COL 40 
               SOURCE SEQ-RECORD OBJECT SEQ-RECORD.

           03  FILLER PIC X(26) ROW 24 COL 30 VALUE
               "PRESS (ENTER) TO CONTINUE.".

       01  FILE-SCREEN USAGE IS DISPLAY-WS.
           03  FILLER  PIC X(11) ROW  1 COL 20 VALUE
               "TRY THINGS:".
           03  FILLER  PIC X(11) ROW  1 COL 60 VALUE
               "FILE-SCREEN".
           03  FILLER  PIC X(35) ROW  3 COL  2 VALUE
               "(1)  CREATE FILE-A AND WRITE IT".
           03  FILLER  PIC X(35) ROW  4 COL  2 VALUE
               "(2)  Auto test FILE-B.".
           03  FILLER  PIC X(36) ROW  5 COL  2 VALUE
               "(3)  SORT SORT-B-FILE".
           03  FILLER  PIC X(35) ROW  6 COL  2 VALUE
               "(4)  DELETE RECORDS FROM FILE-A.".
           03  FILLER  PIC X(36) ROW  7 COL  2 VALUE
               "(5)  TRY START LOCK DETECTION LOGIC.".
           03  FILLER  PIC X(35) ROW  8 COL  2 VALUE
               "(6)  CREATE A SEQUENTIAL FILE.".
           03  FILLER  PIC X(35) ROW  9 COL  2 VALUE
               "(7)  CREATE FILE-B.".
           03  FILLER  PIC X(35) ROW 10 COL  2 VALUE
               "(8)  OPEN FILE-B I-O.".
           03  FILLER  PIC X(35) ROW 11 COL  2 VALUE
               "(9)  OPEN FILE-B SHARED.".
           03  FILLER  PIC X(35) ROW 12 COL  2 VALUE
               "(10) CLOSE FILE-B.".
           03  FILLER  PIC X(35) ROW 13 COL  2 VALUE
               "(11) WRITE FILE-B.".
           03  FILLER  PIC X(35) ROW 14 COL  2 VALUE
               "(12) READ FILE-B FILE-B-KEY.".
           03  FILLER  PIC X(35) ROW 15 COL  2 VALUE
               "(13) READ FILE-B FILE-B-ALTKEY-1.".
           03  FILLER  PIC X(35) ROW 16 COL  2 VALUE
               "(14) READ FILE-B FILE-B-ALTKEY-2.".
           03  FILLER  PIC X(35) ROW 17 COL  2 VALUE
               "(15) OPEN FILE-B INPUT.".
           03  FILLER  PIC X(35) ROW 20 COL  2 VALUE
               "(16) EXIT".

           03  FILLER  PIC X(35) ROW  3 COL 40 VALUE
               "(17) READ FILE-B NEXT INTO ALT.".
           03  FILLER  PIC X(35) ROW  4 COL 40 VALUE
               "(18) WRITE FILE-B FROM ALT.".
           03  FILLER  PIC X(35) ROW  5 COL 40 VALUE
               "(19) READ FILE-B HOLD.".
           03  FILLER  PIC X(35) ROW  6 COL 40 VALUE
               "(20) READ FILE-B HOLD & REWRITE.".
           03  FILLER  PIC X(35) ROW  7 COL 40 VALUE
               "(21) READ FILE-B HOLD & DELETE.".
           03  FILLER  PIC X(35) ROW  8 COL 40 VALUE
               "(22) OPEN EXTEND SEQUENTIAL".
           03  FILLER  PIC X(35) ROW  9 COL 40 VALUE
               "(23) OPEN OUTPUT SEQUENTIAL".
           03  FILLER  PIC X(35) ROW 10 COL 40 VALUE
               "(24) CLOSE SEQUENTIAL".
           03  FILLER  PIC X(35) ROW 11 COL 40 VALUE
               "(25) WRITE EXTEND TO SEQUENTIAL".
           03  FILLER  PIC X(35) ROW 12 COL 40 VALUE
               "(26) START FILE-B.".
           03  FILLER  PIC X(35) ROW 13 COL 40 VALUE
               "(27) READ FILE-B HOLD TIMEOUT".
           03  FILLER  PIC X(35) ROW 14 COL 40 VALUE
               "(28) START FILE-B FILE-B-ALTKEY-1".
           03  FILLER  PIC X(35) ROW 15 COL 40 VALUE
               "(29) FREE ALL".
           03  FILLER  PIC X(35) ROW 16 COL 40 VALUE
               "(30) OPEN FILE-B SPECIAL-INPUT.".
           03  FILLER  PIC X(35) ROW 17 COL 40 VALUE
               "(31) OPEN SHARED SEQUENTIAL".
           03  FILLER  PIC X(35) ROW 20 COL 40 VALUE
               "(32) NEXT SCREEN".

           03  FILLER  PIC X(23) ROW 24 COL 10 VALUE
               "PRESS (16) TO EXIT".

      ******************************************************************
      *$MF_CODE
      *LOCAL-STORAGE SECTION.
      *$MF_END
      ******************************************************************

      /*****************************************************************
      *                                                                *
      *                         PROCEDURE DIVISION                     *
      *                                                                *
      ******************************************************************
      *
       PROCEDURE DIVISION.

       DECLARATIVES.

      ******************************************************************
      *                                                                *
      *      TRY OUT DECLARATIVES PROCESSING IN WISP.                  *
      *      WISP OUGHT TO 1) INSERT STUFF INTO FILE-A'S DECLARATIVES, *
      *      2) MAKE A FILE-B DECLARATIVE, USING FILE-A-B-DECLARATIVE  *
      *      3) DELETE THE DEADLOCK, 4) GENERATE DECLARATIVES FOR THE  *
      *      FILES WHICH DON'T HAVE THEM.                              *
      *                                                                *
      ******************************************************************

       FILE-A-B-DECLARATIVE SECTION.
           USE AFTER ERROR PROCEDURE ON FILE-A, FILE-B.

       FILE-A-B-START-DECL.
           IF FILSTAT  = "00" THEN
              MOVE "00" TO FILSTAT.

      *** THIS SHOULD BE DELETED BY WISP.

       DEADLOCK-DECLARATIVE SECTION.
           USE AFTER DEADLOCK.

       DEADLOCK-PARA.
           MOVE "00" TO FILE-A-STAT.
           MOVE "00" TO FILE-B-STAT.

      *** END OF CODE TO BE DELETED.

       END DECLARATIVES.

       FILE-TESTS.

           DISPLAY AND READ FILE-SCREEN ON CRT
           PFKEYS  1,  2 , 3,  4,  5,  6,  7,  8,  9, 10,
                  11, 12, 13, 14, 15, 16  17, 18, 19, 20,
                  21, 22, 23, 24, 25, 26, 27, 28, 29, 30,
                  31, 32.

           IF PF-KEY = "01" THEN
              PERFORM DO-FILE
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "02" THEN
              PERFORM AUTO-TEST-FILE-B THRU 
       	       	      AUTO-TEST-FILE-B-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "03" THEN
	      PERFORM SORT-B-FILE-PROC
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "04" THEN
              PERFORM DELETE-INDEXED-RECORDS
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "05" THEN
              PERFORM TRY-START-LOCKING
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "06" THEN
              PERFORM DO-SEQUENTIAL-FILE THRU DO-SEQUENTIAL-FILE-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "07" THEN
              PERFORM CREATE-FILE-B
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "08" THEN
              PERFORM OPEN-FILE-B-I-O
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "09" THEN
              PERFORM OPEN-FILE-B-SHARED
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "10" THEN
              PERFORM CLOSE-FILE-B
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "11" THEN
              PERFORM WRITE-FILE-B THRU WRITE-FILE-B-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "12" THEN
              PERFORM READ-FILE-B THRU READ-FILE-B-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "13" THEN
              PERFORM READ-FILE-B-ALT-1 THRU READ-FILE-B-ALT-1-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "14" THEN
              PERFORM READ-FILE-B-ALT-2 THRU READ-FILE-B-ALT-2-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "15" THEN
              PERFORM OPEN-FILE-B-INPUT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "17" THEN
              PERFORM READ-FILE-B-INTO
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "18" THEN
              PERFORM WRITE-FILE-B-FROM THRU WRITE-FILE-B-FROM-EXIT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "19" THEN
              PERFORM READ-FILE-B-HOLD
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "20" THEN
              PERFORM READ-FILE-B-HOLD-REWRITE
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "21" THEN
              PERFORM READ-FILE-B-HOLD-DELETE
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "22" THEN
              PERFORM OPEN-EXTEND-SEQ
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "23" THEN
              PERFORM OPEN-OUTPUT-SEQ
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "24" THEN
              PERFORM CLOSE-SEQ
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "25" THEN
              PERFORM WRITE-EXTEND-SEQ
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "26" THEN
              PERFORM START-FILE-B
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "27" THEN
              PERFORM READ-FILE-B-TIMEOUT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "28" THEN
              PERFORM START-FILE-B-ALTKEY
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "29" THEN
              PERFORM DO-FREE-ALL
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "30" THEN
              PERFORM OPEN-FILE-B-SPECIAL-INPUT
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "31" THEN
              PERFORM OPEN-SHARED-SEQ
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "32" THEN
              GO TO FILE-TESTS-2
           ELSE IF PF-KEY = "16" THEN
              GO TO EXIT-PROGRAM.

           GO TO FILE-TESTS.

       FILE-TESTS-2.


           CALL "QAFILE2" USING PF-KEY.

           IF PF-KEY = "32" THEN
              GO TO FILE-TESTS
           ELSE IF PF-KEY = "16" THEN
              GO TO EXIT-PROGRAM.

           GO TO FILE-TESTS-2.


      ******************************************************************
      * NOW DO SOME FILE WORK.
      ******************************************************************

      ******************************************************************
      * FIRST CREATE THE FILE.
      ******************************************************************

       DO-FILE.
           OPEN OUTPUT FILE-A.

           MOVE "TEST NAME" TO NAME-VALUE.
           MOVE "TEST ADDRESS" TO ADDRESS-VALUE.

           PERFORM WRITE-INDEX-RECORDS VARYING INDEX-COUNTER
                   FROM 1 BY 1 UNTIL INDEX-COUNTER IS EQUAL TO 200.
           CLOSE FILE-A.

	   DISPLAY "FILE-A CREATED".
	   
       WRITE-INDEX-RECORDS.
           MOVE INDEX-COUNTER TO FILE-A-KEY.
           WRITE FILE-A-RECORD.

      ******************************************************************

       DELETE-INDEXED-RECORDS.
           OPEN I-O FILE-A.

           MOVE 1 TO FILE-A-KEY.
           START FILE-A KEY IS EQUAL TO FILE-A-KEY-X.
           PERFORM DELETE-INDEX-RECORDS VARYING INDEX-COUNTER
                   FROM 1 BY 1 UNTIL INDEX-COUNTER IS EQUAL TO 200.
           DISPLAY
           "ALL RECORDS ARE DELETED, PRESS (ENTER) TO CLOSE THE FILE".
           CLOSE FILE-A.

       DELETE-INDEX-RECORDS.
           MOVE INDEX-COUNTER TO FILE-A-KEY.
           READ FILE-A.
           DELETE FILE-A.

       OPEN-FILE-FOR-I-O.
      ******************************************************************
      * NOW DO AN INDEXED READ OF THE FILE.
      ******************************************************************

           OPEN I-O FILE-A.

           DISPLAY "FILE-A IS OPENED FOR I-O NOW, STATUS " FILE-A-STAT.

           MOVE 1 TO FILE-A-KEY.

           READ FILE-A.

           DISPLAY "DOING A READ OF FILE-A, STATUS " FILE-A-STAT.

           CLOSE FILE-A.

       OPEN-FILE-SHARED.
      ******************************************************************
      * NOW DO AN INDEXED READ OF THE FILE.
      ******************************************************************

           OPEN SHARED FILE-A.
           DISPLAY "FILE-A IS OPENED SHARED NOW, STATUS " FILE-A-STAT.
           MOVE 1 TO FILE-A-KEY.
           READ FILE-A.
           DISPLAY "DOING A READ W/O HOLD OF FILE-A, STATUS "
                      FILE-A-STAT.
           MOVE 1 TO FILE-A-KEY.
           READ FILE-A WITH HOLD.
           DISPLAY "DOING A READ WITH HOLD OF FILE-A, STATUS "
                      FILE-A-STAT.
           CLOSE FILE-A.

      ******************************************************************
      * NOW DO A START ON THE FILE.
      ******************************************************************
       TRY-START-LOCKING.

           OPEN SHARED FILE-A.
           DISPLAY "FILE-A IS OPENED SHARED FOR TESTING START, STATUS "
                  FILE-A-STAT.
           MOVE 1 TO FILE-A-KEY.
           START FILE-A KEY IS = FILE-A-KEY-X.
           DISPLAY "SUCCESSFULL START OF FILE-A, STATUS " FILE-A-STAT.
           CLOSE FILE-A.

      ******************************************************************

       DO-SEQUENTIAL-FILE.
           MOVE "SEQUENTIAL FILE TO OPEN" TO FLV-TITLE.
           MOVE SEQ-FILE-NAME             TO FLV-FILE.
           MOVE SEQ-LIBRARY               TO FLV-LIB.
           MOVE SEQ-VOLUME                TO FLV-VOL.

           DISPLAY AND READ FILE-LIB-VOL ON CRT
           PFKEY 16.

           IF PF-KEY = "16" THEN GO TO DO-SEQUENTIAL-FILE-EXIT.

           MOVE FLV-FILE  TO SEQ-FILE-NAME.
           MOVE FLV-LIB   TO SEQ-LIBRARY.
           MOVE FLV-VOL   TO SEQ-VOLUME.

           OPEN OUTPUT SEQUENTIAL-FILE.

           MOVE "         1         2" TO SEQ-RECORD.
           WRITE SEQ-RECORD.
           MOVE "12345678901234567890" TO SEQ-RECORD.
           WRITE SEQ-RECORD.
           MOVE "         1         2" TO SEQ-RECORD.
           WRITE SEQ-RECORD.
           MOVE "12345678901234567890" TO SEQ-RECORD.
           WRITE SEQ-RECORD.
           MOVE "         1         2" TO SEQ-RECORD.
           WRITE SEQ-RECORD.
           MOVE "12345678901234567890" TO SEQ-RECORD.
           WRITE SEQ-RECORD.

           CLOSE SEQUENTIAL-FILE.

           MOVE "SEQUENTIAL FILE CREATED." TO FLV-TITLE.
           MOVE SEQ-FILE-NAME             TO FLV-FILE.
           MOVE SEQ-LIBRARY               TO FLV-LIB.
           MOVE SEQ-VOLUME                TO FLV-VOL.

           DISPLAY AND READ FILE-LIB-VOL ON CRT
           PFKEY 16.

           IF PF-KEY NOT = "16" THEN GO TO DO-SEQUENTIAL-FILE.

       DO-SEQUENTIAL-FILE-EXIT.
           EXIT.

      ******************************************************************
       AUTO-TEST-FILE-B.
           OPEN OUTPUT FILE-B.
           CLOSE FILE-B.
      *	   DISPLAY "FILE-B CREATED".

           OPEN I-O FILE-B.
      *	   DISPLAY "FILE-B OPENED I-O".

           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

	   MOVE "AAAA" TO FILE-B-KEY.
	   MOVE "AAAA" TO FILE-B-ALTKEY-1.
	   MOVE "AAAA" TO FILE-B-ALTKEY-2.
	   MOVE "1"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "BBBB" TO FILE-B-KEY.
	   MOVE "BBBB" TO FILE-B-ALTKEY-1.
	   MOVE "BBBB" TO FILE-B-ALTKEY-2.
	   MOVE "2"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "CCCC" TO FILE-B-KEY.
	   MOVE "CCCC" TO FILE-B-ALTKEY-1.
	   MOVE "CCCC" TO FILE-B-ALTKEY-2.
	   MOVE "3"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "ABBB" TO FILE-B-KEY.
	   MOVE "AAAA" TO FILE-B-ALTKEY-1.
	   MOVE "BBBB" TO FILE-B-ALTKEY-2.
	   MOVE "4"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "AABB" TO FILE-B-KEY.
	   MOVE "AAAA" TO FILE-B-ALTKEY-1.
	   MOVE "BBBB" TO FILE-B-ALTKEY-2.
	   MOVE "5"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "AAAB" TO FILE-B-KEY.
	   MOVE "AAAA" TO FILE-B-ALTKEY-1.
	   MOVE "BBBB" TO FILE-B-ALTKEY-2.
	   MOVE "6"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "EEEE" TO FILE-B-KEY.
	   MOVE "BBBB" TO FILE-B-ALTKEY-1.
	   MOVE "AAAA" TO FILE-B-ALTKEY-2.
	   MOVE "7"    TO B-VALUE-1.
	   MOVE SPACES TO B-VALUE-2.
	   PERFORM AUTO-WRITE-B.

	   MOVE "DDDDCCCCBBBB" TO FILE-B-KEYS-ALT.
	   MOVE "8"    TO B-VALUE-1-ALT.
	   MOVE SPACES TO B-VALUE-2-ALT.
           WRITE FILE-B-RECORD FROM FILE-B-RECORD-ALT
               INVALID KEY
               DISPLAY 
       	       "WRITE FILE-B ALT *** INVALID KEY *** RECNO=", 
       	       	       B-VALUE-1-ALT.

      *    DISPLAY "FILE-B RECORDS WRITTEN".

           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "EEEE" TO FILE-B-KEY.
	   READ FILE-B
	       INVALID KEY
	       DISPLAY "READ FILE-B *** INVALID KEY *** KEY=",
	       FILE-B-KEY.
	   IF B-VALUE-1 NOT = "7" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 1 failed".
	       
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "XXXX" TO FILE-B-KEY.
	   READ FILE-B
	       INVALID KEY.
	   IF FILE-B-STAT NOT = 23 THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 2 failed".

           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "AAAA" TO FILE-B-ALTKEY-1.
           READ FILE-B KEY IS FILE-B-ALTKEY-1
	       INVALID KEY
	       DISPLAY "READ FILE-B *** INVALID KEY *** KEY=",
	       FILE-B-ALTKEY-1.
	   IF B-VALUE-1 NOT = "1" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 3 failed".

           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "BBBB" TO FILE-B-ALTKEY-1.
           READ FILE-B KEY IS FILE-B-ALTKEY-1
	       INVALID KEY
	       DISPLAY "READ FILE-B *** INVALID KEY *** KEY=",
	       FILE-B-ALTKEY-1.
	   IF B-VALUE-1 NOT = "2" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 4 failed".

           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "CCCC" TO FILE-B-ALTKEY-2.
           READ FILE-B KEY IS FILE-B-ALTKEY-2
	       INVALID KEY
	       DISPLAY "READ FILE-B *** INVALID KEY *** KEY=",
	       FILE-B-ALTKEY-2.
	   IF B-VALUE-1 NOT = "3" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 5 failed".


      *	   DISPLAY "FILE-B FINISHED KEYED READ TESTS".

           CLOSE FILE-B.
      *	   DISPLAY "FILE-B CLOSED".

           OPEN I-O FILE-B.
      *	   DISPLAY "FILE-B OPENED I-O".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "1" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 6 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "6" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 7 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "5" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 8 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "4" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 9 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "2" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 10 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "3" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 11 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "8" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 12 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "7" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 13 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END.
	   IF FILE-B-STAT NOT = 10 THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 14 failed".

      *	   DISPLAY "FILE-B FINISHED SEQUENTIAL READ TESTS".


           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "CCCC" TO FILE-B-KEY.
           START FILE-B
               INVALID KEY
               DISPLAY "START FILE-B *** ERROR *** Test 15 failed".
	       
           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "3" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 16 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "8" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 17 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "7" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 18 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END.
	   IF FILE-B-STAT NOT = 10 THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 19 failed".
	   

           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
	   MOVE "BBBB" TO FILE-B-ALTKEY-1.
           START FILE-B KEY FILE-B-ALTKEY-1 EQUAL FILE-B-ALTKEY-1
               INVALID KEY
               DISPLAY "START FILE-B *** ERROR *** Test 20 failed".


           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "2" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 21 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "7" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 22 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "3" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 23 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               DISPLAY "FILE-B READ NEXT *** ERROR AT END ****".
	   IF B-VALUE-1-ALT NOT = "8" THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 24 failed".

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END.
	   IF FILE-B-STAT NOT = 10 THEN
	       DISPLAY "READ FILE-B *** ERROR *** Test 25 failed".

      *	   DISPLAY "FILE-B FINISHED START AND READ TESTS".
	   
           CLOSE FILE-B.
      *	   DISPLAY "FILE-B CLOSED".

	   DISPLAY "FILE-B AUTO TESTS FINISHED".

       AUTO-TEST-FILE-B-EXIT.
       	   EXIT.

       AUTO-WRITE-B.
           WRITE FILE-B-RECORD
               INVALID KEY
               DISPLAY "WRITE FILE-B *** INVALID KEY *** RECNO=", 
       	       	       B-VALUE-1.

      ******************************************************************

       CREATE-FILE-B.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           OPEN OUTPUT FILE-B.
           CLOSE FILE-B.

           MOVE "FILE CREATED" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       OPEN-FILE-B-I-O.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           OPEN I-O FILE-B.

           MOVE "OPEN I-O" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       OPEN-FILE-B-SHARED.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           OPEN SHARED FILE-B.

           MOVE "OPEN SHARED" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       OPEN-FILE-B-INPUT.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           OPEN INPUT FILE-B.

           MOVE "OPEN INPUT" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       OPEN-FILE-B-SPECIAL-INPUT.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           OPEN SPECIAL-INPUT FILE-B.

           MOVE "OPEN SPECIAL-INPUT" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       CLOSE-FILE-B.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           CLOSE FILE-B.

           MOVE "CLOSE" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       WRITE-FILE-B.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.

           MOVE "ENTER A RECORD TO WRITE" TO B-SCREEN-TITLE.
           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY = "16" THEN GO TO WRITE-FILE-B-EXIT.

           MOVE "THE RECORD WRITTEN" TO B-SCREEN-TITLE.

           WRITE FILE-B-RECORD
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.
           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY NOT = "16" THEN GO TO WRITE-FILE-B.
       WRITE-FILE-B-EXIT.
           EXIT.

       WRITE-FILE-B-FROM.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER ALT RECORD TO WRITE" TO B-SCREEN-TITLE.
           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY = "16" THEN GO TO WRITE-FILE-B-FROM-EXIT.

           MOVE "THE RECORD WRITTEN" TO B-SCREEN-TITLE.
           WRITE FILE-B-RECORD FROM FILE-B-RECORD-ALT
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY NOT = "16" THEN GO TO WRITE-FILE-B-FROM.
       WRITE-FILE-B-FROM-EXIT.
           EXIT.

       START-FILE-B.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER A KEY TO START ON" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           MOVE "THE START IS DONE" TO B-SCREEN-TITLE.

           START FILE-B
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       START-FILE-B-ALTKEY.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER A FILE-B-ALTKEY-1 TO START ON" 
                 TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           MOVE "THE START IS DONE" TO B-SCREEN-TITLE.

           START FILE-B KEY FILE-B-ALTKEY-1 EQUAL FILE-B-ALTKEY-1
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       DO-FREE-ALL.
           FREE ALL.
	   DISPLAY "FREE ALL HAS BEEN DONE".

       READ-FILE-B.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER FILE-B-KEY FOR READ" TO B-SCREEN-TITLE.
           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY = "16" THEN GO TO READ-FILE-B-EXIT.

           MOVE "THE RECORD READ" TO B-SCREEN-TITLE.
           READ FILE-B
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY NOT = "16" THEN GO TO READ-FILE-B.
       READ-FILE-B-EXIT.
           EXIT.

       READ-FILE-B-INTO.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "THE RECORD READ" TO B-SCREEN-TITLE.
           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.

           READ FILE-B NEXT RECORD INTO FILE-B-RECORD-ALT
               AT END
               MOVE "*** AT END ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY NOT = "16" THEN GO TO READ-FILE-B-INTO.

       READ-FILE-B-ALT-1.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER FILE-B-ALTKEY-1 FOR READ" TO B-SCREEN-TITLE.
           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY = "16" THEN GO TO READ-FILE-B-ALT-1-EXIT.

           MOVE "THE RECORD READ" TO B-SCREEN-TITLE.
           READ FILE-B KEY IS FILE-B-ALTKEY-1
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY NOT = "16" THEN GO TO READ-FILE-B-ALT-1.
       READ-FILE-B-ALT-1-EXIT.
           EXIT.

       READ-FILE-B-ALT-2.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER FILE-B-ALTKEY-2 FOR READ" TO B-SCREEN-TITLE.
           MOVE "(ENTER) to continue, (16) to EXIT" 
		to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY = "16" THEN GO TO READ-FILE-B-ALT-2-EXIT.

           MOVE "THE RECORD READ" TO B-SCREEN-TITLE.
           READ FILE-B KEY IS FILE-B-ALTKEY-2
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT PFKEY 16.
           IF PF-KEY NOT = "16" THEN GO TO READ-FILE-B-ALT-2.
       READ-FILE-B-ALT-2-EXIT.
           EXIT.

       READ-FILE-B-HOLD.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER FILE-B-KEY FOR READ & HOLD" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           MOVE "READ & HELD" TO B-SCREEN-TITLE.
           READ FILE-B WITH HOLD
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       READ-FILE-B-TIMEOUT.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "READ WITH HOLD TIMEOUT 5 SECS"
                TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           MOVE "READ & HELD" TO B-SCREEN-TITLE.
           READ FILE-B WITH HOLD
               TIMEOUT 5 SECONDS
               MOVE "*** TIMEOUT ***" TO B-SCREEN-TITLE
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       READ-FILE-B-HOLD-REWRITE.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER FILE-B-KEY FOR READ & REWRITE" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           MOVE "READ & HELD; MODIFY FOR REWRITE" TO B-SCREEN-TITLE.
           READ FILE-B WITH HOLD
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           REWRITE FILE-B-RECORD.
           MOVE "RECORD HAS BEEN REWRITTEN" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

       READ-FILE-B-HOLD-DELETE.
           MOVE SPACES TO FILE-B-RECORD.
           MOVE SPACES TO FILE-B-RECORD-ALT.
           MOVE "ENTER FILE-B-KEY FOR READ & DELETE" TO B-SCREEN-TITLE.
           MOVE "Press (ENTER) to continue." to B-SCREEN-FOOTER.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           MOVE "READ & HELD; WILL DELETE" TO B-SCREEN-TITLE.
           READ FILE-B WITH HOLD
               INVALID KEY
               MOVE "*** INVALID KEY ***" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.

           DELETE FILE-B  RECORD.

           MOVE "RECORD HAS BEEN DELETED" TO B-SCREEN-TITLE.

           DISPLAY AND READ FILE-B-SCREEN ON CRT.


       OPEN-EXTEND-SEQ.
           OPEN EXTEND SEQUENTIAL-FILE.
	   DISPLAY "SEQUENTIAL-FILE OPENED EXTEND".

       OPEN-SHARED-SEQ.
           OPEN SHARED SEQUENTIAL-FILE.
	   DISPLAY "SEQUENTIAL-FILE OPENED SHARED".

       OPEN-OUTPUT-SEQ.
           OPEN OUTPUT SEQUENTIAL-FILE.
	   DISPLAY "SEQUENTIAL-FILE OPENED OUTPUT".

       CLOSE-SEQ.
           CLOSE SEQUENTIAL-FILE.
	   DISPLAY "SEQUENTIAL-FILE CLOSED".

       WRITE-EXTEND-SEQ.
           MOVE SPACES TO SEQ-RECORD.
           MOVE "ENTER A RECORD TO WRITE" TO SEQ-SCREEN-TITLE.

           DISPLAY AND READ SEQ-SCREEN ON CRT.

           MOVE "THE RECORD WRITTEN" TO SEQ-SCREEN-TITLE.
           WRITE SEQ-RECORD.

           DISPLAY AND READ SEQ-SCREEN ON CRT.


      ******************************************************************
       SORT-B-FILE-PROC.

       	   DISPLAY "BEFORE SORT SORT-B-FILE".
       
       	   SORT SORT-B-FILE
	       ON DESCENDING KEY SORT-B-KEY
	       USING FILE-B
	       GIVING FILE-B-SEQ.

	   DISPLAY "Check the file BFILESEQ for descending keys".

      

      ******************************************************************

      ******************************************************************


      ******************************************************************

      ******************************************************************
      * ALL DONE.
      ******************************************************************
       EXIT-PROGRAM.
           EXIT PROGRAM.

       STOP-RUN.
           STOP RUN.
