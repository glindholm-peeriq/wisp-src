000100**   Copyright (c) 1997-2002 NeoMedia Technologies, All rights reserved.
000200**   $Id:$
000300**
000400**   File:       WACUDISPLAY.cob
000500**
000600**   Project:    WISP for Acucobol with Native Screens
000700**
000800**   Purpose:    Support the DISPLAY statement.
000900**
001000**   Build:      ccbl -Da4 -o WACUDISPLAY.acu WACUDISPLAY.cob
001100**
001200**
001300
001400 IDENTIFICATION DIVISION.
001500 PROGRAM-ID.
001600     WACUDISPLAY.
001700
001800 ENVIRONMENT DIVISION.
001900 CONFIGURATION SECTION.
002000 SPECIAL-NAMES.
002100     CURSOR         IS WISP-CURSOR
002200     CRT STATUS     IS WISP-CRT-STATUS
002300     SCREEN CONTROL IS WISP-SCREEN-CONTROL.
002400
002500
002600 DATA DIVISION.
002700
002800 WORKING-STORAGE SECTION.
002900
003000*****************************************************************
003100*    Native Screens fields
003200*****************************************************************
003300
003400*    Special-names CURSOR clause.
003500 01  WISP-CURSOR.
003600     05  WISP-CURSOR-LINE    PIC 999 VALUE 1.
003700     05  WISP-CURSOR-COL     PIC 999 VALUE 1.
003800
003900*    Special-names CRT STATUS clause.
004000 01  WISP-CRT-STATUS.
004100     05  WISP-CRT-KEY-1               PIC X.
004200         88  WISP-CRT-STATUS-TERMINATED  VALUE '0'.
004300         88  WISP-CRT-STATUS-EXCEPTION   VALUE '1'.
004400         88  WISP-CRT-STATUS-EOF         VALUE '2'.
004500         88  WISP-CRT-STATUS-TIMEOUT     VALUE '3'.
004600         88  WISP-CRT-STATUS-NOITEM      VALUE '9'.
004700     05  WISP-CRT-KEY-EXCEPTION       PIC X COMP-X.
004800         88  WISP-CRT-EX-HELP            VALUE 33.
004900         88  WISP-CRT-EX-GETKEY          VALUE 34.
005000         88  WISP-CRT-KEY-AUTOSKIP       VALUE 49.
005100     05  WISP-CRT-KEY-3               PIC X COMP-X.
005200         88  WISP-CRT-KEY-TAB            VALUE  9.
005300         88  WISP-CRT-KEY-ENTER          VALUE 13.
005400
005500*    Special-names SCREEN CONTROL clause.
005600 01  WISP-SCREEN-CONTROL.
005700     05  WISP-ACCEPT-CONTROL     PIC 9   VALUE 0.
005800     05  WISP-CONTROL-VALUE      PIC 999 VALUE 1.
005900     05  WISP-CONTROL-HANDLE     USAGE HANDLE.
006000     05  WISP-CONTROL-ID         PIC XX COMP-X.
006100
006200
006300 LINKAGE SECTION.
006400 01  WISP-APPLICATION-NAME   PIC X(8).
006500 01  WISP-DISPLAY-FIELDS-DATA PIC X(1185).
006600 01  FILLER REDEFINES WISP-DISPLAY-FIELDS-DATA.
006700     05  WISP-DISPLAY-FIELDS OCCURS 15 PIC X(79).
006800
006900
007000 SCREEN SECTION.
007100
007200*    Native translation of DISPLAY verb.
007300 01  WISP-DISPLAY-SCREEN.
007400     05  LINE 1 COL 2 VALUE "DISPLAY FROM PROGRAM ".
007500     05  LINE 1 COL 24 FROM WISP-APPLICATION-NAME.
007600     05  LINE 7.
007700         10  COL 2 LINE PLUS 1 PIC X(79) OCCURS 15
007800             FROM WISP-DISPLAY-FIELDS.
007900     05  COL 2 LINE 24 VALUE "PRESS (ENTER) TO CONTINUE PROGRAM.".
008000
008100
008200 PROCEDURE DIVISION USING WISP-APPLICATION-NAME
008300                          WISP-DISPLAY-FIELDS-DATA.
008400
008500 WISP-DISPLAY-PARA.
008600     PERFORM WITH TEST AFTER UNTIL
008700         (WISP-CRT-STATUS-TERMINATED AND WISP-CRT-KEY-ENTER)
008800         DISPLAY WINDOW ERASE
008900         DISPLAY WISP-DISPLAY-SCREEN
009000         ACCEPT OMITTED LINE 1 COL 1
009100             EXCEPTION CONTINUE
009200         END-ACCEPT
009300         IF WISP-CRT-STATUS-EXCEPTION
009400             AND WISP-CRT-EX-HELP THEN
009500             CALL "WACUHELP"
009600         END-IF
009700     END-PERFORM.
009800
009900
010000 9999-EXIT.
010100     EXIT PROGRAM.
010200
010300 9999-STOP.
010400     STOP RUN.
