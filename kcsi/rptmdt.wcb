000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTMDT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/20/89.
000500 DATE-COMPILED.
000600*----------------------------------------------------------------
000700* ALLOWS DATA FILES TO BE CHANGED
000800*----------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptmdt.wcb 1.5 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
000000     COPY LKVALNAM.
002700
002800 01  MODIFY-INPUT-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(48) ROW 1 COLUMN 2 VALUE
003100         "                              MODIFY INPUT FILES".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 2 COLUMN 3
003400         SOURCE UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(74) ROW 3 COLUMN 2 VALUE
003700         "     Warning  --  When changing these files you must be
003800-        "sure that they are".
003900     05  FILLER
004000         PIC X(47) ROW 4 COLUMN 2 VALUE
004100         "valid for the Report Definition being modified.".
004200     05  FILLER
004300         PIC X(46) ROW 7 COLUMN 2 VALUE
004400         "                                 Primary Files".
004500     05  FILLER
004600         PIC X(25) ROW 9 COLUMN 2 VALUE
004700         "    Data File:    File = ".
004800     05  FILE-1-ITEM
004900         PIC X(8) ROW 9 COLUMN 28
005000         SOURCE FILE-1-FIELD
005100         OBJECT FILE-1-FIELD.
005200     05  FILLER
005300         PIC X(10) ROW 9 COLUMN 39 VALUE
005400         "Library = ".
005500     05  LIB-1-ITEM
005600         PIC X(8) ROW 9 COLUMN 50
005700         SOURCE LIB-1-FIELD
005800         OBJECT LIB-1-FIELD.
005900     05  FILLER
006000         PIC X(9) ROW 9 COLUMN 61 VALUE
006100         "Volume = ".
006200     05  VOL-1-ITEM
006300         PIC X(6) ROW 9 COLUMN 71
006400         SOURCE VOL-1-FIELD
006500         OBJECT VOL-1-FIELD.
006600     05  FILLER
006700         PIC X(25) ROW 10 COLUMN 2 VALUE
006800         " Control File:    File = ".
006900     05  FILE-2-ITEM
007000         PIC X(8) ROW 10 COLUMN 28
007100         SOURCE FILE-2-FIELD
007200         OBJECT FILE-2-FIELD.
007300     05  FILLER
007400         PIC X(10) ROW 10 COLUMN 39 VALUE
007500         "Library = ".
007600     05  LIB-2-ITEM
007700         PIC X(8) ROW 10 COLUMN 50
007800         SOURCE LIB-2-FIELD
007900         OBJECT LIB-2-FIELD.
008000     05  FILLER
008100         PIC X(9) ROW 10 COLUMN 61 VALUE
008200         "Volume = ".
008300     05  VOL-2-ITEM
008400         PIC X(6) ROW 10 COLUMN 71
008500         SOURCE VOL-2-FIELD
008600         OBJECT VOL-2-FIELD.
008700     05  FILLER
008800         PIC X(48) ROW 13 COLUMN 2 VALUE
008900         "                                 Secondary Files".
009000     05  FILLER
009100         PIC X(25) ROW 15 COLUMN 2 VALUE
009200         "    Data File:    File = ".
009300     05  FILE-3-ITEM
009400         PIC X(8) ROW 15 COLUMN 28
009500         SOURCE FILE-3-FIELD
009600         OBJECT FILE-3-FIELD.
009700     05  FILLER
009800         PIC X(10) ROW 15 COLUMN 39 VALUE
009900         "Library = ".
010000     05  LIB-3-ITEM
010100         PIC X(8) ROW 15 COLUMN 50
010200         SOURCE LIB-3-FIELD
010300         OBJECT LIB-3-FIELD.
010400     05  FILLER
010500         PIC X(9) ROW 15 COLUMN 61 VALUE
010600         "Volume = ".
010700     05  VOL-3-ITEM
010800         PIC X(6) ROW 15 COLUMN 71
010900         SOURCE VOL-3-FIELD
011000         OBJECT VOL-3-FIELD.
011100     05  FILLER
011200         PIC X(25) ROW 16 COLUMN 2 VALUE
011300         " Control File:    File = ".
011400     05  FILE-4-ITEM
011500         PIC X(8) ROW 16 COLUMN 28
011600         SOURCE FILE-4-FIELD
011700         OBJECT FILE-4-FIELD.
011800     05  FILLER
011900         PIC X(10) ROW 16 COLUMN 39 VALUE
012000         "Library = ".
012100     05  LIB-4-ITEM
012200         PIC X(8) ROW 16 COLUMN 50
012300         SOURCE LIB-4-FIELD
012400         OBJECT LIB-4-FIELD.
012500     05  FILLER
012600         PIC X(9) ROW 16 COLUMN 61 VALUE
012700         "Volume = ".
012800     05  VOL-4-ITEM
012900         PIC X(6) ROW 16 COLUMN 71
013000         SOURCE VOL-4-FIELD
013100         OBJECT VOL-4-FIELD.
013200     05  FILLER
013300         PIC X(65) ROW 22 COLUMN 2 VALUE
013400         "              ** Press (16) to continue without changing
013500-        " files **".
013600
013700 01  MODIFY-INPUT-TABLE.
013800     05  UPPER-MSG-FIELD               PIC X(78) VALUE SPACE.
013900     05  MODIFY-INPUT-FIELDS.
014000         07  FILE-1-FIELD              PIC X(8) VALUE SPACE.
014100         07  LIB-1-FIELD               PIC X(8) VALUE SPACE.
014200         07  VOL-1-FIELD               PIC X(6) VALUE SPACE.
014300         07  FILE-2-FIELD              PIC X(8) VALUE SPACE.
014400         07  LIB-2-FIELD               PIC X(8) VALUE SPACE.
014500         07  VOL-2-FIELD               PIC X(6) VALUE SPACE.
014600         07  FILE-3-FIELD              PIC X(8) VALUE SPACE.
014700         07  LIB-3-FIELD               PIC X(8) VALUE SPACE.
014800         07  VOL-3-FIELD               PIC X(6) VALUE SPACE.
014900         07  FILE-4-FIELD              PIC X(8) VALUE SPACE.
015000         07  LIB-4-FIELD               PIC X(8) VALUE SPACE.
015100         07  VOL-4-FIELD               PIC X(6) VALUE SPACE.
015200     05  FILLER REDEFINES MODIFY-INPUT-FIELDS.
015300         07  MODIFY-LINE         OCCURS 4 TIMES.
015400             09  FILE-FIELD      PIC X(08).
015500             09  LIB-FIELD       PIC X(08).
015600             09  VOL-FIELD       PIC X(06).
015700
015800 01  IDX                 BINARY VALUE 0.
015900 01  MESSAGE-LITERALS.
016000     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
016100         "* Error * File, Library, Volume names may not be blank".
016200     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
016300         "* Error * Invalid File Name".
016400     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
016500         "* Error * Invalid Library Name".
016600     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
016700         "* Error * Invalid Volume Name".
000000     05  MUST-BE-BLANK-LITERAL      PIC X(78) VALUE
000000         "* Error * Secondary File Cannot be Added".
016800
020600 LINKAGE SECTION.
020700     COPY WSRPTCFL.
020800     COPY WSRPTREC.
020900
021000 PROCEDURE DIVISION USING
021100     RPT-CTL-FIELDS
021200     RPT-RECORDS.
021300
021400 MAIN-LOGIC SECTION.
021500 PROGRAM-BEGIN.
000000     MOVE "RPTMDT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
021600     PERFORM OPENING-PROCEDURE.
021700     PERFORM MAIN-PROCESS.
021800     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTMDT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
021900 MAIN-LOGIC-EXIT.
022000     EXIT PROGRAM.
022100 MAIN-LOGIC-STOP.
022200     STOP RUN.
022300 LEVEL-2 SECTION.
022400 OPENING-PROCEDURE.
022500     OPEN I-O  CRT-FILE.
022600 CLOSING-PROCEDURE.
022700     CLOSE CRT-FILE.
022800
022900 MAIN-PROCESS.
023000       PERFORM INIT-THE-SCREEN.
023100       PERFORM DISPLAY-THE-SCREEN.
023200
023300 INIT-THE-SCREEN.
023400     MOVE UPPER-ENTRY-FAC TO
023500         FAC OF FILE-1-ITEM
023600         FAC OF LIB-1-ITEM
023700         FAC OF VOL-1-ITEM
023800         FAC OF FILE-2-ITEM
023900         FAC OF LIB-2-ITEM
024000         FAC OF VOL-2-ITEM
024100         FAC OF FILE-3-ITEM
024200         FAC OF LIB-3-ITEM
024300         FAC OF VOL-3-ITEM
024400         FAC OF FILE-4-ITEM
024500         FAC OF LIB-4-ITEM
024600         FAC OF VOL-4-ITEM.
024700
024800     MOVE DISPLAY-HI-FAC TO FAC OF UPPER-MSG-ITEM.
024900     MOVE RDF-PRIM-FILE TO FILE-1-FIELD.
025000     MOVE RDF-PRIM-LIBRARY TO LIB-1-FIELD.
025100     MOVE RDF-PRIM-VOLUME TO VOL-1-FIELD.
025200     MOVE RCF-PRIM-FILE TO FILE-2-FIELD.
025300     MOVE RCF-PRIM-LIBRARY TO LIB-2-FIELD.
025400     MOVE RCF-PRIM-VOLUME TO VOL-2-FIELD.
025500     MOVE RDF-SEC-FILE TO FILE-3-FIELD.
025600     MOVE RDF-SEC-LIBRARY TO LIB-3-FIELD.
025700     MOVE RDF-SEC-VOLUME TO VOL-3-FIELD.
025800     MOVE RCF-SEC-FILE TO FILE-4-FIELD.
025900     MOVE RCF-SEC-LIBRARY TO LIB-4-FIELD.
026000     MOVE RCF-SEC-VOLUME TO VOL-4-FIELD.
026100
026200 RECORD-THE-DATA.
026300     MOVE FILE-1-FIELD TO RDF-PRIM-FILE.
026400     MOVE LIB-1-FIELD TO RDF-PRIM-LIBRARY.
026500     MOVE VOL-1-FIELD TO RDF-PRIM-VOLUME.
026600     MOVE FILE-2-FIELD TO RCF-PRIM-FILE.
026700     MOVE LIB-2-FIELD TO RCF-PRIM-LIBRARY.
026800     MOVE VOL-2-FIELD TO RCF-PRIM-VOLUME.
026900     MOVE FILE-3-FIELD TO RDF-SEC-FILE.
027000     MOVE LIB-3-FIELD TO RDF-SEC-LIBRARY.
027100     MOVE VOL-3-FIELD TO RDF-SEC-VOLUME.
027200     MOVE FILE-4-FIELD TO RCF-SEC-FILE.
027300     MOVE LIB-4-FIELD TO RCF-SEC-LIBRARY.
027400     MOVE VOL-4-FIELD TO RCF-SEC-VOLUME.
027500
027600 DISPLAY-THE-SCREEN.
027700     MOVE SPACES TO SCREEN-ERROR-STATUS.
027800
027900     DISPLAY AND READ ALTERED MODIFY-INPUT-SCREEN ON CRT-FILE
028000     PFKEY 16.
028100
028200     MOVE SPACE TO UPPER-MSG-FIELD.
028300
028400     IF NOT PFKEY-16-PRESSED
028500         PERFORM VAL-THE-ENTRY
028600         IF SCREEN-ENTRY-ERROR
028700             GO TO DISPLAY-THE-SCREEN
028800         ELSE
028900             PERFORM RECORD-THE-DATA.
029000
029100 VAL-THE-ENTRY.
029200     PERFORM VAL-THE-LINE
029300         VARYING IDX FROM 1 BY 1
029400         UNTIL SCREEN-ENTRY-ERROR OR
029500               IDX > 4.
029600
029700 VAL-THE-LINE.
029800     PERFORM VAL-FILE-ID.
029900     IF NOT SCREEN-ENTRY-ERROR
030000         PERFORM VAL-LIB
030100         IF NOT SCREEN-ENTRY-ERROR
030200             PERFORM VAL-VOL.
030300
030400     IF SCREEN-ENTRY-ERROR
030500         IF IDX = 1
030600             SET ERROR-BIT IN FAC OF FILE-1-ITEM ON
030700             SET ERROR-BIT IN FAC OF LIB-1-ITEM ON
030800             SET ERROR-BIT IN FAC OF VOL-1-ITEM ON
030900         ELSE
031000         IF IDX = 2
031100             SET ERROR-BIT IN FAC OF FILE-2-ITEM ON
031200             SET ERROR-BIT IN FAC OF LIB-2-ITEM ON
031300             SET ERROR-BIT IN FAC OF VOL-2-ITEM ON
031400         ELSE
031500         IF IDX = 3
031600             SET ERROR-BIT IN FAC OF FILE-3-ITEM ON
031700             SET ERROR-BIT IN FAC OF LIB-3-ITEM ON
031800             SET ERROR-BIT IN FAC OF VOL-3-ITEM ON
031900         ELSE
032000         IF IDX = 4
032100             SET ERROR-BIT IN FAC OF FILE-4-ITEM ON
032200             SET ERROR-BIT IN FAC OF LIB-4-ITEM ON
032300             SET ERROR-BIT IN FAC OF VOL-4-ITEM ON.
032400
032500 VAL-FILE-ID.
000000     IF IDX = 3 OR
000000        IDX = 4
000000         PERFORM VAL-SECONDARY-FILE-ID
000000     ELSE
000000         PERFORM VAL-A-FILE-ID.
000000
000000 VAL-A-FILE-ID.
032600     IF FILE-FIELD (IDX) = SPACES
032700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
032800         MOVE FILE-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
032900
033000     IF NOT SCREEN-ENTRY-ERROR
033100         MOVE FILE-FIELD (IDX) TO VAL-NAME
033200         PERFORM TEST-THE-NAME
033300         IF SCREEN-ENTRY-ERROR
033400             MOVE INVALID-FILE-LITERAL TO UPPER-MSG-FIELD.
033500
000000 VAL-SECONDARY-FILE-ID.
000000     IF RCF-SEC-FILE NOT = SPACE
000000         PERFORM VAL-A-FILE-ID
000000     ELSE
000000     IF FILE-FIELD(IDX) NOT = SPACE
000000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000         MOVE MUST-BE-BLANK-LITERAL TO UPPER-MSG-FIELD.
000000
000000 VAL-LIB.
000000     IF IDX = 3 OR
000000        IDX = 4
000000         PERFORM VAL-SECONDARY-LIB
000000     ELSE
000000         PERFORM VAL-A-LIB.
000000
033600 VAL-A-LIB.
033700     IF LIB-FIELD (IDX) = SPACES
033800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
033900         MOVE FILE-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
034000
034100     IF NOT SCREEN-ENTRY-ERROR
034200         MOVE LIB-FIELD (IDX) TO VAL-NAME
034300         PERFORM TEST-THE-NAME
034400         IF SCREEN-ENTRY-ERROR
034500             MOVE INVALID-LIB-LITERAL TO UPPER-MSG-FIELD.
034600
000000 VAL-SECONDARY-LIB.
000000     IF RCF-SEC-FILE NOT = SPACE
000000         PERFORM VAL-A-LIB
000000     ELSE
000000     IF LIB-FIELD(IDX) NOT = SPACE
000000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000         MOVE MUST-BE-BLANK-LITERAL TO UPPER-MSG-FIELD.
000000
000000 VAL-VOL.
000000     IF IDX = 3 OR
000000        IDX = 4
000000         PERFORM VAL-SECONDARY-VOL
000000     ELSE
000000         PERFORM VAL-A-VOL.
000000
034700 VAL-A-VOL.
034800     IF VOL-FIELD (IDX) = SPACES
034900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
035000         MOVE FILE-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
035100
035200     IF NOT SCREEN-ENTRY-ERROR
035300         MOVE VOL-FIELD (IDX) TO VAL-NAME
035400         PERFORM TEST-THE-VOL
035500         IF SCREEN-ENTRY-ERROR
035600             MOVE INVALID-VOL-LITERAL TO UPPER-MSG-FIELD.
035700
000000 VAL-SECONDARY-VOL.
000000     IF RCF-SEC-FILE NOT = SPACE
000000         PERFORM VAL-A-VOL
000000     ELSE
000000     IF VOL-FIELD(IDX) NOT = SPACE
000000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000         MOVE MUST-BE-BLANK-LITERAL TO UPPER-MSG-FIELD.
000000
037400 TEST-THE-NAME.
           CALL "VALNAM" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

037400 TEST-THE-VOL.
           CALL "VALVOL" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

000000     COPY PLKTRACE.
