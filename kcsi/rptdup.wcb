000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTDUP.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/28/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* CHECKS FOR DUPLICATE REPORT DEFINITION FILE AND PROMPTS USER
000800* FOR APPROPRIATE ACTION
000900*---------------------------------------------------------------
001000
001100 ENVIRONMENT DIVISION.
001200 CONFIGURATION SECTION.
001300 FIGURATIVE-CONSTANTS.
001400     COPY FIGS.
001500
001600 INPUT-OUTPUT SECTION.
001700
001800 FILE-CONTROL.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200
002300 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptdup.wcb 1.4 9/11/94".
002400     COPY WSGP.
002500     COPY WSINT.
002600     COPY WSCRTS.
000000     COPY LKVALNAM.
002700
002800 01  CORRUPT-GETPARM-FIELDS.
002900     05  CORRUPT-MSG-ID               PIC X(4) VALUE "0001".
003000     05  CORRUPT-PRNAME               PIC X(8) VALUE "CORRUPT".
003100     05  CORRUPT-MSG-ISSUER           PIC X(6) VALUE "RPTDUP".
003200
003300 01  CORRUPT-MSG-COUNT.
003400     05  FILLER                        BINARY   VALUE ZERO.
003500     05  FILLER                        BINARY   VALUE 2.
003600 01  CORRUPT-MSG-TEXT-1               PIC X(48) VALUE
003700         "A Report Definition File with the same Report ID".
003800
003900 01  CORRUPT-MSG-TEXT-2               PIC X(30) VALUE
004000         "already exists on this volume.".
004100 01  CORRUPT-TEXT-01                  PIC X(58) VALUE
004200         "Use (3) to scratch the existing file and create a new on
004300-        "e.".
004400 01  CORRUPT-TEXT-02                  PIC X(42) VALUE
004500         "Otherwise please specify another file name".
004600 01  CORRUPT-TEXT-03                  PIC X(53) VALUE
004700         "(which will become the Report ID), library or volume.".
004800 01  CORRUPT-FILE-KW              PIC X(8) VALUE "FILE".
004900 01  RPT-FILE-NAME           PIC X(8) VALUE SPACE.
005000 01  CORRUPT-FILE-DEFAULT         PIC X(8) VALUE SPACE.
005100 01  CORRUPT-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
005200 01  RPT-LIBRARY-NAME           PIC X(8) VALUE SPACE.
005300 01  CORRUPT-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
005400 01  CORRUPT-VOLUME-KW              PIC X(8) VALUE "VOLUME".
005500 01  RPT-VOLUME-NAME           PIC X(6) VALUE SPACE.
005600 01  CORRUPT-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
005700 01  CORRUPT-TEXT-04                  PIC X(66) VALUE
005800         "Note:  The Report Definition File contains the attribute
005900-        "s selected".
006000 01  CORRUPT-TEXT-05                  PIC X(67) VALUE
006100         "       for the report.  It is used as input to the modif
006200-        "y and print".
006300 01  CORRUPT-TEXT-06                  PIC X(62) VALUE
006400         "       a report options.  The Report ID becomes its file
006500-        " name.".
006600 01  CORRUPT-PFKEY-MASK.
006700     05  CORRUPT-PFKEY-MASK-16        BINARY VALUE 8192.
006800     05  CORRUPT-PFKEY-MASK-32        BINARY VALUE 0.
006900
010500 01  MESSAGE-LITERALS.
010600     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
010700         "* Error * File Name cannot be blank".
010800     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
010900         "* Error * Invalid File Name".
011000     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
011100         "* Error * Invalid Library Name".
011200     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
011300         "* Error * Invalid Volume Name".
011400
011500 01  SCRATCH-VALUES.
011600     05  SCRATCH-TYPE                    PIC X(01) VALUE "F".
011700     05  SCRATCH-FILE-NAME               PIC X(08) VALUE SPACES.
011800     05  SCRATCH-FILE-LIBRARY            PIC X(08) VALUE SPACES.
011900     05  SCRATCH-FILE-VOLUME             PIC X(06) VALUE SPACES.
012000 01  SCRATCH-RETURN-CODE.
012100     05  SCRATCH-RETURN-CODE-1       BINARY VALUE ZEROES.
012200     05  SCRATCH-RETURN-CODE-2       BINARY VALUE ZEROES.
012300
000000     COPY LKPRC.

012400 LINKAGE SECTION.
012500 01  RPT-IO-BLOCK.
012600     COPY LKDIO.
012700
012800 PROCEDURE DIVISION USING
012900     RPT-IO-BLOCK.
013000 MAIN-LOGIC SECTION.
013100 PROGRAM-BEGIN.
000000     MOVE "RPTDUP BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013200     PERFORM OPENING-PROCEDURE.
013300     IF CALLED-RC = 0
013400         PERFORM MAIN-PROCESS.
013500     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTDUP EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013600 MAIN-LOGIC-EXIT.
013700     EXIT PROGRAM.
013800 MAIN-LOGIC-STOP.
013900     STOP RUN.
014000 LEVEL-2 SECTION.
014100 OPENING-PROCEDURE.
014200     MOVE FILE-NAME OF RPT-IO-BLOCK TO RPT-FILE-NAME.
014300     MOVE FILE-LIBRARY OF RPT-IO-BLOCK TO RPT-LIBRARY-NAME.
014400     MOVE FILE-VOLUME OF RPT-IO-BLOCK TO RPT-VOLUME-NAME.
014500     MOVE 0 TO CALLED-RC.
014600     CALL "KEXISTS" USING
               CALLED-RC
014700         RPT-FILE-NAME
014800         RPT-LIBRARY-NAME
014900         RPT-VOLUME-NAME.
015000
015100 CLOSING-PROCEDURE.
015200     MOVE RPT-FILE-NAME TO FILE-NAME OF RPT-IO-BLOCK.
015300     MOVE RPT-LIBRARY-NAME TO FILE-LIBRARY OF RPT-IO-BLOCK.
015400     MOVE RPT-VOLUME-NAME TO FILE-VOLUME OF RPT-IO-BLOCK.
015500
015600 MAIN-PROCESS.
015700     MOVE SPACE TO SCREEN-ERROR-STATUS.
015800     PERFORM CALL-CORRUPT-GETPARM.
015900     MOVE SPACES TO CORRUPT-MSG-TEXT-1  CORRUPT-MSG-TEXT-2.
016000     PERFORM VAL-FILE-NAME.
016100     IF NOT SCREEN-ENTRY-ERROR
016200         PERFORM VAL-LIB-NAME
016300             IF NOT SCREEN-ENTRY-ERROR
016400                PERFORM VAL-VOL-NAME.
016500
016600     IF NOT SCREEN-ENTRY-ERROR
016700         IF GP-PFKEY-3-PRESSED
016800             MOVE RPT-FILE-NAME TO SCRATCH-FILE-NAME
016900             MOVE RPT-LIBRARY-NAME TO SCRATCH-FILE-LIBRARY
017000             MOVE RPT-VOLUME-NAME TO SCRATCH-FILE-VOLUME
017100             MOVE 0 TO SCRATCH-RETURN-CODE
017200             PERFORM SCRATCH-A-FILE.
017300
017400     IF NOT SCREEN-ENTRY-ERROR
017500         MOVE 0 TO CALLED-RC
017600         CALL "KEXISTS" USING
                   CALLED-RC
017700             RPT-FILE-NAME
017800             RPT-LIBRARY-NAME
017900             RPT-VOLUME-NAME.
018000
018100     IF CALLED-RC = 0
018200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
018300     IF SCREEN-ENTRY-ERROR
018400         GO TO MAIN-PROCESS.
018500
018600 CALL-CORRUPT-GETPARM.
018700     CALL "GETPARM" USING
018800         GP-SPECIFY-TYPE, GP-REQUEST-FORM, CORRUPT-PRNAME,
018900         GP-PFKEY-RECEIVER, CORRUPT-MSG-ID,
019000         CORRUPT-MSG-ISSUER,
019100
019200         CORRUPT-MSG-COUNT,
019300         CORRUPT-MSG-TEXT-1, INT48,
019400         CORRUPT-MSG-TEXT-2, INT30,
019500
019600         GP-TEXT-TYPE, CORRUPT-TEXT-01, INT58,
019700         GP-ABS, INT10, GP-ABS, INT02,
019800
019900         GP-TEXT-TYPE, CORRUPT-TEXT-02, INT42,
020000         GP-ABS, INT11, GP-ABS, INT02,
020100
020200         GP-TEXT-TYPE, CORRUPT-TEXT-03, INT53,
020300         GP-ABS, INT12, GP-ABS, INT02,
020400
020500         GP-KW-TYPE, CORRUPT-FILE-KW,
020600         RPT-FILE-NAME, INT08,
020700         GP-ABS, INT15, GP-ABS, INT07,
020800         GP-UPPERCASE-DATA
020900
021000         GP-KW-TYPE, CORRUPT-LIBRARY-KW,
021100         RPT-LIBRARY-NAME, INT08,
021200         GP-ABS, INT15, GP-ABS, INT32,
021300         GP-UPPERCASE-DATA
021400
021500         GP-KW-TYPE, CORRUPT-VOLUME-KW,
021600         RPT-VOLUME-NAME, INT06,
021700         GP-ABS, INT15, GP-ABS, INT57,
021800         GP-UPPERCASE-DATA
021900
022000         GP-TEXT-TYPE, CORRUPT-TEXT-04, INT66,
022100         GP-ABS, INT19, GP-ABS, INT02,
022200
022300         GP-TEXT-TYPE, CORRUPT-TEXT-05, INT67,
022400         GP-ABS, INT20, GP-ABS, INT02,
022500
022600         GP-TEXT-TYPE, CORRUPT-TEXT-06, INT62,
022700         GP-ABS, INT21, GP-ABS, INT02,
022800         GP-ENABLE-ENTER-KEY
022900         GP-PFKEY-TYPE, CORRUPT-PFKEY-MASK.
023000
023100 VAL-FILE-NAME.
023200     IF RPT-FILE-NAME = SPACES
023300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
023400         MOVE FILE-ID-BLANK-LITERAL TO CORRUPT-MSG-TEXT-1.
023500
023600     IF NOT SCREEN-ENTRY-ERROR
023700         MOVE RPT-FILE-NAME TO VAL-NAME
023800         PERFORM TEST-THE-NAME
023900         IF SCREEN-ENTRY-ERROR
024000             MOVE INVALID-FILE-LITERAL TO CORRUPT-MSG-TEXT-1.
024100
024200 VAL-LIB-NAME.
024300     MOVE RPT-LIBRARY-NAME TO VAL-NAME.
024400     PERFORM TEST-THE-NAME.
024500     IF SCREEN-ENTRY-ERROR
024600         MOVE INVALID-LIB-LITERAL TO CORRUPT-MSG-TEXT-1.
024700
024800 VAL-VOL-NAME.
024900     MOVE RPT-VOLUME-NAME TO VAL-NAME.
025000     PERFORM TEST-THE-VOL.
025100     IF SCREEN-ENTRY-ERROR
025200         MOVE INVALID-VOL-LITERAL TO CORRUPT-MSG-TEXT-1.
025300
037400 TEST-THE-NAME.
           CALL "VALNAM" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

037400 TEST-THE-VOL.
           CALL "VALVOL" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

028800
028900 SCRATCH-ROUTINE.
029000     MOVE ZEROES TO SCRATCH-RETURN-CODE-1
029100                    SCRATCH-RETURN-CODE-2.
029200     CALL "SCRATCH" USING  SCRATCH-TYPE,
029300                         SCRATCH-FILE-NAME
029400                         SCRATCH-FILE-LIBRARY
029500                         SCRATCH-FILE-VOLUME
029600                         SCRATCH-RETURN-CODE.
029700 SCRATCH-A-FILE.
029800     MOVE "F" TO SCRATCH-TYPE.
029900     PERFORM SCRATCH-ROUTINE.
030000
030100
030200
030300
000000     COPY PLKTRACE.
