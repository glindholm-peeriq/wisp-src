000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. DTEMNU.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  12/13/89.
000500 DATE-COMPILED.
000600*-----------------------------------------------------------------
000700* ASKS FOR THE DATENTRY FUNCTION:  ADD  MODIFY  DELETE  MODIFY FAC
000800*-----------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800
001900 DATA DIVISION.
002000 FILE SECTION.
002100
002200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)dtemnu.wcb 1.8 9/11/94".
002300     COPY WSGP.
002400     COPY WSINT.
002500     COPY WSCRTS.
002600
002700 01  FUNCTION-GETPARM-FIELDS.
002800     05  FUNCTION-MSG-ID               PIC X(4) VALUE "0002".
002900     05  FUNCTION-PRNAME               PIC X(8) VALUE "OPTIONS".
003000     05  FUNCTION-MSG-ISSUER           PIC X(6) VALUE "DATENT".
003100
003200 01  FUNCTION-MSG-COUNT.
003300     05  FILLER                        BINARY   VALUE ZERO.
003400     05  FILLER                        BINARY   VALUE 1.
003500 01  FUNCTION-MSG-TEXT-1               PIC X(67) VALUE
003600         "           Data Entry Utility".
003700
003800 01  FUNCTION-TEXT-01.
003900     05  FILLER                        PIC X(18) VALUE
004000         "      Data file:  ".
004100     05  D-FILE                        PIC X(8) VALUE SPACE.
004200     05  FILLER                        PIC X(6) VALUE
004300         "  in  ".
004400     05  D-LIB                         PIC X(8) VALUE SPACE.
004500     05  FILLER                        PIC X(6) VALUE
004600         "  on  ".
004700     05  D-VOL                         PIC X(6) VALUE SPACE.
004800 01  FUNCTION-TEXT-02.
004900     05  FILLER                        PIC X(18) VALUE
005000         "   Control file:  ".
005100     05  C-FILE                        PIC X(8) VALUE SPACE.
005200     05  FILLER                        PIC X(6) VALUE
005300         "  in  ".
005400     05  C-LIB                         PIC X(8) VALUE SPACE.
005500     05  FILLER                        PIC X(6) VALUE
005600         "  on  ".
005700     05  C-VOL                         PIC X(6) VALUE SPACE.
005800 01  FUNCTION-TEXT-03                  PIC X(41) VALUE
005900         "                       DATENTRY FUNCTIONS".
006000 01  FUNCTION-TEXT-04                  PIC X(39) VALUE
006100         "                      (3)   Add records".
006200 01  FUNCTION-TEXT-05                  PIC X(42) VALUE
006300         "                      (4)   Modify records".
006400 01  FUNCTION-TEXT-06                  PIC X(42) VALUE SPACE.
000000 01  DELETE-PICK-TEXT                  PIC X(42) VALUE
006500         "                      (5)   Delete records".
006600 01  FUNCTION-TEXT-07                  PIC X(59) VALUE
006700         "                      (6)   Modify field display attribu
006800-        "tes".
000000 01  FUNCTION-TEXT-07A                 PIC X(42) VALUE
000000         "                      (9)   Run INQUIRY".
006900 01  FUNCTION-TEXT-08                  PIC X(51) VALUE
007000         "                      (16)  Exit to respecify files".
007100 01  FUNCTION-TEXT-09                  PIC X(41) VALUE
007200         "                      (32)  Exit DATENTRY".
000000
000000* 01  NUMERIC-KEY-POS PIC 9(4) VALUE ZEROES.
000000 01  WS-FILEORG       PIC X VALUE SPACE.
007800
007900 LINKAGE SECTION.
008000 01  CTL-HEADER-RECORD.
008100     COPY RCCTRLH.
008200     COPY RCCTRLT1.
008300     COPY RCCTRLT2.
008600 01  MODE-FLAG                  PIC X(01).
008700 01  INDEX-OF-KEY               PIC 9(03).
008800 01  CTRL-IO-BLOCK.
008900     COPY LKDIO.
009000 01  DATA-IO-BLOCK.
009100     COPY LKDIO.
009200
009300 PROCEDURE DIVISION USING
009400     CTL-HEADER-RECORD
009500     CF-T1-TABLE
009600     CF-T2-TABLE
009900     MODE-FLAG
010000     INDEX-OF-KEY
010100     CTRL-IO-BLOCK
010200     DATA-IO-BLOCK.
010300 MAIN-LOGIC SECTION.
010400 PROGRAM-BEGIN.
000000     MOVE "DTEMNU BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
010500     PERFORM OPENING-PROCEDURE.
010600     PERFORM MAIN-PROCESS.
010700     PERFORM CLOSING-PROCEDURE.
000000     MOVE "DTEMNU EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
010800 MAIN-LOGIC-EXIT.
010900     EXIT PROGRAM.
011000 MAIN-LOGIC-STOP.
011100     STOP RUN.
011200 LEVEL-2 SECTION.
011300 OPENING-PROCEDURE.
011400     MOVE FILE-NAME    OF CTRL-IO-BLOCK TO C-FILE.
011500     MOVE FILE-LIBRARY OF CTRL-IO-BLOCK TO C-LIB.
011600     MOVE FILE-VOLUME  OF CTRL-IO-BLOCK TO C-VOL.
011700     MOVE FILE-NAME    OF DATA-IO-BLOCK TO D-FILE.
011800     MOVE FILE-LIBRARY OF DATA-IO-BLOCK TO D-LIB.
011900     MOVE FILE-VOLUME  OF DATA-IO-BLOCK TO D-VOL.
000000     MOVE FILE-ORG     OF DATA-IO-BLOCK TO WS-FILEORG.
000000*     MOVE FILE-KEY-POS OF DATA-IO-BLOCK TO
000000*          NUMERIC-KEY-POS.
000000     IF WS-FILEORG = "C"
000000          MOVE SPACE TO FUNCTION-TEXT-06
000000     ELSE
000000          MOVE DELETE-PICK-TEXT TO FUNCTION-TEXT-06.
012000 CLOSING-PROCEDURE.
012100     IF GP-END-KEY-PRESSED
012200         MOVE "E" TO MODE-FLAG
012300     ELSE
012400     IF GP-PFKEY-3-PRESSED
012500         MOVE "A" TO MODE-FLAG
012600     ELSE
012700     IF GP-PFKEY-4-PRESSED
012800         MOVE "B" TO MODE-FLAG
012900     ELSE
013000     IF GP-PFKEY-5-PRESSED
013100         MOVE "C" TO MODE-FLAG
013200     ELSE
013300     IF GP-PFKEY-6-PRESSED
013400         MOVE "D" TO MODE-FLAG
000000     ELSE
000000     IF GP-PFKEY-9-PRESSED
000000         MOVE "I" TO MODE-FLAG
013500     ELSE
013600         MOVE "F" TO MODE-FLAG.
013700
013800 MAIN-PROCESS.
013900     MOVE SPACE TO SCREEN-ERROR-STATUS.
014000     PERFORM CALL-FUNCTION-GETPARM.
014100
014200 CALL-FUNCTION-GETPARM.
014300
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-03-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-04-VAL TO PFKEY-MASK-16.
000000     IF WS-FILEORG NOT = "C"
000000         ADD GP-PF-05-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-06-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-09-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-16-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-32-VAL TO PFKEY-MASK-32.
015000
015100     CALL "GETPARM" USING
015200         GP-SPECIFY-TYPE, GP-REQUEST-FORM, FUNCTION-PRNAME,
015300         GP-PFKEY-RECEIVER, FUNCTION-MSG-ID,
015400         FUNCTION-MSG-ISSUER,
015500
015600         FUNCTION-MSG-COUNT,
015700         FUNCTION-MSG-TEXT-1, INT67,
015800
015900         GP-TEXT-TYPE, FUNCTION-TEXT-01, INT52,
016000         GP-ABS, INT09, GP-ABS, INT02,
016100
016200         GP-TEXT-TYPE, FUNCTION-TEXT-02, INT52,
016300         GP-ABS, INT10, GP-ABS, INT02,
016400
016500         GP-TEXT-TYPE, FUNCTION-TEXT-03, INT41,
016600         GP-ABS, INT12, GP-ABS, INT02,
016700
016800         GP-TEXT-TYPE, FUNCTION-TEXT-04, INT39,
016900         GP-ABS, INT13, GP-ABS, INT02,
017000
017100         GP-TEXT-TYPE, FUNCTION-TEXT-05, INT42,
017200         GP-ABS, INT14, GP-ABS, INT02,
017300
017400         GP-TEXT-TYPE, FUNCTION-TEXT-06, INT42,
017500         GP-ABS, INT15, GP-ABS, INT02,
017600
017700         GP-TEXT-TYPE, FUNCTION-TEXT-07, INT59,
017800         GP-ABS, INT16, GP-ABS, INT02,
017900
017700         GP-TEXT-TYPE, FUNCTION-TEXT-07A, INT42,
017800         GP-ABS, INT17, GP-ABS, INT02,
017900
018000         GP-TEXT-TYPE, FUNCTION-TEXT-08, INT51,
018100         GP-ABS, INT19, GP-ABS, INT02,
018200
018300         GP-TEXT-TYPE, FUNCTION-TEXT-09, INT41,
018400         GP-ABS, INT20, GP-ABS, INT02,
018500         GP-DISABLE-ENTER-KEY
018600         GP-PFKEY-TYPE, PFKEY-MASK.
018700
018800     IF NOT GP-PFKEY-3-PRESSED  AND
018900        NOT GP-PFKEY-4-PRESSED  AND
019000        NOT GP-PFKEY-5-PRESSED  AND
019100        NOT GP-PFKEY-6-PRESSED  AND
019100        NOT GP-PFKEY-9-PRESSED  AND
019200        NOT GP-PFKEY-16-PRESSED AND
019300        NOT GP-PFKEY-32-PRESSED
019400         GO TO CALL-FUNCTION-GETPARM.
019500
000000     COPY PLKTRACE.
