000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTWOP.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  11/04/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* Asks for the file library and volume.
000800*---------------------------------------------------------------
000800*---------------------------------------------------------------
000000* Mods:
000000* Jan 23, 1993
000000*  A 255 byte parameter limit caused this to fail in
000000*         VAX COBOL which does not yet use GETPARMBUILD. So
000000*         each field was formmatted as a single text field with
000000*         Spaces in the middle of the field, and the Keyword
000000*         was then defined to lay over the blank portion
000000*         of the field.
000000* Feb 24, 1992
000000*         Replaced RPT-PROGRESS with RPT-EXT-SORT and put it
000000*         back on the screen.
000000* 06/29/91 Removed RPT-PROGRESS from the GETPARM Screen, and
000000*         forced it to "NO" (see VAL-PROGRESS).
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800
001900 DATA DIVISION.
002000 FILE SECTION.
002100
002200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptwop.wcb 1.8 9/11/94".
002300     COPY WSGP.
002400     COPY WSINT.
002500     COPY WSCRTS.
002600 01  OPTIONS-GETPARM-FIELDS.
002700     05  OPTIONS-MSG-ID               PIC X(4) VALUE "0002".
002800     05  OPTIONS-PRNAME               PIC X(8) VALUE "OPTIONS".
002900     05  OPTIONS-MSG-ISSUER           PIC X(6) VALUE "REPORT".
003000
003100 01  OPTIONS-MSG-COUNT.
003200     05  FILLER                        BINARY   VALUE ZERO.
003300     05  FILLER                        BINARY   VALUE 1.
003400 01  OPTIONS-ERROR-TEXT               PIC X(79) VALUE SPACE.
003500 01  OPTIONS-TEXT-01                  PIC X(71) VALUE
003600         "This option is used to print a report that has been prev
003700-        "iously defined.".
004000 01  OPTIONS-TEXT-03                  PIC X(66) VALUE
004100     "Report                         (Filename of the Report Defin
000000-    "ition)".
004200 01  OPTIONS-ID-KW              PIC X(8) VALUE "ID".
004500 01  OPTIONS-TEXT-05                  PIC X(63) VALUE
004600     "Report                         (Any date, plus edit characte
000000-    "rs)".
004700 01  OPTIONS-DATE-KW              PIC X(8) VALUE "DATE".
005000 01  OPTIONS-TEXT-07                  PIC X(48) VALUE
005100     "Output                         (PRINTER/DISPLAY)".
005200 01  OPTIONS-DEVICE-KW              PIC X(8) VALUE "DEVICE".
005500 01  OPTIONS-TEXT-09                  PIC X(47) VALUE
005600     "Change Data                         (YES or NO)".
005700 01  OPTIONS-FILES-KW              PIC X(8) VALUE "FILES".
006000 01  OPTIONS-TEXT-11                  PIC X(53) VALUE
006100     "Count                         (YES, NO, or 001 - 999)".
006200 01  OPTIONS-OPTION-KW              PIC X(8) VALUE "OPTION".
006500 01  OPTIONS-TEXT-13                  PIC X(39) VALUE
006600     "Sum                         (YES or NO)".
006700 01  OPTIONS-ONLY-KW              PIC X(8) VALUE "ONLY".
007000 01  OPTIONS-TEXT-15                  PIC X(43) VALUE
007100     "Lines Per                         (05 - 99)".
007200 01  OPTIONS-PAGE-KW              PIC X(8) VALUE "PAGE".
007500 01  OPTIONS-TEXT-17                  PIC X(57) VALUE
007600     "Select                         (Combination of 1 2 and 3)".
007700 01  OPTIONS-LINES-KW              PIC X(8) VALUE "LINES".
008000 01  OPTIONS-TEXT-19                  PIC X(63) VALUE
008100     "Print Line                         (Combination of numbers 0
000000-    "-5)".
008200 01  OPTIONS-SPACING-KW              PIC X(8) VALUE "SPACING".
000000 01  OPTIONS-TEXT-20                 PIC X(56) VALUE
000000     "Use                         (NO = Use Internal Sort)".
000000 01  OPTIONS-EXT-SORT-KW              PIC X(8) VALUE "WISPSORT".
008500 01  OPTIONS-TEXT-21                  PIC X(58) VALUE
008600         "**  Press (14) for an explanation of the Print Options
008700-        "**".
009000 01  OPTIONS-TEXT-23                  PIC X(48) VALUE
009100         "**  Press (ENTER) to Continue or (16) to Exit **".
000000
000000*PF 14 and 16
009200 01  OPTIONS-PFKEY-MASK.
009400     05  OPTIONS-PFKEY-MASK-16  BINARY VALUE 5.
009400     05  OPTIONS-PFKEY-MASK-32  BINARY VALUE 0.
000000
009700 01  NUMERIC-FIELD  PIC 999.
009800 01  FILLER REDEFINES NUMERIC-FIELD.
009900     05  NUMERIC-1   PIC 9.
010000     05  NUMERIC-2   PIC 9.
010100     05  NUMERIC-3   PIC 9.
010200 01  RPT-DIGIT  PIC X.
010300 LINKAGE SECTION.
010400     COPY WSRPTOPT.
010500 01  PASSED-MESSAGE PIC X(79).
000000     COPY LKPRC.
010600* PROCEDURE DIVISION.
010700 PROCEDURE DIVISION USING
           CALLED-RC
010800     RPT-OPTIONS
010900     PASSED-MESSAGE.
011000 MAIN-LOGIC SECTION.
011100 PROGRAM-BEGIN.
000000     MOVE "RPTWOP BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
011200*     MOVE SPACE TO RPT-OPTIONS.
011300     PERFORM OPENING-PROCEDURE.
011400     PERFORM MAIN-PROCESS.
011500     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTWOP EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
011600 MAIN-LOGIC-EXIT.
011700     EXIT PROGRAM.
011800 MAIN-LOGIC-STOP.
011900     STOP RUN.
012000 LEVEL-2 SECTION.
012100 OPENING-PROCEDURE.
012200     MOVE PASSED-MESSAGE TO OPTIONS-ERROR-TEXT.
012300 CLOSING-PROCEDURE.
012400     IF GP-END-KEY-PRESSED
012500         MOVE 16 TO CALLED-RC
012600     ELSE
012700         MOVE ZEROES TO CALLED-RC.
012800 MAIN-PROCESS.
012900     MOVE SPACE TO SCREEN-ERROR-STATUS.
013000     PERFORM CALL-OPTIONS-GETPARM.
013100     MOVE SPACE TO OPTIONS-ERROR-TEXT.
013200     IF GP-PFKEY-14-PRESSED
013300         CALL "RPTPHLP"
000000         GO TO MAIN-PROCESS
013400     ELSE
013500     IF GP-ENTER-KEY-PRESSED
013600         PERFORM VAL-OPTIONS
013700         IF SCREEN-ENTRY-ERROR
013800             GO TO MAIN-PROCESS.
013900
014000* ALL FIELDS ARE VALIDATED HERE EXCEPT THE ACTUAL FILES.
014100 VAL-OPTIONS.
014200     PERFORM VAL-ID.
014300     IF NOT SCREEN-ENTRY-ERROR
014400         PERFORM VAL-DATE.
014500     IF NOT SCREEN-ENTRY-ERROR
014600         PERFORM VAL-DEVICE.
014700     IF NOT SCREEN-ENTRY-ERROR
014800         PERFORM VAL-FILES.
014900     IF NOT SCREEN-ENTRY-ERROR
015000         PERFORM VAL-OPTION.
015100     IF NOT SCREEN-ENTRY-ERROR
015200         PERFORM VAL-ONLY.
015300     IF NOT SCREEN-ENTRY-ERROR
015400         PERFORM VAL-PAGE.
015500     IF NOT SCREEN-ENTRY-ERROR
015600         PERFORM VAL-LINES.
015700     IF NOT SCREEN-ENTRY-ERROR
015800         PERFORM VAL-SPACING.
015700     IF NOT SCREEN-ENTRY-ERROR
015800         PERFORM VAL-EXT-SORT.
015900 VAL-ID.
016000     IF RPT-ID = SPACE
016100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016200         MOVE "Report ID is Required." TO OPTIONS-ERROR-TEXT.
016300 VAL-DATE.
016400 VAL-DEVICE.
016500     IF RPT-DEVICE NOT = "PRINTER" AND "DISPLAY"
016600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016700         MOVE "Device must be PRINTER or DISPLAY"
016800           TO OPTIONS-ERROR-TEXT.
016900 VAL-FILES.
017000     IF RPT-FILES NOT = "YES" AND "NO"
017100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
017200         MOVE "FILES must be YES or NO"
017300           TO OPTIONS-ERROR-TEXT.
017400 VAL-OPTION.
017500     IF RPT-OPTION = "YES" OR "NO"
017600         NEXT SENTENCE
017700     ELSE
017800         PERFORM VAL-NUMERIC-OPTION.
017900 VAL-NUMERIC-OPTION.
018000     MOVE WITH CONVERSION RPT-OPTION TO NUMERIC-FIELD
018100       ON ERROR MOVE ZEROES TO NUMERIC-FIELD.
018200     IF NUMERIC-FIELD = ZEROES
018300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
018400         MOVE "OPTION must be YES or NO or 000 - 999"
018500           TO OPTIONS-ERROR-TEXT
018600     ELSE
018700         MOVE NUMERIC-FIELD TO RPT-OPTION.
018800
018900 VAL-ONLY.
019000     IF RPT-ONLY NOT = "YES" AND "NO"
019100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
019200         MOVE "ONLY must be YES or NO"
019300           TO OPTIONS-ERROR-TEXT.
018900 VAL-EXT-SORT.
000000* FORCE "NO" for external sort
000000*     MOVE "NO " TO RPT-EXT-SORT.
019000     IF RPT-EXT-SORT NOT = "YES" AND "NO"
019100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
019200         MOVE "WISPSORT must be YES or NO"
019300           TO OPTIONS-ERROR-TEXT.
019400 VAL-PAGE.
019500     IF RPT-PAGE < 5
019600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
019700         MOVE "PAGE must be 05 - 99"
019800           TO OPTIONS-ERROR-TEXT.
019900 VAL-LINES.
020000     PERFORM VAL-LINE-1.
020100     IF NOT SCREEN-ENTRY-ERROR
020200         PERFORM VAL-LINE-2.
020300     IF NOT SCREEN-ENTRY-ERROR
020400         PERFORM VAL-LINE-3.
020500 VAL-LINE-1.
020600     MOVE RPT-LINE-1 TO RPT-DIGIT.
020700     PERFORM VAL-A-LINE.
020800 VAL-LINE-2.
020900     MOVE RPT-LINE-2 TO RPT-DIGIT.
021000     PERFORM VAL-A-LINE.
021100 VAL-LINE-3.
021200     MOVE RPT-LINE-3 TO RPT-DIGIT.
021300     PERFORM VAL-A-LINE.
021400 VAL-A-LINE.
021500     IF RPT-DIGIT < "0" OR
021600        RPT-DIGIT > "3"
021700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
021800         MOVE "Each LINE must be 1 - 3"
021900           TO OPTIONS-ERROR-TEXT.
022000
022100 VAL-SPACING.
022200     PERFORM VAL-SPACING-1.
022300     IF NOT SCREEN-ENTRY-ERROR
022400         PERFORM VAL-SPACING-2.
022500     IF NOT SCREEN-ENTRY-ERROR
022600         PERFORM VAL-SPACING-3.
022700 VAL-SPACING-1.
022800     MOVE RPT-SPACING-1 TO RPT-DIGIT.
022900     PERFORM VAL-A-SPACING.
023000 VAL-SPACING-2.
023100     MOVE RPT-SPACING-2 TO RPT-DIGIT.
023200     PERFORM VAL-A-SPACING.
023300 VAL-SPACING-3.
023400     MOVE RPT-SPACING-3 TO RPT-DIGIT.
023500     PERFORM VAL-A-SPACING.
023600 VAL-A-SPACING.
023700     IF RPT-DIGIT < "0" OR
023800        RPT-DIGIT > "5"
023900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
024000         MOVE "Each SPACING must be 1 - 5"
024100           TO OPTIONS-ERROR-TEXT.
024200
024500 CALL-OPTIONS-GETPARM.
024800     CALL "GETPARM" USING
024900         GP-SPECIFY-TYPE, GP-REQUEST-FORM, OPTIONS-PRNAME,
025000         GP-PFKEY-RECEIVER, OPTIONS-MSG-ID,
025100         OPTIONS-MSG-ISSUER,
025200
025300         OPTIONS-MSG-COUNT,
025400         OPTIONS-ERROR-TEXT, INT79,
025500
025600          GP-TEXT-TYPE, OPTIONS-TEXT-01, INT71,
025700          GP-ABS, INT09, GP-ABS, INT06,
025800
026200          GP-TEXT-TYPE, OPTIONS-TEXT-03, INT66,
026300          GP-ABS, INT12, GP-ABS, INT13,
026400
026500         GP-KW-TYPE, OPTIONS-ID-KW,
026600         RPT-ID, INT08,
026700         GP-ABS, INT12, GP-ABS, INT20,
026800         GP-UPPERCASE-DATA
026900
027300          GP-TEXT-TYPE, OPTIONS-TEXT-05, INT63,
027400          GP-ABS, INT13, GP-ABS, INT13,
027500
027600         GP-KW-TYPE, OPTIONS-DATE-KW,
027700         RPT-DATE, INT08,
027800         GP-ABS, INT13, GP-ABS, INT20,
027900         GP-ALPHANUMERIC-DATA
028000
028400          GP-TEXT-TYPE, OPTIONS-TEXT-07, INT48,
028500          GP-ABS, INT14, GP-ABS, INT13,
028600
028700         GP-KW-TYPE, OPTIONS-DEVICE-KW,
028800         RPT-DEVICE, INT07,
028900         GP-ABS, INT14, GP-ABS, INT20,
029000         GP-UPPERCASE-DATA
029100
029500          GP-TEXT-TYPE, OPTIONS-TEXT-09, INT47,
029600          GP-ABS, INT15, GP-ABS, INT08,
029700
029800         GP-KW-TYPE, OPTIONS-FILES-KW,
029900         RPT-FILES, INT03,
030000         GP-ABS, INT15, GP-ABS, INT20,
030100         GP-UPPERCASE-DATA
030200
030600         GP-TEXT-TYPE, OPTIONS-TEXT-11, INT53,
030700         GP-ABS, INT16, GP-ABS, INT14,
030800
030900         GP-KW-TYPE, OPTIONS-OPTION-KW,
031000         RPT-OPTION, INT03,
031100         GP-ABS, INT16, GP-ABS, INT20,
031200         GP-ALPHANUMERIC-DATA
031300
031700         GP-TEXT-TYPE, OPTIONS-TEXT-13, INT39,
031800         GP-ABS, INT17, GP-ABS, INT16,
031900
032000         GP-KW-TYPE, OPTIONS-ONLY-KW,
032100         RPT-ONLY, INT03,
032200         GP-ABS, INT17, GP-ABS, INT20,
032300         GP-UPPERCASE-DATA
032400
032800         GP-TEXT-TYPE, OPTIONS-TEXT-15, INT43,
032900         GP-ABS, INT18, GP-ABS, INT10,
033000
033100         GP-KW-TYPE, OPTIONS-PAGE-KW,
033200         RPT-PAGE, INT02,
033300         GP-ABS, INT18, GP-ABS, INT20,
033400         GP-NUMERIC-DATA
033500
033900         GP-TEXT-TYPE, OPTIONS-TEXT-17, INT57,
034000         GP-ABS, INT19, GP-ABS, INT13,
034100
034200         GP-KW-TYPE, OPTIONS-LINES-KW,
034300         RPT-LINES, INT03,
034400         GP-ABS, INT19, GP-ABS, INT20,
034500         GP-ALPHANUMERIC-DATA
034600
035000         GP-TEXT-TYPE, OPTIONS-TEXT-19, INT63,
035100         GP-ABS, INT20, GP-ABS, INT09,
035200
035300         GP-KW-TYPE, OPTIONS-SPACING-KW,
035400         RPT-SPACING, INT03,
035500         GP-ABS, INT20, GP-ABS, INT20,
035600         GP-ALPHANUMERIC-DATA
000000
000000         GP-TEXT-TYPE, OPTIONS-TEXT-20, INT56,
000000         GP-ABS, INT21, GP-ABS, INT16,
000000
000000         GP-KW-TYPE, OPTIONS-EXT-SORT-KW,
000000         RPT-EXT-SORT, INT03
000000         GP-ABS, INT21, GP-ABS, INT20,
035600         GP-ALPHANUMERIC-DATA
035700
036100         GP-TEXT-TYPE, OPTIONS-TEXT-21, INT58,
036200         GP-ABS, INT23, GP-ABS, INT12,
036300
036700         GP-TEXT-TYPE, OPTIONS-TEXT-23, INT46,
036800         GP-ABS, INT24, GP-ABS, INT17,
036900         GP-ENABLE-ENTER-KEY
037000         GP-PFKEY-TYPE, OPTIONS-PFKEY-MASK.
037100
037200     IF NOT GP-PFKEY-14-PRESSED  AND
037300        NOT GP-PFKEY-16-PRESSED  AND
037400        NOT GP-ENTER-KEY-PRESSED
037500         GO TO CALL-OPTIONS-GETPARM.
037600
000000     COPY PLKTRACE.
