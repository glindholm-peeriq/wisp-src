000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQDAT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/20/90.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE DATA FILE:  FILENAME LIRARY VOLUME
000800*              OPEN MODE AND OPTION
000900*---------------------------------------------------------------
000900*---------------------------------------------------------------
000000* Mods:
000000* July 21, 91 mjb
000000*     Modified to display an error message passed from
000000*     KCSIINQ if any.
000000* July 91 mjb
000000* Changed file etc name validations to call VALNAM & VALVOL
000900*---------------------------------------------------------------
001000
001100 ENVIRONMENT DIVISION.
001200 CONFIGURATION SECTION.
001300 FIGURATIVE-CONSTANTS.
001400     COPY FIGS.
001500
001600 INPUT-OUTPUT SECTION.
001700
001800 FILE-CONTROL.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200
002300 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  TEST-BIN      BINARY VALUE 1.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqdat.wcb 1.10 9/11/94".
002400     COPY WSGP.
002500     COPY WSINT.
002600     COPY WSCRTS.
002700
002800 01  DATA-GETPARM-FIELDS.
002900     05  DATA-MSG-ID               PIC X(4) VALUE "1001".
003000     05  DATA-PRNAME               PIC X(8) VALUE "INPUT".
003100     05  DATA-MSG-ISSUER           PIC X(6) VALUE "INQUIR".
003200
003300 01  DATA-MSG-COUNT.
003400     05  FILLER                        BINARY   VALUE ZERO.
003500     05  FILLER                        BINARY   VALUE 2.
003600 01  DATA-MSG-TEXT-1               PIC X(79) VALUE SPACE.
003800
003900 01  DATA-MSG-TEXT-2               PIC X(52) VALUE
004000     "Allows retrieval of selected portions of data files.".
004200 01  DATA-TEXT-01                  PIC X(65) VALUE
004300         "Please supply the information on the data file to be int
004400-        "errogated".
004500 01  DATA-TEXT-02                  PIC X(30) VALUE
004600         "and press (ENTER) to continue.".
004700 01  DATA-FILE-KW              PIC X(8) VALUE "FILE".
004800 01  DATA-FILE-FIELD           PIC X(8) VALUE SPACE.
004900 01  DATA-FILE-DEFAULT         PIC X(8) VALUE SPACE.
005000 01  DATA-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
005100 01  DATA-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
005200 01  DATA-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
005300 01  DATA-VOLUME-KW              PIC X(8) VALUE "VOLUME".
005400 01  DATA-VOLUME-FIELD           PIC X(6) VALUE SPACE.
005500 01  DATA-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
005600 01  DATA-MODE-KW              PIC X(8) VALUE "MODE".
005700 01  DATA-MODE-FIELD           PIC X(6) VALUE "INPUT ".
005800 01  DATA-MODE-DEFAULT         PIC X(6) VALUE SPACE.
005900 01  DATA-CHANGE-KW              PIC X(8) VALUE "CHANGE".
006000 01  DATA-CHANGE-FIELD           PIC X(3) VALUE "NO ".
006100 01  DATA-CHANGE-DEFAULT         PIC X(3) VALUE SPACE.
006200 01  DATA-TEXT-03                  PIC X(70) VALUE
006300         "       You may DISPLAY the results of your inquiry at th
006400-        "e workstation,".
006500 01  DATA-TEXT-04                  PIC X(60) VALUE
006600         "       or EXTRACT a data file to be used by another prog
006700-        "ram.".
006800 01  DATA-OPTION-KW              PIC X(8) VALUE "OPTION".
006900 01  DATA-OPTION-FIELD           PIC X(7) VALUE "DISPLAY".
007000 01  DATA-OPTION-DEFAULT         PIC X(7) VALUE SPACE.
007100 01  DATA-TEXT-05                  PIC X(38) VALUE
007200         "(ENTER) Continue or  (16) Exit INQUIRY".
000000
000000*PF 16
007300 01  DATA-PFKEY-MASK.
007500     05  DATA-PFKEY-MASK-16  BINARY VALUE 1.
007700     05  DATA-PFKEY-MASK-32  BINARY VALUE 0.
007800
007900 01  MESSAGE-LITERALS.
008000     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
008100         "* Error * File Name cannot be blank".
008200     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
008300         "* Error * Invalid File Name".
008400     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
008500         "* Error * Invalid Library Name".
008600     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
008700         "* Error * Invalid Volume Name".
008800
000000     COPY LKVALNAM.
000000
000000     COPY LKPRC.
000000
012400 LINKAGE SECTION.
000000 01  E-MSG  PIC X(79).
012500 01  DATA-IO-BLOCK.
012600     COPY LKDIO.
012700 01  PASS-CHANGE         PIC X(03).
012800 01  PASS-OPTION         PIC X(07).
012900     COPY WSRPTREC.
013000
013100 PROCEDURE DIVISION USING
000000     E-MSG
013200     DATA-IO-BLOCK
013300     PASS-CHANGE
013400     PASS-OPTION
013500     RPT-RECORDS.
013600 MAIN-LOGIC SECTION.
013700 PROGRAM-BEGIN.
000000     MOVE "INQDAT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013800     PERFORM OPENING-PROCEDURE.
013900     PERFORM MAIN-PROCESS.
014000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQDAT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014100 MAIN-LOGIC-EXIT.
014200     EXIT PROGRAM.
014300 MAIN-LOGIC-STOP.
014400     STOP RUN.
014500 LEVEL-2 SECTION.
014600 OPENING-PROCEDURE.
014700     MOVE RDF-PRIM-FILE TO DATA-FILE-FIELD.
014800     MOVE RDF-PRIM-LIBRARY TO DATA-LIBRARY-FIELD.
014900     MOVE RDF-PRIM-VOLUME TO DATA-VOLUME-FIELD.
015000
015100 CLOSING-PROCEDURE.
015200     IF GP-PFKEY-16-PRESSED
015300         MOVE SPACES TO RDF-PRIM-FILE
015400                        DATA-IO-BLOCK.
015800
015900 MAIN-PROCESS.
000000     PERFORM MAIN-LOOP-INIT.
000000     PERFORM MAIN-LOOP.
000000 MAIN-LOOP-INIT.
016400     MOVE E-MSG TO DATA-MSG-TEXT-2.
000000
000000 MAIN-LOOP.
016000     MOVE SPACE TO SCREEN-ERROR-STATUS.
000000     MOVE ZEROES TO CALLED-RC.
016200
016300     PERFORM CALL-DATA-GETPARM.
016400     MOVE SPACES TO DATA-MSG-TEXT-2.
016500     IF NOT GP-PFKEY-16-PRESSED
016600         PERFORM VAL-FILE-NAME
016700         IF NOT SCREEN-ENTRY-ERROR
016800             PERFORM VAL-LIB-NAME
016900                 IF NOT SCREEN-ENTRY-ERROR
017000                    PERFORM VAL-VOL-NAME
017100                    IF NOT SCREEN-ENTRY-ERROR
017200                         PERFORM VAL-MODE-FLAG
017300                         IF NOT SCREEN-ENTRY-ERROR
017400                             PERFORM VAL-CHANGE-FLAG
017500                             IF NOT SCREEN-ENTRY-ERROR
017600                                 PERFORM VAL-OPTION-FLAG.
017700
017800     IF NOT GP-PFKEY-16-PRESSED
017900         IF NOT SCREEN-ENTRY-ERROR
018000             MOVE 0 TO CALLED-RC
018100             CALL "KEXISTS" USING
000000                 CALLED-RC
018200                 DATA-FILE-FIELD
018300                 DATA-LIBRARY-FIELD
018400                 DATA-VOLUME-FIELD.
018500
018600     IF CALLED-RC NOT = 0
018700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
018800         MOVE "Specified Data File does not exist."
018900             TO DATA-MSG-TEXT-2.
019000
019100     IF NOT SCREEN-ENTRY-ERROR
019200         MOVE DATA-FILE-FIELD TO RDF-PRIM-FILE
019300                                 RCF-PRIM-FILE
019400             FILE-NAME OF DATA-IO-BLOCK
019500         MOVE DATA-LIBRARY-FIELD TO RDF-PRIM-LIBRARY
019600             FILE-LIBRARY OF DATA-IO-BLOCK
019700         MOVE DATA-VOLUME-FIELD TO RDF-PRIM-VOLUME
019800             FILE-VOLUME OF DATA-IO-BLOCK
019900         MOVE DATA-OPTION-FIELD TO PASS-OPTION
020000         MOVE DATA-CHANGE-FIELD TO PASS-CHANGE.
020100
020200     IF SCREEN-ENTRY-ERROR
020300         GO TO MAIN-LOOP.
020400
020500 CALL-DATA-GETPARM.
000000* Load the version number.
000000     CALL "INQVERS" USING
000000         DATA-MSG-TEXT-1
000000         TEST-BIN.
020700
000000     MOVE HEX40 TO GP-PFKEY-RECEIVER.
020800     CALL "GETPARM" USING
020900         GP-SPECIFY-TYPE, GP-REQUEST-FORM, DATA-PRNAME,
021000         GP-PFKEY-RECEIVER, DATA-MSG-ID,
021100         DATA-MSG-ISSUER,
021200
021300         DATA-MSG-COUNT,
021400         DATA-MSG-TEXT-1, INT79,
021500         DATA-MSG-TEXT-2, INT51,
021600
021700         GP-TEXT-TYPE, DATA-TEXT-01, INT65,
021800         GP-ABS, INT11, GP-ABS, INT02,
021900
022000         GP-TEXT-TYPE, DATA-TEXT-02, INT30,
022100         GP-ABS, INT12, GP-ABS, INT02,
022200
022300         GP-KW-TYPE, DATA-FILE-KW,
022400         DATA-FILE-FIELD, INT08,
022500         GP-ABS, INT15, GP-ABS, INT03,
022600         GP-ALPHANUMERIC-DATA
022700
022800         GP-KW-TYPE, DATA-LIBRARY-KW,
022900         DATA-LIBRARY-FIELD, INT08,
023000         GP-ABS, INT15, GP-ABS, INT37,
023100         GP-ALPHANUMERIC-DATA
023200
023300         GP-KW-TYPE, DATA-VOLUME-KW,
023400         DATA-VOLUME-FIELD, INT06,
023500         GP-ABS, INT15, GP-ABS, INT58,
023600         GP-ALPHANUMERIC-DATA
023700
023800         GP-KW-TYPE, DATA-MODE-KW,
023900         DATA-MODE-FIELD, INT06,
024000         GP-ABS, INT16, GP-ABS, INT22,
024100         GP-UPPERCASE-DATA
024200
024300         GP-KW-TYPE, DATA-CHANGE-KW,
024400         DATA-CHANGE-FIELD, INT03,
024500         GP-ABS, INT17, GP-ABS, INT22,
024600         GP-UPPERCASE-DATA
024700
024800         GP-TEXT-TYPE, DATA-TEXT-03, INT70,
024900         GP-ABS, INT19, GP-ABS, INT02,
025000
025100         GP-TEXT-TYPE, DATA-TEXT-04, INT60,
025200         GP-ABS, INT20, GP-ABS, INT02,
025300
025400         GP-KW-TYPE, DATA-OPTION-KW,
025500         DATA-OPTION-FIELD, INT07,
025600         GP-ABS, INT22, GP-ABS, INT22,
025700         GP-UPPERCASE-DATA
025800
025900         GP-TEXT-TYPE, DATA-TEXT-05, INT38,
026000         GP-ABS, INT24, GP-ABS, INT02,
026100         GP-ENABLE-ENTER-KEY
026200         GP-PFKEY-TYPE, DATA-PFKEY-MASK.
000000     IF GP-PFKEY-RECEIVER = LOW-VALUES
000000         MOVE HEX40 TO GP-PFKEY-RECEIVER.
026300
026800 VAL-FILE-NAME.
026900
027000     IF DATA-FILE-FIELD = SPACES
027100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
027200         MOVE FILE-ID-BLANK-LITERAL TO DATA-MSG-TEXT-2.
027300
027400     IF NOT SCREEN-ENTRY-ERROR
027500         MOVE DATA-FILE-FIELD TO VAL-NAME
027600         PERFORM TEST-THE-NAME
027700         IF SCREEN-ENTRY-ERROR
027800             MOVE INVALID-FILE-LITERAL TO DATA-MSG-TEXT-2.
027900
028000 VAL-LIB-NAME.
028100
028200     MOVE DATA-LIBRARY-FIELD TO VAL-NAME.
028300     PERFORM TEST-THE-NAME.
028400     IF SCREEN-ENTRY-ERROR
028500         MOVE INVALID-LIB-LITERAL TO DATA-MSG-TEXT-2.
028600
028700 VAL-VOL-NAME.
028800
028900     MOVE DATA-VOLUME-FIELD TO VAL-NAME.
029000     PERFORM TEST-THE-VOL.
029100     IF SCREEN-ENTRY-ERROR
029200         MOVE INVALID-VOL-LITERAL TO DATA-MSG-TEXT-2.
029300
029400 VAL-MODE-FLAG.
029500     IF DATA-MODE-FIELD NOT = "INPUT "  AND
029600        DATA-MODE-FIELD NOT = "SHARED"
029700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
029800         MOVE "** Error ** Must be 'INPUT' or 'SHARED'"
029900             TO DATA-MSG-TEXT-2.
030000
030100 VAL-CHANGE-FLAG.
030200     IF DATA-CHANGE-FIELD NOT = "NO "  AND
030300        DATA-CHANGE-FIELD NOT = "YES"
030400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
030500         MOVE "** Error ** Must be 'NO' or 'YES'"
030600             TO DATA-MSG-TEXT-2.
030700
030800 VAL-OPTION-FLAG.
030900     IF DATA-OPTION-FIELD NOT = "DISPLAY"  AND
031000        DATA-OPTION-FIELD NOT = "EXTRACT"
031100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
031200         MOVE "** Error ** Must be 'DISPLAY' or 'EXTRACT'"
031300             TO DATA-MSG-TEXT-2.
031400
031500 TEST-THE-NAME.
000000     CALL "VALNAM" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
031500 TEST-THE-VOL.
000000     CALL "VALVOL" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
000000     COPY PLKTRACE.
