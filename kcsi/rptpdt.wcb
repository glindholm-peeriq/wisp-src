000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTPDT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/08/89.
000500 DATE-COMPILED.
000600*--------------------------------------------------------------
000700* DEFINES THE PRIMARY DATA FILE:  NAME  LIBRARY  VOLUME
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptpdt.wcb 1.6 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
000000     COPY LKVALNAM.
002700
002800 01  PRIME-DATA-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(27) ROW 8 COLUMN 27 VALUE
003100         "PRIMARY DATA FILE SELECTION".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 9 COLUMN 03
003400         SOURCE  UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(62) ROW 10 COLUMN 9 VALUE
003700         "Specify the name of the primary file to be used in the r
003800-        "eport.".
003900     05  FILLER
004000         PIC X(67) ROW 11 COLUMN 7 VALUE
004100         "The primary file is the only file on which a sort can be
004200-        " performed,".
004300     05  FILLER
004400         PIC X(66) ROW 12 COLUMN 7 VALUE
004500         "and it must contain the key to chain to any other file u
004600-        "sed in the".
004700     05  FILLER
004800         PIC X(7) ROW 13 COLUMN 37 VALUE
004900         "report.".
005000     05  FILLER
005100         PIC X(14) ROW 16 COLUMN 2 VALUE
005200         "Primary File =".
005300     05  PRIME-DATA-FILE-ITEM
005400         PIC X(8) ROW 16 COLUMN 17
005500         SOURCE PRIME-DATA-FILE-FIELD
005600         OBJECT PRIME-DATA-FILE-FIELD.
005700     05  FILLER
005800         PIC X(12) ROW 16 COLUMN 30 VALUE
005900         "in Library =".
006000     05  PRIME-DATA-LIB-ITEM
006100         PIC X(8) ROW 16 COLUMN 43
006200         SOURCE PRIME-DATA-LIB-FIELD
006300         OBJECT PRIME-DATA-LIB-FIELD.
006400     05  FILLER
006500         PIC X(11) ROW 16 COLUMN 55 VALUE
006600         "on Volume =".
006700     05  PRIME-DATA-VOL-ITEM
006800         PIC X(6) ROW 16 COLUMN 67
006900         SOURCE PRIME-DATA-VOL-FIELD
007000         OBJECT PRIME-DATA-VOL-FIELD.
007100     05  FILLER
007200         PIC X(33) ROW 19 COLUMN 24 VALUE
007300         "**  Press (ENTER) to Continue  **".
007400
007500 01  PRIME-DATA-FIELDS.
007600     05  UPPER-MSG-FIELD                   PIC X(78) VALUE SPACE.
007700     05  PRIME-DATA-FILE-FIELD             PIC X(8) VALUE SPACE.
007800     05  PRIME-DATA-LIB-FIELD              PIC X(8) VALUE SPACE.
007900     05  PRIME-DATA-VOL-FIELD              PIC X(6) VALUE SPACE.
008000
008100 01  MESSAGE-LITERALS.
008200     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
008300         "* Error * File Name cannot be blank".
008400     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
008500         "* Error * Invalid File Name".
008600     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
008700         "* Error * Invalid Library Name".
008800     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
008900         "* Error * Invalid Volume Name".
009000
012600 LINKAGE SECTION.
012700     COPY WSRPTREC.
012800
012900 PROCEDURE DIVISION USING
013000     RPT-RECORDS.
013100
013200 MAIN-LOGIC SECTION.
013300 PROGRAM-BEGIN.
000000     MOVE "RPTPDT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013400     PERFORM OPENING-PROCEDURE.
013500     PERFORM MAIN-PROCESS.
013600     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTPDT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013700 MAIN-LOGIC-EXIT.
013800     EXIT PROGRAM.
013900 MAIN-LOGIC-STOP.
014000     STOP RUN.
014100 LEVEL-2 SECTION.
014200 OPENING-PROCEDURE.
014300     OPEN I-O  CRT-FILE.
014400 CLOSING-PROCEDURE.
014500     CLOSE CRT-FILE.
014600
014700 MAIN-PROCESS.
014800     MOVE UPPER-ENTRY-FAC TO FAC OF PRIME-DATA-FILE-ITEM
014900                             FAC OF PRIME-DATA-LIB-ITEM
015000                             FAC OF PRIME-DATA-VOL-ITEM.
015100     MOVE DISPLAY-HI-FAC  TO FAC OF UPPER-MSG-ITEM.
015200
015300     PERFORM NAME-THE-PRIMARY-FILE.
015400
015500     MOVE PRIME-DATA-FILE-FIELD TO
015600         RDF-PRIM-FILE
015700         RCF-PRIM-FILE.
015800     IF PRIME-DATA-LIB-FIELD NOT = SPACES
015900         MOVE PRIME-DATA-LIB-FIELD TO RDF-PRIM-LIBRARY.
016000     IF PRIME-DATA-VOL-FIELD NOT = SPACES
016100         MOVE PRIME-DATA-VOL-FIELD TO RDF-PRIM-VOLUME.
016200
016300 NAME-THE-PRIMARY-FILE.
016400
016500     MOVE SPACES TO SCREEN-ERROR-STATUS.
016600
016700     DISPLAY AND READ ALTERED PRIME-DATA-SCREEN ON CRT-FILE.
016800
016900     MOVE SPACE TO UPPER-MSG-FIELD.
017000
017100     IF NOT PFKEY-16-PRESSED
017200         PERFORM  VAL-FILE-ID
017300         IF NOT SCREEN-ENTRY-ERROR  AND
017400            PRIME-DATA-LIB-FIELD NOT = SPACES
017500             PERFORM  VAL-LIB-ID
017600             IF NOT SCREEN-ENTRY-ERROR  AND
017700                PRIME-DATA-VOL-FIELD NOT = SPACES
017800                 PERFORM VAL-VOL-ID.
017900
018000     IF NOT PFKEY-16-PRESSED
018100         IF SCREEN-ENTRY-ERROR
018200             GO TO NAME-THE-PRIMARY-FILE.
018300     CALL "BLNKSC".
018400 VAL-FILE-ID.
018500
018600     IF PRIME-DATA-FILE-FIELD = SPACES
018700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
018800         SET ERROR-BIT IN FAC OF PRIME-DATA-FILE-ITEM ON
018900         MOVE FILE-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
019000
019100     IF NOT SCREEN-ENTRY-ERROR
019200         MOVE PRIME-DATA-FILE-FIELD TO VAL-NAME
019300         PERFORM TEST-THE-NAME
019400         IF SCREEN-ENTRY-ERROR
019500             MOVE INVALID-FILE-LITERAL TO UPPER-MSG-FIELD
019600             SET ERROR-BIT IN FAC OF PRIME-DATA-FILE-ITEM ON.
019700
019800 VAL-LIB-ID.
019900
020000     MOVE PRIME-DATA-LIB-FIELD TO VAL-NAME.
020100     PERFORM TEST-THE-NAME.
020200     IF SCREEN-ENTRY-ERROR
020300         MOVE INVALID-LIB-LITERAL TO UPPER-MSG-FIELD
020400         SET ERROR-BIT IN FAC OF PRIME-DATA-LIB-ITEM ON.
020500
020600 VAL-VOL-ID.
020700
020800     MOVE PRIME-DATA-VOL-FIELD TO VAL-NAME.
020900     PERFORM TEST-THE-VOL.
021000     IF SCREEN-ENTRY-ERROR
021100         MOVE INVALID-VOL-LITERAL TO UPPER-MSG-FIELD
021200         SET ERROR-BIT IN FAC OF PRIME-DATA-VOL-ITEM ON.
021300
037400 TEST-THE-NAME.
           CALL "VALNAM" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

037400 TEST-THE-VOL.
           CALL "VALVOL" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

000000*     COPY PLCLRCRT.
000000     COPY PLCRT.


000000     COPY PLKTRACE.
