000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTMAIN.
000300*--------------------------------------------------
000400* Version number is in RPTOPT.WCB
000500* A PROGRAM TO IMITATE THE WANG REPORT UTILITY.
000600* THIS MAIN MODULE ACTS PRIMARILY AS A DISPATCHER
000700* TO SCREEN PROCESSING MODULES THAT ACCEPT REPORT
000800* DEFINITION AND DO VALIDATION.
000000*
000900*--------------------------------------------------
001000*--------------------------------------------------
001100* Mods:
001200* 1.00.10.ul Jan 17 90
001300*   Modified RPTWMN to pass a CALLER identifying
001400*   flag to RPTCALL. Modifield RCAL(RPTCALL) to save
001500*   the flag.
001600*   Modified rbld to call either rpt_write or inq_write
001700*   inq_write is called when the CALLER flag is "S" which
001800*   causes a spin-off file rather than a report to be
001900*   generated. Modified rpt_write (rwrt.c) to always display
002000*   the report file when the CALLER flag is "I" for inquiry.
002100*   SO the flags now are "R" = generate a report for REPORT
002200*                        "I" = generate a report for INQUIRY
002300*                        "S" = generate a spinoff file.
002400*   The R and I flags causes identical behaviour except that
002500*   the I flag forces a display at the end of the print run.
002600*   Added iwrt.c which contains the code inq_write() to generate
002700*   the spinoff code. WHen a spinoff is requested, the file
002800*   lib a vol are passed in the print-file lib and volume names
002900*   All of the above have no effect on the user, but make the
003000*   whole 'C' subsystem from report compatible with inquiry.
000000*
000000*   Must pass a zero in pfkey code when calling RPTOPT
000000*    to enable the full menu.
003100*--------------------------------------------------
003105*--------------------------------------------------
003107*    1-Apr-1993.  HLM.
003110*    Modified to include hidden GETPARM call to
003115*    pick up report name, control file and data
003120*    files from PUTPARMS
003125*--------------------------------------------------
003200 ENVIRONMENT DIVISION.
003300 CONFIGURATION SECTION.
003400 FIGURATIVE-CONSTANTS.
003500     COPY FIGS.
003600 INPUT-OUTPUT SECTION.
003700 FILE-CONTROL.
003800     COPY SLCRT.
003900 DATA DIVISION.
004000 FILE SECTION.
004100     COPY FDCRT.
004200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptmain.wcb 1.10 9/11/94".
004300     COPY RCRPT.
004400
000000 01  REPORT-STYLE             PIC XX.
000000
004500 01  NEXT-START               BINARY VALUE 1.
004600 01  SAVE-SEQ                 PIC 99.
004700 01  MENU-PICK                BINARY VALUE ZEROES.
004800     88  CREATING-REPORT-DEF  VALUE 2.
004900     88  MODIFYING-REPORT-DEF VALUE 3.
005000     88  PRINTING-REPORT      VALUE 4.
005100     88  EXITING-REPORT       VALUE 16.
005200
005300     COPY WSUSAGE.
005400     COPY WSFILEIO.
005500     COPY WSCRT.
005600
005700* RPT-FILE-HEADER FIELDS AND GENERAL WS
005800     COPY WSRPT.
005900
006000 01  FIELDS-FOR-RPTOPT.
006100     05  PASSING-PFKEY-CODE BINARY VALUE 0.
006200     05  PASSING-MSG-TEXT   PIC X(79) VALUE SPACES.
006300
006400 01  FIELDS-FOR-RPTDEF.
006500     05  PASSING-RPT-ID      PIC X(08) VALUE SPACES.
006600     05  PASSING-ADD-DATA    PIC X(03) VALUE SPACES.
006605 
006610 01  FLDS-FOR-PUTPARM-CK.
006615     05  THE-MODE            PIC X(06) VALUE "INPUT".
006620     05  THE-PRNAME          PIC X(08) VALUE SPACES.
006625     05  UPPER-MSG-FIELD     PIC X(79) VALUE SPACES.
006630     05  GETPARM-TYPE        PIC X(02) VALUE "ID".
006633     05  PP-RPT-FIL-BLOCK.
006635         07  PP-RPT-FIL-NAM   PIC X(08) VALUE SPACES.
006640         07  PP-RPT-FIL-LIB   PIC X(08) VALUE SPACES.
000645         07  PP-RPT-FIL-VOL   PIC X(06) VALUE SPACES.
000647     05  PP-CTL-FIL-BLOCK.
006635         07  PP-CTL-FIL-NAM  PIC X(08) VALUE SPACES.
006640         07  PP-CTL-FIL-LIB  PIC X(08) VALUE SPACES.
000645         07  PP-CTL-FIL-VOL  PIC X(06) VALUE SPACES.
000647     05  PP-CTL2-FIL-BLOCK.
006635         07  PP-CTL2-FIL-NAM PIC X(08) VALUE SPACES.
006640         07  PP-CTL2-FIL-LIB PIC X(08) VALUE SPACES.
000645         07  PP-CTL2-FIL-VOL PIC X(06) VALUE SPACES.
000647     05  PP-IN1-FIL-BLOCK.
006635         07  PP-IN1-FIL-NAM  PIC X(08) VALUE SPACES.
006640         07  PP-IN1-FIL-LIB  PIC X(08) VALUE SPACES.
000645         07  PP-IN1-FIL-VOL  PIC X(06) VALUE SPACES.
000647     05  PP-IN2-FIL-BLOCK.
006635         07  PP-IN2-FIL-NAM  PIC X(08) VALUE SPACES.
006640         07  PP-IN2-FIL-LIB  PIC X(08) VALUE SPACES.
000645         07  PP-IN2-FIL-VOL  PIC X(06) VALUE SPACES.
006700
006800 01  LOAD-SWITCH             PIC X(01) VALUE SPACES.
006900 01  RPT-PFKEY               PIC 9(02) VALUE 0.
007000 01  RPT-IO-BLOCK.
007100     COPY LKDIO.
007200
000000 01  TEMP-IO-BLOCK.
000000     COPY LKDIO.
000000
007300     COPY WSRPTOPT.
007400     COPY WSRPTCFL.
007500     COPY WSRPTREC.
007600
000000     COPY LKPRC.
000000
007700 PROCEDURE DIVISION.
007800 MAIN-LOGIC SECTION.
007900 PROGRAM-BEGIN.
000000     MOVE "RPTMAIN BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
008000     PERFORM OPENING-PROCEDURE.
008100     PERFORM PROGRAM-INIT.
008200     PERFORM MAIN-PROCESS.
008300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTMAIN EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
008400 MAIN-LOGIC-EXIT.
008500     EXIT PROGRAM.
008600 MAIN-LOGIC-STOP.
008700     STOP RUN.
008800 THE-OTHER SECTION.
008900 OPENING-PROCEDURE.
009000     PERFORM OPEN-CRT-FILE.
009100 PROGRAM-INIT.
000000     CALL "INIDIO" USING RPT-IO-BLOCK.
000000     MOVE "I" TO FILE-ORG OF RPT-IO-BLOCK.
009200     MOVE SPACES TO
009300         RPT-CTL-FIELDS
009400         RPT-RECORDS.
009500     MOVE SPACES TO RPT-ID.
009600     PERFORM EXTRACT-USAGE-CONSTANTS.
009700     MOVE SPACES TO RCF-PRIM-LIBRARY.
009800     STRING
009900         USER-ID DELIMITED BY SPACE
010000         "CTL" DELIMITED BY SIZE
010100         INTO RCF-PRIM-LIBRARY.
010200     MOVE RCF-PRIM-LIBRARY TO RCF-SEC-LIBRARY.
010300     MOVE SPACES TO FILE-LIBRARY OF RPT-IO-BLOCK.
010400     STRING
010500         USER-ID DELIMITED BY SPACE
010600         "RPT" DELIMITED BY SIZE
010700         INTO FILE-LIBRARY OF RPT-IO-BLOCK.
010800
010900     MOVE INLIB TO RDF-SEC-LIBRARY
011000                   RDF-PRIM-LIBRARY.
011100     MOVE INVOL TO RDF-PRIM-VOLUME
011200                   RDF-SEC-VOLUME
011300                   RCF-PRIM-VOLUME
011400                   RCF-SEC-VOLUME
011500                   FILE-VOLUME OF RPT-IO-BLOCK.
011600
011700 CLOSING-PROCEDURE.
011800     PERFORM CLOSE-CRT-FILE.
012000
012100 MAIN-PROCESS.
012200     PERFORM ENTER-OPTIONS.
012300     IF NOT EXITING-REPORT
012400         GO TO MAIN-PROCESS
012500         .
012600
012700*-----------------------------------------------
012800* THE REPORT OPTIONS SCREEN IS A MENU REQUESTING
012900* THE DESIRED ACTIVITY.
013000*-----------------------------------------------
013100 ENTER-OPTIONS.
013200     PERFORM PROGRAM-INIT.
000000     MOVE ZEROES TO PASSING-PFKEY-CODE.
013300     CALL "RPTOPT" USING
013400         PASSING-PFKEY-CODE
013500         PASSING-MSG-TEXT
000000         REPORT-STYLE.
013600
013700     MOVE PASSING-PFKEY-CODE TO MENU-PICK.
013800     IF NOT EXITING-REPORT
013900         PERFORM PROCESS-OPTIONS-BY-STYLE
014000         GO TO ENTER-OPTIONS.
014100
014200*-----------------------------------------------
014300* THE REPORT OPTIONS SCREEN DISPATCHER
014400*-----------------------------------------------
000000
000000 PROCESS-OPTIONS-BY-STYLE.
000000     IF REPORT-STYLE = "89"
000000         PERFORM PROCESS-OPTIONS-89
000000     ELSE
000000         PERFORM PROCESS-OPTIONS-90.
000000
000000* Report front end 1989 style.
014500 PROCESS-OPTIONS-89.
014600
014700     IF CREATING-REPORT-DEF
014800         PERFORM CREATE-REPORT-DEF
014900     ELSE
015000     IF MODIFYING-REPORT-DEF
015100         PERFORM MODIFY-REPORT-DEF
015200     ELSE
015300     IF PRINTING-REPORT
015400         PERFORM PRINT-REPORT
015500     .
015600
000000* Report front end 1990 onward style.
014500 PROCESS-OPTIONS-90.
014600
014700     IF CREATING-REPORT-DEF
014800         PERFORM CREATE-REPORT-DEF-90
014900     ELSE
015000     IF MODIFYING-REPORT-DEF
015100         PERFORM MODIFY-REPORT-DEF-90
015200     ELSE
015300     IF PRINTING-REPORT
015400         PERFORM PRINT-REPORT
015500     .
015600
000000* Hooks for now.
000000 CREATE-REPORT-DEF-90.
000000     DISPLAY "Create Report Definition 1990".
000000
000000 MODIFY-REPORT-DEF-90.
000000     DISPLAY " Modify Report Definition 1990".
000000
015700*----------------------------------------------------------------
015800* CREATE REPORT DEF OPTION:
015900*      THIS THING CALLS ALL THE PIECES NECESSARY TO OPENING A
016000*      REPORT DEFINITION FILE.  THE RPT-RECORDS ARE PASSED TO
016100*      EACH PROGRAM, SOME PIECE OF THE MONKEY PUZZLE IS FILLED
016200*      IN AND THEN PASSED BACK TO THIS PROGRAM.
016300*
016400*      IF WE SUCCEED IN MAKING IT THROUGH THE PRIMARY,
016500*      SECONDARY AND NEW FIELDS NAMING AND NO VIOLATION OF
016600*      THE 80-FIELD OR 132-CHARACTER-LINE LIMITS HAS
016700*      OCCURRED, THE WHOLE KABOODLE IS PASSED TO RPTMAK
016800*      WHERE THE REPORT DEFINITION FILE IS CREATED.
016900*
017000*      WHEN THE FILE IS OPEN WE PASS THE DATA TO RPTMNU
017100*      FROM WHICH ALL MODIFICATIONS ARE MADE TO THE DATA
017200*      CONTAINED IN THE REPORT RECORDS.  THERE ARE TWO
017300*      PFKEYS FROM RPTMNU WHICH ARE VALID:
017400*             14  FORGET THE WHOLE THING, DO NOT MODIFY AND EXIT
017500*             16  SAVE MY CHANGES (RPTMAK) AND EXIT
017600*---------------------------------------------------------------
017700 CREATE-REPORT-DEF.
017800     PERFORM CLOSING-PROCEDURE.
000000     MOVE SPACE TO FILE-NAME OF RPT-IO-BLOCK.
038460     MOVE "RPTDEF" TO THE-PRNAME.
000000     CALL "RPTFIL" USING
038420         CALLED-RC
038425         FILE-NAME OF RPT-IO-BLOCK
038430         FILE-LIBRARY OF RPT-IO-BLOCK
038435         FILE-VOLUME OF RPT-IO-BLOCK
038440         THE-MODE
038445         THE-PRNAME
038450         UPPER-MSG-FIELD
038455         GETPARM-TYPE.
017900     MOVE 0 TO RPT-PFKEY
018000     CALL "RPTDEF" USING
018100         RPT-PFKEY
018200         PASSING-RPT-ID
018300         RPT-RECORDS.
000000     IF FILE-NAME OF RPT-IO-BLOCK NOT =  SPACE
000000         MOVE FILE-NAME OF RPT-IO-BLOCK TO PASSING-RPT-ID.
000000
018400     PERFORM OPENING-PROCEDURE.
018500
018600     IF PASSING-RPT-ID NOT = SPACES  AND
018700        RPT-PFKEY NOT = 16
018800         MOVE 1 TO RFL-IDX
018900         PERFORM FILL-IN-ADD-SEQUENCE
019000         PERFORM DATA-PARAMETER-LOGIC
019100         IF RHD-FIELD-COUNT = 0
019200             PERFORM CLOSING-PROCEDURE
019300             CALL "RPTXDT" USING
019400                 PASSING-PFKEY-CODE
019500                 PASSING-MSG-TEXT
019600             PERFORM OPENING-PROCEDURE
019700             GO TO CREATE-REPORT-DEF
019800         ELSE
019900         IF RHD-FIELD-COUNT < 81
020000             MOVE "C" TO LOAD-SWITCH
020100             PERFORM COUNT-LENGTH-OF-LINES
020200             IF RPT-PFKEY NOT = 16
020300                 MOVE "C" TO LOAD-SWITCH
020400                 PERFORM CREATE-THE-REPORT-FILE
020500                 IF RPT-PFKEY NOT = 16
020600                     PERFORM ENTER-THE-REPORT-OPTIONS.
020700
020800 FILL-IN-ADD-SEQUENCE.
020900     IF RFL-NAME (RFL-IDX) = SPACES
021000         MOVE 0 TO RFL-SEQ (RFL-IDX).
021100     ADD 1 TO RFL-IDX.
021200     IF RFL-IDX < 81
021300         GO TO FILL-IN-ADD-SEQUENCE.
021400
021500 DATA-PARAMETER-LOGIC.
021600     PERFORM DEFINE-1ST-DATA-PARAMETERS.
021700
021800     IF RHD-SEC-FILE = 1  AND
021900        RHD-FIELD-COUNT NOT = 0  AND
022000        RHD-FIELD-COUNT < 81
022100         PERFORM DEFINE-2ND-DATA-PARAMETERS.
022200
022300     IF RHD-FIELD-COUNT < 81  AND
022400        RHD-FIELD-COUNT NOT = 0
022500         PERFORM DEFINE-NEW-DATA-PARAMETERS.
022600
022700     IF RHD-FIELD-COUNT > 80 AND
022800        RHD-FIELD-COUNT NOT = 99
022900         PERFORM CLOSING-PROCEDURE
023000         CALL "RPTXFL"
023100         PERFORM OPENING-PROCEDURE.
023200
023300 COUNT-LENGTH-OF-LINES.
023400     CALL "RPTCLN" USING
023500         RPT-RECORDS.
023600
023700     MOVE ZEROES TO RPT-PFKEY.
023800     IF RHD-LINE-1-LEN > 132  OR
023900        RHD-LINE-2-LEN > 132  OR
024000        RHD-LINE-3-LEN > 132
024100         PERFORM CLOSING-PROCEDURE
024200         CALL "RPTXLN" USING
024300             LOAD-SWITCH
024400             RPT-PFKEY
024500             RPT-RECORDS
024600         PERFORM OPENING-PROCEDURE.
024700
024800 CREATE-THE-REPORT-FILE.
024900     MOVE PASSING-RPT-ID TO FILE-NAME OF RPT-IO-BLOCK.
025000     MOVE 100 TO FILE-SPACE OF RPT-IO-BLOCK.
025100     IF LOAD-SWITCH = "C"
025200         CALL "RPTDUP" USING
025300             RPT-IO-BLOCK.
025400     CALL "RPTMAK" USING
025500         LOAD-SWITCH
025600         RPT-IO-BLOCK
025700         RPT-RECORDS.
025800
025900 ENTER-THE-REPORT-OPTIONS.
026000     PERFORM CLOSING-PROCEDURE.
026100     IF RPT-PFKEY = ZEROES
026200         MOVE 1 TO RPT-PFKEY.
026300
026400     CALL "RPTMNU" USING
026500         LOAD-SWITCH
026600         RPT-PFKEY
026700         RPT-CTL-FIELDS
026800         RPT-RECORDS.
026900
027000     IF RPT-PFKEY NOT = 14
027100         MOVE "M" TO LOAD-SWITCH
027200         CALL "RPTMAK" USING
027300             LOAD-SWITCH
027400             RPT-IO-BLOCK
027500             RPT-RECORDS.
027600
027700     PERFORM OPENING-PROCEDURE.
027800
027900 DEFINE-1ST-DATA-PARAMETERS.
028000     PERFORM CLOSING-PROCEDURE.
028100     MOVE "P" TO LOAD-SWITCH.
028200     CALL "RPTPDT" USING
028300         RPT-RECORDS.
028400     CALL "RPTCTL" USING
028500         LOAD-SWITCH
028600         RPT-RECORDS.
028700     IF RHD-FIELD-COUNT NOT = 99
028800         CALL "RPTCTLL" USING
028900             LOAD-SWITCH
029000             RPT-CTL-FIELDS
029100             RPT-RECORDS
029200         CALL "RPTPFL" USING
029300             RPT-CTL-FIELDS
029400             RPT-RECORDS
029500         CALL "RPTCTLP" USING
029600             LOAD-SWITCH
029700             RPT-CTL-FIELDS
029800             RPT-RECORDS
029900     PERFORM OPENING-PROCEDURE.
030000
030100 DEFINE-2ND-DATA-PARAMETERS.
030200     PERFORM CLOSING-PROCEDURE.
030300     MOVE "S" TO LOAD-SWITCH.
030400
030500     CALL "RPTSDT" USING
030600         RPT-CTL-FIELDS
030700         RPT-RECORDS.
030800     CALL "RPTCTL" USING
030900         LOAD-SWITCH
031000         RPT-RECORDS.
031100     IF RHD-FIELD-COUNT NOT = 99
031200        CALL "RPTCTLL" USING
031300             LOAD-SWITCH
031400             RPT-CTL-FIELDS
031500             RPT-RECORDS
031600         CALL "RPTSFL" USING
031700             RPT-CTL-FIELDS
031800             RPT-RECORDS
031900         CALL "RPTCTLP" USING
032000             LOAD-SWITCH
032100             RPT-CTL-FIELDS
032200             RPT-RECORDS
032300         CALL "RPTAKA" USING
032400             RPT-CTL-FIELDS
032500             RPT-RECORDS.
032600     PERFORM OPENING-PROCEDURE.
032700
032800 DEFINE-NEW-DATA-PARAMETERS.
032900     PERFORM CLOSING-PROCEDURE.
033000     MOVE "C" TO LOAD-SWITCH.
033100     CALL "RPTNEW" USING
033200         LOAD-SWITCH.
033300     IF LOAD-SWITCH = "Y"
033400         MOVE "N" TO LOAD-SWITCH
033500         CALL "RPTNFL" USING
033600             LOAD-SWITCH
033700             RPT-CTL-FIELDS
033800             RPT-RECORDS
033900         CALL "RPTCTLP" USING
034000             LOAD-SWITCH
034100             RPT-CTL-FIELDS
034200             RPT-RECORDS
034300         CALL "RPTDNW" USING
034400             RPT-PFKEY
034500             RPT-CTL-FIELDS
034600             RPT-RECORDS.
034700     PERFORM OPENING-PROCEDURE.
034800
034900*-----------------------------------------------
035000* THE MODIFY REPORT DEF OPTION CONSISTS OF CALLING
035100* THE PROGRAM WHICH  ENABLES CHANGES TO REPORT RECORDS.
035200*           DATA-FILES-RECORD
035300*           CONTROL-FILES-RECORD
035400*-----------------------------------------------
035500 MODIFY-REPORT-DEF.
035600     PERFORM CLOSING-PROCEDURE.
035700*     MOVE SPACES TO FILE-NAME OF RPT-IO-BLOCK.
035700     MOVE SPACES TO TEMP-IO-BLOCK.
035705     IF PASSING-RPT-ID = SPACES
035707         PERFORM INIT-PP-FIELDS
035711         MOVE SPACES TO FILE-SPEC OF TEMP-IO-BLOCK
035707         MOVE "RPTDEF" TO THE-PRNAME
035710         PERFORM CK-FOR-PUTPARM-INFO
035711         MOVE SPACES TO FILE-SPEC OF TEMP-IO-BLOCK
035730         MOVE "CONTROL" TO THE-PRNAME
035735         PERFORM CK-FOR-PUTPARM-INFO
035711         MOVE SPACES TO FILE-SPEC OF TEMP-IO-BLOCK
035740         MOVE "CONTROL2" TO THE-PRNAME
035745         PERFORM CK-FOR-PUTPARM-INFO
035711         MOVE SPACES TO FILE-SPEC OF TEMP-IO-BLOCK
035750         MOVE "INPUT1" TO THE-PRNAME
035755         PERFORM CK-FOR-PUTPARM-INFO
035711         MOVE SPACES TO FILE-SPEC OF TEMP-IO-BLOCK
035760         MOVE "INPUT2" TO THE-PRNAME
035765         PERFORM CK-FOR-PUTPARM-INFO.
035770   
035775     IF PP-RPT-FIL-BLOCK NOT = SPACES
035780         MOVE PP-RPT-FIL-BLOCK TO FILE-SPEC OF RPT-IO-BLOCK
035785         MOVE FILE-NAME OF RPT-IO-BLOCK   
035790             TO PASSING-RPT-ID.
000000
035800     CALL "RPTMOD" USING
035900         RPT-PFKEY
036000         PASSING-RPT-ID
036100         RPT-IO-BLOCK.
036200     PERFORM OPENING-PROCEDURE.
036300
036400     MOVE FILE-NAME OF RPT-IO-BLOCK TO PASSING-RPT-ID.
036500     IF PASSING-RPT-ID NOT = SPACES
036600         CALL "RPTLOD" USING
000000             CALLED-RC
036700             RPT-IO-BLOCK
036800             RPT-RECORDS
036900         MOVE 1 TO RFL-IDX
037000         PERFORM FILL-IN-THE-SEQUENCE
036805         PERFORM OVERRIDE-WITH-PP-DATA
037100         PERFORM MODIFY-DATA-PARAMETERS
037200         IF RHD-FIELD-COUNT = 99
037300             PERFORM CLOSING-PROCEDURE
037400             CALL "RPTXCL"
037500             PERFORM OPENING-PROCEDURE
037600         ELSE
037700         IF RHD-FIELD-COUNT < 81
037800             MOVE "C" TO LOAD-SWITCH
037900             PERFORM COUNT-LENGTH-OF-LINES
038000             IF RPT-PFKEY NOT = 16
038100                 MOVE "M" TO LOAD-SWITCH
038200                 PERFORM CREATE-THE-REPORT-FILE
038300                 IF RPT-PFKEY NOT = 16
038400                     PERFORM ENTER-THE-REPORT-OPTIONS.
038405 
038406 INIT-PP-FIELDS.
038407     MOVE SPACES TO PP-RPT-FIL-BLOCK
038408                    PP-CTL-FIL-BLOCK
038409                    PP-CTL2-FIL-BLOCK
038410                    PP-IN1-FIL-BLOCK
038411                    PP-IN2-FIL-BLOCK.
038405 
038410 CK-FOR-PUTPARM-INFO.
038415     CALL "RPTFIL" USING
038420         CALLED-RC
038425         FILE-NAME OF TEMP-IO-BLOCK
038430         FILE-LIBRARY OF TEMP-IO-BLOCK
038435         FILE-VOLUME OF TEMP-IO-BLOCK
038440         THE-MODE
038445         THE-PRNAME
038450         UPPER-MSG-FIELD
038455         GETPARM-TYPE.
038460     IF THE-PRNAME = "RPTDEF"
038465         MOVE FILE-SPEC OF TEMP-IO-BLOCK TO PP-RPT-FIL-BLOCK
038470     ELSE
038475     IF THE-PRNAME = "CONTROL"
038480         MOVE FILE-SPEC OF TEMP-IO-BLOCK TO PP-CTL-FIL-BLOCK
038470     ELSE
038475     IF THE-PRNAME = "CONTROL2"
038480         MOVE FILE-SPEC OF TEMP-IO-BLOCK TO PP-CTL2-FIL-BLOCK
038470     ELSE
038475     IF THE-PRNAME = "INPUT1"
038480         MOVE FILE-SPEC OF TEMP-IO-BLOCK TO PP-IN1-FIL-BLOCK
038470     ELSE
038475     IF THE-PRNAME = "INPUT2"
038480         MOVE FILE-SPEC OF TEMP-IO-BLOCK TO PP-IN2-FIL-BLOCK.
038500
038600 FILL-IN-THE-SEQUENCE.
038700     IF RFL-NAME (RFL-IDX) = SPACES
038800         MOVE SPACE TO RFL-LINE-CODE (RFL-IDX)
038900         MOVE 99 TO RFL-SEQ (RFL-IDX).
039000     ADD 1 TO RFL-IDX.
039100     IF RFL-IDX < 81
039200         GO TO FILL-IN-THE-SEQUENCE.
039205
039210 OVERRIDE-WITH-PP-DATA.
039211     IF PP-CTL-FIL-NAM NOT = SPACES
039212         MOVE PP-CTL-FIL-NAM TO RCF-PRIM-FILE.
039213     IF PP-CTL-FIL-LIB NOT = SPACES
039214         MOVE PP-CTL-FIL-LIB TO RCF-PRIM-LIBRARY.
039215     IF PP-CTL-FIL-VOL NOT = SPACES
039216         MOVE PP-CTL-FIL-VOL TO RCF-PRIM-VOLUME.
039211     IF PP-CTL2-FIL-NAM NOT = SPACES
039212         MOVE PP-CTL2-FIL-NAM TO RCF-SEC-FILE.
039213     IF PP-CTL2-FIL-LIB NOT = SPACES
039214         MOVE PP-CTL2-FIL-LIB TO RCF-SEC-LIBRARY.
039215     IF PP-CTL2-FIL-VOL NOT = SPACES
039216         MOVE PP-CTL2-FIL-VOL TO RCF-SEC-VOLUME.
039211     IF PP-IN1-FIL-NAM NOT = SPACES
039212         MOVE PP-IN1-FIL-NAM TO RDF-PRIM-FILE.
039213     IF PP-IN1-FIL-LIB NOT = SPACES
039214         MOVE PP-IN1-FIL-LIB TO RDF-PRIM-LIBRARY.
039215     IF PP-IN1-FIL-VOL NOT = SPACES
039216         MOVE PP-IN1-FIL-VOL TO RDF-PRIM-VOLUME.
039211     IF PP-IN2-FIL-NAM NOT = SPACES
039212         MOVE PP-IN2-FIL-NAM TO RDF-SEC-FILE.
039213     IF PP-IN2-FIL-LIB NOT = SPACES
039214         MOVE PP-IN2-FIL-LIB TO RDF-SEC-LIBRARY.
039215     IF PP-IN2-FIL-VOL NOT = SPACES
039216         MOVE PP-IN2-FIL-VOL TO RDF-SEC-VOLUME.
039300
039400 MODIFY-DATA-PARAMETERS.
039500     MOVE SPACE TO LOAD-SWITCH.
039600     IF RPT-PFKEY = 8
039700         PERFORM MODIFY-DATA-FILES.
039800
039900     IF RHD-FIELD-COUNT < 81
040000         PERFORM CLOSING-PROCEDURE
040100         CALL "RPTMOR" USING
040200             LOAD-SWITCH
040300         PERFORM OPENING-PROCEDURE.
040400
040500     IF LOAD-SWITCH = "Y"
040600         PERFORM ADD-PRIMARY-FIELDS.
040700
040800     IF LOAD-SWITCH = "Y"
040900         IF RHD-SEC-FILE = 1  AND
041000            RHD-FIELD-COUNT < 81
041100             PERFORM ADD-SECONDARY-FIELDS.
041200
041300     IF RHD-FIELD-COUNT < 81
041400         PERFORM ADD-NEW-FIELDS.
041500
041600     IF RHD-FIELD-COUNT < 80
041700         PERFORM CLOSING-PROCEDURE
041800         CALL "RPTAKA" USING
041900             RPT-CTL-FIELDS
042000             RPT-RECORDS
042100         CALL "RPTCKFL" USING
042200             LOAD-SWITCH
042300             RPT-CTL-FIELDS
042400             RPT-RECORDS
042500         PERFORM OPENING-PROCEDURE
042600         PERFORM MODIFY-NEW-FIELDS.
042700
042800     IF LOAD-SWITCH = "Y"
042900         IF RHD-FIELD-COUNT > 80  AND
043000            RHD-FIELD-COUNT = 99
043100             PERFORM CLOSING-PROCEDURE
043200             CALL "RPTXFL"
043300             PERFORM OPENING-PROCEDURE.
043400
043500 MODIFY-DATA-FILES.
043600     PERFORM CLOSING-PROCEDURE.
043700     CALL "RPTMDT" USING
043800         RPT-CTL-FIELDS
043900         RPT-RECORDS.
044000     CALL "RPTCKFL" USING
044100         LOAD-SWITCH
044200         RPT-CTL-FIELDS
044300         RPT-RECORDS.
044400     PERFORM OPENING-PROCEDURE.
044500
044600 ADD-PRIMARY-FIELDS.
044700     PERFORM CLOSING-PROCEDURE.
044800     MOVE "P" TO LOAD-SWITCH.
044900     CALL "RPTCTLM" USING
045000         LOAD-SWITCH
045100         RPT-CTL-FIELDS
045200         RPT-RECORDS.
045300     IF LOAD-SWITCH = "P"
045400         CALL "RPTPFL" USING
045500             RPT-CTL-FIELDS
045600             RPT-RECORDS
045700         CALL "RPTCTLP" USING
045800             LOAD-SWITCH
045900             RPT-CTL-FIELDS
046000             RPT-RECORDS.
046100     MOVE "Y" TO LOAD-SWITCH
046200     PERFORM OPENING-PROCEDURE.
046300
046400 ADD-SECONDARY-FIELDS.
046500     PERFORM CLOSING-PROCEDURE.
046600     MOVE "S" TO LOAD-SWITCH.
046700     CALL "RPTCTLM" USING
046800         LOAD-SWITCH
046900         RPT-CTL-FIELDS
047000         RPT-RECORDS.
047100     IF LOAD-SWITCH = "S"
047200         CALL "RPTSFL" USING
047300             RPT-CTL-FIELDS
047400             RPT-RECORDS
047500         CALL "RPTCTLP" USING
047600             LOAD-SWITCH
047700             RPT-CTL-FIELDS
047800             RPT-RECORDS
047900     MOVE "Y" TO LOAD-SWITCH.
048000     PERFORM OPENING-PROCEDURE.
048100
048200 ADD-NEW-FIELDS.
048300     PERFORM CLOSING-PROCEDURE.
048400     IF LOAD-SWITCH = "Y"
048500         MOVE "C" TO LOAD-SWITCH
048600         CALL "RPTNEW" USING
048700             LOAD-SWITCH.
048800
048900     IF LOAD-SWITCH = "Y"
049000         MOVE "M" TO LOAD-SWITCH
049100         CALL "RPTNFL" USING
049200             LOAD-SWITCH
049300             RPT-CTL-FIELDS
049400             RPT-RECORDS
049500         MOVE "N" TO LOAD-SWITCH
049600         CALL "RPTCTLP" USING
049700             LOAD-SWITCH
049800             RPT-CTL-FIELDS
049900             RPT-RECORDS.
050000     PERFORM OPENING-PROCEDURE.
050100
050200 MODIFY-NEW-FIELDS.
050300     PERFORM CLOSING-PROCEDURE.
050400     IF RHD-NEW-FIELDS = 1
050500         MOVE "M" TO LOAD-SWITCH
050600         CALL "RPTNEW" USING
050700             LOAD-SWITCH
050800         IF LOAD-SWITCH = "Y"
050900             CALL "RPTDNW" USING
051000                 RPT-PFKEY
051100                 RPT-CTL-FIELDS
051200                 RPT-RECORDS
051300         MOVE "Y" TO LOAD-SWITCH.
051400     PERFORM OPENING-PROCEDURE.
051500
051600 PRINT-REPORT.
051700     PERFORM CLOSING-PROCEDURE.
051800     CALL "RPTWMN" USING
051900         PASSING-RPT-ID.
052000     PERFORM OPENING-PROCEDURE.
052100
052200* GENERAL ROUTINES.
052300     COPY PLCRT.
052400     COPY PLUSAGE.
052500
000000     COPY PLKTRACE.
