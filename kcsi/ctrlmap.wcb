000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLMAP.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  09/17/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* THIS ROUTINE TRACKS THE MAPPING OF UPDATEABLE AND NON
000800* UPDATEABLE FIELDS TO CHECK FOR OVERLAYS.
000900* TWO MAPS ARE USED ONE CONTAINS UPDATEABLE ELEMENTS
001000* THE OTHER CONTAINS NON-UPDATEABLES AS 'X'. GROUP ITEMS
001100* ARE TAGGED AS 'G' AND THE REMAINING OCCURENCES ARE TAGGED
001200* AS H. AN UPDATEABLE FIELD MAY NOT OCCUR WITHIN AN H AREA
001300* AND AN UPDATEABLE FIELD MAY NOT OVERLAY ANOTHER.
001400*---------------------------------------------------------------
001500
001600 ENVIRONMENT DIVISION.
001700 CONFIGURATION SECTION.
001800 FIGURATIVE-CONSTANTS.
001900     COPY FIGS.
002000
002100 INPUT-OUTPUT SECTION.
002200
002300 FILE-CONTROL.
002400
002500 DATA DIVISION.
002600 FILE SECTION.
002700
002800 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrlmap.wcb 1.4 9/11/94".
       01  FILLER.
001700     05  MAP-START-POS          PIC 9999.
001800     05  MAP-END-POS            PIC 9999.
001900     05  MAP-OCCURRENCES        PIC 99.
002000     05  MAP-LEN                PIC 999.
002100     05  MAP-RC        PIC 9999.

003000     COPY WSCRTS.
003100 01  THE-U-MAP         PIC X(2040) VALUE SPACE.
003200 01  FILLER REDEFINES THE-U-MAP.
003300     05  U-MAP-CHAR    PIC X OCCURS 2040 TIMES.
003400 01  THE-X-MAP         PIC X(2040) VALUE SPACE.
003500 01  FILLER REDEFINES THE-X-MAP.
003600     05  X-MAP-CHAR    PIC X OCCURS 2040 TIMES.
003700 01  MAP-INDEX       BINARY VALUE ZEROES.
003800 01  MAP-END         BINARY VALUE ZEROES.
003900 01  START-INDEX     BINARY VALUE ZEROES.
004000 01  STOP-INDEX      BINARY VALUE ZEROES.
004100 01  OCCURS-COUNT    PIC 99 VALUE ZEROES.
004200 01  SET-CHAR        PIC X.
004300 01  SAVE-MAP-START-POS     PIC 9999.
004400 01  SAVE-MAP-END-POS       PIC 9999.
004600 LINKAGE SECTION.
004700 01  CTRL-IO-BLOCK.
004800     COPY LKDIO.
000100 01  CTRL-HEADER-RECORDS.
004900     COPY RCCTRLH.
       01  CF-RECORD.
005000     COPY RCCTRLF.
005100     COPY LKCTRLAR.
005200     COPY RCCTRLT1.
005300     COPY RCCTRLT2.
000000     COPY LKPRC.
005400 PROCEDURE DIVISION USING
000000     CALLED-RC
005500     COPY LKCTRL.
005600 MAIN-LOGIC SECTION.
005700 PROGRAM-BEGIN.
000000     MOVE "CTRLMAP BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     MOVE ZEROES TO CALLED-RC.
005800     PERFORM MAIN-PROCESS.
005900     MOVE MAP-RC TO CALLED-RC.
000000     MOVE "CTRLMAP EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
006200 MAIN-LOGIC-STOP.
006300     STOP RUN.
006400 LEVEL-2 SECTION.
006500 MAIN-PROCESS.
006600     MOVE ZEROES TO MAP-RC.
006700     MOVE SPACE TO CF-MSG.
006800     IF CF-ACTION-CODE = "INIT"
006900         PERFORM INIT-MAP
007000     ELSE
007100     IF CF-ACTION-CODE = "ADD"
007200         PERFORM ADD-TO-MAP
007300     ELSE
007400     IF CF-ACTION-CODE = "DELETE"
007500         PERFORM DELETE-FROM-MAP
007600     ELSE
007700     IF CF-ACTION-CODE = "VAL"
007800         PERFORM VAL-FOR-MAP.
008000
008100 INIT-MAP.
008200     MOVE SPACE TO THE-U-MAP.
008300     MOVE SPACE TO THE-X-MAP.
008400
008500*----------------------------------------------------------
008600* THIS LOGIC IS INTENDED TO MAP FIELDS
008700* A NON-UPDATEABLE TABLE IS MAPPED AS A GROUP 'G' FOR THE
008800* FIRST ENTRY AND THEN AS AN 'H' FOR THE REMAINDER.
008900* ADD LOGIC
009000*-----------------------------------------------------------------
009100 ADD-TO-MAP.
009200     PERFORM SET-MAP-START-AND-END.
009300     IF CX-UPDATE-CODE = 0
009400         PERFORM SET-MAP-UPDATES
009500     ELSE
009600     IF CX-OCCURRENCES = 1
009700         PERFORM SET-MAP-NOUPS
009800     ELSE
009900         PERFORM SET-MAP-GROUPS.
010000 SET-MAP-UPDATES.
010100     MOVE "U" TO SET-CHAR.
010200     PERFORM SET-UPDATES.
010300 SET-UPDATES.
010400     PERFORM SET-U-MAP-CHAR
010500         VARYING MAP-INDEX FROM MAP-START-POS BY 1
010600         UNTIL MAP-INDEX > MAP-END-POS.
010700 SET-U-MAP-CHAR.
010800     MOVE SET-CHAR TO U-MAP-CHAR(MAP-INDEX).
010900 SET-MAP-NOUPS.
011000     MOVE "X" TO SET-CHAR.
011100     PERFORM SET-X-MAP-CHAR
011200         VARYING MAP-INDEX FROM MAP-START-POS BY 1
011300         UNTIL MAP-INDEX > MAP-END-POS.
011400 SET-X-MAP-CHAR.
011500     IF X-MAP-CHAR(MAP-INDEX) NOT = "G"
011600         MOVE SET-CHAR TO X-MAP-CHAR(MAP-INDEX).
011700 SET-MAP-GROUPS.
011800     MOVE "G" TO SET-CHAR.
011900     PERFORM SET-X-MAP-CHAR
012000         VARYING MAP-INDEX FROM MAP-START-POS BY 1
012100         UNTIL MAP-INDEX > MAP-END-POS.
012200     MOVE MAP-START-POS TO SAVE-MAP-START-POS.
012300     COMPUTE MAP-START-POS = MAP-START-POS + MAP-LEN.
012400     PERFORM SET-MAP-ELEMENTS.
012500     MOVE SAVE-MAP-START-POS TO MAP-START-POS.
012600 SET-MAP-ELEMENTS.
012700     PERFORM SET-H-MAP-CHAR
012800         VARYING MAP-INDEX FROM MAP-START-POS BY 1
012900         UNTIL MAP-INDEX > MAP-END-POS.
013000 SET-H-MAP-CHAR.
013100     MOVE "H" TO X-MAP-CHAR(MAP-INDEX).
013200
013300*----------------------------------------------------------
013400* DELETE LOGIC REVERSES OUT A FIELD
013500*----------------------------------------------------------
013600 DELETE-FROM-MAP.
013700     PERFORM SET-MAP-START-AND-END.
013800     IF CX-UPDATE-CODE = 0
013900         PERFORM UNSET-MAP-UPDATES
014000     ELSE
014100     IF CX-OCCURRENCES > 1
014200         PERFORM UNSET-MAP-GROUPS.
014300
014400 UNSET-MAP-UPDATES.
014500     MOVE SPACE TO SET-CHAR.
014600     PERFORM SET-UPDATES.
014700 UNSET-MAP-GROUPS.
014800     PERFORM UNSET-X-MAP-CHAR
014900         VARYING MAP-INDEX FROM MAP-START-POS BY 1
015000         UNTIL MAP-INDEX > MAP-END-POS.
015100 UNSET-X-MAP-CHAR.
015200     MOVE SPACE TO X-MAP-CHAR(MAP-INDEX).
015300*----------------------------------------------------------
015400* TEST LOGIC ENSURES THAT A NEW FIELD WILL NOT OVERLAY
015500* ANOTHER.
015600* THIS LOGIC IS INTENDED TO CHECK THAT TWO UPDATE-ABLE FIELDS
015700* DO NOT OVERLAY ONE ANOTHER. EACH UPDATEABLE FIELD IS
015800* CHECKED AGAINST THE UPDATE MAP. FOR OVERLAYS.
015900*----------------------------------------------------------
016000 VAL-FOR-MAP.
016100     IF CX-UPDATE-CODE = 0
016200         PERFORM VAL-UP
016300     ELSE
016400         PERFORM VAL-NON-UP.
016500 VAL-NON-UP.
016600     IF CX-OCCURRENCES = 1
016700         NEXT SENTENCE
016800     ELSE
016900         PERFORM VAL-NON-UP-TABLE.
017000*-----------------------------------------------------------------
017100* A NON-UPDATEABLE TABLE MAY NOT OVERLAY AN UPDATEABLE FIELD
017200* ANYWHERE EXCEPT WITHIN THE FIRST ENTRY. CHECK FROM ENTRY 2 TO
017300* THE END OF THE RANGE FOR AN UPDATEABLE FIELD.
017400* AN OCCURS TABLE MAY NOT OVERLAY ANOTHER IN THE SAME SPACE
017500*-----------------------------------------------------------------
017600 VAL-NON-UP-TABLE.
017700     PERFORM SET-MAP-START-AND-END.
017800     PERFORM TEST-MAP-GROUPS.
017900     IF MAP-RC NOT = ZEROES
018000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
018100         MOVE
018200          "A Group Table Already Exists in This Area."
018300            TO CF-MSG
018400     ELSE
018500*SKIP FIRST ENTRY.
018600         ADD CX-INT-LEN TO MAP-START-POS
018700         PERFORM TEST-MAP-UPDATES
018800         IF MAP-RC NOT = ZEROES
018900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
019000             MOVE
019100              "Update Field Exists Outside First Table Entry."
019200                TO CF-MSG.
019400 VAL-UP.
019500     IF CX-OCCURRENCES = 1
019600         PERFORM VAL-UP-ONE
019700     ELSE
019800         PERFORM VAL-UP-MANY.
019900
020000*-----------------------------------------------------------------
020100* VAL-UP-ONE, JUST CHECK FOR A STRAIGHT-OVERLAY.
020200* OR OVERLAYING A GROUP ELEMENT.
020300*-----------------------------------------------------------------
020400 VAL-UP-ONE.
020500     PERFORM SET-MAP-START-AND-END.
020600     PERFORM TEST-MAP-UPDATES.
020700     IF MAP-RC NOT = ZEROES
020800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
020900         MOVE "Update Field Overlays Another"
021000            TO CF-MSG
021100     ELSE
021200         PERFORM TEST-MAP-ELEMENTS
021300         IF MAP-RC NOT = ZEROES
021400             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
021500             MOVE
021600            "Update Field Cannot Exist Outside First Table Entry."."
021700                TO CF-MSG.
021900 VAL-UP-MANY.
022000     PERFORM SET-MAP-START-AND-END.
022100     PERFORM TEST-MAP-UPDATES.
022200     IF MAP-RC NOT = ZEROES
022300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
022400         MOVE "Update Field Overlays Another"
022500            TO CF-MSG
022600     ELSE
022700         PERFORM TEST-MAP-GROUPS
022800         IF MAP-RC NOT = ZEROES
022900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
023000             MOVE
023100              "A Group Table Already Exists in This Area."
023200                TO CF-MSG.
023400 TEST-MAP-UPDATES.
023500     PERFORM DUMMY-ROUTINE
023600        VARYING MAP-INDEX FROM MAP-START-POS BY 1
023700        UNTIL U-MAP-CHAR(MAP-INDEX) = "U"
023800                OR
023900              MAP-INDEX > MAP-END-POS.
024100     IF MAP-INDEX > MAP-END-POS
024200         NEXT SENTENCE
024300     ELSE
024400         COMPUTE MAP-RC = MAP-INDEX.
024500 TEST-MAP-GROUPS.
024600     PERFORM DUMMY-ROUTINE
024700        VARYING MAP-INDEX FROM MAP-START-POS BY 1
024800        UNTIL X-MAP-CHAR(MAP-INDEX) = "G" OR "H"
024900                OR
025000              MAP-INDEX > MAP-END-POS.
025200     IF MAP-INDEX > MAP-END-POS
025300         NEXT SENTENCE
025400     ELSE
025500         COMPUTE MAP-RC = MAP-INDEX.
025600 TEST-MAP-ELEMENTS.
025700     PERFORM DUMMY-ROUTINE
025800        VARYING MAP-INDEX FROM MAP-START-POS BY 1
025900        UNTIL X-MAP-CHAR(MAP-INDEX) = "H"
026000                OR
026100              MAP-INDEX > MAP-END-POS.
026300     IF MAP-INDEX > MAP-END-POS
026400         NEXT SENTENCE
026500     ELSE
026600         COMPUTE MAP-RC = MAP-INDEX.
026700* UTILITY ROUTINES
026800 SET-MAP-START-AND-END.
026900     COMPUTE MAP-OCCURRENCES = CX-OCCURRENCES.
027000     COMPUTE MAP-LEN = CX-INT-LEN.
027100     COMPUTE MAP-START-POS = CX-START-POS
027200     COMPUTE MAP-END-POS =
027300        ( CX-INT-LEN *
027400          CX-OCCURRENCES )
027500        + CX-START-POS - 1.
027700 DUMMY-ROUTINE.
027800     EXIT.
000000     COPY PLKTRACE.
