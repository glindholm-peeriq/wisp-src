000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTTIT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/15/89.
000500 DATE-COMPILED.
000600*-------------------------------------------------------------
000700* DEFINES THE REPORT TITLES PAGE HEADINGS CONTROL BREAK LABELS
000800*-------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rpttit.wcb 1.4 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002700
002800 01  REPORT-TITLE-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(24) ROW 1 COLUMN 28 VALUE
003100         "REPORT TITLE INFORMATION".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 2 COLUMN 3
003400         SOURCE UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(13) ROW 3 COLUMN 3 VALUE
003700         "Report Title:".
003800     05  RPTTTL-TITLE-1-ITEM
003900         PIC X(60) ROW 3 COLUMN 18
004000         SOURCE RPTTTL-TITLE-1-FIELD
004100         OBJECT RPTTTL-TITLE-1-FIELD.
004200     05  RPTTTL-TITLE-2-ITEM
004300         PIC X(60) ROW 4 COLUMN 18
004400         SOURCE RPTTTL-TITLE-2-FIELD
004500         OBJECT RPTTTL-TITLE-2-FIELD.
004600     05  RPTTTL-TITLE-3-ITEM
004700         PIC X(60) ROW 5 COLUMN 18
004800         SOURCE RPTTTL-TITLE-3-FIELD
004900         OBJECT RPTTTL-TITLE-3-FIELD.
005000     05  FILLER
005100         PIC X(31) ROW 6 COLUMN 8 VALUE
005200         "Default title is the Report-ID.".
005300     05  FILLER
005400         PIC X(13) ROW 8 COLUMN 3 VALUE
005500         "Page Heading:".
005600     05  RPTTTL-HEADING-1-ITEM
005700         PIC X(60) ROW 8 COLUMN 18
005800         SOURCE RPTTTL-HEADING-1-FIELD
005900         OBJECT RPTTTL-HEADING-1-FIELD.
006000     05  RPTTTL-HEADING-2-ITEM
006100         PIC X(60) ROW 9 COLUMN 18
006200         SOURCE RPTTTL-HEADING-2-FIELD
006300         OBJECT RPTTTL-HEADING-2-FIELD.
006400     05  RPTTTL-HEADING-3-ITEM
006500         PIC X(60) ROW 10 COLUMN 18
006600         SOURCE RPTTTL-HEADING-3-FIELD
006700         OBJECT RPTTTL-HEADING-3-FIELD.
006800     05  FILLER
006900         PIC X(43) ROW 11 COLUMN 8 VALUE
007000         "Page heading defaults to the same as title.".
007100     05  FILLER
007200         PIC X(59) ROW 13 COLUMN 3 VALUE
007300         "Control Break Descriptor:  Column                Descrip
007400-        "tor".
007500     05  FILLER
007600         PIC X(7) ROW 14 COLUMN 18 VALUE
007700         "Level 1".
007800     05  RPTTTL-COL-1-ITEM
007900         PIC 9(03) ROW 14 COLUMN 32
008000         SOURCE RPTTTL-COL-1-FIELD
008100         OBJECT RPTTTL-COL-1-FIELD.
008200     05  RPTTTL-BREAK-1-ITEM
008300         PIC X(40) ROW 14 COLUMN 38
008400         SOURCE RPTTTL-BREAK-1-FIELD
008500         OBJECT RPTTTL-BREAK-1-FIELD.
008600     05  FILLER
008700         PIC X(7) ROW 15 COLUMN 18 VALUE
008800         "Level 2".
008900     05  RPTTTL-COL-2-ITEM
009000         PIC 9(03) ROW 15 COLUMN 32
009100         SOURCE RPTTTL-COL-2-FIELD
009200         OBJECT RPTTTL-COL-2-FIELD.
009300     05  RPTTTL-BREAK-2-ITEM
009400         PIC X(40) ROW 15 COLUMN 38
009500         SOURCE RPTTTL-BREAK-2-FIELD
009600         OBJECT RPTTTL-BREAK-2-FIELD.
009700     05  FILLER
009800         PIC X(7) ROW 16 COLUMN 18 VALUE
009900         "Level 3".
010000     05  RPTTTL-COL-3-ITEM
010100         PIC 9(03) ROW 16 COLUMN 32
010200         SOURCE RPTTTL-COL-3-FIELD
010300         OBJECT RPTTTL-COL-3-FIELD.
010400     05  RPTTTL-BREAK-3-ITEM
010500         PIC X(40) ROW 16 COLUMN 38
010600         SOURCE RPTTTL-BREAK-3-FIELD
010700         OBJECT RPTTTL-BREAK-3-FIELD.
010800     05  FILLER
010900         PIC X(7) ROW 17 COLUMN 18 VALUE
011000         "Level 4".
011100     05  RPTTTL-COL-4-ITEM
011200         PIC 9(03) ROW 17 COLUMN 32
011300         SOURCE RPTTTL-COL-4-FIELD
011400         OBJECT RPTTTL-COL-4-FIELD.
011500     05  RPTTTL-BREAK-4-ITEM
011600         PIC X(40) ROW 17 COLUMN 38
011700         SOURCE RPTTTL-BREAK-4-FIELD
011800         OBJECT RPTTTL-BREAK-4-FIELD.
011900     05  FILLER
012000         PIC X(7) ROW 18 COLUMN 18 VALUE
012100         "Level 5".
012200     05  RPTTTL-COL-5-ITEM
012300         PIC 9(03) ROW 18 COLUMN 32
012400         SOURCE RPTTTL-COL-5-FIELD
012500         OBJECT RPTTTL-COL-5-FIELD.
012600     05  RPTTTL-BREAK-5-ITEM
012700         PIC X(40) ROW 18 COLUMN 38
012800         SOURCE RPTTTL-BREAK-5-FIELD
012900         OBJECT RPTTTL-BREAK-5-FIELD.
	   COPY RPTPFMNU.
013800
013900 01  REPORT-TITLE-TABLE.
014000     05  UPPER-MSG-FIELD                   PIC X(78) VALUE SPACE.
014100     05  REPORT-TITLE-FIELDS.
014200         07  RPTTTL-TITLE-1-FIELD          PIC X(60) VALUE SPACE.
014300         07  RPTTTL-TITLE-2-FIELD          PIC X(60) VALUE SPACE.
014400         07  RPTTTL-TITLE-3-FIELD          PIC X(60) VALUE SPACE.
014500         07  RPTTTL-HEADING-1-FIELD        PIC X(60) VALUE SPACE.
014600         07  RPTTTL-HEADING-2-FIELD        PIC X(60) VALUE SPACE.
014700         07  RPTTTL-HEADING-3-FIELD        PIC X(60) VALUE SPACE.
014800     05  FILLER1 REDEFINES REPORT-TITLE-FIELDS.
014900         07  TITLE-FIELD               PIC X(60) OCCURS 6 TIMES.
015000     05  REPORT-CTL-DESC-FIELDS.
015100         07  RPTTTL-BREAK-1-FIELD          PIC X(40) VALUE SPACE.
015200         07  RPTTTL-COL-1-FIELD            PIC 9(03) VALUE ZERO.
015300         07  RPTTTL-BREAK-2-FIELD          PIC X(40) VALUE SPACE.
015400         07  RPTTTL-COL-2-FIELD            PIC 9(03) VALUE ZERO.
015500         07  RPTTTL-BREAK-3-FIELD          PIC X(40) VALUE SPACE.
015600         07  RPTTTL-COL-3-FIELD            PIC 9(03) VALUE ZERO.
015700         07  RPTTTL-BREAK-4-FIELD          PIC X(40) VALUE SPACE.
015800         07  RPTTTL-COL-4-FIELD            PIC 9(03) VALUE ZERO.
015900         07  RPTTTL-BREAK-5-FIELD          PIC X(40) VALUE SPACE.
016000         07  RPTTTL-COL-5-FIELD            PIC 9(03) VALUE ZERO.
016100     05  FILLER2 REDEFINES REPORT-CTL-DESC-FIELDS.
016200         07  CTL-DESC-LINE                 OCCURS 5 TIMES.
016300             09  CTL-BREAK-FIELD           PIC X(40).
016400             09  CTL-COL-FIELD             PIC 9(03).
016500     05  IDX                     PIC 9(02) VALUE 0.
016600
016700 LINKAGE SECTION.
016800 01  RPT-PFKEY               PIC 9(02).
016900     COPY WSRPTCFL.
017000     COPY WSRPTREC.
017100
017200 PROCEDURE DIVISION USING
017300     RPT-PFKEY
017400     RPT-CTL-FIELDS
017500     RPT-RECORDS.
017600
017700 MAIN-LOGIC SECTION.
017800 PROGRAM-BEGIN.
000000     MOVE "RPTTIT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
017900     PERFORM OPENING-PROCEDURE.
018000     PERFORM MAIN-PROCESS.
018100     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTTIT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
018200 MAIN-LOGIC-EXIT.
018300     EXIT PROGRAM.
018400 MAIN-LOGIC-STOP.
018500     STOP RUN.
018600 LEVEL-2 SECTION.
018700 OPENING-PROCEDURE.
018800     OPEN I-O  CRT-FILE.
018900 CLOSING-PROCEDURE.
019000     CLOSE CRT-FILE.
019100
019200 MAIN-PROCESS.
019300     PERFORM LOAD-DEFAULTS.
019400     PERFORM GET-THE-DATA.
019500
019600
019700 LOAD-DEFAULTS.
019800     PERFORM LOAD-TITLES
019900         VARYING IDX FROM 1 BY 1
020000         UNTIL IDX > 6.
020100
020200     PERFORM LOAD-CTL-BREAKS
020300         VARYING IDX FROM 1 BY 1
020400         UNTIL IDX > 5.
020500
020600     MOVE DISPLAY-HI-FAC TO FAC OF UPPER-MSG-ITEM.
020700
020800     MOVE UPPER-ENTRY-FAC TO
020900          FAC OF RPTTTL-TITLE-1-ITEM
021000          FAC OF RPTTTL-TITLE-2-ITEM
021100          FAC OF RPTTTL-TITLE-3-ITEM
021200          FAC OF RPTTTL-HEADING-1-ITEM
021300          FAC OF RPTTTL-HEADING-2-ITEM
021400          FAC OF RPTTTL-HEADING-3-ITEM
021500          FAC OF RPTTTL-BREAK-1-ITEM
021600          FAC OF RPTTTL-BREAK-2-ITEM
021700          FAC OF RPTTTL-BREAK-3-ITEM
021800          FAC OF RPTTTL-BREAK-4-ITEM
021900          FAC OF RPTTTL-BREAK-5-ITEM.
022000
022100     MOVE NUMERIC-ENTRY-FAC TO
022200          FAC OF RPTTTL-COL-1-ITEM
022300          FAC OF RPTTTL-COL-2-ITEM
022400          FAC OF RPTTTL-COL-3-ITEM
022500          FAC OF RPTTTL-COL-4-ITEM
022600          FAC OF RPTTTL-COL-5-ITEM.
022700
022800 LOAD-TITLES.
022900     MOVE RTT-TITLE (IDX) TO TITLE-FIELD (IDX).
023000
023100 LOAD-CTL-BREAKS.
023200     MOVE RCD-BREAK (IDX) TO CTL-BREAK-FIELD (IDX).
023300     IF RCD-BREAK (IDX) = SPACES
023400         MOVE 1 TO CTL-COL-FIELD (IDX)
023500     ELSE
023600         MOVE RCD-COL (IDX) TO CTL-COL-FIELD (IDX).
023700
023800 RELOAD-DEFAULTS.
023900     PERFORM RELOAD-TITLES
024000         VARYING IDX FROM 1 BY 1
024100         UNTIL IDX > 6.
024200
024300     PERFORM RELOAD-CTL-BREAKS
024400         VARYING IDX FROM 1 BY 1
024500         UNTIL IDX > 5.
024600
024700 RELOAD-TITLES.
024800     MOVE TITLE-FIELD (IDX) TO RTT-TITLE (IDX).
024900
025000 RELOAD-CTL-BREAKS.
025100     MOVE CTL-BREAK-FIELD (IDX) TO RCD-BREAK (IDX).
025200     IF CTL-BREAK-FIELD (IDX) NOT = SPACES
025300         MOVE CTL-COL-FIELD (IDX) TO RCD-COL (IDX)
025400     ELSE
025500         MOVE 1 TO RCD-COL (IDX).
025600
025700 GET-THE-DATA.
025800     MOVE SPACES TO SCREEN-ERROR-STATUS.
025900
026000     DISPLAY AND READ ALTERED REPORT-TITLE-SCREEN ON CRT-FILE
026100     PFKEYS 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 16.
026200
026300     MOVE SPACE TO UPPER-MSG-FIELD.
026400
026500     IF PFKEY-16-PRESSED
026600         MOVE 1 TO RPT-PFKEY
026700     ELSE
026800         PERFORM VAL-DATA-ENTRY
026900         IF SCREEN-ENTRY-ERROR
027000             GO TO GET-THE-DATA
027100         ELSE
027200         PERFORM RELOAD-DEFAULTS
027300         IF ENTER-KEY-PRESSED
027400             MOVE 3 TO RPT-PFKEY
027500         ELSE
027600             MOVE PFKEY-CODE TO RPT-PFKEY.
027700
027800 VAL-DATA-ENTRY.
027900
028000     IF RPTTTL-TITLE-1-FIELD = SPACES
028100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
028200         MOVE "* Error * First title line is required"
028300             TO UPPER-MSG-FIELD
028400         SET ERROR-BIT IN FAC OF RPTTTL-TITLE-1-ITEM ON.
028500
028600     IF NOT SCREEN-ENTRY-ERROR
028700         PERFORM VAL-COL-ENTRY.
028800
028900 VAL-COL-ENTRY.
029000     PERFORM VAL-EACH-COL
029100         VARYING IDX FROM 1 BY 1
029200         UNTIL SCREEN-ENTRY-ERROR  OR
029300               IDX > 5.
029400
029500     IF SCREEN-ENTRY-ERROR
029600         MOVE "* Error * Maximum Column Position is 132"
029700              TO UPPER-MSG-FIELD
029800         IF IDX = 1
029900             SET ERROR-BIT IN FAC OF RPTTTL-COL-1-ITEM ON
030000         ELSE
030100         IF IDX = 2
030200             SET ERROR-BIT IN FAC OF RPTTTL-COL-2-ITEM ON
030300         ELSE
030400         IF IDX = 3
030500             SET ERROR-BIT IN FAC OF RPTTTL-COL-3-ITEM ON
030600         ELSE
030700         IF IDX = 4
030800             SET ERROR-BIT IN FAC OF RPTTTL-COL-4-ITEM ON
030900         ELSE
031000         IF IDX = 5
031100             SET ERROR-BIT IN FAC OF RPTTTL-COL-5-ITEM ON.
031200
031300 VAL-EACH-COL.
031400     IF CTL-COL-FIELD (IDX) > 132
031500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
031600
031700
000000     COPY PLKTRACE.
