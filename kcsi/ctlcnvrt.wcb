000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.     CTLCNVRT.
000300 AUTHOR.         HEATHER MLODINOW.
000400 DATE-WRITTEN.   13-JAN-1993.
000500
000600*    THIS UTILITY TAKES A CONSECUTIVE 130-BYTE RECORD FILE
000700*    AND MAKES AN INDEXED CONTROL FILE OF IT.  IF THE USER
000800*    SELECTS TO REARRANGE THE ALTERNATE KEYS TO MEET
000900*    ACCUCOBOL'S STANDARD, THE ALTERNATE KEYS ARE PLACED
001000*    IN THE ORDER IN WHICH THEY APPEAR IN THE FILE.  IF TWO
001100*    ALTERNATE KEYS ARE DEFINED BEGINNING IN THE SAME POSITION
001200*    IT'S FIRST-IN-ORDER-DEFINED-FIRST.
001300
001400 ENVIRONMENT DIVISION.
001500
001600 CONFIGURATION SECTION.
001700
001800 SOURCE-COMPUTER. WANG-VS.
001900 OBJECT-COMPUTER. WANG-VS.
002000
002100 FIGURATIVE-CONSTANTS.
002200
002300     DISPLAY-HI-FAC          IS "84"
002400     DISPLAY-LOW-FAC         IS "8C"
002500     POSITION-CURSOR         IS "A0"
002600     ERROR-BIT               IS "10"
002700     FAC-BIT                 IS "80"
002800     UPPER-ENTRY-FAC         IS "81"
002900     DIM-MOD-ALL-ULINE-FAC   IS "A8".
003000
003100 INPUT-OUTPUT SECTION.
003200
003300 FILE-CONTROL.
003400
003500     SELECT CRT-FILE
003600         ASSIGN TO "CRTFILE", "DISPLAY"
003700
003800         ORGANIZATION    IS  SEQUENTIAL
003900         ACCESS MODE     IS  RANDOM
004000         RELATIVE KEY    IS  CRT-ROW-KEY
004100         PFKEY           IS  PFKEY-CODE
004200         CURSOR POSITION IS  CURSOR-POSITION
004300         FILE STATUS     IS  CRT-FILE-STATUS.
004400
000000*$SEQBINARY
004500     SELECT IN-CTL-FILE
004600         ASSIGN TO "INCTL", "DISK", NODISPLAY
004700         ORGANIZATION IS SEQUENTIAL
004800         ACCESS MODE IS SEQUENTIAL
004900         FILE STATUS IS IN-CTL-FILE-STATUS.
005000
005100     SELECT OUT-CTL-FILE
005200         ASSIGN TO "OUTCTL", "DISK", NODISPLAY
005300         ORGANIZATION    IS  INDEXED
005400         ACCESS MODE     IS  DYNAMIC
005500         RECORD KEY      IS  OUT-CTL-KEY
005600         FILE STATUS     IS  OUT-CTL-FILE-STATUS.
005700
005800/ *** DATA DIVISION ***
005900
006000 DATA DIVISION.
006100
006200 FILE SECTION.
006300
006400 FD  CRT-FILE
006500     LABEL RECORDS ARE   OMITTED,
006600     RECORD CONTAINS     84 TO 1924 CHARACTERS,
006700     DATA RECORDS ARE  CRT-RECORD
006800                       CRT-1-ROW-RECORD.
006900
007000 01  CRT-RECORD.
007100
007200     03  CRT-ORDER-AREA.
007300         05  CRT-ORDER-AREA-1                PIC X(01).
007400         05  CRT-ORDER-AREA-2                PIC X(01).
007500         05  CRT-ORDER-AREA-3                PIC X(01).
007600         05  CRT-ORDER-AREA-4                PIC X(01).
007700
007800     03  CRT-SCREEN-AREA                     PIC X(1920).
007900
008000 01  CRT-1-ROW-RECORD.
008100
008200     03  CRT-1-ROW-ORDER-AREA                PIC X(04).
008300     03  CRT-1-ROW-SCREEN-AREA.
008400         05  CRT-1-ROW-COLUMN                PIC X(01)
008500                                                 OCCURS 80 TIMES.
008600
008700 FD  OUT-CTL-FILE
008800     LABEL RECORDS ARE STANDARD
008900     RECORD CONTAINS 130 COMPRESSED CHARACTERS
009000     DATA RECORD IS OUT-CTL-RECORD
009100     VALUE OF
009200         FILENAME  IS  OUT-CTL-FILE-NAME
009300         LIBRARY   IS  OUT-CTL-LIB
009400         VOLUME    IS  OUT-CTL-VOL
009500         SPACE     IS  OUT-CTL-SPACE.
009600
009700 01  OUT-CTL-RECORD.
009800     03  FILLER                  PIC X(2).
009900     03  OUT-CTL-KEY             PIC X(8).
010000     03  OUT-CTL-DATA            PIC X(120).
010100     03  OUT-CTL-ALTKEYS         REDEFINES OUT-CTL-DATA.
010200         05  OUT-CTL-ONE-ALTKEY  PIC X(9) OCCURS 8 TIMES.
010300         05  FILLER              PIC X(48).
010400     03  OUT-CTL-FLDS            REDEFINES OUT-CTL-DATA.
010500         05  FILLER              PIC X(4).
010600         05  OUT-CTL-ALTKEY-POS  PIC 9(4).
010700         05  FILLER              PIC X(112).
010800
010900 FD  IN-CTL-FILE
011000     LABEL RECORDS ARE STANDARD
011100     RECORD CONTAINS 130 COMPRESSED CHARACTERS
011200     DATA RECORD IS IN-CTL-RECORD
011300     VALUE OF
011400         FILENAME  IS  IN-CTL-FILE-NAME
011500         LIBRARY   IS  IN-CTL-LIB
011600         VOLUME    IS  IN-CTL-VOL
011700         SPACE     IS  IN-CTL-SPACE.
011800
011900 01  IN-CTL-RECORD.
012000     03  FILLER                  PIC X(130).
012100
012200/ *** WORKING STORAGE SECTION ***
012300
000000 WORKING-STORAGE SECTION.
000000 
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctlcnvrt.wcb 1.10 9/11/94".
012500
012600 01  FILE-STATUS-CODE-TABLE.
012700     05  I-O-OK                      PIC  X(2) VALUE "00".
012800     05  AT-END                      PIC  X(2) VALUE "10".
012900     05  INVALID-KEY                 PIC  X(2) VALUE "21".
013000     05  DUPLICATE-KEY               PIC  X(2) VALUE "22".
013100     05  RECORD-NOT-FOUND            PIC  X(2) VALUE "23".
013200
013300 77  CRT-ROW-KEY                         PIC 9(2)  VALUE 1.
013400
013500 01  CRT-1-ROW-MESSAGE-AREA.
013600     05  FILLER                          PIC X(01)   VALUE SPACES.
013700     05  CRT-1-ROW-WAIT-MESSAGE          PIC X(78)   VALUE SPACES.
013800
013900 01  CRT-FILE-STATUS                     PIC X(2)  VALUE SPACES.
014000 01  FILLER                             REDEFINES CRT-FILE-STATUS.
014100     05  CRT-FILE-STATUS-1               PIC X(01).
014200     05  CRT-FILE-STATUS-2               PIC X(01).
014300
014400 01  CURSOR-POSITION.
014500     03  CURSOR-COLUMN                   BINARY    VALUE ZEROES.
014600     03  CURSOR-ROW                      BINARY    VALUE ZEROES.
014700
014800 77  PFKEY-CODE                          PIC 9(2)  VALUE ZEROES.
014900     88  PFKEY-16-PRESSED                    VALUE 16.
015000
015100 01  CRT-ORDER-AREA-WS.
015200     03  CRT-LINE-NUMBER                 PIC X(1).
015300     03  CRT-WCC-BYTE                    PIC X(1).
015400     03  CURSOR-ORDER-AREA.
015500         05  CRT-CURSOR-COL-LOC          PIC X(1).
015600         05  CRT-CURSOR-ROW-LOC          PIC X(1).
015700
015800 77  SAVE-CURSOR-ORDER-AREA              PIC X(2)  VALUE SPACES.
015900 77  CRT-WCC-BYTE-SAVE                   PIC X(1).
016000
016100 01  LOW-POS                     PIC 9(4) VALUE 0.
016200 01  LOW-IDX                     PIC 9(2) VALUE 0.
016300 01  CTL-IDX                     PIC 9(2) VALUE 0.
016400 01  SEQ-IDX                     PIC 9(2) VALUE 0.
016500
016600 01  SEQ-TABLE.
016700     05  SEQ-ONE-ALTKEY          PIC X(9) OCCURS 16 TIMES.
016800
016900 01  ALTKEY-TABLE.
017000     05  WS-ALTKEYS              OCCURS 16 TIMES.
017100         07  WS-ONE-ALTKEY.
017200             09  WS-ALTKEY-NAME      PIC X(8).
017300             09  WS-ALTKEY-DUPS      PIC X(1).
017400         07  WS-ALTKEY-POS       PIC 9(4).
017500
017600 01  IN-CTL-SPACE                PIC 9(6) VALUE 1.
017700 01  IN-CTL-FILE-STATUS          PIC X(2) VALUE SPACES.
017800 01  IN-CTL-FILE-DATA.
017900     05  IN-CTL-FILE-NAME        PIC X(8) VALUE SPACES.
018000     05  IN-CTL-LIB              PIC X(8) VALUE SPACES.
018100     05  IN-CTL-VOL              PIC X(6) VALUE SPACES.
018200
018300 01  OUT-CTL-SPACE               PIC 9(6) VALUE 1.
018400 01  OUT-CTL-FILE-STATUS         PIC X(2) VALUE SPACES.
018500 01  OUT-CTL-FILE-DATA.
018600     05  OUT-CTL-FILE-NAME       PIC X(8) VALUE SPACES.
018700     05  OUT-CTL-LIB             PIC X(8) VALUE SPACES.
018800     05  OUT-CTL-VOL             PIC X(6) VALUE SPACES.
018900
019000 01  ST-PTR                      BINARY VALUE 0.
019100 01  WS-IDX                      PIC 9(2) VALUE 0.
000000 01  WSA-IDX                     PIC 9(2) VALUE 0.
019200
019300 01  FN-FILE                     PIC X(8) VALUE "?".
019400 01  FN-LIB                      PIC X(8) VALUE SPACES.
019500 01  FN-VOL                      PIC X(8) VALUE SPACES.
019600
019700 01  FN-START-INT.
019800     05  FILLER                  BINARY VALUE 0.
019900     05  FN-START                BINARY VALUE 1.
020000
020100 01  FN-GET-INT.
020200     05  FILLER                  BINARY VALUE 0.
020300     05  FN-GET                  BINARY VALUE 1.
020400
020500 01  FN-UFB-DATA.
020600     05  FN-UFB-MASS             PIC X(1320).
020700     05  FILLER                  REDEFINES
020800         FN-UFB-MASS             OCCURS 60.
020900         10  FN-UFB-VOL          PIC X(6).
021000         10  FN-UFB-LIB          PIC X(8).
021100         10  FN-UFB-FILE-NAME    PIC X(8).
021200/
021300 01  ENTRY-SCRN USAGE IS DISPLAY-WS.
021400     05  FILLER              PIC X(80)  ROW 02 COLUMN 04
021500         VALUE   "King Computer Services, Inc.".
021600     05  FILLER              PIC X(40)  ROW 04 COLUMN 23
021700         VALUE   "INFORMATION REQUIRED BY CONVERSION".
021800     05  FILLER              PIC X(40)  ROW 05 COLUMN 23
021900         VALUE   "       OF CONSECUTIVE FILES".
022000     05  FILLER              PIC X(40)  ROW 06 COLUMN 23
022100         VALUE   "     TO INDEXED CONTROL FILES".
022200
022300     05  SCRN-INS-ITEM-1     PIC X(76)  ROW 09 COLUMN 04
022400         VALUE   "Please specify the library and volume of the con
022500-                "secutive files, the ".
022600
022700     05  SCRN-INS-ITEM-2     PIC X(76)  ROW 10 COLUMN 04
022800         VALUE   "library and volume where the control files are t
022900-                "o reside, press (ENTER):".
023000
023100     05  FILLER              PIC X(18)  ROW 13 COLUMN 04
023200         VALUE   "Consecutive files:".
023300
023400     05  FILLER              PIC X(07)  ROW 13 COLUMN 24
023500         VALUE   "Library".
023600     05  SCRN-IN-LIB-ITEM    PIC X(08)  ROW 13 COLUMN 35
023700         SOURCE  SCRN-IN-LIB-FLD
023800         OBJECT  SCRN-IN-LIB-FLD.
023900
024000     05  FILLER              PIC X(06)  ROW 13 COLUMN 44
024100         VALUE   "Volume".
024200     05  SCRN-IN-VOL-ITEM    PIC X(06)  ROW 13 COLUMN 55
024300         SOURCE  SCRN-IN-VOL-FLD
024400         OBJECT  SCRN-IN-VOL-FLD.
024500
024600     05  FILLER              PIC X(19)  ROW 15 COLUMN 04
024700         VALUE   "Control files:".
024800
024900     05  FILLER              PIC X(07)  ROW 15 COLUMN 24
025000         VALUE   "Library".
025100     05  SCRN-OUT-LIB-ITEM   PIC X(08)  ROW 15 COLUMN 35
025200         SOURCE  SCRN-OUT-LIB-FLD
025300         OBJECT  SCRN-OUT-LIB-FLD.
025400
025500     05  FILLER              PIC X(06)  ROW 15 COLUMN 44
025600         VALUE   "Volume".
025700     05  SCRN-OUT-VOL-ITEM   PIC X(06)  ROW 15 COLUMN 55
025800         SOURCE  SCRN-OUT-VOL-FLD
025900         OBJECT  SCRN-OUT-VOL-FLD.
026000
026100     05  FILLER              PIC X(30)  ROW 17 COLUMN 04
026200         VALUE   "Accucobol alt key format ?".
026300     05  ALT-KEY-ITEM        PIC X(01)  ROW 17 COLUMN 35
026400         SOURCE  ALT-KEY-FLG
026500         OBJECT  ALT-KEY-FLG.
026600
026700     05  MSG-ITEM-1          PIC X(79)  ROW 23 COLUMN 02
026800         SOURCE  MSG-FLD-1.
026900     05  MSG-ITEM-2          PIC X(79)  ROW 24 COLUMN 02
027000         SOURCE  MSG-FLD-2.
027100
027200 01  SCRN-ALPH-FLDS.
027300     05  SCRN-IN-LIB-FLD         PIC X(8) VALUE SPACES.
027400     05  SCRN-IN-VOL-FLD         PIC X(6) VALUE SPACES.
027500     05  SCRN-OUT-LIB-FLD        PIC X(8) VALUE SPACES.
027600     05  SCRN-OUT-VOL-FLD        PIC X(8) VALUE SPACES.
027700     05  MSG-FLD-1               PIC X(79) VALUE SPACES.
027800     05  MSG-FLD-2               PIC X(79) VALUE SPACES.
027900     05  ALT-KEY-FLG             PIC X(1) VALUE "N".
028000
028100 01  PROC-MSG.
028200     05  FILLER              PIC X(19)
028300         VALUE   "Processing file:".
028400     05  PROC-FILE-FLD       PIC X(08).
028500     05  FILLER              PIC X(04)
028600         VALUE   " in".
028700     05  PROC-LIB-FLD        PIC X(08).
028800     05  FILLER              PIC X(04)
028900         VALUE   " on".
029000     05  PROC-VOL-FLD        PIC X(06).
029100
029200/ *** PROCEDURE DIVISION ***
029300
029400 PROCEDURE DIVISION.
029500
032800/ *** LEVEL-1 - PROGRAM MAIN LOGIC ***
032900
033000 MAIN-LOGIC.
033100
033200     OPEN I-O CRT-FILE.
033300     PERFORM ENTER-PGM-PARAMS.
033400     IF NOT PFKEY-16-PRESSED
033500         IF FN-UFB-MASS NOT = SPACES
033600             PERFORM PROCESS-THE-FILES
033700             CLOSE CRT-FILE.
033800
033900 MAIN-LOGIC-EXIT.
034000     STOP RUN.
034100
034200 ENTER-PGM-PARAMS.
034300     MOVE DISPLAY-HI-FAC TO FAC OF MSG-ITEM-1
034400                            FAC OF MSG-ITEM-2.
034500
034600     DISPLAY AND READ ALTERED ENTRY-SCRN ON CRT-FILE
034700     PFKEY 16.
034800
034900     IF NOT PFKEY-16-PRESSED
035000         IF SCRN-OUT-LIB-FLD = SPACES  OR
035100            SCRN-IN-LIB-FLD = SPACES  OR
035200            SCRN-IN-VOL-FLD = SPACES  OR
035300            SCRN-OUT-VOL-FLD = SPACES
035400             MOVE "Please enter library and volume names"
035500                 TO MSG-FLD-2
035600             GO TO ENTER-PGM-PARAMS
035700         ELSE
035800             PERFORM CHECK-IF-IN-LIB-VOL-EXISTS
035900             IF FN-UFB-LIB (1) = SPACES  OR
036000                FN-UFB-VOL (1) = SPACES
036100                MOVE "Invalid sequential files library or volume"
036200                     TO MSG-FLD-2
036300                 GO TO ENTER-PGM-PARAMS
036400             ELSE
036500                 PERFORM CHECK-IF-OUT-VOL-EXISTS
036600                 IF FN-UFB-VOL(1) = SPACES
036700                     MOVE "Invalid output volume" TO MSG-FLD-2
036800                     GO TO ENTER-PGM-PARAMS
036900                 ELSE
037000                 IF ALT-KEY-FLG NOT = "N"  AND
037100                    ALT-KEY-FLG NOT = "Y"
037200                     MOVE "N" TO ALT-KEY-FLG
037300                 ELSE
037400                     MOVE 1 TO ST-PTR
037500                     PERFORM FIND-THE-FILES.
037600
037700 PROCESS-THE-FILES.
037800     PERFORM PROCESS-ONE-FILE
037900         VARYING WSA-IDX FROM 1 BY 1
038000         UNTIL WSA-IDX > 60  OR
038100               FN-UFB-FILE-NAME (WSA-IDX) = SPACES.
038200
038300     IF WSA-IDX > 60
038400         ADD 60 TO ST-PTR
038500         PERFORM FIND-THE-FILES
038600         GO TO PROCESS-THE-FILES.
038700
038800     PERFORM ENTER-PGM-PARAMS.
038900     IF NOT PFKEY-16-PRESSED
039000         IF FN-UFB-MASS NOT = SPACES
039100             GO TO PROCESS-THE-FILES.
039200
039300 PROCESS-ONE-FILE.
039400     MOVE FN-UFB-FILE-NAME (WSA-IDX) TO IN-CTL-FILE-NAME
039500                                  PROC-FILE-FLD
039600                                  OUT-CTL-FILE-NAME.
039700
039800     MOVE FN-UFB-LIB (WSA-IDX) TO IN-CTL-LIB
039900                                 PROC-LIB-FLD.
040000     MOVE FN-UFB-VOL (WSA-IDX) TO IN-CTL-VOL
040100                                 PROC-VOL-FLD.
040200
040300     MOVE PROC-MSG TO CRT-1-ROW-WAIT-MESSAGE.
040400     MOVE 24         TO CRT-ROW-KEY.
040500     MOVE LOW-VALUES TO CRT-1-ROW-ORDER-AREA.
040600     MOVE CRT-1-ROW-MESSAGE-AREA TO CRT-1-ROW-SCREEN-AREA.
040700     MOVE DISPLAY-HI-FAC TO CRT-1-ROW-COLUMN (1).
040800     REWRITE CRT-1-ROW-RECORD.
040900     CLOSE CRT-FILE.
041000     OPEN I-O CRT-FILE.
041100
041200     OPEN INPUT IN-CTL-FILE.
041300     READ IN-CTL-FILE NEXT RECORD
000000         AT END MOVE AT-END TO IN-CTL-FILE-STATUS.
041400
041500     MOVE SCRN-OUT-LIB-FLD TO OUT-CTL-LIB.
041600     MOVE SCRN-OUT-VOL-FLD TO OUT-CTL-VOL.
041700     MOVE IN-CTL-SPACE TO OUT-CTL-SPACE.
041800
041900     OPEN OUTPUT OUT-CTL-FILE.
042000     CLOSE OUT-CTL-FILE.
042100     OPEN I-O OUT-CTL-FILE.
042200
042300     PERFORM PROCESS-IN-CTL-RECS
042400         UNTIL IN-CTL-FILE-STATUS NOT = I-O-OK.
042500
042600     IF ALT-KEY-FLG = "Y"
042700         PERFORM REARRANGE-ALT-KEYS.
042800
042900     CLOSE OUT-CTL-FILE
043000           IN-CTL-FILE.
043100
043200 PROCESS-IN-CTL-RECS.
043300     MOVE IN-CTL-RECORD TO OUT-CTL-RECORD.
043400     WRITE OUT-CTL-RECORD
000000         INVALID KEY 
000000           MOVE DUPLICATE-KEY TO OUT-CTL-FILE-STATUS.
043500     READ IN-CTL-FILE NEXT RECORD
000000         AT END MOVE AT-END TO IN-CTL-FILE-STATUS.
043600
043700 REARRANGE-ALT-KEYS.
043800
043900     PERFORM INIT-ALTKEY-TABLES
044000         VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 16.
044100
044200     MOVE " KEY1" TO OUT-CTL-KEY.
044300     PERFORM READ-OUT-CTL-FILE-RECORD.
044400     IF OUT-CTL-FILE-STATUS = I-O-OK
044500         MOVE 1 TO CTL-IDX
044600                   WS-IDX
044700         PERFORM MOVE-ALT-KEYS-IN.
044800
044900     IF WS-ALTKEY-NAME (8) NOT = SPACES
045000         MOVE " KEY2" TO OUT-CTL-KEY
045100         PERFORM READ-OUT-CTL-FILE-RECORD
045200         IF OUT-CTL-FILE-STATUS = I-O-OK
045300             MOVE 1 TO CTL-IDX
045400             PERFORM MOVE-ALT-KEYS-IN.
045500
045600     IF WS-ALTKEY-NAME (1) NOT = SPACES
045700         PERFORM GET-ALTKEY-POS
045800             VARYING WS-IDX FROM 1 BY 1
045900             UNTIL WS-IDX > 16  OR
046000                   WS-ALTKEY-NAME (WS-IDX) = SPACES
046100         MOVE 1 TO LOW-IDX
046200                   SEQ-IDX
046300         MOVE WS-ALTKEY-POS (1) TO LOW-POS
046400         PERFORM REORDER-THE-ALTKEYS
046500         PERFORM WRITE-THEM-OUT.
046600
000000 READ-OUT-CTL-FILE-RECORD.
051700     READ OUT-CTL-FILE RECORD
000000        INVALID KEY 
000000         MOVE RECORD-NOT-FOUND TO OUT-CTL-FILE-STATUS.
000000
000000 HOLD-OUT-CTL-RECORD.
051700     READ OUT-CTL-FILE RECORD WITH HOLD
000000        INVALID KEY 
000000         MOVE RECORD-NOT-FOUND TO OUT-CTL-FILE-STATUS.
000000
046700 REORDER-THE-ALTKEYS.
046800
046900     PERFORM FIND-ANYTHING-LOWER
047000         VARYING WS-IDX FROM LOW-IDX  BY 1
047100         UNTIL WS-IDX > 16.
047200
047300     PERFORM MOVE-TO-TABLE.
047400     IF LOW-POS NOT = 0
047500         GO TO REORDER-THE-ALTKEYS.
047600
047700 FIND-ANYTHING-LOWER.
047800     IF WS-ONE-ALTKEY (WS-IDX) NOT = SPACES
047900         IF WS-ALTKEY-POS (WS-IDX) < LOW-POS
048000             MOVE WS-IDX TO LOW-IDX
048100             MOVE WS-ALTKEY-POS (WS-IDX) TO LOW-POS.
048200
048300 MOVE-TO-TABLE.
048400     MOVE WS-ONE-ALTKEY (LOW-IDX) TO SEQ-ONE-ALTKEY (SEQ-IDX).
048500     MOVE SPACES TO WS-ONE-ALTKEY (LOW-IDX).
048600     ADD 1 TO SEQ-IDX.
048700
048800     ADD 1 TO LOW-IDX.
048900     PERFORM MOVE-EQUAL-VALUES
049000         VARYING WS-IDX FROM LOW-IDX BY 1 UNTIL WS-IDX > 16.
049100
049200     MOVE 0 TO LOW-POS.
049300     PERFORM INIT-NEXT-LOW-VALUE
049400         VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 16
049500             OR LOW-POS NOT = 0.
049600
049700 INIT-NEXT-LOW-VALUE.
049800     IF WS-ONE-ALTKEY (WS-IDX) NOT = SPACES
049900         MOVE WS-ALTKEY-POS (WS-IDX) TO LOW-POS
050000         MOVE WS-IDX TO LOW-IDX.
050100
050200 MOVE-EQUAL-VALUES.
050300     IF WS-ALTKEY-POS (WS-IDX) = LOW-POS
050400         MOVE WS-ONE-ALTKEY (WS-IDX) TO SEQ-ONE-ALTKEY (SEQ-IDX)
050500         MOVE SPACES TO WS-ONE-ALTKEY (WS-IDX)
050600         ADD 1 TO SEQ-IDX.
050700
050800 INIT-ALTKEY-TABLES.
050900
051000     MOVE SPACES TO SEQ-ONE-ALTKEY (WS-IDX)
051100                    WS-ONE-ALTKEY (WS-IDX).
051200
051300     MOVE 0 TO WS-ALTKEY-POS (WS-IDX).
051400
051500 GET-ALTKEY-POS.
051600     MOVE WS-ALTKEY-NAME (WS-IDX) TO OUT-CTL-KEY.
051700     PERFORM READ-OUT-CTL-FILE-RECORD.
051800
051900     IF OUT-CTL-FILE-STATUS = I-O-OK
052000         MOVE OUT-CTL-ALTKEY-POS TO WS-ALTKEY-POS (WS-IDX)
052100     ELSE
052200         MOVE 9999 TO WS-ALTKEY-POS (WS-IDX).
052300
052400 MOVE-ALT-KEYS-IN.
052500     MOVE OUT-CTL-ONE-ALTKEY (CTL-IDX) TO WS-ONE-ALTKEY (WS-IDX).
052600     ADD 1 TO CTL-IDX.
052700     ADD 1 TO WS-IDX.
052800     IF CTL-IDX NOT = 9
052900         IF OUT-CTL-ONE-ALTKEY (CTL-IDX) NOT = SPACES
053000             GO TO MOVE-ALT-KEYS-IN.
053100
053200 WRITE-THEM-OUT.
053300
053400     MOVE 1 TO CTL-IDX
053500               SEQ-IDX.
053600
053700     MOVE " KEY1" TO OUT-CTL-KEY.
053800     PERFORM HOLD-OUT-CTL-RECORD.
053900     IF OUT-CTL-FILE-STATUS = I-O-OK
054000         PERFORM MOVE-SEQ-KEYS-OUT
054100         REWRITE OUT-CTL-RECORD.
054200
054300     IF SEQ-ONE-ALTKEY (9) NOT = SPACES
054400         MOVE " KEY2" TO OUT-CTL-KEY
053800         PERFORM HOLD-OUT-CTL-RECORD
054600         IF OUT-CTL-FILE-STATUS = I-O-OK
054700             MOVE 1 TO CTL-IDX
054800             PERFORM MOVE-SEQ-KEYS-OUT
054900             REWRITE OUT-CTL-RECORD.
055000
055100 MOVE-SEQ-KEYS-OUT.
055200     MOVE SEQ-ONE-ALTKEY (SEQ-IDX)
055300         TO OUT-CTL-ONE-ALTKEY (CTL-IDX).
055400
055500     ADD 1 TO CTL-IDX.
055600     ADD 1 TO SEQ-IDX.
055700     IF CTL-IDX NOT = 9
055800         IF SEQ-ONE-ALTKEY (SEQ-IDX) NOT = SPACES
055900             GO TO MOVE-SEQ-KEYS-OUT.
056000
056100 CHECK-IF-IN-LIB-VOL-EXISTS.
056200     MOVE "?" TO FN-FILE.
056300     MOVE SCRN-IN-LIB-FLD TO FN-LIB.
056400     MOVE SCRN-IN-VOL-FLD TO FN-VOL.
056500     MOVE 1 TO FN-START
056600               FN-GET.
056700     MOVE SPACES TO FN-UFB-MASS.
056800
056900     CALL "FIND" USING FN-FILE
057000                       FN-LIB
057100                       FN-VOL
057200                       FN-START-INT
057300                       FN-GET-INT
057400                       FN-UFB-MASS.
057500
057600 CHECK-IF-OUT-VOL-EXISTS.
057700     MOVE "?" TO FN-FILE
057800                 FN-LIB.
057900
058000     MOVE SCRN-OUT-VOL-FLD TO FN-VOL.
058100     MOVE 1 TO FN-START
058200               FN-GET.
058300     MOVE SPACES TO FN-UFB-MASS.
058400
058500     CALL "FIND" USING FN-FILE
058600                       FN-LIB
058700                       FN-VOL
058800                       FN-START-INT
058900                       FN-GET-INT
059000                       FN-UFB-MASS.
059100
059200 FIND-THE-FILES.
059300     MOVE "?" TO FN-FILE.
059400     MOVE SCRN-IN-LIB-FLD TO FN-LIB.
059500     MOVE SCRN-IN-VOL-FLD TO FN-VOL.
059600     MOVE ST-PTR TO FN-START
059700     MOVE 60 TO FN-GET.
059800     MOVE SPACES TO FN-UFB-MASS.
059900
060000     CALL "FIND" USING FN-FILE
060100                       FN-LIB
060200                       FN-VOL
060300                       FN-START-INT
060400                       FN-GET-INT
060500                       FN-UFB-MASS.
060600
060700 CLOSING-PROCEDURE.
060800     CLOSE CRT-FILE
060900           IN-CTL-FILE
061000           OUT-CTL-FILE.
061100
