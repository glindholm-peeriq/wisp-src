000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLPRT.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  10/07/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* PROVIDES THE FRONT SCREEN FOR THE LIST ACTION AND THE PRINTING
000800* LOGIC
000900*---------------------------------------------------------------
001000
001100 ENVIRONMENT DIVISION.
001200 CONFIGURATION SECTION.
001300 FIGURATIVE-CONSTANTS.
001400     COPY FIGS.
001500
001600 INPUT-OUTPUT SECTION.
001700
001800 FILE-CONTROL.
001900     COPY SLPRT.
002000     COPY SLCRT.
002100 DATA DIVISION.
002200 FILE SECTION.
002300     COPY FDPRT.
002400     COPY FDCRT.
002500 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT  PIC X(50) VALUE
000000     "@(#)ctrlprt.wcb 1.6 9/11/94".
002600     COPY WSCRT.
002610     COPY WSCONST.
002700 01  CTLSRC-GETPARM-FIELDS.
002800     05  CTLSRC-MSG-ID               PIC X(4) VALUE "0001".
002900     05  CTLSRC-PRNAME               PIC X(8) VALUE "CTLSRC".
003000     05  CTLSRC-MSG-ISSUER           PIC X(6) VALUE "CONTRL".
003100
003200 01  CTLSRC-MSG-COUNT.
003300     05  FILLER                        BINARY   VALUE ZERO.
003400     05  FILLER                        BINARY   VALUE 1.
003500 01  CTLSRC-MSG-TEXT-1.
003600     05  FILLER                  PIC X(24) VALUE
003700     "  List for Control File ".
003800     05  CTLSRC-FILE             PIC X(8).
003900     05  FILLER                  PIC X(12) VALUE
004000     " in Library ".
004100     05  CTLSRC-LIBRARY          PIC X(8).
004200     05  FILLER                  PIC X(11) VALUE
004300     " on Volume ".
004400     05  CTLSRC-VOLUME           PIC X(6).
004500
004600 01  CTLSRC-TEXT-01.
004700     05  FILLER            PIC X(12) VALUE
004800     "Records are ".
004900     05  CTLSRC-FILE-TYPE  PIC X(12).
005000     05  FILLER            PIC X(16) VALUE
005100     " Record size is ".
005200     05  CTLSRC-RECORD-LEN  PIC ZZZ9.
005300 01  CTLSRC-TEXT-02.
005400     05  FILLER            PIC X(21) VALUE
005500     "File organization is ".
005600     05  CTLSRC-FILE-ORG   PIC X(34).
005700 01  CTLSRC-INDEXED-FILE-ORG.
005800     05  FILLER            PIC X(26) VALUE
005900     "indexed, with key field = ".
006000     05  CTLSRC-PRIMARY-KEY PIC X(8).
006100 01  CTLSRC-TEXT-03        PIC X(20).
006200
006300 01  PATHS-FIELDS.
006400     05 PATH-FIELD PIC X(9) OCCURS 16 TIMES.
006500 01  FILLER REDEFINES PATHS-FIELDS.
006600     05  PATHS-1-FIELD PIC X(72).
006700     05  PATHS-2-FIELD PIC X(72).
006800 01  PATH-IDX BINARY.
006900 01  WORK-IDX BINARY.
007000
007100 01  CTLSRC-ERROR-TEXT                  PIC X(40) VALUE
007200         "File Information is incomplete.".
007300 01  CTLSRC-TEXT-05            PIC X(56) VALUE
007400     "Operation Allowed? Operation Allowed? Operation Allowed?".
007500 01  CTLSRC-TEXT-06.
007600     05  FILLER                PIC X(12) VALUE
007700     "REPORT      ".
007800     05  CTLSRC-REPORT-ALLOWED PIC X(3).
007900     05  FILLER                PIC X(16) VALUE
008000     "    UPDATE      ".
008100     05  CTLSRC-UPDATE-ALLOWED PIC X(3).
008200     05  FILLER                PIC X(16) VALUE
008300     "    DELETE      ".
008400     05  CTLSRC-DELETE-ALLOWED PIC X(3).
008500 01  CTLSRC-TEXT-07.
008600     05  FILLER                PIC X(13) VALUE
008700     "Comment 1:".
008800     05  CTLSRC-COMMENT1       PIC X(60).
008900 01  CTLSRC-TEXT-08.
009000     05  FILLER                PIC X(13) VALUE
009100     "Comment 2:".
009200     05  CTLSRC-COMMENT2       PIC X(60).
009300 01  CTLSRC-TEXT-09.
009400     05  FILLER                PIC X(13) VALUE
009500     "Comment 3:".
009600     05  CTLSRC-COMMENT3       PIC X(60).
009700 01  CTLSRC-TEXT-10            PIC X(72) VALUE
009800     "(ENTER) to Continue Display (16)Exit".
009900 01  CTLSRC-TEXT-11            PIC X(17) VALUE
010000     "(SCREEN, PRINTER)".
010100
010200 01  CTLSRC-SCR USAGE IS DISPLAY-WS.
010300     COPY SCGPEM.
010500     05  FILLER  PIC X(69) ROW 7 COLUMN 5
010600     SOURCE CTLSRC-MSG-TEXT-1.
010700     05  FILLER  PIC X(44) ROW 9 COLUMN 14
010800     SOURCE CTLSRC-TEXT-01.
010900     05  FILLER  PIC X(55) ROW 10 COLUMN 14
011000     SOURCE CTLSRC-TEXT-02.
011100     05  FILLER  PIC X(20) ROW 11 COLUMN 14
011200     SOURCE CTLSRC-TEXT-03.
011300     05  FILLER  PIC X(72) ROW 12 COLUMN 4
011400     SOURCE PATHS-1-FIELD.
011500     05  FILLER  PIC X(72) ROW 13 COLUMN 4
011600     SOURCE PATHS-2-FIELD.
011700     05  FILLER  PIC X(40) ROW 14 COLUMN 4
011800     SOURCE CTLSRC-ERROR-TEXT.
011900     05  FILLER  PIC X(56) ROW 15 COLUMN 12
012000     SOURCE CTLSRC-TEXT-05.
012100     05  FILLER  PIC X(53) ROW 16 COLUMN 12
012200     SOURCE CTLSRC-TEXT-06.
012300     05  FILLER  PIC X(73) ROW 18 COLUMN 4
012400     SOURCE CTLSRC-TEXT-07.
012500     05  FILLER  PIC X(73) ROW 19 COLUMN 4
012600     SOURCE CTLSRC-TEXT-08.
012700     05  FILLER  PIC X(72) ROW 20 COLUMN 4
012800     SOURCE CTLSRC-TEXT-09.
012900     05  FILLER  PIC X(17) ROW 22 COLUMN 45
013000     SOURCE CTLSRC-TEXT-11.
013100     05  FILLER  PIC X(72) ROW 24 COLUMN 4
013200     SOURCE CTLSRC-TEXT-10.
013300     05  CTLSRC-DEVICE-ITEM PIC X(7) ROW 22 COLUMN 23
013400     SOURCE CTLSRC-DEVICE
013500     OBJECT CTLSRC-DEVICE.
013600
010400     COPY WSGPEM.
013700 01  CTLSRC-DEVICE-KW              PIC X(8) VALUE "DEVICE".
013800 01  CTLSRC-DEVICE                 PIC X(7) VALUE "SCREEN".
014500 01  SAVE-ACTION-CODE  PIC X(10).
014700     COPY WSGP.
014800     COPY WSINT.
014900     COPY WSPRT.
015000     COPY WSFILEIO.
015100
015200 01  TWO-SPACES            PIC XX VALUE SPACE.
015300* FORMATTED PRINT LINES.
015400 01  FORMAT-FIELD.
015500     05  FF-START-POS      PIC ZZZ9.
015600     05  FILLER            PIC XX.
015700     05  FF-TYPE           PIC X.
015800     05  FILLER            PIC XX.
015900     05  FF-INT-LEN        PIC ZZ9.
016000     05  FILLER            PIC X.
016100     05  FF-EXT-LEN        PIC ZZ9.
016200     05  FILLER            PIC XX.
016300     05  FF-DECIMAL-POS    PIC 9.
016400     05  FILLER            PIC XXX.
016500     05  FF-OCCURRENCES    PIC Z9.
016600     05  FILLER            PIC X(5).
016700     05  RU-FIELD          PIC X(3).
016800     05  FILLER            PIC XXXX.
016900     05  FF-UPDATE-SEQ     PIC ZZ.
017000     05  FILLER            PIC X(2).
017100     05  DATE-STAMP-FIELD  PIC X(5).
017200     05  FILLER            PIC X.
017300     05  FF-CUMMULATIVE-NAME PIC X(8).
017400     05  FILLER            PIC XX.
017500     05  TABLE-OR-RANGE-FIELD PIC X(6).
017600 01  REPORT-ONLY           PIC X(3) VALUE "R".
017700 01  UPDATE-ONLY           PIC X(3) VALUE "U".
017800 01  REPORT-AND-UPDATE     PIC X(3) VALUE "R/U".
017900 01  COLUMN-1             PIC X(70) VALUE
018000     "Field   Start Int Int Ext Dec Occur Report/ Upd  Stamp  Cum
018100-    "    Table/".
018200
018300 01  COLUMN-2             PIC X(70)  VALUE
018400     "Name    Posn  Fmt Len Len Pos Count Update  Seq        Field
018500-    "    Range".
018600
018700 01  LIST-LINE.
018800     05  LIST-NAME  PIC X(8).
018900     05  LIST-FORMAT PIC X(63).
019000 LINKAGE SECTION.
019100 01  CTRL-IO-BLOCK.
019200     COPY LKDIO.
019300 01  CTRL-HEADER-RECORDS.
019400     COPY RCCTRLH.
019500 01  CF-RECORD.
019600     COPY RCCTRLF.
019700     COPY LKCTRLAR.
019800     COPY RCCTRLT1.
019900     COPY RCCTRLT2.
000000     COPY LKPRC.
020000 PROCEDURE DIVISION USING
000000     CALLED-RC
020100     COPY LKCTRL.
020200 MAIN-LOGIC SECTION.
020300 PROGRAM-BEGIN.
000000     MOVE "CTRLPRT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
020400     PERFORM OPENING-PROCEDURE.
020500     PERFORM MAIN-PROCESS.
020600     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CTRLPRT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
020700 MAIN-LOGIC-STOP.
020800     STOP RUN.
020900 LEVEL-2 SECTION.
021000 OPENING-PROCEDURE.
000000     PERFORM OPEN-CRT-FILE.
021100 CLOSING-PROCEDURE.
000000     PERFORM CLOSE-CRT-FILE.  
021300 MAIN-PROCESS.
021400     PERFORM ENTER-KEY.
021500 ENTER-KEY.
021600     PERFORM ENTER-LIST-INIT.
021700     PERFORM ENTER-LIST-ENTRY.
021800 ENTER-LIST-INIT.
021900     PERFORM INIT-CTLSRC-GETPARM.
022000     PERFORM INIT-SORT.
022100 ENTER-LIST-ENTRY.
022200     MOVE SPACE TO SCREEN-ERROR-STATUS.
022300     PERFORM CALL-CTLSRC-GETPARM.
022400     MOVE SPACE TO CTLSRC-ERROR-TEXT.
000000     MOVE PFKEY-CODE TO CALLED-RC.
022500     IF ENTER-KEY-PRESSED
022600         PERFORM VAL-DEVICE
022700         IF SCREEN-ENTRY-ERROR
022800             GO TO ENTER-LIST-ENTRY
022900         ELSE
023000         IF CTLSRC-DEVICE = "PRINTER"
023100             PERFORM PRINTER-LISTING
000000             MOVE 16 TO CALLED-RC
023200         ELSE
023300             PERFORM CALL-SCREEN-LISTING
023400             IF CALLED-RC = 1
023500                 GO TO ENTER-LIST-ENTRY.
023600 CALL-SCREEN-LISTING.
023700     MOVE "LIST" TO CF-ACTION-CODE.
023800     PERFORM CALL-CTRLLST.
023900 CALL-CTRLLST.
000000     PERFORM CLOSE-CRT-FILE.
024000     CALL "CTRLLST" USING
000000     CALLED-RC
024100     COPY LKCTRL.
000000     PERFORM OPEN-CRT-FILE.
024200 PRINTER-LISTING.
024300     MOVE SPACE TO PRINTER-FILE-LIBRARY
024400                   PRINTER-FILE-VOLUME.
024500     MOVE "##CTRL" TO PRINTER-FILE-NAME.
024600     MOVE 500 TO PRINTER-FILE-SPACE.
024700     OPEN OUTPUT PRINTER-FILE.
024800     MOVE 99 TO LINE-COUNT.
024900     MOVE 0 TO PAGE-NO.
025000     PERFORM PRINT-HEADER.
025100     PERFORM PRINT-LINES
025200     CLOSE PRINTER-FILE.
025300 PRINT-HEADER.
025400     MOVE CTLSRC-MSG-TEXT-1 TO DETAIL-LINE.
025500* COVER THE GETPARM CONTROL CHARACTERS
025600     STRING
025700        TWO-SPACES DELIMITED BY SIZE
025800        INTO DETAIL-LINE.
025900     PERFORM PRINT-ONE-LINE.
026000     PERFORM LINE-FEED.
026100
026200     MOVE CTLSRC-TEXT-01 TO DETAIL-LINE.
026300     PERFORM PRINT-ONE-LINE.
026400     MOVE CTLSRC-TEXT-02 TO DETAIL-LINE.
026500     PERFORM PRINT-ONE-LINE.
026600     MOVE CTLSRC-TEXT-03 TO DETAIL-LINE.
026700     PERFORM PRINT-ONE-LINE.
026800     MOVE PATHS-1-FIELD TO DETAIL-LINE.
026900     PERFORM PRINT-ONE-LINE.
027000     MOVE PATHS-2-FIELD TO DETAIL-LINE.
027100     PERFORM PRINT-ONE-LINE.
027200     PERFORM LINE-FEED.
027300     MOVE CTLSRC-TEXT-05 TO DETAIL-LINE.
027400     PERFORM PRINT-ONE-LINE.
027500     MOVE CTLSRC-TEXT-06 TO DETAIL-LINE.
027600     PERFORM PRINT-ONE-LINE.
027700     PERFORM LINE-FEED.
027800     MOVE CTLSRC-TEXT-07 TO DETAIL-LINE.
027900     PERFORM PRINT-ONE-LINE.
028000     MOVE CTLSRC-TEXT-08 TO DETAIL-LINE.
028100     PERFORM PRINT-ONE-LINE.
028200     MOVE CTLSRC-TEXT-09 TO DETAIL-LINE.
028300     PERFORM PRINT-ONE-LINE.
028400     PERFORM LINE-FEED.
028500     MOVE COLUMN-1 TO DETAIL-LINE.
028600     PERFORM PRINT-ONE-LINE.
028700     MOVE COLUMN-2 TO DETAIL-LINE.
028800     PERFORM PRINT-ONE-LINE.
028900     PERFORM LINE-FEED.
029000
029100 PRINT-ONE-LINE.
029200     IF LINE-COUNT > 55
029300         PERFORM FORM-FEED.
029400     WRITE PRINTER-RECORD FROM DETAIL-LINE AFTER ADVANCING 1.
029500 LINE-FEED.
029600     MOVE SPACE TO PRINTER-RECORD.
029700     WRITE PRINTER-RECORD AFTER ADVANCING 1.
029800     ADD 1 TO LINE-COUNT.
029900 FORM-FEED.
030000     MOVE SPACE TO PRINTER-RECORD.
030100     WRITE PRINTER-RECORD AFTER ADVANCING PAGE.
030200     MOVE 1 TO LINE-COUNT.
030300     ADD 1 TO PAGE-NO.
030400 PRINT-LINES.
030500     MOVE 1 TO CF-IDX.
030600     IF T2-NAME(CF-IDX)  NOT = SPACE
030700         PERFORM PRINT-ONE-ENTRY.
030800 PRINT-ONE-ENTRY.
030900     MOVE SPACE TO LIST-LINE.
031000     PERFORM INIT-AN-ENTRY.
031100     MOVE LIST-LINE TO DETAIL-LINE.
031200     PERFORM PRINT-ONE-LINE.
031300     ADD 1 TO CF-IDX.
031400     IF CF-IDX NOT > CONST-CF-MAX-ENTRY
031500        IF T2-NAME(CF-IDX) NOT = SPACE
031600            GO TO PRINT-ONE-ENTRY.
031700 INIT-AN-ENTRY.
031800     MOVE SPACE TO FORMAT-FIELD.
031900     MOVE T2-NAME(CF-IDX) TO LIST-NAME.
032000     PERFORM LOAD-FORMAT-FIELD.
032100
032200     IF T1-REPORT-CODE(CF-IDX) = ZEROES
032300         IF T1-UPDATE-CODE(CF-IDX) = ZEROES
032400             MOVE REPORT-AND-UPDATE TO RU-FIELD
032500         ELSE
032600             MOVE REPORT-ONLY TO RU-FIELD
032700     ELSE
032800     IF T1-UPDATE-CODE(CF-IDX) = ZEROES
032900         MOVE UPDATE-ONLY TO RU-FIELD.
033000     PERFORM INIT-DATE-STAMP.
033100     IF T2-TABLE-NAME(CF-IDX) NOT = SPACE
033200         MOVE T2-TABLE-NAME(CF-IDX) TO
033300           TABLE-OR-RANGE-FIELD
033400     ELSE
033500     IF T2-LO-RANGE(CF-IDX) = SPACE AND
033600        T2-HI-RANGE(CF-IDX) = SPACE
033700         NEXT SENTENCE
033800     ELSE
033900         MOVE "Range" TO TABLE-OR-RANGE-FIELD.
034000
034100     MOVE FORMAT-FIELD TO LIST-FORMAT.
034200 INIT-DATE-STAMP.
034300     IF T2-VALIDATION(CF-IDX) = "DF"
034400         MOVE "Date1" TO DATE-STAMP-FIELD
034500     ELSE
034600     IF T2-VALIDATION(CF-IDX) = "GD"
034700         MOVE "Date2" TO DATE-STAMP-FIELD
034800     ELSE
034900     IF T2-VALIDATION(CF-IDX) = "DS"
035000         MOVE "Day" TO DATE-STAMP-FIELD
035100     ELSE
035200     IF T2-VALIDATION(CF-IDX) = "TS"
035300         MOVE "Time" TO DATE-STAMP-FIELD
035400     ELSE
035500     IF T2-VALIDATION(CF-IDX) = "CS"
035600         MOVE "D&T5" TO DATE-STAMP-FIELD
035700     ELSE
035800     IF T2-VALIDATION(CF-IDX) = "CM"
035900         MOVE "D&T6" TO DATE-STAMP-FIELD.
036000*---------------------------------------------------------------
036100* LIST IS SORTED IN POSITION ORDER
036200* CHANGE AND DELETE ARE IN ALPHABETICAL ORDER
036300*---------------------------------------------------------------
036400 INIT-SORT.
036500     MOVE "SORTPOS" TO CF-ACTION-CODE.
036600     PERFORM CALL-CTRLARY.
036700 CALL-CTRLARY.
036800     CALL "CTRLARY" USING
000000     CALLED-RC
036900     COPY LKCTRL.
037000 VAL-DEVICE.
037100     IF CTLSRC-DEVICE NOT = "SCREEN" AND "PRINTER"
037200         MOVE "'SCREEN or 'PRINTER'" TO CTLSRC-ERROR-TEXT
037300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
037400 CALL-CTLSRC-GETPARM.
037500* NOT REALLY A GETPARM.
037600     MOVE CTLSRC-PRNAME TO EM-PRNAME.
037700     MOVE CTLSRC-MSG-ID TO EM-MSG-ID.
037800     MOVE CTLSRC-MSG-ISSUER TO EM-MSG-ISSUER.
037900     MOVE "CONTROL" TO EM-BY.
038000
038100     DISPLAY AND READ CTLSRC-SCR ON CRT-FILE
038200        PFKEYS 16.
038300
038400 INIT-CTLSRC-GETPARM.
038500     MOVE FILE-NAME OF CTRL-IO-BLOCK TO CTLSRC-FILE.
038600     MOVE FILE-LIBRARY OF CTRL-IO-BLOCK TO CTLSRC-LIBRARY.
038700     MOVE FILE-VOLUME OF CTRL-IO-BLOCK TO CTLSRC-VOLUME.
038800     IF CH-FILE-TYPE = "F"
038900         MOVE "fixed-length" TO CTLSRC-FILE-TYPE
039000     ELSE
039100     IF CH-FILE-TYPE = "V"
039200         MOVE "variable" TO CTLSRC-FILE-TYPE
039300     ELSE
039400     IF CH-FILE-TYPE = "C"
039500         MOVE "compressed" TO CTLSRC-FILE-TYPE.
039600     IF CH-FILE-ORG = "C"
039700         MOVE "consecutive" TO CTLSRC-FILE-ORG
039800         MOVE SPACE TO CTLSRC-TEXT-03
039900                       PATHS-1-FIELD
040000                       PATHS-2-FIELD
000000     ELSE
039600     IF CH-FILE-ORG = "R"
039700         MOVE "relative" TO CTLSRC-FILE-ORG
039800         MOVE SPACE TO CTLSRC-TEXT-03
039900                       PATHS-1-FIELD
040000                       PATHS-2-FIELD
040100     ELSE
040200         MOVE CH-PRIMARY-KEY TO CTLSRC-PRIMARY-KEY
040300         MOVE CTLSRC-INDEXED-FILE-ORG TO CTLSRC-FILE-ORG
040400         MOVE "Alternate paths are:" TO CTLSRC-TEXT-03
040500         PERFORM INIT-ALTKEYS.
040600     MOVE SPACE TO CTLSRC-ERROR-TEXT.
040700
000000     MOVE CH-RECORD-LEN TO CTLSRC-RECORD-LEN.
000000
040800     IF CH-REPORT-ALLOWED = 0
040900         MOVE "YES" TO CTLSRC-REPORT-ALLOWED
041000     ELSE
041100         MOVE "NO " TO CTLSRC-REPORT-ALLOWED.
041200     IF CH-UPDATE-ALLOWED = 0
041300         MOVE "YES" TO CTLSRC-UPDATE-ALLOWED
041400     ELSE
041500         MOVE "NO " TO CTLSRC-UPDATE-ALLOWED.
041600     IF CH-DELETE-ALLOWED = 0
041700         MOVE "YES" TO CTLSRC-DELETE-ALLOWED
041800     ELSE
041900         MOVE "NO " TO CTLSRC-DELETE-ALLOWED.
042000     MOVE CC1-COMMENT TO CTLSRC-COMMENT1.
042100     MOVE CC2-COMMENT TO CTLSRC-COMMENT2.
042200     MOVE CC3-COMMENT TO CTLSRC-COMMENT3.
042300     MOVE "SCREEN" TO CTLSRC-DEVICE.
042400
042500 INIT-ALTKEYS.
042600     MOVE 1 TO PATH-IDX.
042700     PERFORM INIT-ONE-ALTKEY.
042800 INIT-ONE-ALTKEY.
042900     COMPUTE WORK-IDX = PATH-IDX - 8.
043000     IF PATH-IDX > 8
043100         MOVE CA2-KEYNAME(WORK-IDX) TO PATH-FIELD(PATH-IDX)
043200     ELSE
043300         MOVE CA1-KEYNAME(PATH-IDX) TO PATH-FIELD(PATH-IDX).
043400     ADD 1 TO PATH-IDX.
043500     IF PATH-IDX NOT > 16
043600         GO TO INIT-ONE-ALTKEY.
043700
043800     COPY PLCRT.
000000
000000 LOAD-FORMAT-FIELD.
           MOVE T1-START-POS(CF-IDX) TO FF-START-POS.
           MOVE T2-TYPE(CF-IDX) TO FF-TYPE.
           MOVE T1-INT-LEN(CF-IDX) TO FF-INT-LEN.
           MOVE T1-EXT-LEN(CF-IDX) TO FF-EXT-LEN.
           MOVE T1-DECIMAL-POS(CF-IDX) TO FF-DECIMAL-POS.
           MOVE T1-OCCURRENCES(CF-IDX) TO FF-OCCURRENCES.
           MOVE T1-UPDATE-SEQ(CF-IDX) TO FF-UPDATE-SEQ.
           MOVE T2-CUMMULATIVE-NAME(CF-IDX) TO FF-CUMMULATIVE-NAME.
000000     COPY PLKTRACE.
