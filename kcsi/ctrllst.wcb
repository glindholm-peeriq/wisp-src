000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLLST.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  10/07/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* PROVIDES THE FIELD LIST AND SELECTION SCREEN FOR DELETE
000800* AND CHANGE
000900* RETURNS THE SELECTION IN CF-RECORD AND THE INDEX OF THE
001000* SELECTED ITEM IN CF-IDX.
001100*---------------------------------------------------------------
001200
001300 ENVIRONMENT DIVISION.
001400 CONFIGURATION SECTION.
001500 FIGURATIVE-CONSTANTS.
001600     COPY FIGS.
001700
001800 INPUT-OUTPUT SECTION.
001900
002000 FILE-CONTROL.
002100     COPY SLCRT.
002200
002300 DATA DIVISION.
002400 FILE SECTION.
002500     COPY FDCRT.
002600
002700 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrllst.wcb 1.4 9/11/94".
002800 01  SAVE-ACTION-CODE  PIC X(10).
002900     COPY WSCRT.
003000     COPY WSFILEIO.
003010     COPY WSCONST.
003100*---------------------------------------------------------------
003200* LIST SCREEN USED FOR LIST, CHANGE AND DELTE OPTIONS
003300*---------------------------------------------------------------
003400 01  LIST-SCREEN USAGE IS DISPLAY-WS.
003500     05  FILLER   PIC X(31) ROW 01 COLUMN 25
003600         SOURCE LIST-HEADER-FIELD.
003700     05  SUPPLY-PROMPT-ITEM PIC X(44) ROW 03 COLUMN 02 VALUE
003800     "Supply the field name or position the cursor".
003900     05  NAME-PROMPT-ITEM PIC X(13) ROW 5 COLUMN 02 VALUE
004000     "Field Name  :".
000000* Next screen item is deliberately one position too long.
000000* The WISP screen driver does an auto-tab-out action so that
000000* if you type an eight character field, the cursor is tabbed
000000* to the first tab stop. Pressing Enter at that point causes
000000* the first field to be selected rather than the one that
000000* was typed in. This extra space prevents this from occurring.
004100     05  NAME-ITEM        PIC X(9) ROW 5 COLUMN 16
004200         SOURCE NAME-FIELD
004300         OBJECT NAME-FIELD.
004400
004500     05  MESSAGE-ITEM     PIC X(79) ROW 6 COLUMN 2
004600         SOURCE MESSAGE-FIELD.
004700     05  FILLER           PIC X(70)  ROW 07 COLUMN 7 VALUE
004800     "Field   Start Int Int Ext Dec Occur Report/ Upd  Stamp  Cum
004900-    "    Table/".
005000
005100     05  L1-ITEM          PIC X(70)  ROW 08 COLUMN 7 VALUE
005200     "Name    Posn  Fmt Len Len Pos Count Update  Seq        Field
005300-    "    Range".
005400
005500     05  FILLER OCCURS 10 TIMES ROW 09.
005600         10  LIST-NAME-ITEM PIC X(8) ROW 09 COLUMN 05
005700             SOURCE LIST-NAME
005800             OBJECT LIST-NAME.
005900         10  LIST-FORMAT-ITEM PIC X(63) ROW 09 COLUMN 16
006000             SOURCE LIST-FORMAT.
006100     05  FILLER           PIC X(20) ROW 20 COLUMN 03 VALUE
006200     "File organization is".
006300     05  FILLER           PIC X(55) ROW 20 COLUMN 24
006400         SOURCE FILE-ORG-FIELD.
006500     05  FILLER           PIC X(72) ROW 21 COLUMN 5
006600         SOURCE PATHS-1-FIELD.
006700     05  FILLER           PIC X(72) ROW 22 COLUMN 5
006800         SOURCE PATHS-2-FIELD.
006900     05  FOOTER-1-ITEM    PIC X(79) ROW 23 COLUMN 2
007000         SOURCE FOOTER-1-FIELD.
007100     05  FOOTER-2-ITEM    PIC X(79) ROW 24 COLUMN 2
007200         SOURCE FOOTER-2-FIELD.
007300
007400 01  LIST-FIELDS.
007500     05  LIST-HEADER-FIELD PIC X(31).
007600     05  NAME-FIELD  PIC X(8).
007700     05  FILE-ORG-FIELD PIC X(55).
007800     05  LIST-ENTRIES.
007900         10 LIST-ENTRY OCCURS 10 TIMES.
008000            15  LIST-NAME  PIC X(8).
008100            15  LIST-FORMAT PIC X(63).
008200     05  PATHS-FIELDS.
008300         10 PATH-FIELD PIC X(9) OCCURS 16 TIMES.
008400     05  FILLER REDEFINES PATHS-FIELDS.
008500         10  PATHS-1-FIELD PIC X(72).
008600         10  PATHS-2-FIELD PIC X(72).
008700     05  FOOTER-1-FIELD  PIC X(79).
008800     05  FOOTER-2-FIELD  PIC X(79).
008900
009000* NUMBER OF ELEMENTS DISPLAYED DOWN AND THE FIRST ROW
009100 01  MAX-ENTRY  BINARY VALUE 10.
009200 01  FIRST-ROW   BINARY VALUE 9.
009300 01  LAST-ROW    BINARY VALUE 18.
009400 01  ENTRY-IDX  BINARY.
009500 01  PAGE-IDX   BINARY.
009600 01  PATH-IDX  BINARY.
009700 01  WORK-IDX   BINARY.
009800 01  FOUND-IDX  BINARY.
009900
010000 01  SAVE-ARRAY-CODE  PIC X(10).
010100
010200 01  DELETE-HEADER PIC X(31) VALUE
010300     "  Select a field for deletion  ".
010400 01  CHANGE-HEADER PIC X(31) VALUE
010500     "Select a field for modification".
010600 01  LIST-HEADER PIC X(31) VALUE
010700     "   List Control File Fields    ".
010800 01  INDEX-FIELD.
010900     05  FILLER PIC X(20) VALUE
011000     "indexed, with key = ".
011100     05  KEY-FIELD  PIC X(8) VALUE SPACE.
011200     05  FILLER  PIC X(21) VALUE
011300     " Alternate paths are:".
011400
011500 01  FORMAT-FIELD.
011600     05  FF-START-POS      PIC ZZZ9.
011700     05  FILLER            PIC XX.
011800     05  FF-TYPE           PIC X.
011900     05  FILLER            PIC XX.
012000     05  FF-INT-LEN        PIC ZZ9.
012100     05  FILLER            PIC X.
012200     05  FF-EXT-LEN        PIC ZZ9.
012300     05  FILLER            PIC XX.
012400     05  FF-DECIMAL-POS    PIC 9.
012500     05  FILLER            PIC XXX.
012600     05  FF-OCCURRENCES    PIC Z9.
012700     05  FILLER            PIC X(5).
012800     05  RU-FIELD          PIC X(3).
012900     05  FILLER            PIC XXXX.
013000     05  FF-UPDATE-SEQ     PIC ZZ.
013100     05  FILLER            PIC X(2).
013200     05  DATE-STAMP-FIELD  PIC X(5).
013300     05  FILLER            PIC X.
013400     05  FF-CUMMULATIVE-NAME PIC X(8).
013500     05  FILLER            PIC XX.
013600     05  TABLE-OR-RANGE-FIELD PIC X(6).
013700 01  REPORT-ONLY           PIC X(3) VALUE "R".
013800 01  UPDATE-ONLY           PIC X(3) VALUE "U".
013900 01  REPORT-AND-UPDATE     PIC X(3) VALUE "R/U".
014000 LINKAGE SECTION.
014100 01  CTRL-IO-BLOCK.
014200     COPY LKDIO.
014300 01  CTRL-HEADER-RECORDS.
014400     COPY RCCTRLH.
014500 01  CF-RECORD.
014600     COPY RCCTRLF.
014700     COPY LKCTRLAR.
014800     COPY RCCTRLT1.
014900     COPY RCCTRLT2.
000000     COPY LKPRC.
015000 PROCEDURE DIVISION USING
000000     CALLED-RC
015100     COPY LKCTRL.
015200 MAIN-LOGIC SECTION.
015300 PROGRAM-BEGIN.
000000     MOVE "CTRLLST BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
015400     PERFORM OPENING-PROCEDURE.
015500     PERFORM MAIN-PROCESS.
015600     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CTRLLST EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
015900 MAIN-LOGIC-STOP.
016000     STOP RUN.
016100 LEVEL-2 SECTION.
016200 OPENING-PROCEDURE.
016300     PERFORM OPEN-CRT-FILE.
016400 CLOSING-PROCEDURE.
016500     MOVE PFKEY-CODE TO CALLED-RC.
016600     PERFORM CLOSE-CRT-FILE.
016700 MAIN-PROCESS.
016800     PERFORM ENTER-KEY.
016900 ENTER-KEY.
017000     PERFORM ENTER-KEY-INIT.
017100     PERFORM ENTER-KEY-ENTRY.
017200 ENTER-KEY-INIT.
017300     PERFORM INIT-PROMPTS.
017400     PERFORM INIT-FIELDS.
017500     PERFORM INIT-FACS.
017600     PERFORM INIT-PFS.
017700     PERFORM INIT-FOOTERS.
017800     PERFORM INIT-SORT.
017900     PERFORM INIT-PAGE-1.
018000     MOVE SPACE TO MESSAGE-FIELD.
018100 ENTER-KEY-ENTRY.
018200     MOVE SPACE TO SCREEN-ERROR-STATUS.
018300     DISPLAY AND READ LIST-SCREEN ON CRT-FILE
018400      PFKEYS PF-1 PF-2 PF-4 PF-5 PF-16.
018500     MOVE SPACE TO MESSAGE-FIELD.
018600     IF PFKEY-2-PRESSED
018700         PERFORM FIRST-PAGE
018800         GO TO ENTER-KEY-ENTRY.
018900
019000     IF PFKEY-4-PRESSED
019100         PERFORM PREV-PAGE
019200         GO TO ENTER-KEY-ENTRY.
019300
019400     IF PFKEY-5-PRESSED
019500         PERFORM NEXT-PAGE
019600         GO TO ENTER-KEY-ENTRY.
019700     IF ENTER-KEY-PRESSED
000000         PERFORM VAL-FOR-ENTER-KEY                                061791
000000         IF SCREEN-ENTRY-ERROR                                    061791
000000             GO TO ENTER-KEY-ENTRY.                               061791
000000
000000*---------------------------------------------------------------
000000* If we are just listing, then treat ENTER-KEY as an error
000000* If the cursor was positioned, then move the entry up to
000000* the field and proceed with validation as if the user had
000000* typed it in.
000000*---------------------------------------------------------------
000000 VAL-FOR-ENTER-KEY.                                               061791
000000*     IF CF-ACTION-CODE = "LIST"
000000*         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS                  061791
000000*     ELSE
019800     PERFORM VAL-CURSOR-POS
019900     IF ENTRY-IDX NOT = ZEROES
020000         MOVE LIST-NAME(ENTRY-IDX) TO NAME-FIELD.                 061791
020200     IF NOT SCREEN-ENTRY-ERROR                                    061791
020300         PERFORM VAL-NAME-FIELD.                                  061791
020600
020700 INIT-PROMPTS.
020800     IF CF-ARRAY-CODE = "LIST"
020900         MOVE LIST-HEADER TO LIST-HEADER-FIELD
021000     ELSE
021100     IF CF-ARRAY-CODE = "CHANGE"
021200         MOVE CHANGE-HEADER TO LIST-HEADER-FIELD
021300     ELSE
021400     IF CF-ARRAY-CODE = "DELETE"
021500         MOVE DELETE-HEADER TO LIST-HEADER-FIELD.
021600 INIT-FIELDS.
021700     MOVE SPACE TO LIST-FIELDS.
021800     IF CH-PRIMARY-KEY = SPACE
021900         MOVE "consecutive" TO FILE-ORG-FIELD
022000     ELSE
022100         MOVE CH-PRIMARY-KEY TO KEY-FIELD
022200         MOVE INDEX-FIELD TO FILE-ORG-FIELD
022300         PERFORM INIT-ALTKEYS.
022400 INIT-ALTKEYS.
022500     MOVE 1 TO PATH-IDX.
022600     PERFORM INIT-ONE-ALTKEY.
022700 INIT-ONE-ALTKEY.
022800     COMPUTE WORK-IDX = PATH-IDX - 8.
022900     IF PATH-IDX > 8
023000         MOVE CA2-KEYNAME(WORK-IDX) TO PATH-FIELD(PATH-IDX)
023100     ELSE
023200         MOVE CA1-KEYNAME(PATH-IDX) TO PATH-FIELD(PATH-IDX).
023300     ADD 1 TO PATH-IDX.
023400     IF PATH-IDX NOT > 16
023500         GO TO INIT-ONE-ALTKEY.
023600 INIT-FACS.
023700     MOVE UNDERLINE-FAC TO
023800          FAC OF FOOTER-1-ITEM
023900          FAC OF L1-ITEM.
024000     IF CF-ARRAY-CODE = "LIST"
024100         MOVE HIDE-FAC TO
024200             FAC OF NAME-PROMPT-ITEM
024300             FAC OF NAME-ITEM
003700             FAC OF SUPPLY-PROMPT-ITEM
024400     ELSE
024500         MOVE DIM-FAC TO
024600             FAC OF NAME-PROMPT-ITEM
003700             FAC OF SUPPLY-PROMPT-ITEM
024700         MOVE UPPER-ENTRY-FAC TO
024800             FAC OF NAME-ITEM.
024900 INIT-FOOTERS.
025000     IF CF-ARRAY-CODE = "LIST"
025100         MOVE SPACE                 TO FOOTER-1-FIELD
025200         MOVE "(1)Header (2)First (4)Previous (5)Next (16)Exit"
025300                                    TO FOOTER-2-FIELD
025400     ELSE
025500         MOVE "(ENTER) to select or:" TO FOOTER-1-FIELD
025600         MOVE "(2)First (4)Previous (5)Next (16)Exit"
025700                                    TO FOOTER-2-FIELD.
025800 INIT-PFS.
025900     MOVE 2 TO PF-2.
026000     MOVE 4 TO PF-4.
026100     MOVE 5 TO PF-5.
026200     MOVE 16 TO PF-16.
026300     IF CF-ARRAY-CODE = "LIST"
026400         MOVE 1 TO PF-1
026500     ELSE
026600         MOVE 16 TO PF-1.
026700 NEXT-PAGE.
026800     IF CF-IDX < CONST-CF-MAX-ENTRY
026900         IF T2-NAME(CF-IDX) NOT = SPACE
027000             PERFORM INIT-A-PAGE.
027100 PREV-PAGE.
027200     IF PAGE-IDX > 1
027300         COMPUTE CF-IDX = ( ( PAGE-IDX - 2) * MAX-ENTRY ) + 1
027400         PERFORM INIT-A-PAGE.
027500 FIRST-PAGE.
027600     PERFORM INIT-PAGE-1.
027700 INIT-PAGE-1.
027800     MOVE 1 TO CF-IDX.
027900     PERFORM INIT-A-PAGE.
028000 INIT-A-PAGE.
028100     COMPUTE PAGE-IDX = ( ( CF-IDX - 1 ) / MAX-ENTRY) + 1.
028200     MOVE SPACE TO LIST-ENTRIES.
028300     PERFORM INIT-ONE-ENTRY
028400      VARYING ENTRY-IDX FROM 1 BY 1
028500       UNTIL ENTRY-IDX > MAX-ENTRY.
028600 INIT-ONE-ENTRY.
028700     IF CF-IDX > CONST-CF-MAX-ENTRY OR
028800        T2-NAME(CF-IDX) = SPACE
028900         PERFORM INIT-A-BLANK
029000     ELSE
029100         PERFORM INIT-AN-ENTRY.
029200     ADD 1 TO CF-IDX.
029300 INIT-A-BLANK.
029400     MOVE HIDE-FAC TO FAC OF LIST-NAME-ITEM(ENTRY-IDX).
029500     MOVE SPACE TO LIST-ENTRY(ENTRY-IDX).
029600 INIT-AN-ENTRY.
029700     MOVE SPACE TO FORMAT-FIELD.
029800     MOVE TABSTOP TO FAC OF LIST-NAME-ITEM(ENTRY-IDX).
029900     MOVE T2-NAME(CF-IDX) TO LIST-NAME(ENTRY-IDX).
030000     PERFORM LOAD-FORMAT-FIELD.
030100     IF T1-REPORT-CODE(CF-IDX) = ZEROES
030200         IF T1-UPDATE-CODE(CF-IDX) = ZEROES
030300             MOVE REPORT-AND-UPDATE TO RU-FIELD
030400         ELSE
030500             MOVE REPORT-ONLY TO RU-FIELD
030600     ELSE
030700     IF T1-UPDATE-CODE(CF-IDX) = ZEROES
030800         MOVE UPDATE-ONLY TO RU-FIELD.
030900     PERFORM INIT-DATE-STAMP.
031000     IF T2-TABLE-NAME(CF-IDX) NOT = SPACE
031100         MOVE T2-TABLE-NAME(CF-IDX) TO
031200           TABLE-OR-RANGE-FIELD
031300     ELSE
031400     IF T2-LO-RANGE(CF-IDX) = SPACE AND
031500        T2-HI-RANGE(CF-IDX) = SPACE
031600         NEXT SENTENCE
031700     ELSE
031800         MOVE "Range" TO TABLE-OR-RANGE-FIELD.
031900
032000     MOVE FORMAT-FIELD TO LIST-FORMAT(ENTRY-IDX).
032100 INIT-DATE-STAMP.
032200     IF T2-VALIDATION(CF-IDX) = "DF"
032300         MOVE "Date1" TO DATE-STAMP-FIELD
032400     ELSE
032500     IF T2-VALIDATION(CF-IDX) = "GD"
032600         MOVE "Date2" TO DATE-STAMP-FIELD
032700     ELSE
032800     IF T2-VALIDATION(CF-IDX) = "DS"
032900         MOVE "Day" TO DATE-STAMP-FIELD
033000     ELSE
033100     IF T2-VALIDATION(CF-IDX) = "TS"
033200         MOVE "Time" TO DATE-STAMP-FIELD
033300     ELSE
033400     IF T2-VALIDATION(CF-IDX) = "CS"
033500         MOVE "D&T5" TO DATE-STAMP-FIELD
033600     ELSE
033700     IF T2-VALIDATION(CF-IDX) = "CM"
033800         MOVE "D&T6" TO DATE-STAMP-FIELD.
033900*---------------------------------------------------------------
034000* LIST IS SORTED IN POSITION ORDER
034100* CHANGE AND DELETE ARE IN ALPHABETICAL ORDER
034200*---------------------------------------------------------------
034300 INIT-SORT.
034400     MOVE CF-ACTION-CODE TO SAVE-ACTION-CODE.
034500     IF CF-ACTION-CODE = "LIST"
034600          PERFORM SORT-BY-POSITION
034700     ELSE
034800          PERFORM SORT-BY-NAME.
034900     MOVE SAVE-ACTION-CODE TO CF-ACTION-CODE.
035000 SORT-BY-POSITION.
035100     MOVE "SORTPOS" TO CF-ACTION-CODE.
035200     PERFORM CALL-CTRLARY.
035300 SORT-BY-NAME.
035400     MOVE "SORTNAME" TO CF-ACTION-CODE.
035500     PERFORM CALL-CTRLARY.
035600 CALL-CTRLARY.
035700     CALL "CTRLARY" USING
000000     CALLED-RC
035800     COPY LKCTRL.
035900* VALIDATION LOGIC
036000 VAL-CURSOR-POS.
036100*     IF CF-ARRAY-CODE = "LIST"
036200*         MOVE ZEROES TO ENTRY-IDX
036300*     ELSE
036400     IF CRT-ROW-POS < FIRST-ROW OR
036500        CRT-ROW-POS > LAST-ROW
036600         MOVE ZEROES TO ENTRY-IDX
036700     ELSE
036800        COMPUTE ENTRY-IDX = CRT-ROW-POS - FIRST-ROW + 1.
036900 VAL-NAME-FIELD.
037000*     IF CF-ARRAY-CODE = "LIST"
037100*         NEXT SENTENCE
037200*     ELSE
037300     IF NAME-FIELD = SPACE
037400         MOVE "Field Name is Required" TO MESSAGE-FIELD
037500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
037600     ELSE
037700         PERFORM LOOK-UP-NAME
037800         IF CF-RETURN-CODE NOT = "00"
037900             MOVE "Field not Found" to MESSAGE-FIELD
038000             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
038100             MOVE WORK-IDX TO CF-IDX.
038200 LOOK-UP-NAME.
038300     MOVE CF-IDX TO WORK-IDX.
038400     MOVE CF-ARRAY-CODE TO SAVE-ARRAY-CODE.
038500     MOVE NAME-FIELD TO CX-NAME.
038600     MOVE "FINDNAME" TO CF-ARRAY-CODE.
038700     PERFORM CALL-CTRLARY.
038800     IF CF-RETURN-CODE = "00"
038900         MOVE SPACE TO CF-RECORD
000000         PERFORM LOAD-CF-RECORD
039000*         MOVE CORRESPONDING CF-T1(CF-IDX) TO CF-RECORD
039100*         MOVE CORRESPONDING CF-T2(CF-IDX) TO CF-RECORD.
039200     MOVE SAVE-ARRAY-CODE TO CF-ARRAY-CODE.
039300* UTILS
039400     COPY PLCRT.
039500 LOAD-FORMAT-FIELD.
039600     MOVE T1-START-POS(CF-IDX) TO FF-START-POS.
039700     MOVE T2-TYPE(CF-IDX) TO FF-TYPE.
039800     MOVE T1-INT-LEN(CF-IDX) TO FF-INT-LEN.
039900     MOVE T1-EXT-LEN(CF-IDX) TO FF-EXT-LEN.
040000     MOVE T1-DECIMAL-POS(CF-IDX) TO FF-DECIMAL-POS.
040100     MOVE T1-OCCURRENCES(CF-IDX) TO FF-OCCURRENCES.
040200     MOVE T1-UPDATE-SEQ(CF-IDX) TO FF-UPDATE-SEQ.
040300     MOVE T2-CUMMULATIVE-NAME(CF-IDX) TO FF-CUMMULATIVE-NAME.
000000 LOAD-CF-RECORD.
003800     MOVE T1-INT-LEN(CF-IDX) TO CX-INT-LEN.
003900     MOVE T1-EXT-LEN(CF-IDX) TO CX-EXT-LEN.
004000     MOVE T1-EDITED-LEN(CF-IDX) TO CX-EDITED-LEN.
004100     MOVE T1-START-POS(CF-IDX) TO CX-START-POS.
004200     MOVE T1-OCCURRENCES(CF-IDX) TO CX-OCCURRENCES.
004300     MOVE T1-ZERO-SUPPRESS(CF-IDX) TO CX-ZERO-SUPPRESS.
004400     MOVE T1-DECIMAL-INSERT(CF-IDX) TO CX-DECIMAL-INSERT.
004500     MOVE T1-SIGN-CONTROL(CF-IDX) TO CX-SIGN-CONTROL.
004600     MOVE T1-DOLLAR-COMMA(CF-IDX) TO CX-DOLLAR-COMMA.
004700     MOVE T1-REPORT-CODE(CF-IDX) TO CX-REPORT-CODE.
004800     MOVE T1-UPDATE-CODE(CF-IDX) TO CX-UPDATE-CODE.
004900     MOVE T1-DECIMAL-POS(CF-IDX) TO CX-DECIMAL-POS.
005000     MOVE T1-BINARY-EDIT(CF-IDX) TO CX-BINARY-EDIT.
005100     MOVE T1-UPDATE-SEQ(CF-IDX) TO CX-UPDATE-SEQ.
005200     MOVE T1-PACKED-DIGITS(CF-IDX) TO CX-PACKED-DIGITS.
005300     MOVE T1-GROUP-FIELDS(CF-IDX) TO CX-GROUP-FIELDS.
006000     MOVE T1-DISPLAY-CODE(CF-IDX) TO CX-DISPLAY-CODE.
001800     MOVE T2-NAME(CF-IDX) TO CX-NAME.
001900     MOVE T2-TYPE(CF-IDX) TO CX-TYPE.
002000     MOVE T2-VALIDATION(CF-IDX) TO CX-VALIDATION.
002100     MOVE T2-TABLE-NAME(CF-IDX) TO CX-TABLE-NAME.
002200     MOVE T2-LO-RANGE(CF-IDX) TO CX-LO-RANGE.
002300     MOVE T2-HI-RANGE(CF-IDX) TO CX-HI-RANGE.
002400     MOVE T2-CUMMULATIVE-NAME(CF-IDX) TO CX-CUMMULATIVE-NAME.
002500     MOVE T2-ALIAS(CF-IDX) TO CX-ALIAS.
002600     MOVE T2-DEFAULT-FAC(CF-IDX) TO CX-DEFAULT-FAC.
000000     COPY PLKTRACE.
