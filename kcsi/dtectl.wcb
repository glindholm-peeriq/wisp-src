000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. DTECTL.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  12/13/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE CONTROL FILE:  FILENAME LIBRARY VOLUME
000800*      AND THE DATA FILE:     FILENAME LIBRARY VOLUME
000900*---------------------------------------------------------------
001000
001100 ENVIRONMENT DIVISION.
001200 CONFIGURATION SECTION.
001300 FIGURATIVE-CONSTANTS.
001400     COPY FIGS.
001500
001600 INPUT-OUTPUT SECTION.
001700
001800 FILE-CONTROL.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200
002300 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  TEST-BIN   BINARY VALUE 1.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)dtectl.wcb 1.12 9/11/94".
002400     COPY WSGP.
002500     COPY WSINT.
002600     COPY WSCRTS.
002700     COPY WSIOCODE.
000000     COPY LKVALNAM.
000000
002800 01  KCSIDTE-VERS                   PIC X(79) VALUE
000000     "DATENTRY v 1.00.00.ul - (c)1990 King Computer Services".
002900 01  GENERAL-FIELD-1                PIC X(500).
003000 01  GENERAL-FIELD-2                PIC X(500).
000000 01  KMATCHRC                       PIC 99.
003100
000000 01  CTRLDIF-PARENT PIC X(8) VALUE "DATENTRY".
003200 01  WORK-IO-BLOCK.
003300     COPY LKDIO.
003400
003500 01  INPUT-GETPARM-FIELDS.
003600     05  INPUT-MSG-ID               PIC X(4) VALUE "0001".
003700     05  INPUT-PRNAME               PIC X(8) VALUE "INPUT".
003800     05  INPUT-MSG-ISSUER           PIC X(6) VALUE "DATENT".
003900
004000 01  INPUT-MSG-COUNT.
004100     05  FILLER                        BINARY   VALUE ZERO.
004200     05  FILLER                        BINARY   VALUE 2.
004300 01  INPUT-MSG-TEXT-1               PIC X(78) VALUE
004400         " ".
004500 01  INPUT-TEXT-01                  PIC X(77) VALUE
004600         " Specify the data and control file and press (ENTER) to
004700-        "continue to the menu.".
004800 01  INPUT-TEXT-02                  PIC X(11) VALUE
004900         " Data file:".
005000 01  INPUT-FILE-KW              PIC X(8) VALUE "FILE".
005100 01  INPUT-FILE-FIELD           PIC X(8) VALUE SPACE.
005200 01  INPUT-FILE-DEFAULT         PIC X(8) VALUE SPACE.
005300 01  INPUT-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
005400 01  INPUT-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
005500 01  INPUT-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
005600 01  INPUT-VOLUME-KW              PIC X(8) VALUE "VOLUME".
005700 01  INPUT-VOLUME-FIELD           PIC X(6) VALUE SPACE.
005800 01  INPUT-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
005900 01  INPUT-TEXT-03                  PIC X(14) VALUE
006000         " Control file:".
006100 01  INPUT-CTLFILE-KW              PIC X(8) VALUE "CTLFILE".
006200 01  INPUT-CTLFILE-FIELD           PIC X(8) VALUE SPACE.
006300 01  INPUT-CTLFILE-DEFAULT         PIC X(8) VALUE SPACE.
006400 01  INPUT-CTLLIB-KW              PIC X(8) VALUE "CTLLIB".
006500 01  INPUT-CTLLIB-FIELD           PIC X(8) VALUE SPACE.
006600 01  INPUT-CTLLIB-DEFAULT         PIC X(8) VALUE SPACE.
006700 01  INPUT-CTLVOL-KW              PIC X(8) VALUE "CTLVOL".
006800 01  INPUT-CTLVOL-FIELD           PIC X(6) VALUE SPACE.
006900 01  INPUT-CTLVOL-DEFAULT         PIC X(6) VALUE SPACE.
007000 01  INPUT-TEXT-04                  PIC X(35) VALUE
007100         " (ENTER) Continue to Menu or Select".
007200 01  INPUT-TEXT-05                PIC X(78) VALUE
007300     "(2) Create data file                           (16) End".
000000
000000* PF 2 and 16
007400 01  INPUT-PFKEY-MASK.
007600     05  INPUT-PFKEY-MASK-16  BINARY VALUE 16385.
007600     05  INPUT-PFKEY-MASK-32  BINARY VALUE 0.
007900
008000 01  MESSAGE-LITERALS.
008100     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
008200         "* Error * File Name cannot be blank".
008300     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
008400         "* Error * Invalid File Name".
008500     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
008600         "* Error * Invalid Library Name".
008700     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
008800         "* Error * Invalid Volume Name".
008900
012500     COPY LKUFB.
012600 01  RECORD-AREA             PIC X(2048) VALUE SPACES.
012700
012800 LINKAGE SECTION.
012900 01  DATA-IO-BLOCK.
013000     COPY LKDIO.
013100 01  CTRL-IO-BLOCK.
013200     COPY LKDIO.
013300 01  CTRL-HEADER-RECORDS.
013400     COPY RCCTRLH.
013500 01  CF-RECORD.
013600     COPY RCCTRLF.
013700     COPY LKCTRLAR.
013800     COPY RCCTRLT1.
013900     COPY RCCTRLT2.
000000     COPY LKPRC.
014000 PROCEDURE DIVISION USING
000000     CALLED-RC
014100     DATA-IO-BLOCK
014200     COPY LKCTRL.
014300 MAIN-LOGIC SECTION.
014400 PROGRAM-BEGIN.
000000     MOVE "DTECTL BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014500     PERFORM OPENING-PROCEDURE.
014600     PERFORM MAIN-PROCESS.
014700     PERFORM CLOSING-PROCEDURE.
000000     MOVE "DTECTL EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014800 MAIN-LOGIC-EXIT.
014900     EXIT PROGRAM.
015000 MAIN-LOGIC-STOP.
015100     STOP RUN.
015200 LEVEL-2 SECTION.
015300 OPENING-PROCEDURE.
000000     MOVE ZEROES TO CALLED-RC.
000000     CALL "DATVERS" USING
000000         KCSIDTE-VERS
000000         TEST-BIN.
015400     MOVE FILE-LIBRARY OF DATA-IO-BLOCK TO INPUT-LIBRARY-FIELD.
015500     MOVE FILE-VOLUME  OF DATA-IO-BLOCK TO INPUT-VOLUME-FIELD.
015600     MOVE FILE-LIBRARY OF CTRL-IO-BLOCK TO INPUT-CTLLIB-FIELD.
015700     MOVE FILE-VOLUME OF CTRL-IO-BLOCK TO INPUT-CTLVOL-FIELD.
000000     PERFORM CLOSE-DATA-FILE.
015800 CLOSING-PROCEDURE.
015900     IF GP-PFKEY-16-PRESSED
016000         MOVE 16 TO CALLED-RC.
016100
016200 MAIN-PROCESS.
016300     MOVE SPACE TO SCREEN-ERROR-STATUS.
016400
016500     PERFORM CALL-INPUT-GETPARM.
016600
016700     IF NOT GP-PFKEY-16-PRESSED
016800         PERFORM VAL-DATA-FIELDS
016900         IF NOT SCREEN-ENTRY-ERROR
017000             PERFORM VAL-CTL-FIELDS.
017100
017200     IF NOT GP-PFKEY-16-PRESSED
017300         IF NOT SCREEN-ENTRY-ERROR
017400             PERFORM LOAD-CTL-FILE
017800             MOVE WORK-IO-BLOCK TO DATA-IO-BLOCK
017500             IF NOT GP-PFKEY-2-PRESSED
017600                 PERFORM VAL-DATA-AND-CTL-FILE
017700             ELSE
017900                 CALL "DTEXST" USING
018000                     DATA-IO-BLOCK
000000                 MOVE ZEROES TO CALLED-RC
000000                 PERFORM OPEN-OUTPUT-DATA-FILE
000000                 PERFORM CLOSE-DATA-FILE
018100                 PERFORM OPEN-IO-DATA-FILE.
018200
018300     IF NOT GP-PFKEY-16-PRESSED
018400         IF SCREEN-ENTRY-ERROR
018500             PERFORM CLOSE-DATA-FILE
018600             GO TO MAIN-PROCESS.
018700
018800
018900* LOAD THE CTRL-FILE AND LOAD THE WORK-IO-BLOCK FROM THE
019000* THE CONTROL-FILE.
019100 LOAD-CTL-FILE.
019200     CALL "DTELOAD" USING
019300     COPY LKCTRL.
019400     MOVE DATA-IO-BLOCK TO WORK-IO-BLOCK.
019500     CALL "CTRLDIO" USING
000000          CALLED-RC
019600          CTRL-IO-BLOCK
019700          WORK-IO-BLOCK
019800          CTRL-HEADER-RECORDS.
019900
020000 VAL-DATA-AND-CTL-FILE.
020100     PERFORM OPEN-IO-DATA-FILE.
020200     MOVE FILE-INFO TO FILE-IO IN DATA-IO-BLOCK.
020300     CALL "KCSIO" USING
020400         DATA-IO-BLOCK
020500         UFB
020600         RECORD-AREA.
020700     MOVE FILE-ID-DATA OF DATA-IO-BLOCK TO GENERAL-FIELD-1.
020800     MOVE FILE-ID-DATA OF WORK-IO-BLOCK TO GENERAL-FIELD-2.
000000     CALL "KMATCH" USING KMATCHRC
000000                         GENERAL-FIELD-1
000000                         GENERAL-FIELD-2.
000000
020900     IF KMATCHRC NOT = ZEROES
000000*    IF GENERAL-FIELD-1 NOT = GENERAL-FIELD-2
000000         CALL "CTRLDIF" USING
000000             WORK-IO-BLOCK
000000             DATA-IO-BLOCK
000000             CTRLDIF-PARENT
021000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
021100         MOVE "Control and Data files are not compatible"
021200             TO  INPUT-MSG-TEXT-1.
021300
021400 CALL-INPUT-GETPARM.
021500
007600     COMPUTE INPUT-PFKEY-MASK-16 = 16385.
021900     CALL "GETPARM" USING
022000         GP-SPECIFY-TYPE, GP-REQUEST-FORM, INPUT-PRNAME,
022100         GP-PFKEY-RECEIVER, INPUT-MSG-ID,
022200         INPUT-MSG-ISSUER,
022300
022400         INPUT-MSG-COUNT,
022500         KCSIDTE-VERS, INT79,
022500         INPUT-MSG-TEXT-1, INT78,
022600
022700         GP-TEXT-TYPE, INPUT-TEXT-01, INT77,
022800         GP-ABS, INT11, GP-ABS, INT02,
022900
023000         GP-TEXT-TYPE, INPUT-TEXT-02, INT11,
023100         GP-ABS, INT17, GP-ABS, INT02,
023200
023300         GP-KW-TYPE, INPUT-FILE-KW,
023400         INPUT-FILE-FIELD, INT08,
023500         GP-ABS, INT18, GP-ABS, INT03,
023600         GP-ALPHANUMERIC-DATA
023700
023800         GP-KW-TYPE, INPUT-LIBRARY-KW,
023900         INPUT-LIBRARY-FIELD, INT08,
024000         GP-ABS, INT18, GP-ABS, INT24,
024100         GP-ALPHANUMERIC-DATA
024200
024300         GP-KW-TYPE, INPUT-VOLUME-KW,
024400         INPUT-VOLUME-FIELD, INT06,
024500         GP-ABS, INT18, GP-ABS, INT45,
024600         GP-ALPHANUMERIC-DATA
024700
024800         GP-TEXT-TYPE, INPUT-TEXT-03, INT14,
024900         GP-ABS, INT20, GP-ABS, INT02,
025000
025100         GP-KW-TYPE, INPUT-CTLFILE-KW,
025200         INPUT-CTLFILE-FIELD, INT08,
025300         GP-ABS, INT21, GP-ABS, INT03,
025400         GP-ALPHANUMERIC-DATA
025500
025600         GP-KW-TYPE, INPUT-CTLLIB-KW,
025700         INPUT-CTLLIB-FIELD, INT08,
025800         GP-ABS, INT21, GP-ABS, INT24,
025900         GP-ALPHANUMERIC-DATA
026000
026100         GP-KW-TYPE, INPUT-CTLVOL-KW,
026200         INPUT-CTLVOL-FIELD, INT06,
026300         GP-ABS, INT21, GP-ABS, INT45,
026400         GP-ALPHANUMERIC-DATA
026500
026600         GP-TEXT-TYPE, INPUT-TEXT-04, INT35,
026700         GP-ABS, INT23, GP-ABS, INT02,
026800
026900         GP-TEXT-TYPE, INPUT-TEXT-05, INT78,
027000         GP-ABS, INT24, GP-ABS, INT02,
027100         GP-ENABLE-ENTER-KEY
027200         GP-PFKEY-TYPE, INPUT-PFKEY-MASK.
027300
027400     IF NOT GP-PFKEY-2-PRESSED  AND
000000        NOT GP-PFKEY-3-PRESSED AND
027500        NOT GP-PFKEY-16-PRESSED AND
027600        NOT GP-ENTER-KEY-PRESSED
027700         GO TO CALL-INPUT-GETPARM.
027800
027900 VAL-CTL-FIELDS.
028000     PERFORM VAL-CTL-FILE.
028100     IF NOT SCREEN-ENTRY-ERROR
028200         PERFORM VAL-CTL-LIB
028300             IF NOT SCREEN-ENTRY-ERROR
028400                PERFORM VAL-CTL-VOL.
028500
028600     IF NOT SCREEN-ENTRY-ERROR
028700         MOVE INPUT-CTLFILE-FIELD TO FILE-NAME OF CTRL-IO-BLOCK
028800         MOVE INPUT-CTLLIB-FIELD TO FILE-LIBRARY OF CTRL-IO-BLOCK
028900         MOVE INPUT-CTLVOL-FIELD TO FILE-VOLUME OF CTRL-IO-BLOCK.
029000
029100     IF NOT SCREEN-ENTRY-ERROR
029200         MOVE 0 TO CALLED-RC
029300         CALL "KEXISTS" USING
000000             CALLED-RC
029400             FILE-NAME OF CTRL-IO-BLOCK
029500             FILE-LIBRARY OF CTRL-IO-BLOCK
029600             FILE-VOLUME OF CTRL-IO-BLOCK.
029700
029800     IF CALLED-RC NOT = 0
029900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
030000         MOVE "Specified Control File does not exist."
030100             TO INPUT-MSG-TEXT-1.
030200
030300 VAL-CTL-FILE.
030400     IF INPUT-CTLFILE-FIELD = SPACES
030500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
030600         MOVE FILE-ID-BLANK-LITERAL TO INPUT-MSG-TEXT-1.
030700
030800     IF NOT SCREEN-ENTRY-ERROR
030900         MOVE INPUT-CTLFILE-FIELD TO VAL-NAME
031000         PERFORM TEST-THE-NAME
031100         IF SCREEN-ENTRY-ERROR
031200             MOVE INVALID-FILE-LITERAL TO INPUT-MSG-TEXT-1.
031300
031400 VAL-CTL-LIB.
031500     MOVE INPUT-CTLLIB-FIELD TO VAL-NAME.
031600     PERFORM TEST-THE-NAME.
031700     IF SCREEN-ENTRY-ERROR
031800         MOVE INVALID-LIB-LITERAL TO INPUT-MSG-TEXT-1.
031900
032000 VAL-CTL-VOL.
032100     MOVE INPUT-CTLVOL-FIELD TO VAL-NAME.
032200     PERFORM TEST-THE-VOL.
032300     IF SCREEN-ENTRY-ERROR
032400         MOVE INVALID-VOL-LITERAL TO INPUT-MSG-TEXT-1.
032500
032600 VAL-DATA-FIELDS.
032700     PERFORM VAL-DATA-FILE.
032800     IF NOT SCREEN-ENTRY-ERROR
032900         PERFORM VAL-DATA-LIB
033000             IF NOT SCREEN-ENTRY-ERROR
033100                PERFORM VAL-DATA-VOL.
033200
033300     IF NOT SCREEN-ENTRY-ERROR
033400         MOVE INPUT-FILE-FIELD TO FILE-NAME OF DATA-IO-BLOCK
033500         MOVE INPUT-LIBRARY-FIELD TO FILE-LIBRARY OF DATA-IO-BLOCK
033600         MOVE INPUT-VOLUME-FIELD TO FILE-VOLUME OF DATA-IO-BLOCK.
033700
033800     IF NOT SCREEN-ENTRY-ERROR
033900         MOVE 0 TO CALLED-RC
034000         CALL "KEXISTS" USING
000000             CALLED-RC
034100             FILE-NAME OF DATA-IO-BLOCK
034200             FILE-LIBRARY OF DATA-IO-BLOCK
034300             FILE-VOLUME OF DATA-IO-BLOCK.
034400
034500     IF CALLED-RC NOT = 0 AND
000000        NOT GP-PFKEY-2-PRESSED
034600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
034700         MOVE "Specified Data File does not exist."
034800             TO INPUT-MSG-TEXT-1.
034900
035000 VAL-DATA-FILE.
035100     IF INPUT-FILE-FIELD = SPACES
035200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
035300         MOVE FILE-ID-BLANK-LITERAL TO INPUT-MSG-TEXT-1.
035400
035500     IF NOT SCREEN-ENTRY-ERROR
035600         MOVE INPUT-FILE-FIELD TO VAL-NAME
035700         PERFORM TEST-THE-NAME
035800         IF SCREEN-ENTRY-ERROR
035900             MOVE INVALID-FILE-LITERAL TO INPUT-MSG-TEXT-1.
036000
036100 VAL-DATA-LIB.
036200     MOVE INPUT-LIBRARY-FIELD TO VAL-NAME.
036300     PERFORM TEST-THE-NAME.
036400     IF SCREEN-ENTRY-ERROR
036500         MOVE INVALID-LIB-LITERAL TO INPUT-MSG-TEXT-1.
036600
036700 VAL-DATA-VOL.
036800     MOVE INPUT-VOLUME-FIELD TO VAL-NAME.
036900     PERFORM TEST-THE-VOL.
037000     IF SCREEN-ENTRY-ERROR
037100         MOVE INVALID-VOL-LITERAL TO INPUT-MSG-TEXT-1.
037200
037300
037400 TEST-THE-NAME.
           CALL "VALNAM" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

037400 TEST-THE-VOL.
           CALL "VALVOL" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

041100     COPY IODATA.
041200*     COPY PLLINK.
000000
000000     COPY PLKTRACE.
