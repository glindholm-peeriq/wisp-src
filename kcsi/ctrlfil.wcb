000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLFIL.
000300*--------------------------------------------------
000400* GETPARM FOR THE FILE NAME LIB AND VOL.
000500*--------------------------------------------------
000600 ENVIRONMENT DIVISION.
000700 CONFIGURATION SECTION.
000800 FIGURATIVE-CONSTANTS.
000900     COPY FIGS.
001000 INPUT-OUTPUT SECTION.
001100 FILE-CONTROL.
001200     COPY SLCRT.
001300 DATA DIVISION.
001400 FILE SECTION.
001500     COPY FDCRT.
001600 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  TEST-BIN      BINARY VALUE 1.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrlfil.wcb 1.5 9/11/94".
001700     COPY WSCRT.
001800 01  CTLFIL-SCREEN USAGE IS DISPLAY-WS.
001900     COPY SCGPEM.
002000     05  FILLER
002100         PIC X(79) ROW 7 COLUMN 2
002200         SOURCE DISPLAY-VERSION.
002300*     05  FILLER
002400*         PIC X(38) ROW 8 COLUMN 17 VALUE
002500*         "(c) 1989, King Computer Services, Inc.".
002600     05  FILLER
002700         PIC X(51) ROW 10 COLUMN 10 VALUE
002800         "The CONTROL file is used by Utilities to define the".
002900     05  FILLER
003000         PIC X(31) ROW 11 COLUMN 19 VALUE
003100         "characteristics of a data file.".
003200     05  FILLER
003300         PIC X(71) ROW 13 COLUMN 4 VALUE
003400         "Specify the control file to be processed and press (ENTE
003500-        "R) to continue:".
003600     05  CTLFIL-ERROR-TEXT-ITEM
003700         PIC X(70) ROW 15 COLUMN 4
003800         SOURCE CTLFIL-ERROR-TEXT.
004000     05  FILLER
004100         PIC X(10) ROW 18 COLUMN 9 VALUE
004200         "FILE     =".
004300     05  FILE-NAME-ITEM
004400         PIC X(8) ROW 18 COLUMN 20
004500         SOURCE FILE-NAME
004600         OBJECT FILE-NAME.
004700     05  FILLER
004800         PIC X(10) ROW 18 COLUMN 31 VALUE
004900         "LIBRARY  =".
005000     05  FILE-LIBRARY-ITEM
005100         PIC X(8) ROW 18 COLUMN 42
005200         SOURCE FILE-LIBRARY
005300         OBJECT FILE-LIBRARY.
005400     05  FILLER
005500         PIC X(10) ROW 18 COLUMN 52 VALUE
005600         "VOLUME   =".
005700     05  FILE-VOLUME-ITEM
005800         PIC X(6) ROW 18 COLUMN 63
005900         SOURCE FILE-VOLUME
006000         OBJECT FILE-VOLUME.
006100     05  FILLER
006200         PIC X(10) ROW 23 COLUMN 2 VALUE
006300         "OR Select:".
006400     05  FILLER
006500         PIC X(45) ROW 24 COLUMN 2 VALUE
006600         "(2) to Create a New File (16) to Exit Control".
000000 01  DISPLAY-VERSION PIC X(79) VALUE
002200         "KCSI CONTROL FILE UTILITY - VERSION 1.10.00.ul".
006700 01  CTLFIL-FIELDS.
006800     05  CTLFIL-ERROR-TEXT           PIC X(70) VALUE SPACE.
006900
007000     COPY WSGPEM.
007100
000000 01  KEXISTS-RC       PIC 999 VALUE 0.
000000
007200 LINKAGE SECTION.
007300 01  CTRL-IO-BLOCK.
007400     COPY LKDIO.
000000     COPY LKPRC.
007500 PROCEDURE DIVISION USING CALLED-RC CTRL-IO-BLOCK.
007600 MAIN-LOGIC SECTION.
007700 PROGRAM-BEGIN.
000000     MOVE "CTRLFIL BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     MOVE ZEROES TO CALLED-RC.
007800     PERFORM OPENING-PROCEDURE.
007900     PERFORM MAIN-PROCESS.
008000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CTRLFIL EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
008100 MAIN-LOGIC-STOP.
008200     STOP RUN.
008300 THE-OTHER SECTION.
008400 OPENING-PROCEDURE.
000000     CALL "CTRLVERS" USING
000000        DISPLAY-VERSION
000000        TEST-BIN.
008500     PERFORM OPEN-CRT-FILE.
008600 CLOSING-PROCEDURE.
008700     PERFORM CLOSE-CRT-FILE.
008800     IF PFKEY-2-PRESSED
008900          MOVE 2 TO CALLED-RC
009000     ELSE
009100     IF PFKEY-16-PRESSED
009200          MOVE 16 TO CALLED-RC
009300     ELSE
009400          MOVE ZEROES TO CALLED-RC.
009500 MAIN-PROCESS.
009600     PERFORM ENTER-CTLFIL.
009700
009800*-----------------------------------------------
009900* THE FIRST CONTROL SCREEN REQUESTING FILE
010000* LIBRARY AND VOLUME.
010100*-----------------------------------------------
010200
010300 ENTER-CTLFIL.
010400     PERFORM ENTER-CTLFIL-INIT.
010500     PERFORM ENTER-CTLFIL-ENTRY.
010600
010700 ENTER-CTLFIL-INIT.
010800     PERFORM INIT-CTLFIL-FIELDS.
010900     PERFORM INIT-CTLFIL-DEFAULTS.
011000
011100 ENTER-CTLFIL-ENTRY.
011200     MOVE SPACE TO SCREEN-ERROR-STATUS.
011300     PERFORM CALL-CTLFIL-GETPARM.
011400     MOVE SPACE TO CTLFIL-ERROR-TEXT.
011500     IF PFKEY-2-PRESSED OR
011600        ENTER-KEY-PRESSED
011700         PERFORM VAL-ENTER-CTLFIL
011800         IF SCREEN-ENTRY-ERROR
011900             GO TO ENTER-CTLFIL-ENTRY.
012000* NOT ENABLED
012100     IF PFKEY-9-PRESSED
012200         PERFORM VAL-CTLFIL-LIB-VOL
012300         IF SCREEN-ENTRY-ERROR
012400             GO TO ENTER-CTLFIL-ENTRY.
012500
012600 INIT-CTLFIL-FIELDS.
012700     MOVE SPACE TO CTLFIL-ERROR-TEXT.
012800
012900 INIT-CTLFIL-DEFAULTS.
013000* NULL
013100
013200* AT THIS LEVEL, THE ONLY VALIDATION IS THAT NONE
013300* OF THE FIELDS ARE BLANKS.
013400 VAL-ENTER-CTLFIL.
013500     PERFORM VAL-CTLFIL-LIB-VOL.
013600     IF PFKEY-2-PRESSED AND
013700        NOT SCREEN-ENTRY-ERROR
013800         PERFORM VAL-CTLFIL-NAME.
013900     IF ENTER-KEY-PRESSED AND
014000        NOT SCREEN-ENTRY-ERROR
014100         PERFORM VAL-ENTER-CTLFIL-NAME.
014200     IF ENTER-KEY-PRESSED AND
014300        FILE-NAME NOT = SPACE AND
014400        NOT SCREEN-ENTRY-ERROR
014500         PERFORM VAL-FILE-EXISTS.
014600* AT THE MOMENT THE LIST OPTION IS NOT AVAILABLE
014700* AND THIS VALIDATION REQUIRES A FILE NAME.
014800 VAL-ENTER-CTLFIL-NAME.
014900     PERFORM VAL-CTLFIL-NAME.
015000 VAL-CTLFIL-NAME.
015100     IF FILE-NAME = SPACE
015200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
015300         MOVE "File Information is Incomplete."
015400             TO CTLFIL-ERROR-TEXT.
015500 VAL-CTLFIL-LIB-VOL.
015600     IF FILE-LIBRARY = SPACE OR
015700        FILE-VOLUME = SPACE
015800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
015900         MOVE "File Information is Incomplete."
016000             TO CTLFIL-ERROR-TEXT.
016100 VAL-FILE-EXISTS.
016200     CALL "KEXISTS" USING
000000          KEXISTS-RC
016300          FILE-NAME
016400          FILE-LIBRARY
016500          FILE-VOLUME.
016600     IF KEXISTS-RC NOT = ZEROES
016700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016800         MOVE "File Specified Does Not Exist."
016900             TO CTLFIL-ERROR-TEXT.
017000
017100 CALL-CTLFIL-GETPARM.
017200
017300     MOVE "CTLFIL" TO EM-PRNAME.
017400     MOVE "0001" TO EM-MSG-ID.
017500     MOVE "CONTRO" TO EM-MSG-ISSUER.
017600     MOVE "CONTROL" TO EM-BY.
017700
017800     DISPLAY AND READ CTLFIL-SCREEN ON CRT-FILE
017900       PFKEYS 2 16.
018000
018100     COPY PLCRT.
018200

000000     COPY PLKTRACE.
