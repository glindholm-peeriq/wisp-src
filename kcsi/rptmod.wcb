000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTMOD.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/20/89.
000500 DATE-COMPILED.
000600*--------------------------------------------------------------
000700* DEFINES WHICH REPORT TO MODIFY
000705*
000710* 1-Apr-1993.  HLM.
000715* Modified to change sequence of checking for file and
000720* and then handling exception.
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptmod.wcb 1.8 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002700
002800 01  MODIFY-REPORT-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(45) ROW 6 COLUMN 2 VALUE
003100         "                              MODIFY A REPORT".
003200     05  UPPER-MSG-ITEM
003300         PIC X(79) ROW 8 COLUMN 2
003400         SOURCE UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(67) ROW 9 COLUMN 2 VALUE
003700         "           The Report Modification Option allows the use
003800-        "r to modify".
003900     05  FILLER
004000         PIC X(48) ROW 10 COLUMN 2 VALUE
004100         "                             an existing report.".
004200     05  FILLER
004300         PIC X(73) ROW 11 COLUMN 2 VALUE
004400         "     Please enter the Report ID (the name of the Report
004500-        "Definition File).".
004600     05  FILLER
004700         PIC X(39) ROW 14 COLUMN 2 VALUE
004800         "                             Report ID:".
004900     05  MODRPT-REPORT-ID-ITEM
005000         PIC X(8) ROW 14 COLUMN 43
005100         SOURCE MODRPT-REPORT-ID-FIELD
005200         OBJECT MODRPT-REPORT-ID-FIELD.
005300     05  FILLER
005400         PIC X(67) ROW 18 COLUMN 2 VALUE
005500         "            **  Press (16) to return to the Report Utili
005600-        "ty Menu  **".
005700     05  FILLER
005800         PIC X(65) ROW 19 COLUMN 2 VALUE
005900         "               **  Press (8) to change data and control
006000-        "files  **".
006100     05  FILLER
006200         PIC X(33) ROW 20 COLUMN 24 VALUE
006300         "**  Press (ENTER) to Continue  **".
006400
006500 01  MODIFY-REPORT-FIELDS.
006600     05  UPPER-MSG-FIELD               PIC X(79) VALUE SPACES.
006700     05  MODRPT-REPORT-ID-FIELD            PIC X(8) VALUE SPACE.
006800
006900 01  MESSAGE-LITERALS.
007000     05  RPT-ID-BLANK-LITERAL         PIC X(78) VALUE
007100         "* Error * Report ID cannot be blank".
007200     05  RPT-ID-INVALID-LITERAL       PIC X(78) VALUE
007300         "* Error * Report ID is Invalid".
007400
007500 01  TEST-RPT-ID-WORK-AREA.
007600     05  TEST-RPT-ID                 PIC X(08) VALUE SPACE.
007700     05  FILLER REDEFINES TEST-RPT-ID.
007800         07  TEST-RPT-ID-CHAR        PIC X(01) OCCURS 8 TIMES.
007900     05  TIDX                        PIC S9(01) VALUE ZERO.
008000
008100 01  INVALID-CHARS-WORK-AREA.
008200     05  CIDX                        PIC S9(02) VALUE ZERO.
008300     05  INVALID-CHARS-TABLE.
008400         07  FILLER                  PIC X(01) VALUE "!".
008500         07  FILLER                  PIC X(01) VALUE "%".
008600         07  FILLER                  PIC X(01) VALUE "^".
008700         07  FILLER                  PIC X(01) VALUE "&".
008800         07  FILLER                  PIC X(01) VALUE "*".
008900         07  FILLER                  PIC X(01) VALUE "(".
009000         07  FILLER                  PIC X(01) VALUE ")".
009100         07  FILLER                  PIC X(01) VALUE "-".
009200         07  FILLER                  PIC X(01) VALUE "_".
009300         07  FILLER                  PIC X(01) VALUE "=".
009400         07  FILLER                  PIC X(01) VALUE "+".
009500         07  FILLER                  PIC X(01) VALUE "]".
009600         07  FILLER                  PIC X(01) VALUE "[".
009700         07  FILLER                  PIC X(01) VALUE "'".
009800         07  FILLER                  PIC X(01) VALUE """".
009900         07  FILLER                  PIC X(01) VALUE ";".
010000         07  FILLER                  PIC X(01) VALUE ":".
010100         07  FILLER                  PIC X(01) VALUE "/".
010200         07  FILLER                  PIC X(01) VALUE "?".
010300         07  FILLER                  PIC X(01) VALUE ">".
010400         07  FILLER                  PIC X(01) VALUE ".".
010500         07  FILLER                  PIC X(01) VALUE "<".
010600         07  FILLER                  PIC X(01) VALUE ",".
010700     05  FILLER REDEFINES INVALID-CHARS-TABLE.
010800         07  INVALID-CHAR            PIC X(01) OCCURS 23 TIMES.
010900
000000 01  THE-MODE      PIC X(6) VALUE "INPUT".
000000 01  THE-PRNAME    PIC X(8) VALUE "RPTDEF".
000000 01  GETPARM-TYPE  PIC XX VALUE "ID".
000000
000000     COPY LKPRC.
000000
011000 LINKAGE SECTION.
011100 01  RPT-PFKEY               PIC 9(02).
011200 01  PASSED-RPT-ID           PIC X(08).
011300 01  RPT-IO-BLOCK.
011400     COPY  LKDIO.
011500
011600 PROCEDURE DIVISION USING
011700     RPT-PFKEY
011800     PASSED-RPT-ID
011900     RPT-IO-BLOCK.
012000
012100 MAIN-LOGIC SECTION.
012200 PROGRAM-BEGIN.
000000     MOVE "RPTMOD BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012300     PERFORM OPENING-PROCEDURE.
012400     PERFORM MAIN-PROCESS.
012500     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTMOD EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012600 MAIN-LOGIC-EXIT.
012700     EXIT PROGRAM.
012800 MAIN-LOGIC-STOP.
012900     STOP RUN.
013000 LEVEL-2 SECTION.
013100 OPENING-PROCEDURE.
013200     OPEN I-O CRT-FILE.
013300     MOVE PASSED-RPT-ID TO MODRPT-REPORT-ID-FIELD.
013400 CLOSING-PROCEDURE.
013500     CLOSE CRT-FILE.
013600
013700 MAIN-PROCESS.
013800     MOVE UPPER-ENTRY-FAC TO FAC OF MODRPT-REPORT-ID-ITEM.
013900     MOVE DISPLAY-HI-FAC  TO FAC OF UPPER-MSG-ITEM.
014000     PERFORM DEFINE-REPORT-FILE-NAME.
014100
014200 DEFINE-REPORT-FILE-NAME.
014300     MOVE SPACES TO SCREEN-ERROR-STATUS.
014400
014500     DISPLAY AND READ ALTERED MODIFY-REPORT-SCREEN ON CRT-FILE
014600     PFKEYS 8, 16
014700     ON PFKEY 16 MOVE SPACE TO SCREEN-ERROR-STATUS.
014800
014900     MOVE SPACE TO UPPER-MSG-FIELD.
015000     IF PFKEY-16-PRESSED
015100         MOVE SPACES TO FILE-NAME OF RPT-IO-BLOCK.
015200
015300     IF NOT PFKEY-16-PRESSED
015400         PERFORM VAL-RPT-ID
015500         IF SCREEN-ENTRY-ERROR
015600             GO TO DEFINE-REPORT-FILE-NAME
015700         ELSE
015800             MOVE PFKEY-CODE TO RPT-PFKEY.
015900
016000 VAL-RPT-ID.
016100
016200     IF MODRPT-REPORT-ID-FIELD = SPACES
016300         SET ERROR-BIT IN FAC OF MODRPT-REPORT-ID-ITEM ON
016400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016500         MOVE RPT-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
016600
016700     MOVE MODRPT-REPORT-ID-FIELD TO TEST-RPT-ID.
016800     COMPUTE TIDX = 0.
016900     PERFORM ARE-THE-CHARS-VALID.
017000
017100     IF CIDX NOT > 23
017200         SET ERROR-BIT IN FAC OF MODRPT-REPORT-ID-ITEM ON
017300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
017400         MOVE RPT-ID-INVALID-LITERAL TO UPPER-MSG-FIELD.
017500
036600     PERFORM COUNT-BACK
036700         VARYING TIDX FROM 8 BY -1
036800         UNTIL TIDX < 1 OR
000000          TEST-RPT-ID-CHAR (TIDX) NOT = SPACE.
037000
037100     PERFORM COUNT-BACK
037200         VARYING TIDX FROM TIDX BY -1
037300         UNTIL TIDX < 1 OR
000000          TEST-RPT-ID-CHAR (TIDX) = SPACE.
037500
018600     IF TIDX NOT < 1
018700         SET ERROR-BIT IN FAC OF MODRPT-REPORT-ID-ITEM ON
018800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
018900         MOVE RPT-ID-INVALID-LITERAL TO UPPER-MSG-FIELD.
019000
019100     IF NOT SCREEN-ENTRY-ERROR
019200         MOVE MODRPT-REPORT-ID-FIELD
019300             TO FILE-NAME OF RPT-IO-BLOCK
043900         PERFORM VAL-THE-FILE-EXISTS
044000         IF CALLED-RC NOT = ZEROES
000000             MOVE "I " TO GETPARM-TYPE
020100             MOVE "Report Definition File could not be found in th
020200-                 "is library."  TO UPPER-MSG-FIELD
000000             PERFORM GET-FILE-NAME
000000             IF CALLED-RC NOT = 0
000000                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
000000
020400 ARE-THE-CHARS-VALID.
020500
020600     ADD 1 TO TIDX.
020700     PERFORM COUNT-BACK VARYING CIDX FROM 1 BY 1
020800         UNTIL CIDX > 23  OR
020900               TEST-RPT-ID-CHAR (TIDX) = INVALID-CHAR (CIDX).
021000
021100     IF CIDX  > 23  AND
021200        TIDX < 8
021300         GO TO ARE-THE-CHARS-VALID.
021400
021500 COUNT-BACK.
021600     EXIT.
021700
042700 GET-FILE-NAME.
042900     CALL "RPTFIL" USING
000000         CALLED-RC
043000         FILE-NAME OF RPT-IO-BLOCK
043100         FILE-LIBRARY OF RPT-IO-BLOCK
043200         FILE-VOLUME OF RPT-IO-BLOCK
043300         THE-MODE
043400         THE-PRNAME
043500         UPPER-MSG-FIELD
000000         GETPARM-TYPE.
000000     IF CALLED-RC NOT = 16
043900         PERFORM VAL-THE-FILE-EXISTS
044000         IF CALLED-RC NOT = ZEROES
044100             GO TO GET-FILE-NAME.
044200 VAL-THE-FILE-EXISTS.
044300     CALL "KEXISTS" USING
000000         CALLED-RC
044400         FILE-NAME OF RPT-IO-BLOCK
044500         FILE-LIBRARY OF RPT-IO-BLOCK
044600         FILE-VOLUME OF RPT-IO-BLOCK.
045100
000000     COPY PLKTRACE.
