000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLRNG.
000300*---------------------------------------------------------------
000400* ALLOWS RANGE OR TABLE ENTRY
000500* CHANGE OVER TO FIELD RECEIVES AND CORRECTLY HANDLE THE
000600* THE TABLE AND RANGE FLAG IN CF-VALIDATION
000000* Mods:
000000*     Apr 4, 1990
000000*     Moved Screen around to improve cosmetics.
000700*---------------------------------------------------------------
000800 ENVIRONMENT DIVISION.
000900 CONFIGURATION SECTION.
001000 FIGURATIVE-CONSTANTS.
001100     COPY FIGS.
001200 INPUT-OUTPUT SECTION.
001300 FILE-CONTROL.
001400     COPY SLCRT.
001500 DATA DIVISION.
001600 FILE SECTION.
001700     COPY FDCRT.
001800 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrlrng.wcb 1.4 9/11/94".
001900*SCREENS/GETPARMS
002000 01  RANGE-SCREEN USAGE IS DISPLAY-WS.
002100     05  FILLER
002200         PIC X(37) ROW 2 COLUMN 16 VALUE
002300         "Field Input Validation Specifications".
002400     05  FILLER
002500         PIC X(52) ROW 4 COLUMN 8 VALUE
002600         "This field may be updated via the Data Entry Utility".
002700     05  FILLER
002800         PIC X(37) ROW 5 COLUMN 14 VALUE
002900         "To specify table or range validation,".
003000     05  FILLER
003100         PIC X(47) ROW 6 COLUMN 11 VALUE
003200         "make the appropriate entries and press (ENTER).".
003300     05  FILLER
003400         PIC X(11) ROW 9 COLUMN 22 VALUE
003500         "Field Name:".
003600     05  NAME-ITEM
003700         PIC X(8) ROW 9 COLUMN 35
003800         SOURCE CX-NAME.
003900     05  FILLER
004000         PIC X(12) ROW 12 COLUMN 24 VALUE
004100         "Table Lookup".
004500     05  FILLER
004600         PIC X(5) ROW 13 COLUMN 24 VALUE
004700         "Name:".
004800     05  FIELD-TABLE-NAME-ITEM
004900         PIC X(6) ROW 13 COLUMN 30
005000         SOURCE TABLE-NAME-FIELD
005100         OBJECT TABLE-NAME-FIELD.
005900     05  FILLER
006000         PIC X(19) ROW 14 COLUMN 24 VALUE
006100         "Use existing Table:".
006200     05  EXISTING-TABLE-ITEM
006300         PIC XXX ROW 14 COLUMN 45
006400         SOURCE EXISTING-TABLE-FIELD
006500         OBJECT EXISTING-TABLE-FIELD.
000000     05  FILLER
000000         PIC X(4)  ROW 16 COLUMN 30
000000         VALUE "-or-".
004200     05  FILLER
004300         PIC X(5) ROW 18 COLUMN 24 VALUE
004400         "Range".
005200     05  FILLER
005300         PIC X(4) ROW 19 COLUMN 24 VALUE
005400         "Low:".
005500     05  LO-RANGE-ITEM
005600         PIC X(16) ROW 19 COLUMN 30
005700         SOURCE LO-RANGE-FIELD
005800         OBJECT LO-RANGE-FIELD.
006600     05  FILLER
006700         PIC X(5) ROW 20 COLUMN 24 VALUE
006800         "High:".
006900     05  HI-RANGE-ITEM
007000         PIC X(16) ROW 20 COLUMN 30
007100         SOURCE HI-RANGE-FIELD
007200         OBJECT HI-RANGE-FIELD.
007300     05  FILLER PIC X(50) ROW 22 COLUMN 2 VALUE
007400         "(ENTER) to Continue OR".
007500     05  FILLER
007600         PIC X(50) ROW 23 COLUMN 2 VALUE
007700         "(1) to Return to field specifications.".
007800     05  FILLER
007900         PIC X(79) ROW 24 COLUMN 2
008000         SOURCE MESSAGE-FIELD.
008100 01  VAL-FIELDS.
008200     05  LO-RANGE-FIELD            PIC X(16) VALUE SPACE.
008300     05  EXISTING-TABLE-FIELD      PIC XXX VALUE SPACE.
008400     05  HI-RANGE-FIELD            PIC X(16) VALUE SPACE.
008500     05  TABLE-NAME-FIELD          PIC X(6) VALUE SPACE.
008600*GENERAL
008700     COPY WSCRT.
008800     COPY WSSCRTCH.
008900 01  WORK-FIELD                    PIC X(17) VALUE SPACE.
009000 01  FILLER REDEFINES WORK-FIELD.
009100     05 WORK-CHAR                  PIC X
009200        OCCURS 17 TIMES.
009300 01  WORK-INDEX                    BINARY VALUE ZERO.
009400 01  LO-RANGE  PIC X(16).
009500 01  HI-RANGE  PIC X(16).
009600 01  SAVE-TABLE-NAME               PIC X(6).
009700
000000     COPY WSIOCODE.
000000     COPY WSCTRL.
000000 01  CTRL-RECORD PIC X(130).
000000  
009800 LINKAGE SECTION.
009900 01  CTRL-IO-BLOCK.
010000     COPY LKDIO.
000100 01  CTRL-HEADER-RECORDS.
010100     COPY RCCTRLH.
       01  CF-RECORD.
010200     COPY RCCTRLF.
010300     COPY LKCTRLAR.
010400     COPY RCCTRLT1.
010500     COPY RCCTRLT2.
010600     COPY LKPRC.
010700 PROCEDURE DIVISION USING
000000     CALLED-RC
010800     COPY LKCTRL.
010900 MAIN-LOGIC SECTION.
011000 PROGRAM-BEGIN.
000000     MOVE "CTRLRNG BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     MOVE ZEROES TO CALLED-RC.
011100     PERFORM OPENING-PROCEDURE.
011200     PERFORM MAIN-PROCESS.
011300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CTRLRNG EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
011600 MAIN-LOGIC-STOP.
011700     STOP RUN.
011800 THE-OTHER SECTION.
011900 OPENING-PROCEDURE.
012000     PERFORM OPEN-CRT-FILE.
012100 CLOSING-PROCEDURE.
012200     PERFORM CLOSE-CRT-FILE.
012300     MOVE PFKEY-CODE TO CALLED-RC.
012400
012500 MAIN-PROCESS.
012600     PERFORM ENTER-RANGE.
012700*-----------------------------------------------
012800* THE RANGE OR TABLE SCREEN.
012900*-----------------------------------------------
013000 ENTER-RANGE.
013100     PERFORM ENTER-RANGE-INIT.
013200     PERFORM ENTER-RANGE-ENTRY.
013300 ENTER-RANGE-INIT.
013400     PERFORM INIT-RANGE-FACS.
013500     PERFORM INIT-RANGE-FIELDS.
013600     PERFORM INIT-RANGE-PFS.
013700 ENTER-RANGE-ENTRY.
013800     MOVE SPACE TO SCREEN-ERROR-STATUS.
013900     DISPLAY AND READ RANGE-SCREEN ON CRT-FILE
014000         PFKEYS 1 PF-3.
014100     MOVE SPACE TO MESSAGE-FIELD.
014200     IF NOT PFKEY-1-PRESSED
014300         PERFORM VAL-RANGE-SCREEN
014400         IF SCREEN-ENTRY-ERROR
014500             GO TO ENTER-RANGE-ENTRY
014600         ELSE
014700         PERFORM ADD-CF-VALIDATION
014800         IF TABLE-NAME-FIELD NOT = SPACE AND
014900            EXISTING-TABLE-FIELD = "NO"
015000             PERFORM CREATE-TABLE.
015100 INIT-RANGE-FIELDS.
015200     PERFORM MOVE-RECORD-TO-SCREEN.
015300     MOVE "NO" TO EXISTING-TABLE-FIELD.
015400 INIT-RANGE-FACS.
015500     MOVE UPLOW-ENTRY-FAC TO
015600          FAC OF LO-RANGE-ITEM
015700          FAC OF HI-RANGE-ITEM.
015800
015900     IF CX-TYPE = "B" OR "Z" OR "U" OR "P"
016000         MOVE NUMERIC-ENTRY-FAC TO
016100          FAC OF LO-RANGE-ITEM
016200          FAC OF HI-RANGE-ITEM.
016400 INIT-RANGE-PFS.
016500     MOVE 1 TO PF-3.
016600 MOVE-RECORD-TO-SCREEN.
016700     MOVE CX-LO-RANGE TO WORK-FIELD.
016800     COMPUTE WORK-INDEX = CX-EXT-LEN + 1.
016900     MOVE DIM-FAC TO WORK-CHAR(WORK-INDEX).
017000     MOVE WORK-FIELD TO LO-RANGE-FIELD.
017100     MOVE CX-HI-RANGE TO WORK-FIELD.
017200     MOVE DIM-FAC TO WORK-CHAR(WORK-INDEX).
017300     MOVE WORK-FIELD TO HI-RANGE-FIELD.
017400     MOVE CX-TABLE-NAME TO TABLE-NAME-FIELD.
017500 ADD-CF-VALIDATION.
017600     MOVE LO-RANGE TO CX-LO-RANGE.
017700     MOVE HI-RANGE TO CX-HI-RANGE.
017800     MOVE TABLE-NAME-FIELD TO CX-TABLE-NAME.
017900     IF TABLE-NAME-FIELD NOT = SPACE
018000         PERFORM ADD-TABLE-VALIDATION
018100     ELSE
018200         PERFORM REMOVE-TABLE-VALIDATION.
018300     IF HI-RANGE NOT = SPACE
018400         PERFORM ADD-RANGE-VALIDATION
018500     ELSE
018600         PERFORM REMOVE-RANGE-VALIDATION.
018700 ADD-TABLE-VALIDATION.
018800     IF CX-VALIDATION = SPACE
018900         MOVE "T" TO CX-VALIDATION
019000     ELSE
019100     IF CX-VALIDATION = "CF"
019200         MOVE "CT" TO CX-VALIDATION.
019400 REMOVE-TABLE-VALIDATION.
019500     IF CX-VALIDATION = "T"
019600         MOVE SPACE TO CX-VALIDATION
019700     ELSE
019800     IF CX-VALIDATION = "CT"
019900         MOVE "CF" TO CX-VALIDATION.
020100 ADD-RANGE-VALIDATION.
020200     IF CX-VALIDATION = SPACE
020300         MOVE "R" TO CX-VALIDATION
020400     ELSE
020500     IF CX-VALIDATION = "CF"
020600         MOVE "CR" TO CX-VALIDATION.
020800 REMOVE-RANGE-VALIDATION.
020900     IF CX-VALIDATION = "R"
021000         MOVE SPACE TO CX-VALIDATION
021100     ELSE
021200     IF CX-VALIDATION = "CR"
021300         MOVE "CF" TO CX-VALIDATION.
021500 VAL-RANGE-SCREEN.
021600     PERFORM VAL-LO-RANGE.
021700     IF NOT SCREEN-ENTRY-ERROR
021800         PERFORM VAL-HI-RANGE.
021900     IF NOT SCREEN-ENTRY-ERROR
022000         PERFORM VAL-TABLE-NAME.
022100     IF NOT SCREEN-ENTRY-ERROR
022200         PERFORM VAL-EXISTING-TABLE.
022300 VAL-LO-RANGE.
022400     MOVE SPACE TO LO-RANGE.
022500     STRING
022600         LO-RANGE-FIELD DELIMITED BY DIM-FAC
022700         INTO LO-RANGE.
022800 VAL-HI-RANGE.
022900     MOVE SPACE TO HI-RANGE.
023000     STRING
023100         HI-RANGE-FIELD DELIMITED BY DIM-FAC
023200         INTO HI-RANGE
023300     IF LO-RANGE = SPACE AND
023400        HI-RANGE = SPACE
023500         NEXT SENTENCE
023600     ELSE
023700     IF LO-RANGE NOT <
023800               HI-RANGE
023900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
024100     IF SCREEN-ENTRY-ERROR
024200         MOVE "Invalid Range" TO MESSAGE-FIELD.
024400 VAL-TABLE-NAME.
024500     IF TABLE-NAME-FIELD NOT = SPACE
024600         IF LO-RANGE NOT = SPACE OR
024700            HI-RANGE NOT = SPACE
024800             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
024900             MOVE "May Not Use Both Table and Range Checking"
025000               TO MESSAGE-FIELD.
025200
025300 VAL-EXISTING-TABLE.
025400     IF TABLE-NAME-FIELD = SPACE
025500         NEXT SENTENCE
025600     ELSE
025700     PERFORM CHECK-TABLE-EXISTS
025800     IF EXISTING-TABLE-FIELD = "YES"
025900         PERFORM VAL-EXISTING
026000     ELSE
026100     IF EXISTING-TABLE-FIELD = "NO"
026200         PERFORM VAL-NOT-EXISTING
026300     ELSE
026400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
026500         MOVE "Existing Table Must be 'YES' or 'NO'."
026600               TO MESSAGE-FIELD.
026700
026800 VAL-EXISTING.
026900     IF CALLED-RC NOT = ZEROES
027000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
027100         MOVE "Table File not Found." TO MESSAGE-FIELD.
027300* IF THE SCREEN SAYS THERE IS NO EXISTING FILE, BUT THERE IS
027400* ONE THEN ENABLE PFKEY 3 AND TRY AGAIN. IF PFKEY-3 IS
027500* ALREADY PRESSED THEN IT'S ALL OKAY
027600 VAL-NOT-EXISTING.
027700     IF CALLED-RC = ZEROES
027800         IF NOT PFKEY-3-PRESSED
027900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
028000             MOVE "Table File Exists. Press (3) to Scratch"
028100              TO MESSAGE-FIELD
028200             MOVE 3 TO PF-3.
028400 CHECK-TABLE-EXISTS.
028500     MOVE CX-TABLE-NAME TO SAVE-TABLE-NAME.
028600     MOVE TABLE-NAME-FIELD TO CX-TABLE-NAME.
028700     MOVE "EXISTS" TO CF-ACTION-CODE.
028800     PERFORM CALL-CTRLTBL.
028900     MOVE SAVE-TABLE-NAME TO CX-TABLE-NAME.
029000
000000*Unknown IO errors in CTRLIO. Partial solution is to close
000000*whenever IO is done to a table file. 
029100 CALL-CTRLTBL.
000000     PERFORM CLOSE-CTRL-FILE.
029200     CALL "CTRLTBL" USING
000000     CALLED-RC
029300     COPY LKCTRL.
000000     PERFORM OPEN-IO-CTRL-FILE.
029400*--------------------------------------------------------------
029500* IF WE GET HERE WITH PFKEY-3-PRESSED, THEN ASSUME THAT AN
029600* EXISTING FILE MUST BE SCRATCHED.
029700*--------------------------------------------------------------
029800 CREATE-TABLE.
029900     IF PFKEY-3-PRESSED
030000         PERFORM SCRATCH-TABLE
030100     PERFORM CLOSE-CRT-FILE.
030200     MOVE "CREATE" TO CF-ACTION-CODE.
030300     PERFORM CALL-CTRLTBL.
030400     PERFORM OPEN-CRT-FILE.
030500 SCRATCH-TABLE.
030600     MOVE TABLE-NAME-FIELD TO
030700         SCRATCH-FILE-NAME.
030800     MOVE FILE-LIBRARY OF CTRL-IO-BLOCK TO
030900         SCRATCH-FILE-LIBRARY.
031000     MOVE FILE-VOLUME OF CTRL-IO-BLOCK TO
031100         SCRATCH-FILE-VOLUME.
031200     PERFORM SCRATCH-A-FILE.
031300
031400     COPY PLCRT.
031500     COPY PLSCRTCH.
000000     COPY IOCTRL.
000000     COPY PLKTRACE.
