000100 IDENTIFICATION DIVISION.
000000 PROGRAM-ID. CRIDTEST.
001300 ENVIRONMENT DIVISION.
000000 CONFIGURATION SECTION.
000000 FIGURATIVE-CONSTANTS.
000000     COPY FIGS.
000000 INPUT-OUTPUT SECTION.
000000 FILE-CONTROL.
000000     COPY SLCRT.
000000     SELECT CUST-FILE 
000000         ASSIGN TO "CUSTOMER", "DISK"
000000         ORGANIZATION IS INDEXED
000000         ACCESS MODE DYNAMIC
000000         FILE STATUS IS CUST-FILE-STATUS
000000         RECORD KEY IS CUST-KEY
000000         ALTERNATE RECORD KEY 01 IS CUST-NAME WITH DUPLICATES.
000000     SELECT CUST-DATA-FILE
000000         ASSIGN TO "CUSTDATA" "DISK"
000000         ORGANIZATION IS RELATIVE
000000         ACCESS MODE IS DYNAMIC
000000         FILE STATUS IS CUST-DATA-FILE-STATUS
000000         RELATIVE KEY IS CUST-DATA-KEY.
000000       
002200 DATA DIVISION.
000000 FILE SECTION.
000000     COPY FDCRT.
000000 FD  CUST-FILE
000000     LABEL RECORDS ARE STANDARD
000000           VALUE OF FILENAME IS CUST-FILE-NAME
000000                    LIBRARY  IS CUST-LIBRARY
000000                    VOLUME   IS CUST-VOLUME
000000                                .
000000 01  CUST-RECORD.
000000     05  CUST-KEY.
000000         10  CUST-NO           PIC 9(3).
000000     05  CUST-NAME             PIC X(20).
000000     05  CUST-BALANCE          PIC S9(6)V99 COMP.
000000
000000 FD  CUST-DATA-FILE
000000     LABEL RECORDS ARE STANDARD
000000           VALUE OF FILENAME IS CUST-DATA-FILE-NAME
000000                    LIBRARY  IS CUST-LIBRARY
000000                    VOLUME   IS CUST-VOLUME
000000                                .
000000 01  CUST-DATA-RECORD.
000000     05  CD-CUST-NO             BINARY. 
000000     05  CD-ZONED               PIC S9(5)V99.
000000     05  CD-BIN2                PIC S9(4) BINARY.
000000     05  CD-BIN3                PIC S9(6) BINARY.
000000     05  CD-BIN4                PIC S9(8) BINARY.
000000     05  CD-UNSIGNED            PIC 9(5)V99.
000000
002500 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)cridtest.wcb 1.5 9/11/94".
000000 01  CUST-FILE-STATUS       PIC XX VALUE "00".
000000 01  CUST-DATA-FILE-STATUS  PIC XX VALUE "00".
000000 01  CUST-FILE-NAME         PIC X(8) VALUE "CUSTOMER".
000000 01  CUST-DATA-FILE-NAME    PIC X(8) VALUE "CUSTDATA".
000000 01  CUST-LIBRARY           PIC X(8) VALUE "MJBDATA".
000000 01  CUST-VOLUME            PIC X(6) VALUE "MJBVOL".
000000 01  CUST-DATA-KEY          PIC 9(6) VALUE ZEROES.
000000 01  SAVE-RECORD            PIC X(2048).
000000
000000 01  NEWFILES  PIC X VALUE SPACE.
000000     COPY WSCRT.
000000
000000 01  CUST-SCREEN USAGE IS DISPLAY-WS.
000000     05  FILLER   PIC X(8) ROW 4 COLUMN 20 VALUE "Customer".
000000     05  NO-ITEM PIC ZZZ ROW 4 COLUMN 40
000000         SOURCE CUST-NO OBJECT CUST-NO.
000000     05  FILLER   PIC X(4) ROW 6 COLUMN 20 VALUE "Name".
000000     05  NAME-ITEM PIC X(20) ROW 6 COLUMN 40
000000         SOURCE CUST-NAME OBJECT CUST-NAME.
000000     05  FILLER PIC X(7) ROW 8 COLUMN 20 VALUE "Balance".
000000     05  BALANCE-ITEM PIC Z(5)9.99- ROW 8 COLUMN 40
000000         SOURCE CUST-BALANCE OBJECT CUST-BALANCE.
000000
000000     05  FILLER  PIC X(5) ROW 10 COLUMN 20 VALUE "Zoned".
000000     05  ZONED-ITEM  PIC Z(4)9.99- ROW 10 COLUMN 40
000000         SOURCE CD-ZONED OBJECT CD-ZONED.
000000     05  FILLER  PIC X(5) ROW 12 COLUMN 20 VALUE "Bin2".
000000     05  BIN2-ITEM  PIC ZZZZ9- ROW 12 COLUMN 40
000000         SOURCE CD-BIN2 OBJECT CD-BIN2.
000000     05  FILLER  PIC X(5) ROW 14 COLUMN 20 VALUE "Bin3".
000000     05  BIN3-ITEM  PIC ZZZZZ9- ROW 14 COLUMN 40
000000         SOURCE CD-BIN3 OBJECT CD-BIN3.
000000     05  FILLER  PIC X(5) ROW 16 COLUMN 20 VALUE "Bin4".
000000     05  BIN4-ITEM  PIC ZZZZZZZ9- ROW 16 COLUMN 40
000000         SOURCE CD-BIN4 OBJECT CD-BIN4.
000000     05  FILLER  PIC X(8) ROW 18 COLUMN 20 VALUE "Unsigned".
000000     05  UNSIGNED-ITEM  PIC ZZZZ9.99 ROW 18 COLUMN 40
000000         SOURCE CD-UNSIGNED OBJECT CD-UNSIGNED.
000000     05  FILLER  PIC X(79) ROW 24 COLUMN 2
000000         SOURCE MESSAGE-FIELD.
000000     
000000 01  MENU-SCREEN USAGE IS DISPLAY-WS.
000000     05  FILLER  PIC X(20) ROW 4 COLUMN 20 VALUE
000000         " 1.   Add Records".
000000     05  FILLER  PIC X(20) ROW 5 COLUMN 20 VALUE
000000         " 2.   Change Records".
000000     05  FILLER  PIC X(20) ROW 6 COLUMN 20 VALUE
000000         " 3.   Delete Records".
000000     05  FILLER  PIC X(20) ROW 8 COLUMN 20 VALUE
000000         "16.   Exit".
000000
013300 PROCEDURE DIVISION.
013500 MAIN-LOGIC SECTION.
013600 PROGRAM-BEGIN.
000000     MOVE "CRIDTEST BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013700     PERFORM OPENING-PROCEDURE.
013900     PERFORM MAIN-PROCESS.
014000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CRIDTEST EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014100 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
014300 MAIN-LOGIC-STOP.
000000     STOP RUN.
014500 LEVEL-2 SECTION.
000000 OPENING-PROCEDURE.
000000     ACCEPT NEWFILES.
000000     IF NEWFILES = "Y" or "y"
000000          MOVE "Y" TO NEWFILES
000000     ELSE
000000          MOVE "N" TO NEWFILES.
000000     IF NEWFILES = "Y"
000000          OPEN OUTPUT CUST-FILE
000000          OPEN OUTPUT CUST-DATA-FILE
000000          CLOSE CUST-DATA-FILE
000000          CLOSE CUST-FILE.
000000     OPEN SHARED CUST-FILE.
000000     OPEN I-O CUST-DATA-FILE.
000000     PERFORM OPEN-CRT-FILE.
015900 CLOSING-PROCEDURE.
000000     CLOSE CUST-FILE.
000000     CLOSE CUST-DATA-FILE.
000000     PERFORM CLOSE-CRT-FILE.
016400 MAIN-PROCESS.
000000     PERFORM GET-MENU-PICK.
000000     PERFORM MAINTAIN-FILES
000000         UNTIL END-KEY-PRESSED.
000000
000000 MAINTAIN-FILES.
000000     IF PFKEY-1-PRESSED
000000         PERFORM ADD-RECORDS
000000            UNTIL END-KEY-PRESSED
000000     ELSE
000000     IF PFKEY-2-PRESSED
000000         PERFORM CHANGE-RECORDS
000000            UNTIL END-KEY-PRESSED
000000     ELSE
000000     IF PFKEY-3-PRESSED
000000         PERFORM DELETE-RECORDS
000000            UNTIL END-KEY-PRESSED
000000           .
000000     PERFORM GET-MENU-PICK.
000000
000000 GET-MENU-PICK.
000000     DISPLAY AND READ MENU-SCREEN ON CRT-FILE
000000         ONLY PFKEYS 1, 2, 3, 16.
000000
000000 ADD-RECORDS.
000000     PERFORM ADD-RECORDS-INIT.
000000     PERFORM ADD-RECORDS-ENTRY.
000000 ADD-RECORDS-INIT.
000000     PERFORM INIT-CUST-RECORD.
000000     PERFORM INIT-CUST-DATA-RECORD.
000000 INIT-CUST-RECORD.
000000     MOVE SPACE TO CUST-RECORD.
000000     MOVE ZEROES TO
000000           CUST-NO
000000           CUST-BALANCE.
000000 INIT-CUST-DATA-RECORD.
000000     MOVE SPACE TO CUST-DATA-RECORD.
000000     MOVE ZEROES TO
000000           CD-CUST-NO
000000           CD-ZONED
000000           CD-UNSIGNED
000000           CD-BIN2
000000           CD-BIN3
000000           CD-BIN4
000000             .
000000
000000 ADD-RECORDS-ENTRY.
000000     MOVE SPACE TO SCREEN-ERROR-STATUS.
000000     DISPLAY AND READ CUST-SCREEN ON CRT-FILE
000000         PFKEYS 16.
000000     MOVE SPACE TO MESSAGE-FIELD.
000000     IF NOT END-KEY-PRESSED
000000         PERFORM VAL-ADD-FIELDS
000000         IF SCREEN-ENTRY-ERROR
000000             GO TO ADD-RECORDS-ENTRY
000000         ELSE
000000             PERFORM ADD-THE-RECORD.
000000
000000 ADD-THE-RECORD.
000000    WRITE CUST-RECORD.
000000    WRITE CUST-DATA-RECORD.
000000 VAL-ADD-FIELDS.
000000    PERFORM VAL-CUST-NO.
000000    IF NOT SCREEN-ENTRY-ERROR
000000        PERFORM VAL-KEY-FOR-ADD.
000000 VAL-CUST-NO.
000000    IF CUST-NO = ZERO  
000000        MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000        MOVE "Cust No may not be 0" TO MESSAGE-FIELD
000000    ELSE
000000        MOVE CUST-NO TO CUST-DATA-KEY CD-CUST-NO.
000000 VAL-KEY-FOR-ADD.
000000     READ CUST-FILE RECORD
000000        INVALID KEY
000000          MOVE "22" TO CUST-FILE-STATUS.
000000     IF CUST-FILE-STATUS < "10"
000000        MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000        MOVE "Record already on file" TO MESSAGE-FIELD.
000000 VAL-KEY-FOR-KEY.
000000     READ CUST-FILE RECORD
000000        INVALID KEY
000000          MOVE "22" TO CUST-FILE-STATUS.
000000     IF CUST-FILE-STATUS > "09"
000000        MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000        MOVE "Record not found" TO MESSAGE-FIELD.
000000 CHANGE-RECORDS.
000000     PERFORM ENTER-THE-KEY.
000000     IF NOT END-KEY-PRESSED
000000*         PERFORM CHANGE-RECORDS-INIT
000000         PERFORM CHANGE-RECORDS-ENTRY.
000000 CHANGE-RECORDS-ENTRY.
000000     MOVE SPACE TO SCREEN-ERROR-STATUS.
000000     DISPLAY AND READ CUST-SCREEN ON CRT-FILE
000000         PFKEYS 16.
000000     MOVE SPACE TO MESSAGE-FIELD.
000000     IF NOT END-KEY-PRESSED
000000         PERFORM CHANGE-THE-RECORD.
000000 CHANGE-THE-RECORD.
000000     MOVE CUST-RECORD TO SAVE-RECORD.
000000     READ CUST-FILE RECORD.
000000     MOVE SAVE-RECORD TO CUST-RECORD.
000000     REWRITE CUST-RECORD.
000000     MOVE CUST-DATA-RECORD TO SAVE-RECORD.
000000     READ CUST-DATA-FILE RECORD.
000000     MOVE SAVE-RECORD TO CUST-DATA-RECORD.
000000     REWRITE CUST-DATA-RECORD.
000000     
000000 DELETE-RECORDS.
000000     PERFORM ENTER-THE-KEY.
000000     IF NOT END-KEY-PRESSED
000000*         PERFORM DELETE-RECORDS-INIT
000000         PERFORM DELETE-RECORDS-ENTRY.
000000
000000 DELETE-RECORDS-ENTRY.
000000     MOVE SPACE TO SCREEN-ERROR-STATUS.
000000     MOVE "Press (ENTER) to DELETE" TO MESSAGE-FIELD.
000000     DISPLAY AND READ CUST-SCREEN ON CRT-FILE
000000         PFKEYS 16.
000000     MOVE SPACE TO MESSAGE-FIELD.
000000     IF NOT END-KEY-PRESSED
000000         PERFORM DELETE-THE-RECORD.
000000 DELETE-THE-RECORD.
000000     READ CUST-FILE RECORD.
000000     DELETE CUST-FILE RECORD.
000000     READ CUST-DATA-FILE RECORD.
000000     DELETE CUST-DATA-FILE RECORD.
000000 
000000 ENTER-THE-KEY.
000000     PERFORM ENTER-KEY-INIT.
000000     PERFORM ENTER-KEY-ENTRY.
000000 ENTER-KEY-INIT.
000000     PERFORM ADD-RECORDS-INIT.
000000 ENTER-KEY-ENTRY.
000000     MOVE SPACE TO SCREEN-ERROR-STATUS.
000000     DISPLAY AND READ CUST-SCREEN ON CRT-FILE
000000         PFKEYS 16.
000000     MOVE SPACE TO MESSAGE-FIELD.
000000     IF NOT END-KEY-PRESSED
000000         PERFORM VAL-KEY-FIELDS
000000         IF SCREEN-ENTRY-ERROR
000000              GO TO ENTER-KEY-ENTRY
000000         ELSE
000000              PERFORM LOAD-THE-RECORDS.
000000
000000 VAL-KEY-FIELDS.
000000     PERFORM VAL-CUST-NO.
000000     IF NOT SCREEN-ENTRY-ERROR
000000         PERFORM VAL-KEY-FOR-KEY.
000000 LOAD-THE-RECORDS.
000000     READ CUST-DATA-FILE RECORD
000000          INVALID KEY MOVE "22" TO CUST-DATA-FILE-STATUS
000000          DISPLAY "Missing Record on CUST-DATA-FILE".
000000     COPY PLCRT.

000000     COPY PLKTRACE.
