000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQCTL.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/19/90.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE CONTROL FILE:  FILENAME LIRARY VOLUME
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800
001900 DATA DIVISION.
002000 FILE SECTION.
002100
002200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqctl.wcb 1.8 9/11/94".
002300     COPY WSGP.
002400     COPY WSINT.
002500     COPY WSCRTS.
002600
002700 01  CONTROL-GETPARM-FIELDS.
002800     05  CONTROL-MSG-ID               PIC X(4) VALUE "0010".
002900     05  CONTROL-PRNAME               PIC X(8) VALUE "CONTROL".
003000     05  CONTROL-MSG-ISSUER           PIC X(6) VALUE "INQUIR".
003100
003200 01  CONTROL-MSG-COUNT.
003300     05  FILLER                        BINARY   VALUE ZERO.
003400     05  FILLER                        BINARY   VALUE 2.
003500 01  CONTROL-MSG-TEXT-1               PIC X(1) VALUE
003600         " ".
003700
003800 01  CONTROL-MSG-TEXT-2               PIC X(56) VALUE
003900     "Please specify the Control File to use for this inquiry.".
004000 01  CONTROL-FILE-KW              PIC X(8) VALUE "FILE".
004100 01  CONTROL-FILE-FIELD           PIC X(8) VALUE SPACE.
004200 01  CONTROL-FILE-DEFAULT         PIC X(8) VALUE SPACE.
004300 01  CONTROL-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
004400 01  CONTROL-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
004500 01  CONTROL-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
004600 01  CONTROL-VOLUME-KW              PIC X(8) VALUE "VOLUME".
004700 01  CONTROL-VOLUME-FIELD           PIC X(6) VALUE SPACE.
004800 01  CONTROL-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
004900 01  CONTROL-TEXT-01                  PIC X(50) VALUE
005000         "Note:  The Control File is created by the utility ".
005100 01  CONTROL-TEXT-02                  PIC X(7) VALUE
005200         "CONTROL".
005300 01  CONTROL-TEXT-03                  PIC X(1) VALUE
005400         ",".
005500 01  CONTROL-TEXT-04                  PIC X(74) VALUE
005600         "       and is a prerequisite to running a inquiry.".
006200
006300 01  MESSAGE-LITERALS.
006400     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
006500         "* Error * File Name cannot be blank".
006600     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
006700         "* Error * Invalid File Name".
006800     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
006900         "* Error * Invalid Library Name".
007000     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
007100         "* Error * Invalid Volume Name".
007200
000000     COPY LKVALNAM.
000000     COPY LKPRC.
000000
010800 LINKAGE SECTION.
010900 01  CTL-IO-BLOCK.
011000     COPY LKDIO.
011100     COPY WSRPTREC.
000000
011200 01  CTL-GETPARM-TYPE          PIC XX.
000000
011300 PROCEDURE DIVISION USING
011400     CTL-IO-BLOCK
011500     RPT-RECORDS
000000     CTL-GETPARM-TYPE.
000000
011600 MAIN-LOGIC SECTION.
011700 PROGRAM-BEGIN.
000000     MOVE "INQCTL BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
011800     PERFORM OPENING-PROCEDURE.
011900     PERFORM MAIN-PROCESS.
012000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQCTL EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012100 MAIN-LOGIC-EXIT.
012200     EXIT PROGRAM.
012300 MAIN-LOGIC-STOP.
012400     STOP RUN.
012500 LEVEL-2 SECTION.
012600 OPENING-PROCEDURE.
012700     MOVE RCF-PRIM-FILE TO CONTROL-FILE-FIELD.
012800     MOVE RCF-PRIM-LIBRARY TO CONTROL-LIBRARY-FIELD.
012900     MOVE RCF-PRIM-VOLUME TO CONTROL-VOLUME-FIELD.
013000
013100 CLOSING-PROCEDURE.
013200     IF GP-PFKEY-1-PRESSED
013300         MOVE SPACES TO RCF-PRIM-FILE
013400                 FILE-NAME OF CTL-IO-BLOCK.
013500
013800 MAIN-PROCESS.
013900     MOVE SPACE TO SCREEN-ERROR-STATUS.
014100
014200     PERFORM CALL-CONTROL-GETPARM.
000000
000000* IF THIS ROUTINE LOOPS AT ALL, IT WILL BE BECAUSE OF AN ERROR
000000* SO THE DISPLAY CAN BE FORCED INTO THE OPEN.
000000     MOVE "I" TO CTL-GETPARM-TYPE.
000000
014300     MOVE SPACES TO CONTROL-MSG-TEXT-2.
014400     IF NOT GP-PFKEY-1-PRESSED
014500         PERFORM VAL-FILE-NAME
014600         IF NOT SCREEN-ENTRY-ERROR
014700             PERFORM VAL-LIB-NAME
014800                 IF NOT SCREEN-ENTRY-ERROR
014900                    PERFORM VAL-VOL-NAME.
015000
015100     IF NOT SCREEN-ENTRY-ERROR  AND
015200        NOT GP-PFKEY-1-PRESSED
015300         MOVE 0 TO CALLED-RC
015400         CALL "KEXISTS" USING
000000             CALLED-RC
015500             CONTROL-FILE-FIELD
015600             CONTROL-LIBRARY-FIELD
015700             CONTROL-VOLUME-FIELD.
015800
015900     IF CALLED-RC NOT = 0
016000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016100         MOVE "Specified Control File does not exist."
016200             TO CONTROL-MSG-TEXT-2.
016300
016400     IF NOT SCREEN-ENTRY-ERROR
016500         MOVE CONTROL-FILE-FIELD TO RCF-PRIM-FILE
016600             FILE-NAME OF CTL-IO-BLOCK
016700         MOVE CONTROL-LIBRARY-FIELD TO RCF-PRIM-LIBRARY
016800             FILE-LIBRARY OF CTL-IO-BLOCK
016900         MOVE CONTROL-VOLUME-FIELD TO RCF-PRIM-VOLUME
017000             FILE-VOLUME OF CTL-IO-BLOCK.
017100
017200     IF SCREEN-ENTRY-ERROR
017300         GO TO MAIN-PROCESS.
017400
017500 CALL-CONTROL-GETPARM.
017700
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-01-VAL TO PFKEY-MASK-16.
000000
000000     MOVE HEX40 TO GP-PFKEY-RECEIVER.
017800     CALL "GETPARM" USING
017900         CTL-GETPARM-TYPE, GP-REQUEST-FORM, CONTROL-PRNAME,
018000         GP-PFKEY-RECEIVER, CONTROL-MSG-ID,
018100         CONTROL-MSG-ISSUER,
018200
018300         CONTROL-MSG-COUNT,
018400         CONTROL-MSG-TEXT-1, INT01,
018500         CONTROL-MSG-TEXT-2, INT55,
018600
018700         GP-KW-TYPE, CONTROL-FILE-KW,
018800         CONTROL-FILE-FIELD, INT08,
018900         GP-ABS, INT15, GP-ABS, INT07,
019000         GP-UPPERCASE-DATA
019100
019200         GP-KW-TYPE, CONTROL-LIBRARY-KW,
019300         CONTROL-LIBRARY-FIELD, INT08,
019400         GP-ABS, INT15, GP-ABS, INT30,
019500         GP-UPPERCASE-DATA
019600
019700         GP-KW-TYPE, CONTROL-VOLUME-KW,
019800         CONTROL-VOLUME-FIELD, INT06,
019900         GP-ABS, INT15, GP-ABS, INT53,
020000         GP-UPPERCASE-DATA
020100
020200         GP-TEXT-TYPE, CONTROL-TEXT-01, INT50,
020300         GP-ABS, INT19, GP-ABS, INT02,
020400
020500         GP-TEXT-TYPE, CONTROL-TEXT-02, INT07,
020600         GP-ABS, INT19, GP-ABS, INT54,
020700
020800         GP-TEXT-TYPE, CONTROL-TEXT-03, INT01,
020900         GP-ABS, INT19, GP-ABS, INT63,
021000
021100         GP-TEXT-TYPE, CONTROL-TEXT-04, INT73,
021200         GP-ABS, INT20, GP-ABS, INT02,
021300
021400         GP-ENABLE-ENTER-KEY
021500         GP-PFKEY-TYPE, PFKEY-MASK.
021600
000000     IF GP-PFKEY-RECEIVER = LOW-VALUES
000000         MOVE HEX40 TO GP-PFKEY-RECEIVER.
022000
022100 VAL-FILE-NAME.
022200
022300     IF CONTROL-FILE-FIELD = SPACES
022400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
022500         MOVE FILE-ID-BLANK-LITERAL TO CONTROL-MSG-TEXT-2.
022600
022700     IF NOT SCREEN-ENTRY-ERROR
022800         MOVE CONTROL-FILE-FIELD TO VAL-NAME
022900         PERFORM TEST-THE-NAME
023000         IF SCREEN-ENTRY-ERROR
023100             MOVE INVALID-FILE-LITERAL TO CONTROL-MSG-TEXT-2.
023200
023300 VAL-LIB-NAME.
023400
023500     MOVE CONTROL-LIBRARY-FIELD TO VAL-NAME.
023600     PERFORM TEST-THE-NAME.
023700     IF SCREEN-ENTRY-ERROR
023800         MOVE INVALID-LIB-LITERAL TO CONTROL-MSG-TEXT-2.
023900
024000 VAL-VOL-NAME.
024100
024200     MOVE CONTROL-VOLUME-FIELD TO VAL-NAME.
024300     PERFORM TEST-THE-VOL.
024400     IF SCREEN-ENTRY-ERROR
024500         MOVE INVALID-VOL-LITERAL TO CONTROL-MSG-TEXT-2.
024600
031500 TEST-THE-NAME.
000000     CALL "VALNAM" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
031500 TEST-THE-VOL.
000000     CALL "VALVOL" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
000000     COPY PLKTRACE.
