000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTDEF.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/08/89.
000500 DATE-COMPILED.
000600*--------------------------------------------------------------
000700* DEFINES THE RERPORT FILE:  FILE NAME  ADDITIONAL DATA FILES
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptdef.wcb 1.4 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
000000     COPY LKVALNAM.
002700
002800 01  DEFINE-REPORT-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(15) ROW 2 COLUMN 34 VALUE
003100         "DEFINE A REPORT".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 4 COLUMN 03
003400         SOURCE  UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(70) ROW 5 COLUMN 7 VALUE
003700         "The Report Definition option of the Report Utility enabl
003800-        "es the user to".
003900     05  FILLER
004000         PIC X(64) ROW 6 COLUMN 10 VALUE
004100         "define a report interactively instead of by writing a pr
004200-        "ogram to".
004300     05  FILLER
004400         PIC X(60) ROW 7 COLUMN 12 VALUE
004500         "accomplish the task. the first step is to define the rep
004600-        "ort.".
004700     05  FILLER
004800         PIC X(71) ROW 8 COLUMN 6 VALUE
004900         "The Report ID will become the name of a file containing
005000-        "the information".
005100     05  FILLER
005200         PIC X(35) ROW 9 COLUMN 24 VALUE
005300         "needed to print the desired report.".
005400     05  FILLER
005500         PIC X(9) ROW 13 COLUMN 21 VALUE
005600         "Report ID".
005700     05  DEFRPT-REPORT-ID-ITEM
005800         PIC X(8) ROW 13 COLUMN 32
005900         SOURCE DEFRPT-REPORT-ID-FIELD
006000         OBJECT DEFRPT-REPORT-ID-FIELD.
006100     05  FILLER
006200         PIC X(21) ROW 13 COLUMN 43 VALUE
006300         "(any valid file name)".
006400     05  FILLER
006500         PIC X(20) ROW 15 COLUMN 10 VALUE
006600         "Additional Data File".
006700     05  DEFRPT-ADD-DATA-FILE-ITEM
006800         PIC XXX ROW 15 COLUMN 32
006900         SOURCE DEFRPT-ADD-DATA-FILE-FIELD
007000         OBJECT DEFRPT-ADD-DATA-FILE-FIELD.
007100     05  FILLER
007200         PIC X(33) ROW 15 COLUMN 43 VALUE
007300         "(will the report require a second".
007400     05  FILLER
007500         PIC X(16) ROW 16 COLUMN 50 VALUE
007600         "input data file)".
007700     05  FILLER
007800         PIC X(55) ROW 21 COLUMN 14 VALUE
007900       "**  Press (16) to Return to the Report Utility Menu  **".
008000     05  FILLER
008100         PIC X(33) ROW 22 COLUMN 26 VALUE
008200         "**  Press (ENTER) to Continue  **".
008300
008400 01  DEFINE-REPORT-FIELDS.
008500     05  UPPER-MSG-FIELD                   PIC X(78) VALUE SPACE.
008600     05  DEFRPT-REPORT-ID-FIELD            PIC X(8) VALUE SPACE.
008700     05  DEFRPT-ADD-DATA-FILE-FIELD        PIC XXX VALUE "NO ".
008800
008900 01  MESSAGE-LITERALS.
009000     05  RPT-ID-BLANK-LITERAL         PIC X(78) VALUE
009100         "* Error * Report ID cannot be blank".
009200     05  RPT-ID-INVALID-LITERAL       PIC X(78) VALUE
009300         "* Error * Report ID is Invalid".
009400     05  MUST-BE-YES-NO-LITERAL          PIC X(78) VALUE
009500         "* Error * Value must be 'YES' or 'No'".
009600
013200 LINKAGE SECTION.
013300 01  RPT-PFKEY               PIC 9(02).
013400 01  PASSED-REPORT-ID        PIC X(08).
013500     COPY WSRPTREC.
013600
013700 PROCEDURE DIVISION USING
013800     RPT-PFKEY
013900     PASSED-REPORT-ID
014000     RPT-RECORDS.
014100
014200 MAIN-LOGIC SECTION.
014300 PROGRAM-BEGIN.
000000     MOVE "RPTDEF BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014400     PERFORM OPENING-PROCEDURE.
014500     PERFORM MAIN-PROCESS.
014600     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTDEF EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014700 MAIN-LOGIC-EXIT.
014800     EXIT PROGRAM.
014900 MAIN-LOGIC-STOP.
015000     STOP RUN.
015100 LEVEL-2 SECTION.
015200 OPENING-PROCEDURE.
015300     OPEN I-O CRT-FILE.
015400 CLOSING-PROCEDURE.
015500     CLOSE CRT-FILE.
015600
015700 MAIN-PROCESS.
015800     MOVE UPPER-ENTRY-FAC TO FAC OF DEFRPT-REPORT-ID-ITEM
015900                             FAC OF DEFRPT-ADD-DATA-FILE-ITEM.
016000
016100     MOVE DISPLAY-HI-FAC  TO FAC OF UPPER-MSG-ITEM.
016200
016300     PERFORM DEFINE-REPORT-FILE-NAME.
016400
016500     MOVE 0 TO RHD-USER-EXIT
016600             RHD-SORT
016700             RHD-DATA-LIMIT
016800             RHD-CONTROL-FIELDS
016900             RHD-COLUMN-SUBS
017000             RHD-NEW-FIELDS
017100             RHD-LINE-1-LEN
017200             RHD-FIELD-COUNT
017300             RHD-PRINT-FIELD-COUNT
017400             RHD-LINE-2-LEN
017500             RHD-LINE-3-LEN.
017600     MOVE DEFRPT-REPORT-ID-FIELD TO
017700             PASSED-REPORT-ID
017800             RTT-TITLE (1).
017900
018000     IF DEFRPT-ADD-DATA-FILE-FIELD = "YES"
018100         MOVE 1 TO RHD-SEC-FILE
018200     ELSE
018300         MOVE 0 TO RHD-SEC-FILE
018400         MOVE SPACES TO RDF-SEC-FILE RDF-SEC-LIBRARY
018500             RDF-SEC-VOLUME RDF-KEY-TO-SEC RDF-KEY-OCCURRENCE-A
018600             RCF-SEC-FILE RCF-SEC-LIBRARY RCF-SEC-VOLUME.
018700
018800 DEFINE-REPORT-FILE-NAME.
018900
019000     MOVE SPACES TO SCREEN-ERROR-STATUS.
019100
019200     DISPLAY AND READ ALTERED DEFINE-REPORT-SCREEN ON CRT-FILE
019300     PFKEYS 16.
019400
019500     MOVE SPACE TO UPPER-MSG-FIELD.
019600
019700     IF PFKEY-16-PRESSED
019800         MOVE PFKEY-CODE TO RPT-PFKEY.
019900
020000     IF NOT PFKEY-16-PRESSED
020100         PERFORM  VAL-RPT-ID
020200         IF NOT SCREEN-ENTRY-ERROR
020300             PERFORM  VAL-ADD-DATA-ANSWER.
020400
020500     IF NOT PFKEY-16-PRESSED
020600         IF SCREEN-ENTRY-ERROR
020700             GO TO DEFINE-REPORT-FILE-NAME.
020800
020900 VAL-RPT-ID.
021000
021100     IF DEFRPT-REPORT-ID-FIELD = SPACES
021200         SET ERROR-BIT IN FAC OF DEFRPT-REPORT-ID-ITEM ON
021300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
021400         MOVE RPT-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
021500
021600     MOVE DEFRPT-REPORT-ID-FIELD TO VAL-NAME.
000000     PERFORM TEST-THE-NAME.
022000     IF SCREEN-ENTRY-ERROR
022100         SET ERROR-BIT IN FAC OF DEFRPT-REPORT-ID-ITEM ON
022300         MOVE RPT-ID-INVALID-LITERAL TO UPPER-MSG-FIELD.
022400
037400 TEST-THE-NAME.
           CALL "VALNAM" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.


025400 VAL-ADD-DATA-ANSWER.
025500
025600     IF DEFRPT-ADD-DATA-FILE-FIELD NOT = "YES"  AND
025700        DEFRPT-ADD-DATA-FILE-FIELD NOT = "NO "
025800         SET ERROR-BIT IN FAC OF DEFRPT-ADD-DATA-FILE-ITEM ON
025900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
026000         MOVE MUST-BE-YES-NO-LITERAL TO UPPER-MSG-FIELD.
026100
026200
026300
000000     COPY PLKTRACE.
