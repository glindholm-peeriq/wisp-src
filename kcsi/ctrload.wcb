000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLOAD.
000300*--------------------------------------------------
000900* LOAD AND VERIFY A CONTROL FILE (ASSUMED TO BE OPEN)
001000*--------------------------------------------------
001700 ENVIRONMENT DIVISION.
001800 CONFIGURATION SECTION.
001900 FIGURATIVE-CONSTANTS.
002000     COPY FIGS.
002100 INPUT-OUTPUT SECTION.
002200 FILE-CONTROL.
002300 DATA DIVISION.
002400 FILE SECTION.
002500 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrload.wcb 1.5 9/11/94".
002600 01  PROGRAM-NAME  PIC X(8) VALUE "CTRLOAD".
002800     COPY RCCTRL.
004800     COPY WSFILEIO.
004900     COPY WSIOCODE.
005000     COPY WSCRTS.
005100* CTRL-FILE-HEADER FIELDS AND GENERAL WS
005200     COPY WSCTRL.
000000     COPY LKPRC.
000000 LINKAGE SECTION.
005400 01  CTRL-IO-BLOCK.
005500     COPY LKDIO.
005600 01  CTRL-HEADER-RECORDS.
005700     COPY RCCTRLH.
005800 01  CF-RECORD.
005900     COPY RCCTRLF.
006000     COPY LKCTRLAR.
006100     COPY RCCTRLT1.
006200     COPY RCCTRLT2.
006300 PROCEDURE DIVISION USING
000000     COPY LKCTRL.
006400 MAIN-LOGIC SECTION.
006500 PROGRAM-BEGIN.
000000     MOVE "CTRLOAD BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     MOVE ZEROES TO CALLED-RC.
006800     PERFORM OPENING-PROCEDURE.
007000     PERFORM MAIN-PROCESS.
007100     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CTRLOAD EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
007200 MAIN-LOGIC-EXIT.
007300     EXIT PROGRAM.
007400 MAIN-LOGIC-STOP.
007500     STOP RUN.
007600 THE-OTHER SECTION.
007700 OPENING-PROCEDURE.
009200 CLOSING-PROCEDURE.
009400
009500 MAIN-PROCESS.
000000     PERFORM INIT-CTRL-HEADER-RECORDS.
033700     PERFORM LOAD-CTRL-HEADER-RECORDS.
033800     PERFORM LOAD-FIELD-RECORDS.
033900*-----------------------------------------------
034000* START THE FILE ON THE FIRST FIELD KEY NAME
034100* LOAD THEM IN TO THE ARRAY UNTIL AN ERROR OCCURS
034200* OR AT-END.
034300*-----------------------------------------------
034400 LOAD-FIELD-RECORDS.
000000     MOVE SPACE TO CF-MSG.
034500     PERFORM INIT-CTRL-ARRAY.
034600     PERFORM INIT-MAP.
034700     MOVE "0" TO CTRL-KEY.
034800     PERFORM START-NLT-CTRL-KEY.
034900     IF CTRL-FILE-STATUS NOT = AT-END
035000         PERFORM READ-NEXT-CTRL-RECORD
035100         MOVE CTRL-RECORD TO CF-RECORD
000000         PERFORM CLEAN-UP-CF-RECORD
035200         MOVE SPACE TO CF-MSG
035300                       SCREEN-ERROR-STATUS
035400         PERFORM LOAD-A-FIELD-RECORD UNTIL
035500             CTRL-FILE-STATUS = AT-END
035600             OR CF-MSG NOT = SPACE
035700         PERFORM SORT-BY-SEQ.
035800
035900 LOAD-A-FIELD-RECORD.
036000     PERFORM VAL-FIELD-RECORD.
036100     IF CF-MSG NOT = SPACE
036200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
036300     ELSE
036400         PERFORM APPEND-TO-ARRAY
036500         PERFORM ADD-TO-MAP
036600         PERFORM READ-NEXT-CTRL-RECORD
036700         IF CTRL-FILE-STATUS NOT = AT-END
036800             MOVE CTRL-RECORD TO CF-RECORD
000000             PERFORM CLEAN-UP-CF-RECORD.
000000
000000 CLEAN-UP-CF-RECORD.
000000     IF CX-INT-LEN IS NOT NUMERIC
000000         MOVE ZEROES TO CX-INT-LEN.
000000     IF CX-START-POS IS NOT NUMERIC
000000         MOVE 1 TO CX-START-POS.
000000     IF CX-OCCURRENCES IS NOT NUMERIC
000000         MOVE 1 TO CX-OCCURRENCES.
000000     IF CX-DECIMAL-INSERT IS NOT NUMERIC
000000         MOVE ZEROES TO CX-DECIMAL-INSERT.
000000     IF CX-ZERO-SUPPRESS IS NOT NUMERIC
000000         MOVE ZEROES TO CX-ZERO-SUPPRESS.
000000     IF CX-SIGN-CONTROL IS NOT NUMERIC
000000         MOVE ZEROES TO CX-SIGN-CONTROL.
000000     IF CX-DOLLAR-COMMA IS NOT NUMERIC
000000         MOVE ZEROES TO CX-DOLLAR-COMMA.
000000     IF CX-EXT-LEN IS NOT NUMERIC
000000         MOVE CX-INT-LEN TO CX-EXT-LEN.
000000     IF CX-REPORT-CODE IS NOT NUMERIC
000000         MOVE ZEROES TO CX-REPORT-CODE.
000000     IF CX-UPDATE-CODE IS NOT NUMERIC
000000         MOVE ZEROES TO CX-UPDATE-CODE.
000000     IF CX-DECIMAL-POS IS NOT NUMERIC
000000         MOVE ZEROES TO CX-DECIMAL-POS.
000000     IF CX-BINARY-EDIT IS NOT NUMERIC
000000         MOVE ZEROES TO CX-BINARY-EDIT.
000000     IF CX-UPDATE-SEQ IS NOT NUMERIC
000000         MOVE ZEROES TO CX-UPDATE-SEQ.
000000     IF CX-PACKED-DIGITS IS NOT NUMERIC
000000         MOVE ZEROES TO CX-PACKED-DIGITS.
000000     IF CX-GROUP-FIELDS IS NOT NUMERIC
000000         MOVE ZEROES TO CX-GROUP-FIELDS.
000000     IF CX-DISPLAY-CODE IS NOT NUMERIC
000000         MOVE ZEROES TO CX-DISPLAY-CODE.
000000     IF CX-EDITED-LEN IS NOT NUMERIC
000000         MOVE ZEROES TO CX-EDITED-LEN.
036900* ACCESS TO CONTROL ARRAY
043900 INIT-CTRL-ARRAY.
044000     MOVE "INIT" TO CF-ACTION-CODE.
044100     PERFORM CALL-CTRLARY.
044800 APPEND-TO-ARRAY.
044900     MOVE "APPEND" TO CF-ACTION-CODE.
045000     PERFORM CALL-CTRLARY.
044200 SORT-BY-SEQ.
044300     MOVE "SORTSEQ" TO CF-ACTION-CODE.
044400     PERFORM CALL-CTRLARY.
045700 CALL-CTRLARY.
045800     CALL "CTRLARY" USING
000000     CALLED-RC
045900     COPY LKCTRL.
000000
000000* ACCESS TO THE MAP
043600 INIT-MAP.
043700     MOVE "INIT" TO CF-ACTION-CODE.
043800     PERFORM CALL-CTRLMAP.
043000 ADD-TO-MAP.
043100     MOVE "ADD" TO CF-ACTION-CODE.
043200     PERFORM CALL-CTRLMAP.
046300 CALL-CTRLMAP.
046400     CALL "CTRLMAP" USING
000000     CALLED-RC
046500     COPY LKCTRL.
000000
000000* VALIDATION
045100 VAL-FIELD-RECORD.
045200     MOVE "ADDFIL" TO CF-ACTION-CODE.
045300     PERFORM CALL-CTRLVAL.
045400 CALL-CTRLVAL.
045500     CALL "CTRLVAL" USING
000000     CALLED-RC
045600     COPY LKCTRL.
000000
000000
048100* GENERAL ROUTINES.
048300* INITIALIZATION FOR CTRL HEADERS
048400     COPY PLCTRLH.
048600* IO TO THE CTRL-FILE
048700     COPY IOCTRL.
000000     COPY PLKTRACE.
