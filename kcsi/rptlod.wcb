000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTLOD.
000300 AUTHOR. MO BUDLONG.
000400 DATE-WRITTEN.  09/30/89.
000500 DATE-COMPILED.
000600*--------------------------------------------------------------
000700* LOADS THE REPORT HEADER RECORDS FROM THE REPORT FILE
000800*--------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300       COPY FIGS.
001400 INPUT-OUTPUT SECTION.
001500 FILE-CONTROL.
001600 DATA DIVISION.
001700 FILE SECTION.
001800 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptlod.wcb 1.9 9/11/94".
001900 01  DATA-LIMIT-KEY.
002000     05  DATA-LIMIT-CODE  PIC X.
002100     05  FILLER           PIC X(12).
002200 01  NEW-FIELD-KEY.
002300     05  NEW-FIELD-CODE   PIC X.
002400     05  NEW-FIELD-NAME   PIC X(8).
002500     05  FILLER           PIC X(4).
002600 01  SAVE-LIMIT-CODE       PIC X.
002700 01  SAVE-FIELD-NAME      PIC X(8).
002800 01  WORK-IDX          BINARY.
002900 01  LOAD-ERROR        PIC 99.
003000     88  NO-ERROR      VALUE ZEROES.
003100     COPY WSFILEIO.
003200     COPY WSIOCODE.
003300     COPY WSRPT.
003400     COPY RCRPT.
003500     COPY WSRPTWRK.
       01  CALC-FIELD-COUNT    PIC 99.
003600 LINKAGE SECTION.
003700     COPY WSRPTREC.
003800 01  RPT-IO-BLOCK.
003900     COPY LKDIO.
000000     COPY LKPRC.
004000 PROCEDURE DIVISION USING  CALLED-RC RPT-IO-BLOCK RPT-RECORDS.
004100
004200 MAIN-LOGIC SECTION.
004300 PROGRAM-BEGIN.
000000     MOVE "RPTLOD BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
004400     PERFORM OPENING-PROCEDURE.
000000     IF RPT-FILE-STATUS = ZEROES
004500         PERFORM MAIN-PROCESS.
004600     PERFORM CLOSING-PROCEDURE.
004700     MOVE LOAD-ERROR TO CALLED-RC.
000000     MOVE "RPTLOD EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
004800 MAIN-LOGIC-EXIT.
004900     EXIT PROGRAM.
005000 MAIN-LOGIC-STOP.
005100     STOP RUN.
005200
005300 LEVEL-2 SECTION.
005400 OPENING-PROCEDURE.
005900     MOVE ZEROES TO LOAD-ERROR.
000000     PERFORM DOES-RPT-FILE-EXIST.
000000     IF RPT-FILE-STATUS = ZEROES
005500         PERFORM OPEN-INPUT-RPT-FILE
000000     ELSE
000000         MOVE 95 TO LOAD-ERROR.
005600 CLOSING-PROCEDURE.
005700     PERFORM CLOSE-RPT-FILE.
005800 MAIN-PROCESS.
006000     MOVE SPACE TO RPT-RECORDS RPT-WORK-RECORDS.
006100     PERFORM LOAD-HEADER-RECORD.
006200     IF NO-ERROR
006300     PERFORM LOAD-RDF-RECORD.
006400     IF NO-ERROR
006500     PERFORM LOAD-RCF-RECORD.
006600     IF NO-ERROR
006700     PERFORM LOAD-SORT-RECORDS.
006800     IF NO-ERROR
006900     PERFORM LOAD-RCB-RECORD.
007000     IF NO-ERROR
007100     PERFORM LOAD-TITLE-RECORDS.
007200     IF NO-ERROR
007300     PERFORM LOAD-RCD-RECORDS.
007400     IF NO-ERROR
007500     PERFORM LOAD-LIMIT-RECORDS.
007600     IF NO-ERROR
007700     PERFORM LOAD-NF-RECORDS.
007800     IF NO-ERROR
007900     PERFORM LOAD-FIELD-RECORDS.
	   IF NO-ERROR
	   PERFORM LOAD-AKA-RECORDS.
008000 LOAD-HEADER-RECORD.
008100     MOVE RHD-KEY-VALUE TO RPT-KEY.
008200     PERFORM READ-RPT-RECORD.
008300     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
008400         MOVE 99 TO LOAD-ERROR
008500     ELSE
008600         MOVE RPT-RECORD TO RHD-RECORD.
008700 LOAD-RDF-RECORD.
008800     MOVE RDF-KEY-VALUE TO RPT-KEY.
008900     PERFORM READ-RPT-RECORD.
009000     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
009100         MOVE 99 TO LOAD-ERROR
009200     ELSE
009300         MOVE RPT-RECORD TO RDF-RECORD.
009400 LOAD-RCF-RECORD.
009500     MOVE RCF-KEY-VALUE TO RPT-KEY.
009600     PERFORM READ-RPT-RECORD.
009700     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
009800         MOVE 99 TO LOAD-ERROR
009900     ELSE
010000         MOVE RPT-RECORD TO RCF-RECORD.
010100 LOAD-SORT-RECORDS.
010200     MOVE 1 TO ST-IDX.
010300     MOVE 1 TO RST-KEY-IDX.
010400     MOVE RST-KEY-VALUE TO RPT-KEY.
010500     PERFORM READ-RPT-RECORD.
010600     IF RPT-FILE-STATUS NOT = RECORD-NOT-FOUND
010700         MOVE RPT-RECORD TO RST-RECORD
010800         PERFORM LOAD-A-SORT
010900          VARYING WORK-IDX FROM 1 BY 1
011000           UNTIL WORK-IDX > 6.
011100     MOVE 2 TO RST-KEY-IDX.
011200     MOVE RST-KEY-VALUE TO RPT-KEY.
011300     PERFORM READ-RPT-RECORD.
011400     IF RPT-FILE-STATUS NOT = RECORD-NOT-FOUND
011500         MOVE RPT-RECORD TO RST-RECORD
011600         PERFORM LOAD-A-SORT
011700          VARYING WORK-IDX FROM 1 BY 1
011800           UNTIL WORK-IDX > 2.
011900 LOAD-A-SORT.
012000     MOVE RST-SORT(WORK-IDX) TO ST-SORT(ST-IDX).
012100     ADD 1 TO ST-IDX.
012200
012300 LOAD-RCB-RECORD.
012400     MOVE RCB-KEY-VALUE TO RPT-KEY.
012500     PERFORM READ-RPT-RECORD.
012600     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
012700         NEXT SENTENCE
012800     ELSE
012900         MOVE RPT-RECORD TO RCB-RECORD.
013000     PERFORM ZERO-RCB-OCCURRENCE
013100      VARYING RCB-IDX FROM 1 BY 1
013200       UNTIL RCB-IDX > 5
013300       .
013400 ZERO-RCB-OCCURRENCE.
013500     IF RCB-OCCURRENCE-A(RCB-IDX) = SPACE
013600        MOVE ZEROES TO RCB-OCCURRENCE(RCB-IDX).
013700
013800 LOAD-TITLE-RECORDS.
013900     MOVE RTT-TITLE-VALUE TO RTT-TITLE-OR-PAGE.
014000     PERFORM LOAD-A-TITLE-RECORD
014100      VARYING RTT-KEY-IDX FROM 1 BY 1
014200       UNTIL RTT-KEY-IDX > 6.
014300 LOAD-A-TITLE-RECORD.
014400     MOVE RTT-KEY-IDX TO RTT-IDX.
014500     IF RTT-KEY-IDX = 4
014600         MOVE RTT-PAGE-VALUE TO
014700           RTT-TITLE-OR-PAGE.
014800     MOVE RTT-KEY-VALUE TO RPT-KEY.
014900     PERFORM READ-RPT-RECORD.
015000     IF RPT-FILE-STATUS NOT = RECORD-NOT-FOUND
015100         MOVE RPT-RECORD TO RTT-RECORD(RTT-IDX).
015200 LOAD-RCD-RECORDS.
015300     PERFORM LOAD-AN-RCD-RECORD
015400      VARYING RCD-KEY-IDX FROM 1 BY 1
015500       UNTIL RCD-KEY-IDX > 5.
015600 LOAD-AN-RCD-RECORD.
015700     MOVE RCD-KEY-IDX TO RCD-IDX.
015800     MOVE RCD-KEY-VALUE TO RPT-KEY.
015900     PERFORM READ-RPT-RECORD.
016000     IF RPT-FILE-STATUS NOT = RECORD-NOT-FOUND
016100         MOVE RPT-RECORD TO RCD-RECORD(RCD-IDX).
016200 LOAD-LIMIT-RECORDS.
016300     MOVE 1 TO DL-IDX.
016400     MOVE "C" TO DATA-LIMIT-KEY.
016500     MOVE DATA-LIMIT-KEY TO RPT-KEY.
016600     PERFORM START-NLT-RPT-KEY.
016700     IF RPT-FILE-STATUS NOT = AT-END
016800         PERFORM READ-NEXT-RPT-RECORD
016900         MOVE RPT-RECORD TO RDL-RECORD
017000         IF RPT-FILE-STATUS NOT = AT-END
017100             MOVE RPT-KEY TO DATA-LIMIT-KEY
017200             IF DATA-LIMIT-CODE NOT > "L"
017300                 PERFORM PROCESS-ONE-LIMIT-SET.
017400 PROCESS-ONE-LIMIT-SET.
017500     MOVE DATA-LIMIT-CODE TO SAVE-LIMIT-CODE.
017600     MOVE RDL-NAME TO DL-NAME(DL-IDX).
017700     MOVE RDL-OCCURRENCE-A TO DL-OCCURRENCE-A (DL-IDX).
017800     MOVE RDL-ORIGIN TO DL-ORIGIN(DL-IDX).
000000     MOVE RDL-SET-CONNECTOR TO DL-SET-CONNECTOR(DL-IDX).
017900     MOVE 1 TO DL-ENTRY-IDX.
018000     PERFORM PROCESS-ONE-LIMIT-ENTRY.
000000     IF RPT-FILE-STATUS NOT = AT-END
018100      IF DATA-LIMIT-CODE NOT > "L"
018200          ADD 1 TO DL-IDX
018300          GO TO PROCESS-ONE-LIMIT-SET.
018400 PROCESS-ONE-LIMIT-ENTRY.
018500     MOVE 1 TO WORK-IDX.
018600     PERFORM PROCESS-ONE-LIMIT-ITEM.
018700     PERFORM READ-NEXT-RPT-RECORD.
018800     MOVE RPT-RECORD TO RDL-RECORD.
018900     MOVE RPT-KEY TO DATA-LIMIT-KEY.
019000     IF RPT-FILE-STATUS NOT = AT-END
019100         IF DATA-LIMIT-CODE = SAVE-LIMIT-CODE
019200             GO TO PROCESS-ONE-LIMIT-ENTRY
019300             .
019400 PROCESS-ONE-LIMIT-ITEM.
019500     MOVE RDL-ENTRY(WORK-IDX) TO DL-ENTRY(DL-IDX,DL-ENTRY-IDX).
019600     IF DL-OP-OCCURRENCE (DL-IDX, DL-ENTRY-IDX) = ZEROES
019700         MOVE SPACES TO DL-OP-OCCURRENCE-A (DL-IDX, DL-ENTRY-IDX).
019800     ADD 1 TO DL-ENTRY-IDX.
019900     ADD 1 TO WORK-IDX.
020000     IF WORK-IDX NOT > 3 AND
020100        DL-ENTRY-IDX NOT > 10
020200         GO TO PROCESS-ONE-LIMIT-ITEM.
020300
020400 LOAD-NF-RECORDS.
020500     MOVE 0 TO NF-IDX.
020600     MOVE "R" TO NEW-FIELD-KEY.
020700     MOVE NEW-FIELD-KEY TO RPT-KEY.
020800     PERFORM START-NLT-RPT-KEY.
020900     IF RPT-FILE-STATUS NOT = AT-END
021000         PERFORM READ-NEXT-RPT-RECORD
021100         MOVE RPT-RECORD TO RNF-RECORD
021200         IF RPT-FILE-STATUS NOT = AT-END
021300             MOVE RPT-KEY TO NEW-FIELD-KEY
021400             IF NEW-FIELD-CODE = "R"
021500                 PERFORM PROCESS-ONE-NEW-FIELD
021600                 PERFORM SQUEEZE-ONE-NEW-FIELD.
021700* Modified to force the field into it's correct slot in the
021800* Array for it's creation sequence.
021900 PROCESS-ONE-NEW-FIELD.
022000     MOVE NEW-FIELD-NAME  TO SAVE-FIELD-NAME.
022100     COMPUTE NF-IDX = RNF-SEQ + 1.
022200     MOVE RNF-NAME TO NF-NAME(NF-IDX).
022300     MOVE RNF-TYPE TO NF-TYPE (NF-IDX).
022400     MOVE RNF-LEN TO NF-LEN (NF-IDX).
022500     MOVE RNF-DEC TO NF-DEC (NF-IDX).
022600     MOVE SPACES TO NF-ENTRIES (NF-IDX).
022700     MOVE 1 TO NF-ENTRY-IDX.
022800     PERFORM PROCESS-ONE-NEW-ENTRY.
000000     IF RPT-FILE-STATUS NOT = AT-END
022900      IF NEW-FIELD-CODE = "R"
023000         GO TO PROCESS-ONE-NEW-FIELD.
023100 PROCESS-ONE-NEW-ENTRY.
023200     MOVE 1 TO WORK-IDX.
023300     PERFORM PROCESS-ONE-NEW-ITEM.
023400     PERFORM READ-NEXT-RPT-RECORD.
023500     MOVE RPT-RECORD TO RNF-RECORD.
023600     MOVE RPT-KEY TO NEW-FIELD-KEY.
023700     IF RPT-FILE-STATUS NOT = AT-END
023800         IF NEW-FIELD-CODE = "R" AND
023900            NEW-FIELD-NAME = SAVE-FIELD-NAME
024000             GO TO PROCESS-ONE-NEW-ENTRY.
024100 PROCESS-ONE-NEW-ITEM.
024200     MOVE RNF-ENTRY(WORK-IDX) TO NF-ENTRY(NF-IDX,NF-ENTRY-IDX).
024300     ADD 1 TO NF-ENTRY-IDX.
024400     ADD 1 TO WORK-IDX.
024500     IF WORK-IDX NOT > 3 AND
024600        NF-ENTRY-IDX NOT > 10
024700         GO TO PROCESS-ONE-NEW-ITEM
024800         .
       
024900 LOAD-FIELD-RECORDS.
      *    Calculate the RHD-FIELD-COUNT
           MOVE 0 TO CALC-FIELD-COUNT.
025000     MOVE 1 TO RFL-IDX.
025100     MOVE "X" TO RPT-KEY.
025200     PERFORM START-NLT-RPT-KEY.
025300     IF RPT-FILE-STATUS NOT = AT-END
025400         PERFORM READ-NEXT-RPT-RECORD
025500         IF RPT-FILE-STATUS NOT = AT-END
025600            PERFORM LOAD-ONE-FIELD-RECORD.
	   IF CALC-FIELD-COUNT NOT = RHD-FIELD-COUNT
	       MOVE CALC-FIELD-COUNT TO RHD-FIELD-COUNT.
	       
025700 LOAD-ONE-FIELD-RECORD.
	   ADD 1 TO CALC-FIELD-COUNT.
025800     MOVE RPT-RECORD TO RFL-RECORD(RFL-IDX).

      *	   Fix the SEQ for duplicate fields.
	   IF RFL-SEQ-A (RFL-IDX) = "AA" THEN
	       MOVE 99 TO RFL-SEQ (RFL-IDX).
	       
000000     IF RFL-OCCURRENCE(RFL-IDX) IS NOT NUMERIC
000000         MOVE ZEROES TO RFL-OCCURRENCE(RFL-IDX).
025900     IF RFL-OCCURRENCE (RFL-IDX) = ZEROES
026000         MOVE SPACES TO RFL-OCCURRENCE-A (RFL-IDX).
026100     ADD 1 TO RFL-IDX.
026200     IF RFL-IDX NOT > 80
026300         PERFORM READ-NEXT-RPT-RECORD
026400         IF RPT-FILE-STATUS NOT = AT-END
026500             GO TO LOAD-ONE-FIELD-RECORD.
026600

      *
      *	   Load the AKA records:
      *	   
      *	   This builds the AKA table by reading each AKA record then
      *	   searches the for the matching name and origin.
      *
      *	   This is how it is done on the Wang.
      *
       LOAD-AKA-RECORDS.
	   MOVE 1 TO RFL-IDX.
	   MOVE "T" TO RPT-KEY.
	   PERFORM START-NLT-RPT-KEY.
	   IF RPT-FILE-STATUS NOT = AT-END
	       PERFORM READ-NEXT-RPT-RECORD
	       IF RPT-FILE-STATUS NOT = AT-END
		   MOVE RPT-RECORD TO R-AKA-RECORD
		   IF R-AKA-KEY-TYPE = "T" THEN
		       PERFORM LOAD-EACH-AKA-RECORD.

       LOAD-EACH-AKA-RECORD.
	   PERFORM PROCESS-AKA-RECORD.
	   PERFORM READ-NEXT-RPT-RECORD
	   IF RPT-FILE-STATUS NOT = AT-END
	       MOVE RPT-RECORD TO R-AKA-RECORD
	       IF R-AKA-KEY-TYPE = "T" THEN
		   GO TO LOAD-EACH-AKA-RECORD.
       
       PROCESS-AKA-RECORD.
      *	   Build the AKA table by finding the matching name and origin.
	   PERFORM VARYING RFL-IDX FROM 1 BY 1 UNTIL RFL-IDX > 80
	       IF R-AKA-DUP-NAME = RFL-NAME   (RFL-IDX) AND
		  R-AKA-ORIGIN   = RFL-ORIGIN (RFL-IDX) THEN

		   MOVE R-AKA-TMP-NAME TO CTL-FIELDS-AKA (RFL-IDX)
		   MOVE R-AKA-DUP-NAME TO R-AKA-NAME (RFL-IDX)

	       END-IF
	   END-PERFORM.
      
026700 SQUEEZE-ONE-NEW-FIELD.
026800     MOVE 1 TO NF-IDX.
026900     PERFORM SQUEEZE-A-FIELD
027000      VARYING RNF-ENTRY-IDX FROM 2 BY 1
027100       UNTIL RNF-ENTRY-IDX > 10.
027200
027300 SQUEEZE-A-FIELD.
027400     IF NF-DATA(NF-IDX) NOT = SPACE
027500         ADD 1 TO NF-IDX
027600     ELSE
027700     IF NF-DATA(RNF-ENTRY-IDX) NOT = SPACE
027800         MOVE NF-DATA(RNF-ENTRY-IDX) TO
027900              NF-DATA(NF-IDX)
028000         MOVE SPACE TO NF-DATA(RNF-ENTRY-IDX)
028100         ADD 1 TO NF-IDX.
028200
028300     COPY IORPT.
028400
028500
000000     COPY PLKTRACE.
