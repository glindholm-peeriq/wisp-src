000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQMAIN.
000300*--------------------------------------------------
000400* THIS MAIN MODULE ACTS PRIMARILY AS A DISPATCHER
000500* TO PROCESSING MODULES THAT PERFORM INQUIRY
000000*
000600*--------------------------------------------------
000600*--------------------------------------------------
000000* Mods:
000000* July 21, 1991 mjb
000000*  Added passing E-MSG back and forth to
000000*  INQDAT to allow for errors at this level
000000*  to be displayed on the INQDAT Screen.
000000*
000000*  Added checking CTRLFILE against DATA FILE
000000*  to ensure a match.
000000*
000000*  25-Mar-1993.  HLM.
000000*  Corrected the count of P-FMT-QUERY-FIELDS being
000000*  passed to INQGET
000600*--------------------------------------------------
000700 ENVIRONMENT DIVISION.
000800 CONFIGURATION SECTION.
000900 FIGURATIVE-CONSTANTS.
001000     COPY FIGS.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300     COPY SLCRT.
001400 DATA DIVISION.
001500 FILE SECTION.
001600     COPY FDCRT.
001700 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqmain.wcb 1.20 9/11/94".
000000 01 CTRLDIF-PARENT    PIC X(8) VALUE "INQUIRY".
000000 01  E-MSG  PIC X(79) VALUE SPACE.
000000 01  GENERAL-FIELD-1    PIC X(100).
000000 01  GENERAL-FIELD-2    PIC X(100).
000000 01  KMATCHRC           PIC 99 VALUE ZEROES.
001800     COPY RCRPT.
001900
002000     COPY WSUSAGE.
002100     COPY WSFILEIO.
002200     COPY WSIOCODE.
002300     COPY WSCRT.
002400
002500* RPT-FILE-HEADER FIELDS AND GENERAL WS
002600     COPY WSRPT.
002700
002800 01  RPT-IO-BLOCK.
002900     COPY LKDIO.
003000
003100 01  CTRL-IO-BLOCK.
003200     COPY LKDIO.
000000 01  CTRL2-IO-BLOCK.
003200     COPY LKDIO.
003300 01  CTRL-HEADER-RECORDS.
003400     COPY RCCTRLH.
000000 01  KEY-TO-SEC-IDX   PIC 99 VALUE ZEROES.
003300 01  CTRL2-HEADER-RECORDS.
003400     COPY RCCTRLH.
003500 01  CRF-RECORDS.
000000     03  CRF-RECORD OCCURS 81 TIMES.
003600     COPY RCCTRLF.
000000 01  CF-RECORD.
003600     COPY RCCTRLF.
003700     COPY LKCTRLAR.
003800     COPY RCCTRLT1.
003900     COPY RCCTRLT2.
004000
004100 01  DATA-IO-BLOCK.
004200     COPY LKDIO.
004300
004400 01  DATA-IO-2-BLOCK.
004500     COPY LKDIO.
004600
004700 01  EXTRACT-IO-BLOCK.
004800     COPY LKDIO.
004900
000000 01  CTRLDIF-IO-BLOCK.
004800     COPY LKDIO.
000000
005000 01  FILE-IO-BLOCK.
005100     COPY LKDIO.
005200     COPY LKUFB.
005300
005400 01  PRINTER-FILE-NAME       PIC X(08) VALUE SPACES.
005500 01  PRINTER-FILE-LIBRARY    PIC X(08) VALUE SPACES.
005600 01  PRINTER-FILE-VOLUME     PIC X(06) VALUE SPACES.
005700 01  THIS-CALLER             PIC X(01) VALUE "I".
005800 01  RECORD-AREA             PIC X(2048) VALUE SPACES.
005900*01  THE-DATE                PIC 9(6).
006000*01  FORMATTED-DATE          PIC 99/99/99.

       01  CURR-CCYYMMDD.
           05  CURR-CC     PIC XX.
           05  CURR-YY     PIC XX.
           05  CURR-MM     PIC XX.
           05  CURR-DD     PIC XX.
       01  FORMAT-MM-DD-YY.
           05  FORMAT-MM   PIC XX.
           05  SLASH-1     PIC X VALUE "/".
           05  FORMAT-DD   PIC XX.
           05  SLASH-2     PIC X VALUE "/".
           05  FORMAT-YY   PIC XX.
       01  DATE2-HG        PIC XX VALUE "HG".
006100 01  CHECK-NAME              PIC X(08) VALUE SPACES.
006200 01  PASS-OPTION             PIC X(07) VALUE SPACES.
006300 01  PASS-CHANGE             PIC X(03) VALUE SPACES.
006400 01  PASS-DISPLAY            PIC X(03) VALUE SPACE.
006500
006600 01  PASSING-PFKEY           PIC 9(02) VALUE 0.
006700 01  PASSING-MSG-TEXT        PIC X(79) VALUE SPACE.
000000 01  FILLER.
006800*    03  P-FMT-QUERY-SCREEN  PIC X(553) VALUE SPACE.
006801     03  P-FMT-QUERY-SCREEN  PIC X(593) VALUE SPACE.
000000     03  FILLER REDEFINES
000000         P-FMT-QUERY-SCREEN.
006900     05  P-FMT-QUERY-LINE              OCCURS 7 TIMES.
007000         07  P-FMT-LINE-A    PIC X(40).
007100         07  P-FMT-LINE-B    PIC X(39).
007200     05  P-FMT-QUERY-TITLE   PIC X(40).
007300
007400     COPY WSRPTOPT.
007500     COPY WSRPTCFL.
007600     COPY WSRPTREC.
000000
000000 01  CTL-GETPARM-TYPE  PIC XX.
007700
000000     COPY LKPRC.
000000
007800 PROCEDURE DIVISION.
007900 MAIN-LOGIC SECTION.
008000 PROGRAM-BEGIN.
000000     MOVE "INQMAIN BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     CALL "INIDIO" USING CTRL-IO-BLOCK.
000000     MOVE "I" TO FILE-ORG OF CTRL-IO-BLOCK.
008100     PERFORM OPENING-PROCEDURE.
008200     PERFORM PROGRAM-INIT.
008300     PERFORM MAIN-PROCESS.
008400     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQMAIN EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
008500 MAIN-LOGIC-EXIT.
008600     EXIT PROGRAM.
008700 MAIN-LOGIC-STOP.
008800     STOP RUN.
008900 THE-OTHER SECTION.
009000 OPENING-PROCEDURE.
009100     PERFORM OPEN-CRT-FILE.
009200 PROGRAM-INIT.
009300     MOVE SPACES TO
009400         RPT-CTL-FIELDS
009500         RPT-RECORDS
000000         CTRL2-IO-BLOCK
000000         DATA-IO-2-BLOCK.
009600     MOVE SPACES TO RPT-ID.
009700     PERFORM EXTRACT-USAGE-CONSTANTS.
009800     MOVE SPACES TO RCF-PRIM-LIBRARY.
009900     STRING
010000         USER-ID DELIMITED BY SPACE
010100         "CTL" DELIMITED BY SIZE
010200         INTO RCF-PRIM-LIBRARY.
010300*     MOVE RCF-PRIM-LIBRARY TO RCF-SEC-LIBRARY.
010400     MOVE SPACES TO FILE-LIBRARY OF RPT-IO-BLOCK.
010500     STRING
010600         USER-ID DELIMITED BY SPACE
010700         "RPT" DELIMITED BY SIZE
010800         INTO FILE-LIBRARY OF RPT-IO-BLOCK.
010900
011000     MOVE INLIB TO
000000*                   RDF-SEC-LIBRARY
011100                   RDF-PRIM-LIBRARY.
011200     MOVE INVOL TO RDF-PRIM-VOLUME
011300*                   RDF-SEC-VOLUME
011400                   RCF-PRIM-VOLUME
011500*                   RCF-SEC-VOLUME
011600                   FILE-VOLUME OF RPT-IO-BLOCK.
011700
011800     MOVE 0 TO RHD-SEC-FILE
011900               RHD-SORT
012000               RHD-CONTROL-FIELDS
012100               RHD-COLUMN-SUBS
012200               RHD-NEW-FIELDS
012300               RHD-USER-EXIT.
012400
012500     MOVE 1 TO RHD-DATA-LIMIT.
012600
012700 CLOSING-PROCEDURE.
012800     PERFORM CLOSE-CRT-FILE.
013000
013100 MAIN-PROCESS.
000000     PERFORM MAIN-LOOP-INIT.
000000     PERFORM MAIN-LOOP.
000000 MAIN-LOOP-INIT.
000000     MOVE SPACE TO E-MSG.
000000     MOVE SPACE TO PASS-DISPLAY.
000000 MAIN-LOOP.
013200     PERFORM ENTER-DATA-FILE.
013300     IF RDF-PRIM-FILE NOT = SPACES
013400         PERFORM ENTER-CTL-FILE
013500         IF RCF-PRIM-FILE = SPACES
013600             GO TO MAIN-LOOP
013700         ELSE
013800         IF PASS-OPTION = "EXTRACT"
013900             PERFORM ENTER-EXTRACT-FILE
014000             MOVE FILE-NAME OF EXTRACT-IO-BLOCK TO CHECK-NAME
014100             IF CHECK-NAME = SPACES
014200                 GO TO MAIN-LOOP.
014300
000000     IF RDF-PRIM-FILE NOT = SPACES
000000         PERFORM LOAD-CTRL-TABLES
000000         PERFORM TEST-INPUT-FILE
000000         IF E-MSG NOT = SPACE
000000             GO TO MAIN-LOOP.
000000
014400     IF RDF-PRIM-FILE NOT = SPACES
014500         PERFORM ENTER-GETPARM
014700         PERFORM ENTER-INQUIRY
014800         IF PASSING-PFKEY = 1
000000             PERFORM MAIN-LOOP-INIT
014900             GO TO MAIN-LOOP.
015000
015100     IF RDF-PRIM-FILE NOT = SPACES
015200         PERFORM ANSWER-QUERY
015300         PERFORM ENTER-OPTIONS
015400         IF PASSING-PFKEY = 1
000000             PERFORM MAIN-LOOP-INIT
015500             GO TO MAIN-LOOP.
015600
015700 ENTER-DATA-FILE.
015800     CALL "INQDAT" USING
000000         E-MSG
015900         DATA-IO-BLOCK
016000         PASS-CHANGE
016100         PASS-OPTION
016200         RPT-RECORDS.
000000
000000     IF PASS-CHANGE = "NO"
000000         MOVE "ID" TO CTL-GETPARM-TYPE
000000     ELSE
000000         MOVE "I"  TO CTL-GETPARM-TYPE.
016300
016400 ENTER-CTL-FILE.
016500     CALL "INQCTL" USING
016600         CTRL-IO-BLOCK
016700         RPT-RECORDS
000000         CTL-GETPARM-TYPE.
016800
016900 ENTER-EXTRACT-FILE.
017000     CALL "INQXTR" USING
017100         EXTRACT-IO-BLOCK
017200         RPT-RECORDS.
017300
017400 ENTER-GETPARM.
017500     MOVE SPACES TO P-FMT-QUERY-SCREEN
017600                    PASS-DISPLAY.
017700     CALL "INQGET" USING
017800         PASS-DISPLAY
017900         P-FMT-QUERY-SCREEN.
018000
018100 LOAD-CTRL-TABLES.
018200     CALL "DTELOAD" USING
018300     COPY LKCTRL.
000000     MOVE "SORTNAME" TO CF-ACTION-CODE.
000000     CALL "CTRLARY" USING
000000          CALLED-RC
018300     COPY LKCTRL.
018400
018500 ENTER-INQUIRY.
018600     MOVE 0 TO PASSING-PFKEY.
018700     PERFORM CLOSING-PROCEDURE.
018800     CALL "INQENT" USING
018900         PASS-DISPLAY
019000         RPT-RECORDS
019100         P-FMT-QUERY-SCREEN
019200         PASSING-PFKEY
019300     COPY  LKCTRL.
000000     MOVE P-FMT-QUERY-TITLE TO RTT-TITLE(1).
019400     PERFORM OPENING-PROCEDURE.
019500
019600 ANSWER-QUERY.
019700     PERFORM INIT-THE-QUERY-PARAMS.
019800     PERFORM OPEN-THE-DATA-FILE.
019900     PERFORM QUERY-THE-FILE.
020000     PERFORM CLOSE-THE-DATA-FILE.
020100
020200 INIT-THE-QUERY-PARAMS.
020300     MOVE SPACES TO RPT-ID.
      *    Put MM/DD/YY into RPT-DATE.
020400*    ACCEPT THE-DATE FROM DATE.
020500*    COMPUTE THE-DATE = THE-DATE * 100.0001
020600*    MOVE THE-DATE TO FORMATTED-DATE.
020700*    MOVE FORMATTED-DATE TO RPT-DATE.
           CALL "DATE2" USING DATE2-HG, CURR-CCYYMMDD.
           MOVE CURR-YY TO FORMAT-YY.
           MOVE CURR-MM TO FORMAT-MM.
           MOVE CURR-DD TO FORMAT-DD.
           MOVE FORMAT-MM-DD-YY TO RPT-DATE.

020800     MOVE "PRINTER " TO RPT-DEVICE.
020900     MOVE "NO " TO RPT-FILES.
021000     MOVE "YES" TO RPT-OPTION.
021100     MOVE "NO " TO RPT-ONLY.
021200     MOVE 55 TO RPT-PAGE.
021300     MOVE "123" TO RPT-LINES.
021400     MOVE "000" TO RPT-SPACING.
021500     MOVE "YES" TO RPT-EXT-SORT.
021600
021700     MOVE FILE-NAME OF EXTRACT-IO-BLOCK TO PRINTER-FILE-NAME.
021800     MOVE FILE-LIBRARY OF EXTRACT-IO-BLOCK
021900         TO PRINTER-FILE-LIBRARY.
022000     MOVE FILE-VOLUME OF EXTRACT-IO-BLOCK
022100         TO PRINTER-FILE-VOLUME.
000000     MOVE FILE-ORG OF EXTRACT-IO-BLOCK TO
000000          FILE-ORG OF DATA-IO-2-BLOCK.
022200
022300     IF PASS-OPTION = "EXTRACT"
022400         MOVE "S" TO THIS-CALLER
022500     ELSE
022600         MOVE "I" TO THIS-CALLER.
022700
022800 OPEN-THE-DATA-FILE.
022900*     MOVE DATA-IO-BLOCK TO FILE-IO-BLOCK.
023000     MOVE OPEN-IO TO FILE-IO OF DATA-IO-BLOCK.
023100     PERFORM CLOSING-PROCEDURE.
000000     PERFORM IO-TO-DATA-FILE.
023500     PERFORM OPENING-PROCEDURE.
023600
023700 QUERY-THE-FILE.
023800     PERFORM CLOSING-PROCEDURE.
000000     MOVE SPACE TO CTRL2-HEADER-RECORDS.
000000     CALL "RPTCLD" USING
000000         CALLED-RC
000000         RPT-RECORDS
000000         CRF-RECORDS
000000         CTRL-IO-BLOCK
000000         CTRL2-IO-BLOCK
000000         CTRL2-HEADER-RECORDS
000000         KEY-TO-SEC-IDX.
023900     CALL "PLSWAIT".
000000     MOVE ZEROES TO KEY-TO-SEC-IDX.
024000     CALL "RPTCALL" USING
024100         RPT-OPTIONS
024200         RHD-RECORD
024300         RDF-RECORD
024400         RCB-ENTRIES
024500         RCD-RECORDS
024600         RFL-RECORDS
024700         CRF-RECORDS
024800         RTT-RECORDS
024900         ST-SORTS
025000         DL-FIELDS
025100         NF-FIELDS
025200         DATA-IO-BLOCK
025300         DATA-IO-2-BLOCK
025400         PRINTER-FILE-NAME
025500         PRINTER-FILE-LIBRARY
025600         PRINTER-FILE-VOLUME
025700         THIS-CALLER
000000         KEY-TO-SEC-IDX.
025800*     CALL "BLNKSC".
025900     PERFORM OPENING-PROCEDURE.
026000
026100 CLOSE-THE-DATA-FILE.
026200     MOVE CLOSE-FILE TO FILE-IO OF DATA-IO-BLOCK.
026300     PERFORM CLOSING-PROCEDURE.
000000     PERFORM IO-TO-DATA-FILE.
026700     PERFORM OPENING-PROCEDURE.
026800
026900 ENTER-OPTIONS.
027000     MOVE 0 TO PASSING-PFKEY.
027100     CALL "INQOPT" USING
027200         PASSING-PFKEY
027300         PASSING-MSG-TEXT.
027400
027500     IF PASSING-PFKEY = 2
027600         PERFORM CLOSING-PROCEDURE
027700         CALL "INQMAK" USING
027800             DATA-IO-BLOCK
027900             PASS-CHANGE
028000             PASS-OPTION
028100             CTRL-IO-BLOCK
028200             P-FMT-QUERY-SCREEN
028300             EXTRACT-IO-BLOCK
028400         PERFORM OPENING-PROCEDURE
028500         GO TO ENTER-OPTIONS.
028600
028700     IF PASSING-PFKEY = 3
028800         PERFORM CLOSING-PROCEDURE
028900         CALL "INQRPT" USING
029000             RPT-RECORDS
029100         PERFORM OPENING-PROCEDURE
029200         GO TO ENTER-OPTIONS.
029300
035200*---------------------------------------------------------------
035300* LOAD A SUPPOSED FILE DEFINITION FROM THE CRTRL-FILE ITSELF.
035400* COMPARE THAT TO THE ACTUAL FILE DATA AND ERROR IF THEY DO NOT
035500* MATCH.
035600*---------------------------------------------------------------
035700 TEST-INPUT-FILE.
000000     MOVE SPACE TO E-MSG.
035800     CALL "CTRLDIO" USING
000000           CALLED-RC
035900           CTRL-IO-BLOCK
036000           DATA-IO-BLOCK
036100           CTRL-HEADER-RECORDS.
036200     IF CALLED-RC = ZEROES
036300         MOVE ZEROES TO FILE-ALTKEY-DATA OF DATA-IO-BLOCK
036400         MOVE FILE-ID-DATA OF DATA-IO-BLOCK  TO
036500             GENERAL-FIELD-1
000000         MOVE DATA-IO-BLOCK TO CTRLDIF-IO-BLOCK
036600         PERFORM OPEN-DATA-FILE
036700         PERFORM EXTRACT-DATA-INFO
036800         PERFORM CLOSE-DATA-FILE
036900         MOVE ZEROES TO FILE-ALTKEY-DATA OF DATA-IO-BLOCK
037000         MOVE FILE-ID-DATA OF DATA-IO-BLOCK TO
037100             GENERAL-FIELD-2
000000         CALL "KMATCH" USING KMATCHRC
000000                             GENERAL-FIELD-1
000000                             GENERAL-FIELD-2
000000         MOVE KMATCHRC TO CALLED-RC.
037200*         IF GENERAL-FIELD-1 NOT = GENERAL-FIELD-2
037300*             MOVE 95 TO CALLED-RC.
037400      IF CALLED-RC NOT = ZEROES
000000          CALL "CTRLDIF" USING CTRLDIF-IO-BLOCK DATA-IO-BLOCK
000000                               CTRLDIF-PARENT
037500          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
037600          MOVE
037700          "Format Conflict Between CONTROL and DATA files."
037800            TO E-MSG.
000000 OPEN-DATA-FILE.
000000     MOVE OPEN-INPUT TO FILE-IO OF DATA-IO-BLOCK.
000000     PERFORM IO-TO-DATA-FILE.
000000 CLOSE-DATA-FILE.
000000     MOVE CLOSE-FILE TO FILE-IO OF DATA-IO-BLOCK.
000000     PERFORM IO-TO-DATA-FILE.
000000 EXTRACT-DATA-INFO.
000000     MOVE FILE-INFO TO FILE-IO OF DATA-IO-BLOCK.
000000     PERFORM IO-TO-DATA-FILE.
000000 IO-TO-DATA-FILE.
000000     CALL "KCSIO" USING
000000          DATA-IO-BLOCK
000000          UFB
000000          RECORD-AREA.
000000
029400* GENERAL ROUTINES.
029500     COPY PLCRT.
029600     COPY PLUSAGE.
029700
000000     COPY PLKTRACE.
