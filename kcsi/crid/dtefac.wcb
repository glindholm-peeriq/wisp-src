000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. DTEFAC.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  12/13/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE FAC:  ULINE   DISPLAY   UPLOW
000000* BUG
000000* A BLINK SPECIFICATION IS CREATING AN UNDISPLAYABLE FAC
000000* EVEN THOUGH THE LOGIC IS GOOD AND IN DEBUG, THE FAC
000000* IS CORRECTLY INITIALIZED TO THE RIGHT VALUE. WHEN
000000* DCHG OR DADD TRY TO DISPLAY SUCH A FIELD, THE FIELD
000000* IS FILLED WITH GARBAGE AND THE KEYBOARD SOMETIMES LOCKS
000000* UP. MAYBE SOMETHING IN WISP.
000000* REINSTALLED BLINK. THE PROBLEM IS BLINK AND UNDERLINED
000000* COMBINED.
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600 FILE-CONTROL.
001700 DATA DIVISION.
001800 FILE SECTION.
001900
002000 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)dtefac.wcb 1.8 9/11/94".
002100     COPY WSGP.
002200     COPY WSINT.
002300     COPY WSCRTS.
002310     COPY WSCONST.
002400
000000 01  FIELD-NUMERIC-FLAG        PIC X VALUE SPACE.
000000     88  FIELD-IS-NUMERIC         VALUE "Y".
000000
002500 01  FAC-GETPARM-FIELDS.
002600     05  FAC-MSG-ID               PIC X(4) VALUE "0001".
002700     05  FAC-PRNAME               PIC X(8) VALUE "FAC".
002800     05  FAC-MSG-ISSUER           PIC X(6) VALUE "D006".
002900
003000 01  FAC-MSG-COUNT.
003100     05  FILLER                  BINARY VALUE 0.
003200     05  FILLER                  BINARY VALUE 1.
003300 01  MSG-TEXT-1                  PIC X(78) VALUE " ".
003400
003500 01  FAC-TEXT-01                  PIC X(70) VALUE
003600     " ".
003700 01  FAC-TEXT-03                  PIC X(28) VALUE
003800         "         Field Underlining  ".
003900 01  FAC-ULINE-KW              PIC X(8) VALUE "ULINE".
004000 01  FAC-ULINE-FIELD           PIC X(3) VALUE SPACE.
004100 01  FAC-ULINE-DEFAULT         PIC X(3) VALUE SPACE.
004200 01  FAC-TEXT-04                  PIC X(32) VALUE
004300         "         Field Display Intensity".
004400 01  FAC-DISPLAY-KW              PIC X(8) VALUE "DISPLAY".
004500 01  FAC-DISPLAY-FIELD           PIC X(7) VALUE SPACE.
004600 01  FAC-DISPLAY-DEFAULT         PIC X(7) VALUE SPACE.
004700 01  FAC-TEXT-05                  PIC X(34) VALUE
004800         "         BRIGHT, DIM, BLINK, BLANK".
004900 01  FAC-TEXT-06                  PIC X(42) VALUE
005000         "         Both upper and lower case allowed".
005100 01  FAC-UPLOW-KW              PIC X(8) VALUE "UPLOW".
005200 01  FAC-UPLOW-FIELD           PIC X(3) VALUE SPACE.
005300 01  FAC-UPLOW-DEFAULT         PIC X(3) VALUE SPACE.
000000 01  FAC-UPLOW-TYPE            PIC X.
005400 01  FAC-TEXT-07                  PIC X(71) VALUE
005500         "(ENTER) Set the display attributes and display the next
005600-        "field or Select".
005700 01  FAC-TEXT-08                  PIC X(74) VALUE
005800         "(1) Return to spacing  (2) First field  (4) Previous fie
005900-        "ld  (5) Next field".
006000 01  FAC-TEXT-09                  PIC X(8) VALUE
006100         "(8) Find".
006200 01  FAC-FIELD-KW              PIC X(8) VALUE "FIELD".
006300 01  FAC-FIELD-FIELD           PIC X(8) VALUE SPACE.
006400 01  FAC-FIELD-DEFAULT         PIC X(8) VALUE SPACE.
006500 01  FAC-TEXT-10                  PIC X(36) VALUE
006600         "(16) Exit to DATENTRY Functions Menu".
007200
007300 01  FAC-WORK-FIELD              PIC X VALUE LOW-VALUES.
007400 01  FAC-LITERAL-FIELD.
007500     05  FILLER                  PIC X(52) VALUE
007600         "Please specify field display attributes for  ".
007700     05  FAC-NAME-FIELD          PIC X(08) VALUE SPACES.
007800     05  FILLER                  PIC X(10) VALUE SPACES.
007900
008000 01  FIELD-IDX                   PIC 9(03) VALUE 0.
008100
008200 01  PASS-FILE-NAME          PIC X(08) VALUE SPACE.
008300 01  PASS-LIB-NAME           PIC X(08) VALUE SPACE.
008400 01  PASS-VOL-NAME           PIC X(06) VALUE SPACE.
008500 01  PASS-FIELD-NAME         PIC X(08) VALUE SPACE.
008600 01  PASS-FAC                PIC X(01) VALUE SPACE.
008700
008800 LINKAGE SECTION.
008900 01  CTL-HEADER-RECORD.
009000     COPY RCCTRLH.
009100     COPY RCCTRLT1.
009200     COPY RCCTRLT2.
009300 01  MODE-FLAG                  PIC X(01).
009400 01  INDEX-OF-KEY               PIC 9(03).
009500 01  CTRL-IO-BLOCK.
009600     COPY LKDIO.
009700 01  DATA-IO-BLOCK.
009800     COPY LKDIO.
009900
010000 PROCEDURE DIVISION USING
010100     CTL-HEADER-RECORD
010200     CF-T1-TABLE
010300     CF-T2-TABLE
010400     MODE-FLAG
010500     INDEX-OF-KEY
010600     CTRL-IO-BLOCK
010700     DATA-IO-BLOCK.
010800
010900 MAIN-LOGIC SECTION.
011000 PROGRAM-BEGIN.
000000     MOVE "DTEFAC BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
011100     PERFORM OPENING-PROCEDURE.
011200     PERFORM MAIN-PROCESS.
011300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "DTEFAC EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
011400 MAIN-LOGIC-EXIT.
011500     EXIT PROGRAM.
011600 MAIN-LOGIC-STOP.
011700     STOP RUN.
011800 LEVEL-2 SECTION.
011900 OPENING-PROCEDURE.
012000     MOVE 1 TO FIELD-IDX.
012100     MOVE FILE-NAME OF CTRL-IO-BLOCK TO PASS-FILE-NAME.
012200     MOVE FILE-LIBRARY OF CTRL-IO-BLOCK TO PASS-LIB-NAME.
012300     MOVE FILE-VOLUME OF CTRL-IO-BLOCK TO PASS-VOL-NAME.
012400
012500 CLOSING-PROCEDURE.
012600     IF GP-PFKEY-1-PRESSED
012700         MOVE "D" TO MODE-FLAG
012800     ELSE
012900     IF GP-PFKEY-16-PRESSED
013000         MOVE " " TO MODE-FLAG.
013100
013200 MAIN-PROCESS.
013300     PERFORM LOAD-A-FIELD.
013400     IF FAC-NAME-FIELD NOT = SPACES
013500         MOVE SPACES TO FAC-FIELD-FIELD
013600         PERFORM DISPLAY-A-FIELD
000000     ELSE
000000         MOVE "P" TO GP-PFKEY-RECEIVER.
013700
013800     IF GP-PFKEY-2-PRESSED
013900         MOVE 1 TO FIELD-IDX.
014000
014100     IF GP-PFKEY-4-PRESSED
014200         IF FIELD-IDX NOT = 1
014300             SUBTRACT 1 FROM FIELD-IDX
014400         ELSE
014500             MOVE 1 TO FIELD-IDX.
014600
014700     IF GP-PFKEY-5-PRESSED
014800         IF FIELD-IDX NOT = CONST-CF-MAX-ENTRY
014900             ADD 1 TO FIELD-IDX
015000         ELSE
015100             MOVE 1 TO FIELD-IDX.
015200
015300     IF GP-PFKEY-8-PRESSED
015400         MOVE 1 TO FIELD-IDX
015500         PERFORM FIND-THE-FIELD
015600         IF FIELD-IDX > CONST-CF-MAX-ENTRY
015700             MOVE 1 TO FIELD-IDX.
015800
015900     IF GP-ENTER-KEY-PRESSED
016000         IF FIELD-IDX < CONST-CF-MAX-ENTRY
016100             ADD 1 TO FIELD-IDX
016200         ELSE
016300             MOVE 1 TO FIELD-IDX.
016400
016500     IF NOT GP-PFKEY-1-PRESSED  AND
016600        NOT GP-PFKEY-16-PRESSED
016700         GO TO MAIN-PROCESS.
016800
016900 FIND-THE-FIELD.
017000     IF CF-NAME (FIELD-IDX) NOT = FAC-FIELD-FIELD
017100         ADD 1 TO FIELD-IDX
017200         IF FIELD-IDX NOT > CONST-CF-MAX-ENTRY
017300             GO TO FIND-THE-FIELD.
017400
017500 LOAD-A-FIELD.
000000     MOVE SPACE TO FAC-NAME-FIELD.
017600     IF CF-NAME (FIELD-IDX) = SPACES
017700         MOVE 1 TO FIELD-IDX.
017800
017900     IF CF-UPDATE-CODE (FIELD-IDX) = 1
018000         ADD 1 TO FIELD-IDX
018100         IF FIELD-IDX NOT > CONST-CF-MAX-ENTRY 
018200             GO TO LOAD-A-FIELD
018300         ELSE
018400             NEXT SENTENCE
018500     ELSE
018600         MOVE CF-NAME (FIELD-IDX) TO FAC-NAME-FIELD
018700         MOVE FAC-LITERAL-FIELD TO FAC-TEXT-01
018800         MOVE CF-DEFAULT-FAC (FIELD-IDX) TO FAC-WORK-FIELD
018900         PERFORM DISPLAY-THE-FAC.
019000
000000 IS-FIELD-NUMERIC.
000000     MOVE "Y" TO FIELD-NUMERIC-FLAG.
000000     IF CF-TYPE(FIELD-IDX) = "C"
000000         MOVE "N" TO FIELD-NUMERIC-FLAG
000000     ELSE
000000     IF CF-TYPE(FIELD-IDX) = "B" AND
000000        CF-BINARY-EDIT(FIELD-IDX) = 1
000000         MOVE "N" TO FIELD-NUMERIC-FLAG.
019800
019100 DISPLAY-THE-FAC.
019200     IF FAC-WORK-FIELD = SPACE
000000         MOVE LOW-VALUES TO FAC-WORK-FIELD.
000000     PERFORM IS-FIELD-NUMERIC
000000     IF FIELD-IS-NUMERIC
000000         SET NUMERIC-BIT IN FAC-WORK-FIELD ON
000000         SET UPPER-BIT IN FAC-WORK-FIELD OFF
000000     ELSE
000000         SET NUMERIC-BIT IN FAC-WORK-FIELD OFF.
000000*         SET UPPER-BIT IN FAC-WORK-FIELD ON.
019800
019900     IF DIM-BIT OF FAC-WORK-FIELD IS OFF
020000         MOVE "BRIGHT" TO FAC-DISPLAY-FIELD
020100     ELSE
020200         MOVE "DIM" TO FAC-DISPLAY-FIELD.
020300
000000     IF FIELD-IS-NUMERIC  OR
020400        UPPER-BIT OF FAC-WORK-FIELD IS ON
020500         MOVE "NO " TO FAC-UPLOW-FIELD
020600     ELSE
020700         MOVE "YES" TO FAC-UPLOW-FIELD.
020800
020900     IF ULINE-BIT OF FAC-WORK-FIELD IS ON
021000         MOVE "YES" TO FAC-ULINE-FIELD
021100     ELSE
021200         MOVE "NO " TO FAC-ULINE-FIELD.
021300
021400     IF BLINK-BIT OF FAC-WORK-FIELD IS ON
021500         MOVE "BLINK" TO FAC-DISPLAY-FIELD.
021600
021700     IF BLINK-BIT OF FAC-WORK-FIELD IS ON AND
000000        DIM-BIT OF FAC-WORK-FIELD IS ON
021800         MOVE "BLANK" TO FAC-DISPLAY-FIELD.
021900
022000 PUT-THE-FAC-BACK.
000000     MOVE LOW-VALUES TO FAC-WORK-FIELD.
022100     SET FAC-BIT IN FAC-WORK-FIELD ON.
000000     PERFORM IS-FIELD-NUMERIC.
000000     IF FIELD-IS-NUMERIC
000000         SET NUMERIC-BIT IN FAC-WORK-FIELD ON
019800     ELSE
022200     IF FAC-UPLOW-FIELD = "NO"
022500         SET UPPER-BIT IN FAC-WORK-FIELD ON.
022600
000000* THERE IS AN UNRESOLVED PROBLEM WITH BLINKING
000000* DISPLAYS THAT CAUSES THE TERMINAL TO LOCK UP
000000* FOR NOW THIS FORCES A BLINK TYPE TO BE IGNORED
000000* REINSTALLED 04-09-90
000000*     IF FAC-DISPLAY-FIELD = "BLINK"
000000*         MOVE "BRIGHT" TO FAC-DISPLAY-FIELD.
000000
023100     IF FAC-DISPLAY-FIELD = "DIM"
023200         SET DIM-BIT IN FAC-WORK-FIELD ON
023300     ELSE
023400     IF FAC-DISPLAY-FIELD = "BLINK"
023500         SET BLINK-BIT IN FAC-WORK-FIELD ON
023600     ELSE
023700     IF FAC-DISPLAY-FIELD = "BLANK"
023800         SET BLINK-BIT IN FAC-WORK-FIELD ON
000000         SET DIM-BIT IN FAC-WORK-FIELD ON.
023900
024000     IF FAC-ULINE-FIELD = "YES"
024100         SET ULINE-BIT IN FAC-WORK-FIELD ON
024200     ELSE
024300         SET ULINE-BIT IN FAC-WORK-FIELD OFF.
024400
024500     MOVE CF-NAME (FIELD-IDX) TO PASS-FIELD-NAME.
024600     MOVE FAC-WORK-FIELD TO CF-DEFAULT-FAC (FIELD-IDX)
024700                            PASS-FAC.
024800     CALL "DTEUPD" USING
024900         PASS-FILE-NAME
025000         PASS-LIB-NAME
025100         PASS-VOL-NAME
025200         PASS-FIELD-NAME
025300         PASS-FAC.
025400
025500 DISPLAY-A-FIELD.
025600     MOVE SPACE TO SCREEN-ERROR-STATUS.
025700     PERFORM CALL-FAC-GETPARM.
025800     MOVE SPACE TO MSG-TEXT-1.
025900     PERFORM VAL-THE-FIELDS.
026000     IF SCREEN-ENTRY-ERROR
026100         GO TO DISPLAY-A-FIELD
026200     ELSE
026300         PERFORM PUT-THE-FAC-BACK.
026400
026500 VAL-THE-FIELDS.
026600     PERFORM VAL-THE-ULINE.
026700     IF NOT SCREEN-ENTRY-ERROR
026800         PERFORM VAL-THE-DISPLAY
026900         IF NOT SCREEN-ENTRY-ERROR
027000             PERFORM VAL-THE-UPLOW.
027100
027200 VAL-THE-ULINE.
027300     IF FAC-ULINE-FIELD NOT = "YES"  AND
027400        FAC-ULINE-FIELD NOT = "NO "
027500         MOVE "ULINE must be 'YES' or 'NO'" TO MSG-TEXT-1
027600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
027700
027800 VAL-THE-DISPLAY.
027900     IF FAC-DISPLAY-FIELD NOT = "BRIGHT"  AND
028000        FAC-DISPLAY-FIELD NOT = "DIM   "  AND
028100        FAC-DISPLAY-FIELD NOT = "BLINK "  AND
028200        FAC-DISPLAY-FIELD NOT = "BLANK "
028300         MOVE "DISPLAY must be 'BRIGHT' 'DIM' 'BLINK' or 'BLANK'"
028400             TO MSG-TEXT-1
028500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
028600
028700 VAL-THE-UPLOW.
028800     IF FAC-UPLOW-FIELD NOT = "YES"  AND
028900        FAC-UPLOW-FIELD NOT = "NO "
029000         MOVE "UPLOW must be 'YES' or 'NO'" TO MSG-TEXT-1
029100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
029200
029300 CALL-FAC-GETPARM.
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-01-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-02-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-04-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-05-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-08-VAL TO PFKEY-MASK-16.
000000     ADD GP-PF-16-VAL TO PFKEY-MASK-16.
000000     IF FIELD-IS-NUMERIC
000000         MOVE GP-ALPHA-LOCKED-DATA TO FAC-UPLOW-TYPE
000000     ELSE
000000         MOVE GP-ALPHANUMERIC-DATA To FAC-UPLOW-TYPE.
030000
030100     CALL "GETPARM" USING
030200         GP-SPECIFY-TYPE, GP-REQUEST-FORM, FAC-PRNAME,
030300         GP-PFKEY-RECEIVER, FAC-MSG-ID,
030400         FAC-MSG-ISSUER,
030500
030600         FAC-MSG-COUNT, MSG-TEXT-1, INT78,
030700
030800         GP-TEXT-TYPE, FAC-TEXT-01, INT70,
030900         GP-ABS, INT10, GP-ABS, INT03,
031000
031100         GP-TEXT-TYPE, FAC-TEXT-03, INT28,
031200         GP-ABS, INT12, GP-ABS, INT02,
031300
031400         GP-KW-TYPE, FAC-ULINE-KW,
031500         FAC-ULINE-FIELD, INT03,
031600         GP-ABS, INT12, GP-ABS, INT49,
031700         GP-UPPERCASE-DATA
031800
031900         GP-TEXT-TYPE, FAC-TEXT-04, INT32,
032000         GP-ABS, INT14, GP-ABS, INT02,
032100
032200         GP-KW-TYPE, FAC-DISPLAY-KW,
032300         FAC-DISPLAY-FIELD, INT07,
032400         GP-ABS, INT14, GP-ABS, INT49,
032500         GP-UPPERCASE-DATA
032600
032700         GP-TEXT-TYPE, FAC-TEXT-05, INT34,
032800         GP-ABS, INT15, GP-ABS, INT02,
032900
033000         GP-TEXT-TYPE, FAC-TEXT-06, INT42,
033100         GP-ABS, INT17, GP-ABS, INT02,
033200
033300         GP-KW-TYPE, FAC-UPLOW-KW,
033400         FAC-UPLOW-FIELD, INT03,
033500         GP-ABS, INT17, GP-ABS, INT49,
000000         FAC-UPLOW-TYPE
033600*         GP-UPPERCASE-DATA
033700
033800         GP-TEXT-TYPE, FAC-TEXT-07, INT71,
033900         GP-ABS, INT20, GP-ABS, INT02,
034000
034100         GP-TEXT-TYPE, FAC-TEXT-08, INT74,
034200         GP-ABS, INT21, GP-ABS, INT02,
034300
034400         GP-TEXT-TYPE, FAC-TEXT-09, INT08,
034500         GP-ABS, INT22, GP-ABS, INT02,
034600
034700         GP-KW-TYPE, FAC-FIELD-KW,
034800         FAC-FIELD-FIELD, INT08,
034900         GP-ABS, INT22, GP-ABS, INT13,
035000         GP-ALPHANUMERIC-DATA
035100
035200         GP-TEXT-TYPE, FAC-TEXT-10, INT36,
035300         GP-ABS, INT22, GP-ABS, INT41,
035400         GP-ENABLE-ENTER-KEY
035500         GP-PFKEY-TYPE, PFKEY-MASK.
035600
035700     IF NOT GP-PFKEY-16-PRESSED  AND
035800        NOT GP-ENTER-KEY-PRESSED AND
035900        NOT GP-PFKEY-1-PRESSED  AND
036000        NOT GP-PFKEY-2-PRESSED  AND
036100        NOT GP-PFKEY-4-PRESSED  AND
036200        NOT GP-PFKEY-5-PRESSED  AND
036300        NOT GP-PFKEY-8-PRESSED
036400         GO TO CALL-FAC-GETPARM.
036500
000000     COPY PLKTRACE.
