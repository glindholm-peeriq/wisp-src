000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTCTLP.
000300*--------------------------------------------------
000400* THIS PROGRAM TAKES THE RFL-RECORDS FILLED IN BY
000500* RPTPFL AND RPTSFL AND FILLS IN THE RECORD INFO
000600* FROM THE CONTROL FILE.
000000* If LOAD FLAG of "K" is passed, it assumes that
000000* RFL(81) contains the name of the key to the secondary
000000* and loads these values from the primary control
000000* file.
000700*--------------------------------------------------
000800 ENVIRONMENT DIVISION.
000900 CONFIGURATION SECTION.
001000 FIGURATIVE-CONSTANTS.
001100     COPY FIGS.
001200 INPUT-OUTPUT SECTION.
001300 FILE-CONTROL.
001400*     COPY SLCTRL.
001500 DATA DIVISION.
001600 FILE SECTION.
001700*     COPY FDCTRL.
001900 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptctlp.wcb 1.6 9/11/94".
002000     COPY RCRPT.
001800     COPY RCCTRL.
002100
002200 01  WORK-CTRL-FIELD-RECORD.
002300     COPY RCCTRLF.
002400
002500     COPY WSCTRL.
002600     COPY WSUSAGE.
002700     COPY WSFILEIO.
002800     COPY WSIOCODE.
002900
003000 01  CTRL-IO-BLOCK.
003100     COPY LKDIO.
003200
003300 LINKAGE SECTION.
003400 01  LOAD-SWITCH     PIC X(01).
003500     COPY WSRPTCFL.
003600     COPY WSRPTREC.
003700
003800 PROCEDURE DIVISION USING
003900     LOAD-SWITCH
004000     RPT-CTL-FIELDS
004100     RPT-RECORDS.
004200 MAIN-LOGIC SECTION.
004300 PROGRAM-BEGIN.
000000     MOVE "RPTCTLP BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
004400     PERFORM OPENING-PROCEDURE.
004500     PERFORM PROGRAM-INIT.
004600     PERFORM MAIN-PROCESS.
004700     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTCTLP EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
004800 MAIN-LOGIC-EXIT.
004900     EXIT PROGRAM.
005000 MAIN-LOGIC-STOP.
005100     STOP RUN.
005200 THE-OTHER SECTION.
005300 OPENING-PROCEDURE.
005400 PROGRAM-INIT.
005500 CLOSING-PROCEDURE.
005600 MAIN-PROCESS.
000000       CALL "INIDIO" USING CTRL-IO-BLOCK.
000000     MOVE "I" TO FILE-ORG OF CTRL-IO-BLOCK.
005700       IF LOAD-SWITCH = "P"
005800           MOVE RCF-PRIM-FILE TO FILE-NAME OF CTRL-IO-BLOCK
005900           MOVE RCF-PRIM-LIBRARY TO FILE-LIBRARY OF CTRL-IO-BLOCK
006000           MOVE RCF-PRIM-VOLUME TO FILE-VOLUME OF CTRL-IO-BLOCK
006100           COMPUTE CTLIDX = 0
006200           PERFORM LOAD-CONTROL-FIELDS
006300       ELSE
005700       IF LOAD-SWITCH = "K"
005800           MOVE RCF-PRIM-FILE TO FILE-NAME OF CTRL-IO-BLOCK
005900           MOVE RCF-PRIM-LIBRARY TO FILE-LIBRARY OF CTRL-IO-BLOCK
006000           MOVE RCF-PRIM-VOLUME TO FILE-VOLUME OF CTRL-IO-BLOCK
006100           COMPUTE CTLIDX = 0
006200           PERFORM LOAD-KEY-FIELD
006300       ELSE
006400       IF LOAD-SWITCH = "S"
006500           MOVE RCF-SEC-FILE TO FILE-NAME OF CTRL-IO-BLOCK
006600           MOVE RCF-SEC-LIBRARY TO FILE-LIBRARY OF CTRL-IO-BLOCK
006700           MOVE RCF-SEC-VOLUME TO FILE-VOLUME OF CTRL-IO-BLOCK
006800           COMPUTE CTLIDX = 0
006900           PERFORM LOAD-CONTROL-FIELDS
007000       ELSE
007100       IF LOAD-SWITCH = "N"
007200           COMPUTE CTLIDX = 0
007300           PERFORM LOAD-NEW-FIELDS.
007400
007500 LOAD-KEY-FIELD.
007600
007700     PERFORM OPEN-IO-CTRL-FILE.
007800     PERFORM LOAD-KEY-RECORD.
007900     PERFORM CLOSE-CTRL-FILE.
008000
000000 LOAD-KEY-RECORD.
000000     MOVE 81 TO CTLIDX.
010100     MOVE RFL-NAME (CTLIDX) TO CTRL-KEY.
010200     PERFORM READ-CTRL-RECORD.
010300     IF CTRL-FILE-STATUS = I-O-OK
010400         MOVE CTRL-RECORD TO WORK-CTRL-FIELD-RECORD
010500         PERFORM LOAD-THE-FIELDS.
010600
007500 LOAD-CONTROL-FIELDS.
007600
007700     PERFORM OPEN-IO-CTRL-FILE.
007800     PERFORM LOAD-ONE-RECORD.
007900     PERFORM CLOSE-CTRL-FILE.
008000
008100 LOAD-ONE-RECORD.
008200
008300     ADD 1 TO CTLIDX.
008400
008500     IF CTLIDX NOT > 80
008600        IF RFL-NAME (CTLIDX) = SPACES
008700             MOVE 81 TO CTLIDX
008800        ELSE
008900        IF RFL-ORIGIN (CTLIDX) NOT = 2  AND
009000           LOAD-SWITCH = "S"
009100            GO TO LOAD-ONE-RECORD
009200        ELSE
009300        IF RFL-ORIGIN (CTLIDX) NOT = 1  AND
009400           LOAD-SWITCH = "P"
009500            GO TO LOAD-ONE-RECORD
009600        ELSE
009700        IF RFL-LINE-CODE (CTLIDX) NOT = SPACE
009800            GO TO LOAD-ONE-RECORD.
009900
010000     IF CTLIDX NOT > 80
010100         MOVE RFL-NAME (CTLIDX) TO CTRL-KEY
010200         PERFORM READ-CTRL-RECORD
010300         IF CTRL-FILE-STATUS = I-O-OK
010400             MOVE CTRL-RECORD TO WORK-CTRL-FIELD-RECORD
010500             PERFORM LOAD-THE-FIELDS.
010600
010700     IF CTLIDX NOT > 80
010800         GO TO LOAD-ONE-RECORD.
010900
011000 LOAD-THE-FIELDS.
011100     IF CF-UPDATE-CODE = 1  OR
011200        RFL-SEQ (CTLIDX) > 0
011300         MOVE "Z" TO RFL-LINE-CODE (CTLIDX)
011400         MOVE 99 TO RFL-SEQ (CTLIDX)
011500     ELSE
011600         ADD 1 TO RHD-PRINT-FIELD-COUNT
011700         IF RHD-PRINT-FIELD-COUNT < 41
011800             MOVE "Z" TO RFL-LINE-CODE (CTLIDX)
011900             COMPUTE RFL-SEQ (CTLIDX) = RHD-PRINT-FIELD-COUNT
012000         ELSE
012100             MOVE "Y" TO RFL-LINE-CODE (CTLIDX)
012200             COMPUTE RFL-SEQ (CTLIDX) =
012300                 RHD-PRINT-FIELD-COUNT - 40.
012400
012500     MOVE CF-NAME TO RFL-NAME (CTLIDX)
012600                     RFL-COL-HEAD (CTLIDX).
012700
012800     MOVE SPACES TO RFL-COL-SUB-HEAD (CTLIDX).
012900     MOVE 2 TO RFL-SPACES (CTLIDX).
013000     MOVE CF-EXT-LEN TO RFL-EXT-SIZE (CTLIDX).
013100
013200*     IF CF-IS-CHAR  OR
013300*        CF-IS-BINARY
000000      IF    CF-IS-CHAR
000000         OR  (CF-IS-BINARY AND (CF-BINARY-EDIT < 1))
013400         MOVE "C" TO CTL-FIELDS-TYPE (CTLIDX)
013500         MOVE SPACES TO  RFL-ZERO-SUPPRESS (CTLIDX)
013600                         RFL-SIGN-CONTROL (CTLIDX)
013700                         RFL-DECIMAL-CARRY (CTLIDX)
013800                         RFL-INSERTS (CTLIDX)
013900                         RFL-TOTAL-CODE (CTLIDX)
014000                         RFL-MAXIMUM-CODE (CTLIDX)
014100                         RFL-MINIMUM-CODE (CTLIDX)
014200                         RFL-AVERAGE-CODE (CTLIDX)
014300         MOVE ZERO TO    RFL-DOLLAR-SIGN (CTLIDX)
014400                         RFL-COMMA-CODE (CTLIDX).
014500
014600     IF CF-IS-UNSIGNED  OR
014700        CF-IS-PACKED  OR
014800        CF-IS-ZONED   OR
000000        (CF-IS-BINARY AND (CF-BINARY-EDIT > 0) )
014900         MOVE "N" TO CTL-FIELDS-TYPE (CTLIDX)
015000         IF CF-IS-NO-ZERO-SUPPRESS
015100             MOVE SPACES TO RFL-ZERO-SUPPRESS (CTLIDX)
015200         ELSE
015300         IF CF-IS-ZERO-SUPPRESS
015400             MOVE "Z9" TO RFL-ZERO-SUPPRESS (CTLIDX)
015500         ELSE
015600         IF CF-IS-PROTECT
015700             MOVE "*9" TO RFL-ZERO-SUPPRESS (CTLIDX).
015800
015900     IF CF-IS-UNSIGNED  OR
016000        CF-IS-PACKED  OR
016100        CF-IS-ZONED OR
000000        (CF-IS-BINARY AND (CF-BINARY-EDIT > 0) )
016200         IF CF-IS-NO-SIGN
016300             MOVE SPACES TO RFL-SIGN-CONTROL (CTLIDX)
016400         ELSE
016500         IF  CF-IS-TRAILING-MINUS
016600             MOVE "9" TO RFL-SIGN-CONTROL (CTLIDX)
016700         ELSE
016800         IF CF-IS-CR-ON-MINUS
016900             MOVE "CR" TO RFL-SIGN-CONTROL (CTLIDX)
017000         ELSE
017100         IF CF-IS-DB-ON-MINUS
017200             MOVE "DB" TO RFL-SIGN-CONTROL (CTLIDX).
017300
017400     IF CF-IS-UNSIGNED  OR
017500        CF-IS-PACKED  OR
016100        CF-IS-ZONED OR
000000        (CF-IS-BINARY AND (CF-BINARY-EDIT > 0) )
017700         IF CF-IS-NO-DOLLAR-COMMA
017800             MOVE 0 TO RFL-DOLLAR-SIGN (CTLIDX)
017900             MOVE 0 TO RFL-COMMA-CODE (CTLIDX)
018000         ELSE
018100         IF CF-IS-COMMA
018200             MOVE 0 TO RFL-DOLLAR-SIGN (CTLIDX)
018300             MOVE 1 TO RFL-COMMA-CODE (CTLIDX)
018400         ELSE
018500         IF CF-IS-DOLLAR
018600             MOVE 1 TO RFL-DOLLAR-SIGN (CTLIDX)
018700             MOVE 0 TO RFL-COMMA-CODE (CTLIDX)
018800         ELSE
018900         IF CF-IS-DOLLAR-COMMA
019000             MOVE 1 TO RFL-DOLLAR-SIGN (CTLIDX)
019100             MOVE 1 TO RFL-COMMA-CODE (CTLIDX).
019200
019300     IF CF-IS-UNSIGNED  OR
019400        CF-IS-PACKED  OR
016100        CF-IS-ZONED OR
000000        (CF-IS-BINARY AND (CF-BINARY-EDIT > 0) )
019600         MOVE CF-DECIMAL-POS TO RFL-DECIMAL-CARRY (CTLIDX)
019700                        CTL-FIELDS-DECIMAL (CTLIDX)
019800         MOVE SPACES TO RFL-INSERTS (CTLIDX)
019900                        RFL-TOTAL-CODE (CTLIDX)
020000                        RFL-MAXIMUM-CODE (CTLIDX)
020100                        RFL-MINIMUM-CODE (CTLIDX)
020200                        RFL-AVERAGE-CODE (CTLIDX).
020300
020400     IF    LOAD-SWITCH = "P"
000000        OR LOAD-SWITCH = "K"
020500         MOVE 1 TO RFL-ORIGIN (CTLIDX)
020600     ELSE
020700     IF LOAD-SWITCH = "S"
020800         MOVE 2 TO RFL-ORIGIN (CTLIDX).
020900
021000 LOAD-NEW-FIELDS.
021100     ADD 1 TO CTLIDX.
021200     IF CTLIDX NOT > 80
021300         IF RFL-NAME (CTLIDX) = SPACES
021400             MOVE 81 TO CTLIDX
021500         ELSE
021600         IF RFL-ORIGIN (CTLIDX) NOT = 3
021700             GO TO LOAD-NEW-FIELDS
021800         ELSE
021900         IF RFL-LINE-CODE (CTLIDX) NOT = SPACES
022000             GO TO LOAD-NEW-FIELDS.
022100
022200     IF CTLIDX NOT > 80
022300         PERFORM FIND-MATCH
022400             VARYING NF-IDX FROM 1 BY 1
022500             UNTIL NF-NAME (NF-IDX) = RFL-NAME (CTLIDX)  OR
022600                   NF-IDX > 10
022700         IF NF-IDX NOT > 10
022800             PERFORM LOAD-NEW-RECORD.
022900
023000     IF CTLIDX NOT > 80
023100         GO TO LOAD-NEW-FIELDS.
023200
023300 FIND-MATCH.
023400     EXIT.
023500
023600 LOAD-NEW-RECORD.
023700     IF RFL-SEQ (CTLIDX) NOT > 0
023800         ADD 1 TO RHD-PRINT-FIELD-COUNT
023900         IF RHD-PRINT-FIELD-COUNT < 41
024000             MOVE "Z" TO RFL-LINE-CODE (CTLIDX)
024100             COMPUTE RFL-SEQ (CTLIDX) = RHD-PRINT-FIELD-COUNT
024200         ELSE
024300             MOVE "Y" TO RFL-LINE-CODE (CTLIDX)
024400             COMPUTE RFL-SEQ (CTLIDX) = RHD-PRINT-FIELD-COUNT - 40
024500     ELSE
024600         MOVE "Z" TO RFL-LINE-CODE (CTLIDX).
024700
024800     MOVE NF-NAME (NF-IDX) TO RFL-NAME (CTLIDX).
024900     MOVE NF-NAME (NF-IDX) TO RFL-COL-HEAD (CTLIDX).
025000
025100     MOVE SPACES TO RFL-COL-SUB-HEAD (CTLIDX).
025200     MOVE 2 TO RFL-SPACES (CTLIDX).
025300     MOVE NF-LEN (NF-IDX) TO RFL-EXT-SIZE (CTLIDX).
025400
025500     IF NF-TYPE (NF-IDX) = "C"
025600         MOVE "C" TO CTL-FIELDS-TYPE (CTLIDX)
025700         MOVE SPACES TO RFL-DECIMAL-CARRY (CTLIDX).
025800
025900     IF NF-TYPE (NF-IDX) = "P"
026000         MOVE "N" TO CTL-FIELDS-TYPE (CTLIDX)
026100         MOVE NF-DEC (NF-IDX) TO RFL-DECIMAL-CARRY (CTLIDX)
026200                                 CTL-FIELDS-DECIMAL (CTLIDX).
026300
026400     MOVE SPACES TO  RFL-ZERO-SUPPRESS (CTLIDX)
026500                     RFL-SIGN-CONTROL (CTLIDX)
026600                     RFL-INSERTS (CTLIDX)
026700                     RFL-TOTAL-CODE (CTLIDX)
026800                     RFL-MAXIMUM-CODE (CTLIDX)
026900                     RFL-MINIMUM-CODE (CTLIDX)
027000                     RFL-AVERAGE-CODE (CTLIDX).
027100     MOVE ZERO TO    RFL-DOLLAR-SIGN (CTLIDX)
027200                     RFL-COMMA-CODE (CTLIDX).
027300
027400     MOVE 3 TO RFL-ORIGIN (CTLIDX).
027500
027600* GENERAL ROUTINES.
027700     COPY IOCTRL.
027800     COPY PLUSAGE.
027900
028000
000000     COPY PLKTRACE.
