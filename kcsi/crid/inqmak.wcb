000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQMAK.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/29/90.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE DEFAULTS:  FILE LIBRARY VOLUME DISPLAY
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600 FILE-CONTROL.
001700
001800 DATA DIVISION.
001900 FILE SECTION.
002000
002100 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqmak.wcb 1.7 9/11/94".
002200 01  VERSION-COPYRIGHT         PIC X(79) VALUE
002300     " ".
002400     COPY WSGP.
002500     COPY WSINT.
002600     COPY WSCRTS.
002700
002800 01  SAVQRY-GETPARM-FIELDS.
002900     05  SAVQRY-MSG-ID               PIC X(4) VALUE "1013".
003000     05  SAVQRY-PRNAME               PIC X(8) VALUE "PROGRAM".
003100     05  SAVQRY-MSG-ISSUER           PIC X(6) VALUE "INQUIR".
003200
003300 01  SAVQRY-MSG-COUNT.
003400     05  FILLER                        BINARY   VALUE ZERO.
003500     05  FILLER                        BINARY   VALUE 1.
003600 01  SAVQRY-MSG-TEXT-1               PIC X(68) VALUE
003700         "  Please select the name of the file where this query wi
003800-        "ll be saved.".
003900
004000 01  SAVQRY-TEXT-01                  PIC X(69) VALUE
004100         "  Please fill in the necessary information for the file
004200-        "to be created".
004300 01  SAVQRY-TEXT-02                  PIC X(20) VALUE
004400         "  and press (ENTER).".
004500 01  SAVQRY-FILE-KW              PIC X(8) VALUE "FILE".
004600 01  SAVQRY-FILE-FIELD           PIC X(8) VALUE SPACE.
004700 01  SAVQRY-FILE-DEFAULT         PIC X(8) VALUE SPACE.
004800 01  SAVQRY-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
004900 01  SAVQRY-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
005000 01  SAVQRY-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
005100 01  SAVQRY-VOLUME-KW              PIC X(8) VALUE "VOLUME".
005200 01  SAVQRY-VOLUME-FIELD           PIC X(6) VALUE SPACE.
005300 01  SAVQRY-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
005400 01  SAVQRY-TEXT-03                  PIC X(67) VALUE
005500         "  The query entered may be display for verification or n
005600-        "ot (YES/NO)".
005700 01  SAVQRY-DISPLAY-KW              PIC X(8) VALUE "DISPLAY".
005800 01  SAVQRY-DISPLAY-FIELD           PIC X(3) VALUE SPACE.
005900 01  SAVQRY-DISPLAY-DEFAULT         PIC X(3) VALUE SPACE.
006000 01  SAVQRY-TEXT-04                  PIC X(66) VALUE
006100         "  End INQUIRY program after this query has been complete
006200-        "d (YES/NO)".
006300 01  SAVQRY-RUN-KW              PIC X(8) VALUE "RUN".
006400 01  SAVQRY-RUN-FIELD           PIC X(3) VALUE SPACE.
006500 01  SAVQRY-RUN-DEFAULT         PIC X(3) VALUE SPACE.
006600 01  SAVQRY-TEXT-05                  PIC X(22) VALUE
006700         "  (ENTER) Continue  or".
006800 01  SAVQRY-TEXT-06                  PIC X(67) VALUE
006900         "                                 (1) Return without crea
007000-        "ting a file".
007600
007700     COPY WSUSAGE.
007800
007900 01  INQ-IO-BLOCK.
008000     COPY LKDIO.
008100
008200 01  PASS-DISPLAY            PIC X(03) VALUE SPACES.
008300 01  PASS-END-RUN            PIC X(03) VALUE SPACES.
008400 01  PASS-TITLE              PIC X(40) VALUE SPACES.
008500 01  PASS-QUERY.
008600     05  PASS-QUERY-LINE     PIC X(79) OCCURS 7 TIMES.
008700
008800 01  MESSAGE-LITERALS.
008900     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
009000         "* Error * File Name cannot be blank".
009100     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
009200         "* Error * Invalid File Name".
009300     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
009400         "* Error * Invalid Library Name".
009500     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
009600         "* Error * Invalid Volume Name".
009700
000000     COPY LKVALNAM.
000000
013300 LINKAGE SECTION.
013400 01  DATA-IO-BLOCK.
013500     COPY LKDIO.
013600 01  PASS-CHANGE         PIC X(03).
013700 01  PASS-OPTION         PIC X(07).
013800 01  CTL-IO-BLOCK.
013900     COPY LKDIO.
014000 01  P-FMT-QUERY-SCREEN.
014100     05  P-FMT-QUERY-LINES.
014200         07  P-FMT-QUERY-LINE PIC X(79) OCCURS 7 TIMES.
014300     05  P-FMT-QUERY-TITLE PIC X(40).
014400 01  EXTRACT-IO-BLOCK.
014500     COPY LKDIO.
014600
014700 PROCEDURE DIVISION USING
014800     DATA-IO-BLOCK
014900     PASS-CHANGE
015000     PASS-OPTION
015100     CTL-IO-BLOCK
015200     P-FMT-QUERY-SCREEN
015300     EXTRACT-IO-BLOCK.
015400 MAIN-LOGIC SECTION.
015500 PROGRAM-BEGIN.
000000     MOVE "INQMAK BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
015600     PERFORM OPENING-PROCEDURE.
015700     PERFORM MAIN-PROCESS.
015800     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQMAK EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
015900 MAIN-LOGIC-EXIT.
016000     EXIT PROGRAM.
016100 MAIN-LOGIC-STOP.
016200     STOP RUN.
016300 LEVEL-2 SECTION.
016400 OPENING-PROCEDURE.
016500     MOVE "YES" TO SAVQRY-DISPLAY-FIELD.
016600     MOVE "NO " TO SAVQRY-RUN-FIELD.
016700     PERFORM EXTRACT-USAGE-CONSTANTS.
016800*     MOVE OUTLIB TO SAVQRY-LIBRARY-FIELD.
000000     MOVE SPACE TO SAVQRY-LIBRARY-FIELD.
000000     STRING USER-ID DELIMITED BY SPACE
000000            "INQ" DELIMITED BY SIZE
000000            INTO SAVQRY-LIBRARY-FIELD.
016900     MOVE OUTVOL TO SAVQRY-VOLUME-FIELD.
017000
017100 CLOSING-PROCEDURE.
017200
017300 MAIN-PROCESS.
017400     MOVE SPACE TO SCREEN-ERROR-STATUS.
017500     PERFORM CALL-SAVQRY-GETPARM.
017600
017700     MOVE SPACE TO SAVQRY-MSG-TEXT-1.
017800
017900     IF GP-ENTER-KEY-PRESSED
018000         PERFORM VAL-FILE-NAME
018100         IF NOT SCREEN-ENTRY-ERROR
018200             PERFORM VAL-LIB-NAME
018300             IF NOT SCREEN-ENTRY-ERROR
018400                 PERFORM VAL-VOL-NAME
018500                 IF NOT SCREEN-ENTRY-ERROR
018600                     PERFORM VAL-DISPLAY
018700                     IF NOT SCREEN-ENTRY-ERROR
018800                         PERFORM VAL-END-RUN.
018900
019000     IF SCREEN-ENTRY-ERROR
019100         GO TO MAIN-PROCESS.
019200
019300     IF GP-ENTER-KEY-PRESSED
019400         MOVE SAVQRY-FILE-FIELD TO FILE-NAME OF INQ-IO-BLOCK
019500         MOVE SAVQRY-LIBRARY-FIELD TO FILE-LIBRARY OF INQ-IO-BLOCK
019600         MOVE SAVQRY-VOLUME-FIELD TO FILE-VOLUME OF INQ-IO-BLOCK
019700         CALL "INQXST" USING
019800             INQ-IO-BLOCK
019400         MOVE FILE-NAME OF INQ-IO-BLOCK TO SAVQRY-FILE-FIELD
019500         MOVE FILE-LIBRARY OF INQ-IO-BLOCK TO SAVQRY-LIBRARY-FIELD
019600         MOVE FILE-VOLUME OF INQ-IO-BLOCK TO SAVQRY-VOLUME-FIELD
019900         PERFORM MAKE-THE-QUERY-FILE.
020000
020100 CALL-SAVQRY-GETPARM.
020200
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-01-VAL TO PFKEY-MASK-16.
020400
020500     CALL "GETPARM" USING
020600         GP-SPECIFY-TYPE, GP-REQUEST-FORM, SAVQRY-PRNAME,
020700         GP-PFKEY-RECEIVER, SAVQRY-MSG-ID,
020800         SAVQRY-MSG-ISSUER,
020900
021000         SAVQRY-MSG-COUNT,
021100         SAVQRY-MSG-TEXT-1, INT68,
021200
021300         GP-TEXT-TYPE, SAVQRY-TEXT-01, INT69,
021400         GP-ABS, INT09, GP-ABS, INT02,
021500
021600         GP-TEXT-TYPE, SAVQRY-TEXT-02, INT18,
021700         GP-ABS, INT10, GP-ABS, INT02,
021800
021900         GP-KW-TYPE, SAVQRY-FILE-KW,
022000         SAVQRY-FILE-FIELD, INT08,
022100         GP-ABS, INT12, GP-ABS, INT09,
022200         GP-ALPHANUMERIC-DATA
022300
022400         GP-KW-TYPE, SAVQRY-LIBRARY-KW,
022500         SAVQRY-LIBRARY-FIELD, INT08,
022600         GP-ABS, INT12, GP-ABS, INT30,
022700         GP-ALPHANUMERIC-DATA
022800
022900         GP-KW-TYPE, SAVQRY-VOLUME-KW,
023000         SAVQRY-VOLUME-FIELD, INT06,
023100         GP-ABS, INT12, GP-ABS, INT51,
023200         GP-ALPHANUMERIC-DATA
023300
023400         GP-TEXT-TYPE, SAVQRY-TEXT-03, INT67,
023500         GP-ABS, INT14, GP-ABS, INT02,
023600
023700         GP-KW-TYPE, SAVQRY-DISPLAY-KW,
023800         SAVQRY-DISPLAY-FIELD, INT03,
023900         GP-ABS, INT15, GP-ABS, INT09,
024000         GP-UPPERCASE-DATA
024100
024200         GP-TEXT-TYPE, SAVQRY-TEXT-04, INT66,
024300         GP-ABS, INT17, GP-ABS, INT02,
024400
024500         GP-KW-TYPE, SAVQRY-RUN-KW,
024600         SAVQRY-RUN-FIELD, INT03,
024700         GP-ABS, INT18, GP-ABS, INT09,
024800         GP-UPPERCASE-DATA
024900
025000         GP-TEXT-TYPE, SAVQRY-TEXT-05, INT22,
025100         GP-ABS, INT23, GP-ABS, INT02,
025200
025300         GP-TEXT-TYPE, SAVQRY-TEXT-06, INT67,
025400         GP-ABS, INT24, GP-ABS, INT02,
025500         GP-ENABLE-ENTER-KEY
025600         GP-PFKEY-TYPE, PFKEY-MASK.
000000
000000     IF GP-PFKEY-RECEIVER = LOW-VALUES
000000         MOVE HEX40 TO GP-PFKEY-RECEIVER.
025700
025800     IF NOT GP-PFKEY-1-PRESSED  AND
025900        NOT GP-ENTER-KEY-PRESSED
026000         GO TO CALL-SAVQRY-GETPARM.
026100
026200 VAL-FILE-NAME.
026300
026400     IF SAVQRY-FILE-FIELD = SPACES
026500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
026600         MOVE FILE-ID-BLANK-LITERAL TO SAVQRY-MSG-TEXT-1.
026700
026800     IF NOT SCREEN-ENTRY-ERROR
026900         MOVE SAVQRY-FILE-FIELD TO VAL-NAME
027000         PERFORM TEST-THE-NAME
027100         IF SCREEN-ENTRY-ERROR
027200             MOVE INVALID-FILE-LITERAL TO SAVQRY-MSG-TEXT-1.
027300
027400 VAL-LIB-NAME.
027500
027600     MOVE SAVQRY-LIBRARY-FIELD TO VAL-NAME.
027700     PERFORM TEST-THE-NAME.
027800     IF SCREEN-ENTRY-ERROR
027900         MOVE INVALID-LIB-LITERAL TO SAVQRY-MSG-TEXT-1.
028000
028100 VAL-VOL-NAME.
028200
028300     MOVE SAVQRY-VOLUME-FIELD TO VAL-NAME.
028400     PERFORM TEST-THE-VOL.
028500     IF SCREEN-ENTRY-ERROR
028600         MOVE INVALID-VOL-LITERAL TO SAVQRY-MSG-TEXT-1.
028700
028800 VAL-DISPLAY.
028900     IF SAVQRY-DISPLAY-FIELD NOT = "NO "  AND
029000        SAVQRY-DISPLAY-FIELD NOT = "YES"
029100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
029200         MOVE "** Error ** DISPLAY must be 'YES' or 'NO' "
029300             TO SAVQRY-MSG-TEXT-1.
029400
029500 VAL-END-RUN.
029600     IF SAVQRY-RUN-FIELD NOT = "NO "  AND
029700        SAVQRY-RUN-FIELD NOT = "YES"
029800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
029900         MOVE "** Error ** END RUN must be 'YES' or 'NO' "
030000             TO SAVQRY-MSG-TEXT-1.
030100
030200 MAKE-THE-QUERY-FILE.
030300     PERFORM INIT-THE-PROC-FIELDS.
030400     CALL "GENINQ" USING
030500         INQ-IO-BLOCK
030600         PASS-DISPLAY
030700         PASS-END-RUN
030800         DATA-IO-BLOCK
030900         PASS-CHANGE
031000         PASS-OPTION
031100         CTL-IO-BLOCK
031200         PASS-TITLE
031300         PASS-QUERY
031400         EXTRACT-IO-BLOCK.
031500
031600 INIT-THE-PROC-FIELDS.
031700     MOVE SAVQRY-FILE-FIELD TO
031800         FILE-NAME OF INQ-IO-BLOCK.
031900     MOVE SAVQRY-LIBRARY-FIELD TO
032000         FILE-LIBRARY OF INQ-IO-BLOCK.
032100     MOVE SAVQRY-VOLUME-FIELD TO
032200         FILE-VOLUME OF INQ-IO-BLOCK.
032300
032400     MOVE SAVQRY-RUN-FIELD TO PASS-END-RUN.
032500     MOVE SAVQRY-DISPLAY-FIELD TO PASS-DISPLAY.
032600     MOVE P-FMT-QUERY-TITLE TO PASS-TITLE.
032700     MOVE P-FMT-QUERY-LINES TO PASS-QUERY.
032800
031500 TEST-THE-NAME.
000000     CALL "VALNAM" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
031500 TEST-THE-VOL.
000000     CALL "VALVOL" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
036600
036700* GENERAL ROUTINES.
036800     COPY PLUSAGE.
036900
000000     COPY PLKTRACE.
