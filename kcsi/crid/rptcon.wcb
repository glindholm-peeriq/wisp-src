000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTCON.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/16/89.
000500 DATE-COMPILED.
000600*-------------------------------------------------------------
000700* DEFINES THE CONTROL FIELDS AND ACTIONS
000800*-------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptcon.wcb 1.4 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002700
002800 01  CONTROL-FIELDS-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(45) ROW 1 COLUMN 2 VALUE
003100         "                               CONTROL FIELDS".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 2 COLUMN 3
003400         SOURCE UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(73) ROW 3 COLUMN 2 VALUE
003700         "     Control fields are fields which control the totalin
003800-        "g and printing of".
003900     05  FILLER
004000         PIC X(75) ROW 4 COLUMN 2 VALUE
004100         "   subtotals during the report.  When the value in a con
004200-        "trol field changes,".
004300     05  FILLER
004400         PIC X(75) ROW 5 COLUMN 2 VALUE
004500         "   subtotals are printed.  Specify the fields to be used
004600-        " as control fields,".
004700     05  FILLER
004800         PIC X(76) ROW 6 COLUMN 2 VALUE
004900         "   and their order of importance.  (5: Highest Level, th
005000-        "ru 1: Lowest Level).".
005100     05  FILLER
005200         PIC X(73) ROW 7 COLUMN 2 VALUE
005300         "    Also specify the action to be taken at the control b
005400-        "reak, and whether".
005500     05  FILLER
005600         PIC X(71) ROW 8 COLUMN 2 VALUE
005700         "      you want the control break values printed with eve
005800-        "ry detail line.".
005900     05  FILLER
006000         PIC X(73) ROW 10 COLUMN 2 VALUE
006100         "                                    Action
006200-        "   Control Fields".
006300     05  FILLER
006400         PIC X(77) ROW 11 COLUMN 2 VALUE
006500         "  Field ID       Level      New Page   Skip n Lines    P
006600-        "rinted On Every Line?".
006700     05  CTLFLD-ID-1-ITEM
006800         PIC X(12) ROW 13 COLUMN 2
006900         SOURCE CTLFLD-ID-1-FIELD
007000         OBJECT CTLFLD-ID-1-FIELD.
007100     05  CTLFLD-LEVEL-1-ITEM
007200         PIC Z ROW 13 COLUMN 21
007300         SOURCE CTLFLD-LEVEL-1-FIELD
007400         OBJECT CTLFLD-LEVEL-1-FIELD.
007500     05  CTLFLD-PAGE-1-ITEM
007600         PIC XXX ROW 13 COLUMN 32
007700         SOURCE CTLFLD-PAGE-1-FIELD
007800         OBJECT CTLFLD-PAGE-1-FIELD.
007900     05  CTLFLD-SKIP-1-ITEM
008000         PIC 99 ROW 13 COLUMN 45
008100         SOURCE CTLFLD-SKIP-1-FIELD
008200         OBJECT CTLFLD-SKIP-1-FIELD.
008300     05  CTLFLD-PRINT-1-ITEM
008400         PIC XXX ROW 13 COLUMN 66
008500         SOURCE CTLFLD-PRINT-1-FIELD
008600         OBJECT CTLFLD-PRINT-1-FIELD.
008700     05  CTLFLD-ID-2-ITEM
008800         PIC X(12) ROW 14 COLUMN 2
008900         SOURCE CTLFLD-ID-2-FIELD
009000         OBJECT CTLFLD-ID-2-FIELD.
009100     05  CTLFLD-LEVEL-2-ITEM
009200         PIC Z ROW 14 COLUMN 21
009300         SOURCE CTLFLD-LEVEL-2-FIELD
009400         OBJECT CTLFLD-LEVEL-2-FIELD.
009500     05  CTLFLD-PAGE-2-ITEM
009600         PIC XXX ROW 14 COLUMN 32
009700         SOURCE CTLFLD-PAGE-2-FIELD
009800         OBJECT CTLFLD-PAGE-2-FIELD.
009900     05  CTLFLD-SKIP-2-ITEM
010000         PIC 99 ROW 14 COLUMN 45
010100         SOURCE CTLFLD-SKIP-2-FIELD
010200         OBJECT CTLFLD-SKIP-2-FIELD.
010300     05  CTLFLD-PRINT-2-ITEM
010400         PIC XXX ROW 14 COLUMN 66
010500         SOURCE CTLFLD-PRINT-2-FIELD
010600         OBJECT CTLFLD-PRINT-2-FIELD.
010700     05  CTLFLD-ID-3-ITEM
010800         PIC X(12) ROW 15 COLUMN 2
010900         SOURCE CTLFLD-ID-3-FIELD
011000         OBJECT CTLFLD-ID-3-FIELD.
011100     05  CTLFLD-LEVEL-3-ITEM
011200         PIC Z ROW 15 COLUMN 21
011300         SOURCE CTLFLD-LEVEL-3-FIELD
011400         OBJECT CTLFLD-LEVEL-3-FIELD.
011500     05  CTLFLD-PAGE-3-ITEM
011600         PIC XXX ROW 15 COLUMN 32
011700         SOURCE CTLFLD-PAGE-3-FIELD
011800         OBJECT CTLFLD-PAGE-3-FIELD.
011900     05  CTLFLD-SKIP-3-ITEM
012000         PIC 99 ROW 15 COLUMN 45
012100         SOURCE CTLFLD-SKIP-3-FIELD
012200         OBJECT CTLFLD-SKIP-3-FIELD.
012300     05  CTLFLD-PRINT-3-ITEM
012400         PIC XXX ROW 15 COLUMN 66
012500         SOURCE CTLFLD-PRINT-3-FIELD
012600         OBJECT CTLFLD-PRINT-3-FIELD.
012700     05  CTLFLD-ID-4-ITEM
012800         PIC X(12) ROW 16 COLUMN 2
012900         SOURCE CTLFLD-ID-4-FIELD
013000         OBJECT CTLFLD-ID-4-FIELD.
013100     05  CTLFLD-LEVEL-4-ITEM
013200         PIC Z ROW 16 COLUMN 21
013300         SOURCE CTLFLD-LEVEL-4-FIELD
013400         OBJECT CTLFLD-LEVEL-4-FIELD.
013500     05  CTLFLD-PAGE-4-ITEM
013600         PIC XXX ROW 16 COLUMN 32
013700         SOURCE CTLFLD-PAGE-4-FIELD
013800         OBJECT CTLFLD-PAGE-4-FIELD.
013900     05  CTLFLD-SKIP-4-ITEM
014000         PIC 99 ROW 16 COLUMN 45
014100         SOURCE CTLFLD-SKIP-4-FIELD
014200         OBJECT CTLFLD-SKIP-4-FIELD.
014300     05  CTLFLD-PRINT-4-ITEM
014400         PIC XXX ROW 16 COLUMN 66
014500         SOURCE CTLFLD-PRINT-4-FIELD
014600         OBJECT CTLFLD-PRINT-4-FIELD.
014700     05  CTLFLD-ID-5-ITEM
014800         PIC X(12) ROW 17 COLUMN 2
014900         SOURCE CTLFLD-ID-5-FIELD
015000         OBJECT CTLFLD-ID-5-FIELD.
015100     05  CTLFLD-LEVEL-5-ITEM
015200         PIC Z ROW 17 COLUMN 21
015300         SOURCE CTLFLD-LEVEL-5-FIELD
015400         OBJECT CTLFLD-LEVEL-5-FIELD.
015500     05  CTLFLD-PAGE-5-ITEM
015600         PIC XXX ROW 17 COLUMN 32
015700         SOURCE CTLFLD-PAGE-5-FIELD
015800         OBJECT CTLFLD-PAGE-5-FIELD.
015900     05  CTLFLD-SKIP-5-ITEM
016000         PIC 99 ROW 17 COLUMN 45
016100         SOURCE CTLFLD-SKIP-5-FIELD
016200         OBJECT CTLFLD-SKIP-5-FIELD.
016300     05  CTLFLD-PRINT-5-ITEM
016400         PIC XXX ROW 17 COLUMN 66
016500         SOURCE CTLFLD-PRINT-5-FIELD
016600         OBJECT CTLFLD-PRINT-5-FIELD.
016700     05  FILLER
016800         PIC X(73) ROW 19 COLUMN 2 VALUE
016900         "      Note:  When 'YES' is entered for New Page, Skip n
017000-        "Lines is ignored.".
017100     05  FILLER
017200         PIC X(75) ROW 20 COLUMN 2 VALUE
017300         "             Control break descriptors may be defined in
017400-        " the Titles Screen.".
017410     COPY RPTPFMNU.
018300
018400 01  CONTROL-FIELDS-TABLE.
018500     05  UPPER-MSG-FIELD               PIC X(78) VALUE SPACE.
018600     05  CONTROL-FIELDS.
018700         07  CTLFLD-ID-1-FIELD         PIC X(12) VALUE SPACE.
018800*         07  CTLFLD-LEVEL-1-FIELD      PIC X VALUE SPACE.
018800         07  CTLFLD-LEVEL-1-FIELD      PIC 9 VALUE ZERO.
018900         07  CTLFLD-PAGE-1-FIELD       PIC XXX VALUE SPACE.
019000         07  CTLFLD-SKIP-1-FIELD       PIC 99 VALUE ZERO.
019100         07  CTLFLD-PRINT-1-FIELD      PIC XXX VALUE SPACE.
019200         07  CTLFLD-ID-2-FIELD         PIC X(12) VALUE SPACE.
019300*         07  CTLFLD-LEVEL-2-FIELD      PIC X VALUE SPACE.
018800         07  CTLFLD-LEVEL-2-FIELD      PIC 9 VALUE ZERO.
019400         07  CTLFLD-PAGE-2-FIELD       PIC XXX VALUE SPACE.
019500         07  CTLFLD-SKIP-2-FIELD       PIC 99 VALUE ZERO.
019600         07  CTLFLD-PRINT-2-FIELD      PIC XXX VALUE SPACE.
019700         07  CTLFLD-ID-3-FIELD         PIC X(12) VALUE SPACE.
019800*         07  CTLFLD-LEVEL-3-FIELD      PIC X VALUE SPACE.
018800         07  CTLFLD-LEVEL-3-FIELD      PIC 9 VALUE ZERO.
019900         07  CTLFLD-PAGE-3-FIELD       PIC XXX VALUE SPACE.
020000         07  CTLFLD-SKIP-3-FIELD       PIC 99 VALUE ZERO.
020100         07  CTLFLD-PRINT-3-FIELD      PIC XXX VALUE SPACE.
020200         07  CTLFLD-ID-4-FIELD         PIC X(12) VALUE SPACE.
020300*         07  CTLFLD-LEVEL-4-FIELD      PIC X VALUE SPACE.
018800         07  CTLFLD-LEVEL-4-FIELD      PIC 9 VALUE ZERO.
020400         07  CTLFLD-PAGE-4-FIELD       PIC XXX VALUE SPACE.
020500         07  CTLFLD-SKIP-4-FIELD       PIC 99 VALUE ZERO.
020600         07  CTLFLD-PRINT-4-FIELD      PIC XXX VALUE SPACE.
020700         07  CTLFLD-ID-5-FIELD         PIC X(12) VALUE SPACE.
020300*         07  CTLFLD-LEVEL-5-FIELD      PIC X VALUE SPACE.
018800         07  CTLFLD-LEVEL-5-FIELD      PIC 9 VALUE ZERO.
020900         07  CTLFLD-PAGE-5-FIELD       PIC XXX VALUE SPACE.
021000         07  CTLFLD-SKIP-5-FIELD       PIC 99 VALUE ZERO.
021100         07  CTLFLD-PRINT-5-FIELD      PIC XXX VALUE SPACE.
021200     05  FILLER2 REDEFINES CONTROL-FIELDS.
021300         07  ONE-CONTROL-LINE          OCCURS 5 TIMES.
021400             09  ID-FIELD.
021500                 11  NAME-FIELD        PIC X(08).
021600                 11  O-FIELD.
021700                     13  LEFT-PAREN        PIC X(01).
021800                     13  OCCURS-FIELD      PIC X(02).
021900                     13  RIGHT-PAREN       PIC X(01).
022000             09  LEVEL-FIELD           PIC 9(01).
022100             09  PAGE-FIELD            PIC X(03).
022200             09  LINES-FIELD           PIC 9(02).
022300             09  PRINT-FIELD           PIC X(03).
022400     05  SAVE-RFL-IDX                  PIC 9(02) OCCURS 5 TIMES.
022500     05  ORIGIN-FIELD                  PIC 9(01) OCCURS 5 TIMES.
022600
022700     05  HOLD-CONTROL-LINE.
022800         07  FILLER              PIC X(12).
022900         07  FILLER              PIC 9(01).
023000         07  FILLER              PIC X(03).
023100         07  FILLER              PIC 9(02).
023200         07  FILLER              PIC X(03).
023300     05  HOLD-RFL-IDX            PIC 9(02).
023400     05  HOLD-ORIGIN             PIC 9(01).
023500     05  CONTINUE-SORT           PIC X(01).
023600     05  HIGH-MAN                PIC 9(02).
023700     05  NEXT-DOWN               PIC 9(02).
023800     05  LOW-MAN                 PIC 9(02).
023900     05  NEXT-UP                 PIC 9(02).
024000
024100     05  IDX                           PIC 9(02).
024200     05  VAL-IDX                       PIC 9(02).
024300     05  DUP-IDX                       PIC 9(02).
024400     05  ITS-A-MATCH                   PIC X(01) VALUE "N".
024500     05  RCB-NAME-FLAG     PIC X(01) VALUE "N".
024600         88  RCB-NAME-FOUND       VALUE "Y".
024700
024800 01  POSITION-THE-NAME-FIELDS.
024900     05  POSITION-NAME-IN        PIC X(12) VALUE SPACES.
025000     05  FILLER REDEFINES POSITION-NAME-IN.
025100         07  POS-IN-CHAR         PIC X(01) OCCURS 12 TIMES.
025200     05  POSITION-NAME-OUT       PIC X(12) VALUE SPACES.
025300     05  FILLER  REDEFINES POSITION-NAME-OUT.
025400         07  POS-OUT-CHAR        PIC X(01) OCCURS 8 TIMES.
025500         07  POS-OUT-OCCURS.
025600             09  POS-OUT-LT-PAREN PIC X(01).
025700             09  POS-OUT-OCC-1    PIC X(01).
025800             09  POS-OUT-OCC-2    PIC X(01).
025900             09  POS-OUT-RT-PAREN PIC X(01).
026000     05  POSITIONING-COUNTERS.
026100         07  PAREN-LOCATION      PIC 9(02) VALUE 0.
026200         07  POS-POINTER         PIC 9(02) VALUE 0.
026300         07  RIGHT-PAREN-COUNT   PIC 9(02) VALUE 0.
026400         07  LEFT-PAREN-COUNT    PIC 9(02) VALUE 0.
026500
026600 LINKAGE SECTION.
026700 01  RPT-PFKEY       PIC 9(02).
026800     COPY WSRPTCFL.
026900     COPY WSRPTREC.
027000
027100 PROCEDURE DIVISION USING
027200     RPT-PFKEY
027300     RPT-CTL-FIELDS
027400     RPT-RECORDS.
027500
027600 MAIN-LOGIC SECTION.
027700 PROGRAM-BEGIN.
000000     MOVE "RPTCON BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
027800     PERFORM OPENING-PROCEDURE.
027900     PERFORM MAIN-PROCESS.
028000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTCON EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
028100 MAIN-LOGIC-EXIT.
028200     EXIT PROGRAM.
028300 MAIN-LOGIC-STOP.
028400     STOP RUN.
028500 LEVEL-2 SECTION.
028600 OPENING-PROCEDURE.
028700     OPEN I-O  CRT-FILE.
028800 CLOSING-PROCEDURE.
028900     CLOSE CRT-FILE.
029000
029100 MAIN-PROCESS.
029200     PERFORM LOAD-DEFAULTS.
029300     PERFORM GET-THE-DATA.
029400
029500 LOAD-DEFAULTS.
029600     MOVE 0 TO IDX.
029700     PERFORM LOAD-THE-DATA.
029800     MOVE DISPLAY-HI-FAC TO FAC OF UPPER-MSG-ITEM.
029900     PERFORM JUST-THE-FACS.
030000
030100 LOAD-THE-DATA.
030200     ADD 1 TO IDX.
030300     IF RCB-NAME (IDX) NOT = SPACES
030400         PERFORM LOAD-THE-RCB
030500     ELSE
030600         PERFORM LOAD-BLANKS.
030700
030800     IF IDX NOT = 5
030900         GO TO LOAD-THE-DATA.
031000
031100 LOAD-THE-RCB.
031200     MOVE RCB-NAME (IDX) TO NAME-FIELD (IDX).
031300     IF RCB-OCCURRENCE-A (IDX) NOT < 01  AND
031400        RCB-OCCURRENCE-A (IDX) NOT > 99
031500         MOVE "(" TO LEFT-PAREN (IDX)
031600         MOVE RCB-OCCURRENCE-A (IDX) TO OCCURS-FIELD (IDX)
031700         MOVE ")" TO RIGHT-PAREN (IDX)
031800     ELSE
031900         MOVE SPACES TO O-FIELD (IDX).
032000     MOVE IDX TO LEVEL-FIELD (IDX).
032100     MOVE RCB-ORIGIN (IDX) TO ORIGIN-FIELD (IDX).
032200     IF RCB-ORIGIN (IDX) = 2
032300         MOVE 1 TO RFL-IDX
032400         PERFORM LOAD-RCB-AKA.
032500     IF RCB-ACTION-CODE (IDX) = "YE"
032600         MOVE "YES" TO PAGE-FIELD (IDX)
032700         MOVE 0 TO LINES-FIELD (IDX)
032800     ELSE
032900         MOVE "NO" TO PAGE-FIELD (IDX)
033000         MOVE RCB-ACTION-CODE (IDX) TO LINES-FIELD (IDX).
033100     IF RCB-REPEAT-CODE (IDX) = 1
033200         MOVE "YES" TO PRINT-FIELD (IDX)
033300     ELSE
033400         MOVE "NO " TO PRINT-FIELD (IDX).
033500
033600 LOAD-RCB-AKA.
033700     IF RFL-NAME (RFL-IDX) = RCB-NAME (IDX) AND
033800        RFL-ORIGIN (RFL-IDX) = 2
033900         IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
034000            RFL-OCCURRENCE-A (RFL-IDX) NOT > 99 AND
034100            RFL-OCCURRENCE-A (RFL-IDX) = OCCURS-FIELD (IDX)
034200             IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
034300                 MOVE CTL-FIELDS-AKA (RFL-IDX) TO
034400                         ID-FIELD (IDX)
034500             ELSE
034600                 NEXT SENTENCE
034700         ELSE
034800         IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
034900             MOVE CTL-FIELDS-AKA (RFL-IDX) TO
035000                         ID-FIELD (IDX).
035100     IF RFL-NAME (RFL-IDX) = SPACES
035200         MOVE 80 TO RFL-IDX.
035300     ADD 1 TO RFL-IDX.
035400     IF RFL-IDX NOT > 80
035500         GO TO LOAD-RCB-AKA.
035600
035700 LOAD-BLANKS.
035800     MOVE SPACES TO ID-FIELD (IDX).
035900     MOVE 0 TO LEVEL-FIELD (IDX).
036000     MOVE 0 TO ORIGIN-FIELD (IDX).
036100     MOVE "NO" TO PAGE-FIELD (IDX).
036200     MOVE 0 TO LINES-FIELD (IDX).
036300     MOVE "YES" TO PRINT-FIELD (IDX).
036400
036500 RELOAD-DEFAULTS.
036600     MOVE 0 TO RCB-IDX.
036700     MOVE 0 TO RHD-CONTROL-FIELDS.
036800     MOVE SPACE TO RCB-RECORD.
036900     PERFORM SORT-THE-TABLE.
037000     PERFORM RELOAD-THE-DATA
037100         VARYING IDX FROM 1 BY 1
037200         UNTIL IDX > 5.
037300
037400 SORT-THE-TABLE.
037500     MOVE "N" TO CONTINUE-SORT.
037600     MOVE 5 TO LOW-MAN.
037700     MOVE 4 TO NEXT-UP.
037800     PERFORM SORT-UP-CHAIN.
037900
038000     IF CONTINUE-SORT = "Y"
038100         MOVE "N" TO CONTINUE-SORT
038200         MOVE 1 TO HIGH-MAN
038300         MOVE 2 TO NEXT-DOWN
038400         PERFORM SORT-DOWN-CHAIN.
038500
038600     IF CONTINUE-SORT = "Y"
038700         GO TO SORT-THE-TABLE.
038800
038900 SORT-UP-CHAIN.
039000     IF LEVEL-FIELD (LOW-MAN) NOT = ZEROES
039100         IF LEVEL-FIELD (LOW-MAN) < LEVEL-FIELD (NEXT-UP)
039200             MOVE "Y" TO CONTINUE-SORT
039300             MOVE ONE-CONTROL-LINE (NEXT-UP) TO HOLD-CONTROL-LINE
039400             MOVE SAVE-RFL-IDX (NEXT-UP) TO HOLD-RFL-IDX
039500             MOVE ORIGIN-FIELD (NEXT-UP) TO HOLD-ORIGIN
039600             MOVE ONE-CONTROL-LINE (LOW-MAN)
039700                 TO ONE-CONTROL-LINE (NEXT-UP)
039800             MOVE SAVE-RFL-IDX (LOW-MAN) TO SAVE-RFL-IDX (NEXT-UP)
039900             MOVE ORIGIN-FIELD (LOW-MAN) TO ORIGIN-FIELD (NEXT-UP)
040000             MOVE HOLD-CONTROL-LINE TO ONE-CONTROL-LINE (LOW-MAN)
040100             MOVE HOLD-RFL-IDX TO SAVE-RFL-IDX (LOW-MAN)
040200             MOVE HOLD-ORIGIN TO ORIGIN-FIELD (LOW-MAN).
040300
040400     IF NEXT-UP NOT = 1
040500         SUBTRACT 1 FROM LOW-MAN
040600         SUBTRACT 1 FROM NEXT-UP
040700         GO TO SORT-UP-CHAIN.
040800
040900 SORT-DOWN-CHAIN.
041000     IF LEVEL-FIELD (NEXT-DOWN) NOT = ZEROES
041100         IF LEVEL-FIELD (HIGH-MAN) > LEVEL-FIELD (NEXT-DOWN)
041200             MOVE "Y" TO CONTINUE-SORT
041300             MOVE ONE-CONTROL-LINE (NEXT-DOWN)
041400                 TO HOLD-CONTROL-LINE
041500             MOVE SAVE-RFL-IDX (NEXT-DOWN) TO HOLD-RFL-IDX
041600             MOVE ORIGIN-FIELD (NEXT-DOWN) TO HOLD-ORIGIN
041700             MOVE ONE-CONTROL-LINE (HIGH-MAN)
041800                 TO ONE-CONTROL-LINE (NEXT-DOWN)
041900             MOVE SAVE-RFL-IDX (HIGH-MAN)
042000                 TO SAVE-RFL-IDX (NEXT-DOWN)
042100             MOVE ORIGIN-FIELD (HIGH-MAN)
042200                 TO ORIGIN-FIELD (NEXT-DOWN)
042300             MOVE HOLD-CONTROL-LINE TO ONE-CONTROL-LINE (HIGH-MAN)
042400             MOVE HOLD-RFL-IDX TO SAVE-RFL-IDX (HIGH-MAN)
042500             MOVE HOLD-ORIGIN TO ORIGIN-FIELD (HIGH-MAN).
042600
042700     IF NEXT-DOWN NOT = 5
042800         ADD 1 TO HIGH-MAN
042900         ADD 1 TO NEXT-DOWN
043000         GO TO SORT-DOWN-CHAIN.
043100
043200 RELOAD-THE-DATA.
043300     IF NAME-FIELD (IDX) NOT = SPACES
043400         PERFORM RELOAD-ONE-LINE.
043500
043600 RELOAD-ONE-LINE.
043700     ADD 1 TO RCB-IDX.
043800     MOVE 1 TO RHD-CONTROL-FIELDS.
043900     MOVE SAVE-RFL-IDX (IDX) TO RFL-IDX.
044000     MOVE RFL-NAME (RFL-IDX) TO RCB-NAME (RCB-IDX).
044100     IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
044200        RFL-OCCURRENCE-A (RFL-IDX) NOT > 99
044300         MOVE RFL-OCCURRENCE-A (RFL-IDX) TO
044400                             RCB-OCCURRENCE-A (RCB-IDX)
044500     ELSE
044600         MOVE SPACES TO RCB-OCCURRENCE-A (RCB-IDX).
044700     MOVE ORIGIN-FIELD (IDX) TO RCB-ORIGIN (RCB-IDX).
044800     IF PRINT-FIELD (IDX) = "YES"
044900         MOVE 1 TO RCB-REPEAT-CODE (RCB-IDX)
045000     ELSE
045100         MOVE 0 TO RCB-REPEAT-CODE (RCB-IDX).
045200     IF PAGE-FIELD (IDX) = "YES"
045300         MOVE "YE" TO RCB-ACTION-CODE (RCB-IDX)
045400     ELSE
045500         MOVE LINES-FIELD (IDX) TO RCB-ACTION-CODE (RCB-IDX).
045600
045700 GET-THE-DATA.
045800     MOVE SPACES TO SCREEN-ERROR-STATUS.
045900
046000     DISPLAY AND READ ALTERED CONTROL-FIELDS-SCREEN ON CRT-FILE
046100     PFKEYS 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 16.
046200
046300     MOVE SPACE TO UPPER-MSG-FIELD.
046400
046500     IF PFKEY-16-PRESSED
046600         MOVE 1 TO RPT-PFKEY
046700     ELSE
046800         PERFORM VAL-DATA-ENTRY
046900         IF SCREEN-ENTRY-ERROR
047000             GO TO GET-THE-DATA
047100         ELSE
047200         PERFORM RELOAD-DEFAULTS
047300         IF ENTER-KEY-PRESSED
047400             MOVE 11 TO RPT-PFKEY
047500         ELSE
047600             MOVE PFKEY-CODE TO RPT-PFKEY.
047700
047800 VAL-DATA-ENTRY.
047900     PERFORM VAL-ONE-LINE
048000         VARYING VAL-IDX FROM 1 BY 1
048100         UNTIL SCREEN-ENTRY-ERROR  OR
048200               VAL-IDX > 5.
048300
048400 VAL-ONE-LINE.
048500     IF NAME-FIELD (VAL-IDX) NOT = SPACES
048600         MOVE ID-FIELD (VAL-IDX) TO POSITION-NAME-IN
048700         PERFORM POSITION-THE-NAME
048800         MOVE POSITION-NAME-OUT TO ID-FIELD (VAL-IDX)
048900         PERFORM VAL-ID
049000         IF NOT SCREEN-ENTRY-ERROR
049100             PERFORM VAL-LEVEL
049200             IF NOT SCREEN-ENTRY-ERROR
049300                 PERFORM VAL-PAGE
049400                 IF NOT SCREEN-ENTRY-ERROR
049500                     PERFORM VAL-PRINT.
049600
049700 VAL-ID.
049800     PERFORM IS-IT-THERE.
049900     IF SCREEN-ENTRY-ERROR
050000         MOVE "* Error *  Must be a selected field"
050100             TO UPPER-MSG-FIELD.
050200     IF NOT SCREEN-ENTRY-ERROR  AND
050300        VAL-IDX NOT = 1
050400         PERFORM LOOK-FOR-DUPLICATE
050500             VARYING DUP-IDX FROM 1 BY 1
050600             UNTIL ID-FIELD (VAL-IDX) = ID-FIELD (DUP-IDX)  OR
050700                   DUP-IDX = VAL-IDX
050800         IF DUP-IDX NOT = VAL-IDX
050900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
051000             MOVE "* Error * Cannot duplicate field names"
051100                 TO UPPER-MSG-FIELD.
051200
051300     IF SCREEN-ENTRY-ERROR
051400         IF VAL-IDX = 1
051500             SET ERROR-BIT IN FAC OF CTLFLD-ID-1-ITEM ON
051600         ELSE
051700         IF VAL-IDX = 2
051800             SET ERROR-BIT IN FAC OF CTLFLD-ID-2-ITEM ON
051900         ELSE
052000         IF VAL-IDX = 3
052100             SET ERROR-BIT IN FAC OF CTLFLD-ID-3-ITEM ON
052200         ELSE
052300         IF VAL-IDX = 4
052400             SET ERROR-BIT IN FAC OF CTLFLD-ID-4-ITEM ON
052500         ELSE
052600         IF VAL-IDX = 5
052700             SET ERROR-BIT IN FAC OF CTLFLD-ID-5-ITEM ON.
052800
052900 LOOK-FOR-DUPLICATE.
053000     EXIT.
053100
053200 IS-IT-THERE.
053300     MOVE "N" TO ITS-A-MATCH.
053400     IF ID-FIELD (VAL-IDX) NOT = SPACES
053500         PERFORM CHECK-THE-LIST
053600             VARYING RFL-IDX FROM 1 BY 1
053700             UNTIL ITS-A-MATCH = "Y"  OR
053800                   RFL-IDX > 80
053900         IF ITS-A-MATCH = "N"
054000             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
054100
054200 CHECK-THE-LIST.
054300     IF OCCURS-FIELD (VAL-IDX) IS NUMERIC  AND
054400        OCCURS-FIELD (VAL-IDX) > 0
054500         IF RFL-NAME (RFL-IDX) = NAME-FIELD (VAL-IDX)  AND
054600            RFL-OCCURRENCE-A (RFL-IDX)
054700                 = OCCURS-FIELD (VAL-IDX)
054800             MOVE RFL-IDX TO SAVE-RFL-IDX (VAL-IDX)
054900             MOVE RFL-ORIGIN (RFL-IDX) TO ORIGIN-FIELD (VAL-IDX)
055000             MOVE "Y" TO ITS-A-MATCH
055100         ELSE
055200             NEXT SENTENCE
055300     ELSE
055400     IF OCCURS-FIELD (VAL-IDX) = 0  OR
055500        OCCURS-FIELD (VAL-IDX) = SPACES
055600         IF RFL-NAME (RFL-IDX) = NAME-FIELD (VAL-IDX)  OR
055700            CTL-FIELDS-AKA (RFL-IDX) = NAME-FIELD (VAL-IDX)
055800             MOVE RFL-IDX TO SAVE-RFL-IDX (VAL-IDX)
055900             MOVE RFL-ORIGIN (RFL-IDX) TO ORIGIN-FIELD (VAL-IDX)
056000             MOVE "Y" TO ITS-A-MATCH.
056100
056200 VAL-LEVEL.
056300     IF ID-FIELD (VAL-IDX) = SPACES
056400         MOVE 0 TO LEVEL-FIELD (VAL-IDX)
056500     ELSE
056600         IF LEVEL-FIELD (VAL-IDX)  < 1  OR
056700            LEVEL-FIELD (VAL-IDX)  > 5
056800             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
056900             MOVE "* Error *  Must be a value between 1 and 5"
057000                 TO UPPER-MSG-FIELD.
057100
057200     IF SCREEN-ENTRY-ERROR
057300         IF VAL-IDX = 1
057400             SET ERROR-BIT IN FAC OF CTLFLD-LEVEL-1-ITEM ON
057500         ELSE
057600         IF VAL-IDX = 2
057700             SET ERROR-BIT IN FAC OF CTLFLD-LEVEL-2-ITEM ON
057800         ELSE
057900         IF VAL-IDX = 3
058000             SET ERROR-BIT IN FAC OF CTLFLD-LEVEL-3-ITEM ON
058100         ELSE
058200         IF VAL-IDX = 4
058300             SET ERROR-BIT IN FAC OF CTLFLD-LEVEL-4-ITEM ON
058400         ELSE
058500         IF VAL-IDX = 5
058600             SET ERROR-BIT IN FAC OF CTLFLD-LEVEL-5-ITEM ON.
058700
058800 VAL-PAGE.
058900     IF ID-FIELD (VAL-IDX) = SPACES
059000         MOVE "NO" TO PAGE-FIELD (VAL-IDX)
059100     ELSE
059200         IF PAGE-FIELD (VAL-IDX)  NOT = "YES"  AND
059300            PAGE-FIELD (VAL-IDX)  NOT = "NO "
059400             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
059500             MOVE "* Error *  Must be 'YES' or 'NO'"
059600                 TO UPPER-MSG-FIELD.
059700
059800     IF SCREEN-ENTRY-ERROR
059900         IF VAL-IDX = 1
060000             SET ERROR-BIT IN FAC OF CTLFLD-PAGE-1-ITEM ON
060100         ELSE
060200         IF VAL-IDX = 2
060300             SET ERROR-BIT IN FAC OF CTLFLD-PAGE-2-ITEM ON
060400         ELSE
060500         IF VAL-IDX = 3
060600             SET ERROR-BIT IN FAC OF CTLFLD-PAGE-3-ITEM ON
060700         ELSE
060800         IF VAL-IDX = 4
060900             SET ERROR-BIT IN FAC OF CTLFLD-PAGE-4-ITEM ON
061000         ELSE
061100         IF VAL-IDX = 5
061200             SET ERROR-BIT IN FAC OF CTLFLD-PAGE-5-ITEM ON.
061300
061400 VAL-PRINT.
061500     IF ID-FIELD (VAL-IDX) = SPACES
061600         MOVE "NO" TO PRINT-FIELD (VAL-IDX)
061700     ELSE
061800         IF PRINT-FIELD (VAL-IDX)  NOT = "YES"  AND
061900            PRINT-FIELD (VAL-IDX)  NOT = "NO "
062000             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
062100             MOVE "* Error *  Must be 'YES' or 'NO'"
062200                 TO UPPER-MSG-FIELD.
062300
062400     IF SCREEN-ENTRY-ERROR
062500         IF VAL-IDX = 1
062600             SET ERROR-BIT IN FAC OF CTLFLD-PRINT-1-ITEM ON
062700         ELSE
062800         IF VAL-IDX = 2
062900             SET ERROR-BIT IN FAC OF CTLFLD-PRINT-2-ITEM ON
063000         ELSE
063100         IF VAL-IDX = 3
063200             SET ERROR-BIT IN FAC OF CTLFLD-PRINT-3-ITEM ON
063300         ELSE
063400         IF VAL-IDX = 4
063500             SET ERROR-BIT IN FAC OF CTLFLD-PRINT-4-ITEM ON
063600         ELSE
063700         IF VAL-IDX = 5
063800             SET ERROR-BIT IN FAC OF CTLFLD-PRINT-5-ITEM ON.
063900
064000 JUST-THE-FACS.
064100     MOVE UPPER-ENTRY-FAC TO
064200             FAC OF CTLFLD-ID-1-ITEM
064300             FAC OF CTLFLD-ID-2-ITEM
064400             FAC OF CTLFLD-ID-3-ITEM
064500             FAC OF CTLFLD-ID-4-ITEM
064600             FAC OF CTLFLD-ID-5-ITEM
064700             FAC OF CTLFLD-PAGE-1-ITEM
064800             FAC OF CTLFLD-PAGE-2-ITEM
064900             FAC OF CTLFLD-PAGE-3-ITEM
065000             FAC OF CTLFLD-PAGE-4-ITEM
065100             FAC OF CTLFLD-PAGE-5-ITEM
065200             FAC OF CTLFLD-PRINT-1-ITEM
065300             FAC OF CTLFLD-PRINT-2-ITEM
065400             FAC OF CTLFLD-PRINT-3-ITEM
065500             FAC OF CTLFLD-PRINT-4-ITEM
065600             FAC OF CTLFLD-PRINT-5-ITEM.
065700
065800     MOVE NUMERIC-ENTRY-FAC TO
065900             FAC OF CTLFLD-LEVEL-1-ITEM
066000             FAC OF CTLFLD-LEVEL-2-ITEM
066100             FAC OF CTLFLD-LEVEL-3-ITEM
066200             FAC OF CTLFLD-LEVEL-4-ITEM
066300             FAC OF CTLFLD-LEVEL-5-ITEM
066400             FAC OF CTLFLD-SKIP-1-ITEM
066500             FAC OF CTLFLD-SKIP-2-ITEM
066600             FAC OF CTLFLD-SKIP-3-ITEM
066700             FAC OF CTLFLD-SKIP-4-ITEM
066800             FAC OF CTLFLD-SKIP-5-ITEM.
066900
067000 POSITION-THE-NAME.
067100     MOVE 0 TO LEFT-PAREN-COUNT.
067200     INSPECT POSITION-NAME-IN
067300         TALLYING LEFT-PAREN-COUNT
067400         FOR ALL "(".
067500
067600     MOVE 0 TO RIGHT-PAREN-COUNT.
067700     INSPECT POSITION-NAME-IN
067800         TALLYING RIGHT-PAREN-COUNT
067900         FOR ALL ")".
068000
068100     IF LEFT-PAREN-COUNT NOT = RIGHT-PAREN-COUNT
068200         MOVE POSITION-NAME-IN TO POSITION-NAME-OUT
068300     ELSE
068400     IF LEFT-PAREN-COUNT NOT = 1
068500         MOVE POSITION-NAME-IN TO POSITION-NAME-OUT
068600     ELSE
068700         MOVE SPACES TO POSITION-NAME-OUT
068800         MOVE 1 TO PAREN-LOCATION
068900         PERFORM LOCATE-THE-LEFT-PAREN
069000         MOVE 1 TO POS-POINTER
069100         PERFORM MOVE-IN-NAME-TO-OUT
069200         PERFORM MOVE-IN-OCCURS-TO-OUT.
069300
069400 LOCATE-THE-LEFT-PAREN.
069500     IF POS-IN-CHAR (PAREN-LOCATION) NOT = "("
069600         ADD 1 TO PAREN-LOCATION
069700         IF PAREN-LOCATION NOT > 12
069800             GO TO LOCATE-THE-LEFT-PAREN.
069900 MOVE-IN-NAME-TO-OUT.
070000     IF POS-POINTER < PAREN-LOCATION
070100         MOVE POS-IN-CHAR (POS-POINTER) TO
070200                         POS-OUT-CHAR (POS-POINTER)
070300     ELSE
070400         MOVE SPACE TO POS-OUT-CHAR (POS-POINTER).
070500     ADD 1 TO POS-POINTER.
070600     IF POS-POINTER NOT > 8
070700         GO TO MOVE-IN-NAME-TO-OUT.
070800 MOVE-IN-OCCURS-TO-OUT.
070900     IF PAREN-LOCATION > 12
071000         MOVE SPACES TO POS-OUT-OCCURS
071100     ELSE
071200         ADD 1 TO PAREN-LOCATION
071300         PERFORM POSITION-OCCURS-1-OUT.
071400     IF POS-OUT-OCCURS NOT = SPACES
071500         ADD 1 TO PAREN-LOCATION
071600         PERFORM POSITION-OCCURS-2-OUT.
071700     IF POS-OUT-OCCURS NOT = SPACES
071800         MOVE "(" TO POS-OUT-LT-PAREN
071900         MOVE ")" TO POS-OUT-RT-PAREN.
072000 POSITION-OCCURS-1-OUT.
072100     IF POS-IN-CHAR (PAREN-LOCATION) < 0  OR
072200        POS-IN-CHAR (PAREN-LOCATION) > 9
072300         MOVE SPACES TO POS-OUT-OCCURS.
072400     IF POS-IN-CHAR (PAREN-LOCATION) NOT < 0  AND
072500        POS-IN-CHAR (PAREN-LOCATION) NOT > 9
072600         MOVE POS-IN-CHAR (PAREN-LOCATION) TO
072700                 POS-OUT-OCC-1.
072800 POSITION-OCCURS-2-OUT.
072900     IF POS-IN-CHAR (PAREN-LOCATION) = ")"
073000         IF POS-OUT-OCC-1 = 0
073100             MOVE SPACES TO POS-OUT-OCCURS
073200         ELSE
073300             MOVE POS-OUT-OCC-1 TO POS-OUT-OCC-2
073400             MOVE 0 TO POS-OUT-OCC-1
073500     ELSE
073600     IF POS-IN-CHAR (PAREN-LOCATION) NOT > 9  AND
073700        POS-IN-CHAR (PAREN-LOCATION) NOT < 0
073800         MOVE POS-IN-CHAR (PAREN-LOCATION) TO POS-OUT-OCC-2
073900     ELSE
074000         MOVE SPACES TO POS-OUT-OCCURS.
074100
074200     IF POS-OUT-OCC-1 = 0  AND
074300        POS-OUT-OCC-2 = 0
074400         MOVE SPACES TO POS-OUT-OCCURS.
074500
074600
000000     COPY PLKTRACE.
