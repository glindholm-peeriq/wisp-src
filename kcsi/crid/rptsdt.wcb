000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTSDT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/12/89.
000500 DATE-COMPILED.
000600*----------------------------------------------------------------
000700* DEFINES THE SECONDARY DATA FILE:  NAME    LIBRARY  VOLUME FILEKE
000800*----------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptsdt.wcb 1.6 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002610     COPY LKVALNAM.
002620     COPY WSCONST.
002700
002800 01  SECOND-FILE-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(29) ROW 5 COLUMN 26 VALUE
003100         "Secondary Data File Selection".
003200       05  UPPER-MSG-ITEM
003300           PIC X(78) ROW 7 COLUMN 03
003400           SOURCE  UPPER-MSG-FIELD.
003500       05  FILLER
003600         PIC X(64) ROW 8 COLUMN 8 VALUE
003700         "Specify the name of the secondary file to be used in the
003800-        " report,".
003900     05  FILLER
004000         PIC X(63) ROW 9 COLUMN 8 VALUE
004100         "and specify the field in the primary file to be used as
004200-        "the key".
004300     05  FILLER
004400         PIC X(22) ROW 10 COLUMN 30 VALUE
004500         "to the secondary file.".
004600     05  FILLER
004700         PIC X(16) ROW 13 COLUMN 2 VALUE
004800         "Secondary File =".
004900     05  SECOND-FILE-ID-ITEM
005000         PIC X(8) ROW 13 COLUMN 19
005100         SOURCE SECOND-FILE-ID-FIELD
005200         OBJECT SECOND-FILE-ID-FIELD.
005300     05  FILLER
005400         PIC X(12) ROW 13 COLUMN 30 VALUE
005500         "in Library =".
005600     05  SECOND-FILE-LIB-ITEM
005700         PIC X(8) ROW 13 COLUMN 43
005800         SOURCE SECOND-FILE-LIB-FIELD
005900         OBJECT SECOND-FILE-LIB-FIELD.
006000     05  FILLER
006100         PIC X(11) ROW 13 COLUMN 55 VALUE
006200         "on Volume =".
006300     05  SECOND-FILE-VOL-ITEM
006400         PIC X(6) ROW 13 COLUMN 67
006500         SOURCE SECOND-FILE-VOL-FIELD
006600         OBJECT SECOND-FILE-VOL-FIELD.
006700     05  FILLER
006800         PIC X(13) ROW 15 COLUMN 4 VALUE
006900         "Key to File =".
007000     05  SECOND-FILE-KEY-ITEM
007100         PIC X(12) ROW 15 COLUMN 19
007200         SOURCE SECOND-FILE-KEY-FIELD
007300         OBJECT SECOND-FILE-KEY-FIELD.
007400     05  FILLER
007500         PIC X(33) ROW 19 COLUMN 24 VALUE
007600         "**  Press (ENTER) to Continue  **".
007700
007800 01  SECOND-FILE-SELECTION-FIELDS.
007900     05  UPPER-MSG-FIELD                   PIC X(78) VALUE SPACE.
008000     05  SECOND-FILE-ID-FIELD              PIC X(8) VALUE SPACE.
008100     05  SECOND-FILE-LIB-FIELD             PIC X(8) VALUE SPACE.
008200     05  SECOND-FILE-VOL-FIELD             PIC X(6) VALUE SPACE.
008300     05  SECOND-FILE-KEY-FIELD.
008400         07  ID-FIELD                      PIC X(8) VALUE SPACE.
008500         07  FILLER                        PIC X(1) VALUE SPACE.
008600         07  OCCURS-FIELD                  PIC X(2) VALUE SPACE.
008700         07  FILLER                        PIC X(1) VALUE SPACE.
008800
008900 01  MESSAGE-LITERALS.
009000     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
009100         "* Error * File Name cannot be blank".
009200     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
009300         "* Error * Invalid File Name".
009400     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
009500         "* Error * Invalid Library Name".
009600     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
009700         "* Error * Invalid Volume Name".
009800     05  KEY-ID-BLANK-LITERAL    PIC X(78) VALUE
009900         "* Error * Key field cannot be blank".
010000     05  INVALID-DATA-FLD-LITERAL.
010100         07  FILLER          PIC X(10) VALUE "* Error * ".
010200         07  DATA-FLD-NAME   PIC X(12) VALUE SPACES.
010300         07  FILLER          PIC X(56) VALUE
010400           " is not a field in the primary file".
010500
000000 01  CIDX          PIC 9(5) VALUE ZEROES.
011100 01  KEY-FLAG                     PIC X(01) VALUE "N".
011200         88  KEY-FOUND                VALUE "Y".
011300
000000 LINKAGE SECTION.
014400     COPY WSRPTCFL.
014500     COPY WSRPTREC.
014600
014700 PROCEDURE DIVISION USING
014800     RPT-CTL-FIELDS
014900     RPT-RECORDS.
015000
015100 MAIN-LOGIC SECTION.
015200 PROGRAM-BEGIN.
000000     MOVE "RPTSDT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
015300     PERFORM OPENING-PROCEDURE.
015400     PERFORM MAIN-PROCESS.
015500     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTSDT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
015600 MAIN-LOGIC-EXIT.
015700     EXIT PROGRAM.
015800 MAIN-LOGIC-STOP.
015900     STOP RUN.
016000 LEVEL-2 SECTION.
016100 OPENING-PROCEDURE.
016200     OPEN I-O  CRT-FILE.
016300 CLOSING-PROCEDURE.
016400     CLOSE CRT-FILE.
016500
016600 MAIN-PROCESS.
016700       MOVE UPPER-ENTRY-FAC TO FAC OF SECOND-FILE-ID-ITEM
016800                               FAC OF SECOND-FILE-LIB-ITEM
016900                               FAC OF SECOND-FILE-KEY-ITEM
017000                               FAC OF SECOND-FILE-VOL-ITEM.
017100     MOVE DISPLAY-HI-FAC  TO FAC OF UPPER-MSG-ITEM.
017200
017300     PERFORM NAME-THE-SECONDARY-FILE.
017400
017500       MOVE SECOND-FILE-ID-FIELD TO
017600             RDF-SEC-FILE
017700             RCF-SEC-FILE.
017800       MOVE SECOND-FILE-LIB-FIELD TO RDF-SEC-LIBRARY.
017900       MOVE SECOND-FILE-VOL-FIELD TO RDF-SEC-VOLUME.
018000       MOVE ID-FIELD TO RDF-KEY-TO-SEC.
018100       IF OCCURS-FIELD IS NUMERIC
018200           MOVE OCCURS-FIELD TO RDF-KEY-OCCURRENCE-A
018300       ELSE
018400           MOVE SPACES TO RDF-KEY-OCCURRENCE-A.
018500
018600 NAME-THE-SECONDARY-FILE.
018700
018800     MOVE SPACES TO SCREEN-ERROR-STATUS.
018900
019000       DISPLAY AND READ ALTERED SECOND-FILE-SCREEN ON CRT-FILE.
019100
019200     MOVE SPACE TO UPPER-MSG-FIELD.
019300
019400       PERFORM VAL-FILE-ID.
019500       IF NOT SCREEN-ENTRY-ERROR  AND
019600          SECOND-FILE-LIB-FIELD NOT = SPACES
019700           PERFORM  VAL-LIB-ID
019800           IF NOT SCREEN-ENTRY-ERROR  AND
019900              SECOND-FILE-VOL-FIELD NOT = SPACES
020000               PERFORM VAL-VOL-ID
020100               IF NOT SCREEN-ENTRY-ERROR
020200                   PERFORM VAL-DATA-NAME.
020300
020400       IF SCREEN-ENTRY-ERROR
020500           GO TO NAME-THE-SECONDARY-FILE.
020600       CALL "BLNKSC".
020700 VAL-FILE-ID.
020800
020900       IF SECOND-FILE-ID-FIELD = SPACES
021000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
021100         SET ERROR-BIT IN FAC OF SECOND-FILE-ID-ITEM ON
021200         MOVE FILE-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
021300
021400     IF NOT SCREEN-ENTRY-ERROR
021500           MOVE SECOND-FILE-ID-FIELD TO VAL-NAME
021600         PERFORM TEST-THE-NAME
021700         IF SCREEN-ENTRY-ERROR
021800             MOVE INVALID-FILE-LITERAL TO UPPER-MSG-FIELD
021900               SET ERROR-BIT IN FAC OF SECOND-FILE-ID-ITEM ON.
022000
022100 VAL-LIB-ID.
022200
022300       MOVE SECOND-FILE-LIB-FIELD TO VAL-NAME.
022400     PERFORM TEST-THE-NAME.
022500     IF SCREEN-ENTRY-ERROR
022600         MOVE INVALID-LIB-LITERAL TO UPPER-MSG-FIELD
022700           SET ERROR-BIT IN FAC OF SECOND-FILE-LIB-ITEM ON.
022800
022900 VAL-VOL-ID.
023000
023100     MOVE SECOND-FILE-VOL-FIELD TO VAL-NAME.
023200     PERFORM TEST-THE-VOL.
023300     IF SCREEN-ENTRY-ERROR
023400         MOVE INVALID-VOL-LITERAL TO UPPER-MSG-FIELD
023500           SET ERROR-BIT IN FAC OF SECOND-FILE-VOL-ITEM ON.
023600
023700 TEST-THE-NAME.
000000     CALL "VALNAM" USING VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
023800
023700 TEST-THE-VOL.
000000     CALL "VALVOL" USING VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
023800
027500 VAL-DATA-NAME.
027600       IF SECOND-FILE-KEY-FIELD = SPACES
027700           MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
027800           SET ERROR-BIT IN FAC OF SECOND-FILE-KEY-ITEM ON
027900           MOVE KEY-ID-BLANK-LITERAL TO UPPER-MSG-FIELD.
028000
028100       IF NOT SCREEN-ENTRY-ERROR
028200             MOVE "N" TO KEY-FLAG
028300             PERFORM CHECK-IF-THE-FIELD-IS-THERE
028400               VARYING CIDX FROM 1 BY 1
028500               UNTIL KEY-FOUND  OR
028600                     CIDX > CONST-RPT-CTL-FIELDS  OR
028600                     CIDX > CONST-CF-MAX-ENTRY  OR
028700                     CTL-ID-FIELD (CIDX) = SPACE.
028800
028900     IF NOT SCREEN-ENTRY-ERROR
029000     IF NOT KEY-FOUND
029100           MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
029200           SET ERROR-BIT IN FAC OF SECOND-FILE-KEY-ITEM ON
029300           MOVE CTL-ID-FIELD (CIDX) TO DATA-FLD-NAME
029400           MOVE INVALID-DATA-FLD-LITERAL TO UPPER-MSG-FIELD.
029500
029600 CHECK-IF-THE-FIELD-IS-THERE.
029700     IF CTL-FIELD-NAME (CIDX) = ID-FIELD  AND
029800        OCCURS-OR-NOT (CIDX) = OCCURS-FIELD
029900         MOVE "Y" TO KEY-FLAG.
030000
030100*     COPY PLCLRCRT.
000000     COPY PLCRT.
000000
000000     COPY PLKTRACE.
