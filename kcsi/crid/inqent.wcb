000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQENT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/27/90.
000500 DATE-COMPILED.
000600*----------------------------------------------------------------
000700* HELP SCREENS FOR INQUIRY
000800*----------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqent.wcb 1.9 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002700
002800 01  QUERY-SCREEN USAGE IS DISPLAY-WS.
002900     05  QUERY-1-ITEM            PIC X(79)  ROW 01 COLUMN 02
003000         SOURCE  QUERY-1-FIELD.
003100     05  QUERY-2-ITEM            PIC X(79)  ROW 02 COLUMN 02
003200         SOURCE  QUERY-2-FIELD.
003300     05  QUERY-3-ITEM            PIC X(79)  ROW 03 COLUMN 02
003400         SOURCE  QUERY-3-FIELD.
003500     05  QUERY-4-ITEM            PIC X(79)  ROW 04 COLUMN 02
003600         SOURCE  QUERY-4-FIELD.
003700     05  QUERY-5-ITEM            PIC X(79)  ROW 05 COLUMN 02
003800         SOURCE  QUERY-5-FIELD.
003900     05  QUERY-6-ITEM            PIC X(79)  ROW 06 COLUMN 02
004000         SOURCE  QUERY-6-FIELD.
004100     05  QUERY-7-ITEM            PIC X(79)  ROW 07 COLUMN 02
004200         SOURCE  QUERY-7-FIELD.
004300     05  QUERY-8-ITEM            PIC X(79)  ROW 08 COLUMN 02
004400         SOURCE  QUERY-8-FIELD.
004500     05  QUERY-9-ITEM            PIC X(79)  ROW 09 COLUMN 02
004600         SOURCE  QUERY-9-FIELD.
004700     05  QUERY-10-ITEM           PIC X(79)  ROW 10 COLUMN 02
004800         SOURCE  QUERY-10-FIELD.
004900     05  QUERY-11-ITEM           PIC X(79)  ROW 11 COLUMN 02
005000         SOURCE  QUERY-11-FIELD.
005100     05  QUERY-12-ITEM           PIC X(79)  ROW 12 COLUMN 02
005200         SOURCE  QUERY-12-FIELD
005300         OBJECT  QUERY-12-FIELD.
005400     05  QUERY-13-ITEM           PIC X(79)  ROW 13 COLUMN 02
005500         SOURCE  QUERY-13-FIELD
005600         OBJECT  QUERY-13-FIELD.
005700     05  QUERY-14-ITEM           PIC X(79)  ROW 14 COLUMN 02
005800         SOURCE  QUERY-14-FIELD
005900         OBJECT  QUERY-14-FIELD.
006000     05  QUERY-15-ITEM           PIC X(79)  ROW 15 COLUMN 02
006100         SOURCE  QUERY-15-FIELD
006200         OBJECT  QUERY-15-FIELD.
006300     05  QUERY-16-ITEM           PIC X(79)  ROW 16 COLUMN 02
006400         SOURCE  QUERY-16-FIELD
006500         OBJECT  QUERY-16-FIELD.
006600     05  QUERY-17-ITEM           PIC X(79)  ROW 17 COLUMN 02
006700         SOURCE  QUERY-17-FIELD
006800         OBJECT  QUERY-17-FIELD.
006900     05  QUERY-18-ITEM           PIC X(79)  ROW 18 COLUMN 02
007000         SOURCE  QUERY-18-FIELD
007100         OBJECT  QUERY-18-FIELD.
007200
007300     05  FILLER                  PIC X(35)  ROW 20 COLUMN 02
007400         VALUE   "TITLE FOR QUERY LISTING (OPTIONAL):".
007500     05  QUERY-TITLE-ITEM        PIC X(40)  ROW 20 COLUMN 40
007600         SOURCE  QUERY-TITLE-FIELD
007700         OBJECT  QUERY-TITLE-FIELD.
007800
007900     05  MSG-1-ITEM              PIC X(79)  ROW 22 COLUMN 02
008000         SOURCE  MSG-1-FIELD.
008100     05  MSG-2-ITEM              PIC X(79)  ROW 23 COLUMN 02
008200         SOURCE  MSG-2-FIELD.
008300     05  MSG-3-ITEM              PIC X(79)  ROW 24 COLUMN 02
008400         SOURCE  MSG-3-FIELD.
008500
008600 01  QUERY-FIELDS.
008700     03  Q-FIELDS.
008800         05  QUERY-1-FIELD       PIC X(79) VALUE SPACES.
008900         05  QUERY-2-FIELD       PIC X(79) VALUE SPACES.
009000         05  QUERY-3-FIELD       PIC X(79) VALUE SPACES.
009100         05  QUERY-4-FIELD       PIC X(79) VALUE SPACES.
009200         05  QUERY-5-FIELD       PIC X(79) VALUE SPACES.
009300         05  QUERY-6-FIELD       PIC X(79) VALUE SPACES.
009400         05  QUERY-7-FIELD       PIC X(79) VALUE SPACES.
009500         05  QUERY-8-FIELD       PIC X(79) VALUE SPACES.
009600         05  QUERY-9-FIELD       PIC X(79) VALUE SPACES.
009700         05  QUERY-10-FIELD      PIC X(79) VALUE SPACES.
009800         05  QUERY-11-FIELD      PIC X(79) VALUE SPACES.
009900         05  QUERY-12-FIELD      PIC X(79) VALUE SPACES.
010000         05  QUERY-13-FIELD      PIC X(79) VALUE SPACES.
010100         05  QUERY-14-FIELD      PIC X(79) VALUE SPACES.
010200         05  QUERY-15-FIELD      PIC X(79) VALUE SPACES.
010300         05  QUERY-16-FIELD      PIC X(79) VALUE SPACES.
010400         05  QUERY-17-FIELD      PIC X(79) VALUE SPACES.
010500         05  QUERY-18-FIELD      PIC X(79) VALUE SPACES.
010600         05  QUERY-TITLE-FIELD   PIC X(40) VALUE SPACES.
010700     03  FILLER REDEFINES Q-FIELDS.
010800         05  DISPLAY-FIELDS      PIC X(79) OCCURS 4 TIMES.
010900         05  FMT-SCREEN          PIC X(553).
000000         05  FILLER REDEFINES FMT-SCREEN.
011000             07  FMT-FIELDS      PIC X(79) OCCURS 7 TIMES.
011100         05  USER-SCREEN         PIC X(553).
000000         05  FILLER REDEFINES USER-SCREEN.
011200             07  USER-FIELDS     PIC X(79) OCCURS 7 TIMES.
011300         05  USER-TITLE          PIC X(40).
011400     03  MSG-TEXTS.
011500         05  MSG-1-FIELD         PIC X(79) VALUE SPACES.
011600         05  MSG-2-FIELD         PIC X(79) VALUE SPACES.
011700         05  MSG-3-FIELD         PIC X(79) VALUE SPACES.
011800
011900 01  FIRST-IN-FLAG               PIC X(01) VALUE "Y".
012000 01  PASS-RC                     PIC 9(02) VALUE 0.
012100 01  PASS-MESSAGE                PIC X(79) VALUE SPACES.
012200
012300 01  FMT-ABSENT-MSG-TEXT.
012400     05  FMT-ABSENT-NAME         PIC X(31).
012500     05  FILLER                  PIC X(40) VALUE
012600         " is not defined in the Control file.".
012700
012800 01  FMT-OCCURS-MSG-TEXT.
012900     05  FMT-OCCURS-NAME         PIC X(08).
013000     05  FILLER                  PIC X(13) VALUE
013100         " occurs only ".
013200     05  FMT-OCCURS-NO           PIC X(02).
013300     05  FILLER                  PIC X(56) VALUE
013400         "times.".
013500
013600 01  FMT-UPDATE-MSG-TEXT.
013700     05  FMT-UPDATE-NAME         PIC X(08).
013800     05  FILLER                  PIC X(71) VALUE
013900         " is not a reportable field.".
014000
014100 01  WS-LIN-LEN                  PIC 9(03) VALUE 0.
014100 01  WS-WRK-LEN                  PIC 9(03) VALUE 0.
014200 01  WS-FLD-SEQ                  PIC 9(02) VALUE 0.
014300
000000     COPY LKPRC.
000000
014400 LINKAGE SECTION.
014500 01  PASS-DISPLAY            PIC X(03).
014600     COPY WSRPTREC.
014700 01  P-FMT-QUERY-SCREEN.
014800     05  P-FMT-QUERY-LINES.
014900         07  P-FMT-QUERY-LINE    PIC X(79) OCCURS 7 TIMES.
015000     05  P-FMT-QUERY-TITLE   PIC X(40).
015100 01  PASSING-PFKEY           PIC 9(02).
015200 01  CTRL-IO-BLOCK.
015300     COPY LKDIO.
015400 01  CTRL-HEADER-RECORDS.
015500     COPY RCCTRLH.
015600 01  CF-RECORD.
015700     COPY RCCTRLF.
015800     COPY LKCTRLAR.
015900     COPY RCCTRLT1.
016000     COPY RCCTRLT2.
016100
016200 PROCEDURE DIVISION USING
016300     PASS-DISPLAY
016400     RPT-RECORDS
016500     P-FMT-QUERY-SCREEN
016600     PASSING-PFKEY
016700     COPY LKCTRL.
016800
016900 MAIN-LOGIC SECTION.
017000 PROGRAM-BEGIN.
000000     MOVE "INQENT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
017100     PERFORM PROGRAM-INIT.
017200     PERFORM OPENING-PROCEDURE.
017300     PERFORM MAIN-PROCESS.
017400     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQENT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
017500 MAIN-LOGIC-EXIT.
017600     EXIT PROGRAM.
017700 MAIN-LOGIC-STOP.
017800     STOP RUN.
017900 LEVEL-2 SECTION.
018000 PROGRAM-INIT.
018100     MOVE "Y" TO FIRST-IN-FLAG.
018200*     MOVE SPACES TO QUERY-FIELDS.
018300     MOVE SPACES TO PASS-MESSAGE.
018400 OPENING-PROCEDURE.
018500     PERFORM OPEN-CRT-FILE.
018600 CLOSING-PROCEDURE.
018700     PERFORM CLOSE-CRT-FILE.
018800
018900 MAIN-PROCESS.
000000     MOVE LOW-VALUES TO CRT-POS-COL-ROW.
019000     IF PASS-DISPLAY = "YES"
019100         MOVE P-FMT-QUERY-LINES TO USER-SCREEN
019200                                   FMT-SCREEN
019300         MOVE P-FMT-QUERY-TITLE TO USER-TITLE
019400         PERFORM ENTER-THE-QUERY
019500         PERFORM VAL-THE-QUERY
019600         IF SCREEN-ENTRY-ERROR
019700             MOVE SPACES TO FMT-SCREEN
019800             MOVE "Y" TO FIRST-IN-FLAG
019900             PERFORM INIT-FIRST-IN
020000             PERFORM GET-THE-QUERY
020100         ELSE
020200             MOVE "N" TO FIRST-IN-FLAG
020300             PERFORM INIT-NOT-FIRST-IN
020400             PERFORM GET-THE-QUERY.
020500
020600     IF PASS-DISPLAY = SPACES
020700         PERFORM INIT-FIRST-IN
020800         PERFORM GET-THE-QUERY.
020900
021000     IF PASS-DISPLAY = "NO "
021100         MOVE P-FMT-QUERY-LINES TO FMT-SCREEN
021200                                   USER-SCREEN
021300         MOVE P-FMT-QUERY-TITLE TO USER-TITLE
021400         PERFORM ENTER-THE-QUERY
021500         PERFORM VAL-THE-QUERY
021600         IF SCREEN-ENTRY-ERROR
021700             MOVE SPACES TO FMT-SCREEN
021800             MOVE "Y" TO FIRST-IN-FLAG
021900             PERFORM INIT-FIRST-IN
022000             PERFORM GET-THE-QUERY.
022100
022200 GET-THE-QUERY.
022300
022400     MOVE SPACES TO SCREEN-ERROR-STATUS.
000000     MOVE CRT-POS-OA TO ORDER-AREA OF QUERY-SCREEN.
022500
022600     DISPLAY AND READ ALTERED QUERY-SCREEN  ON CRT-FILE
022700     PFKEYS 1, 2, 14, 15, 16.
022800
022900     MOVE SPACES TO PASS-MESSAGE.
000000     PERFORM SAVE-CURSOR-POS.
023000
023100     IF ENTER-KEY-PRESSED
023200         PERFORM ENTER-THE-QUERY
023300         PERFORM VAL-THE-QUERY
023400         IF SCREEN-ENTRY-ERROR
023500             MOVE SPACES TO FMT-SCREEN
023600             PERFORM INIT-FIRST-IN
023700             MOVE "Y" TO FIRST-IN-FLAG
023800             GO TO GET-THE-QUERY
023900         ELSE
024000             PERFORM INIT-NOT-FIRST-IN
024100             MOVE "N" TO FIRST-IN-FLAG
024200             GO TO GET-THE-QUERY.
024300
024400     IF PFKEY-1-PRESSED
024500         MOVE 1 TO PASSING-PFKEY.
024600
024700     IF PFKEY-2-PRESSED
024800         PERFORM SET-UPLOW-ON
024900         GO TO GET-THE-QUERY.
025000
025100     IF PFKEY-14-PRESSED
025200         PERFORM CLOSING-PROCEDURE
025300         CALL "INQHLP"
025400         PERFORM OPENING-PROCEDURE
025500         GO TO GET-THE-QUERY.
025600
025700     IF PFKEY-15-PRESSED
025800         PERFORM CLOSING-PROCEDURE
025900         CALL "INQNAM" USING
026000           CTRL-IO-BLOCK
026100           CTRL-HEADER-RECORDS
026200           CF-RECORD
026300           CF-VALUES
026400           CF-T1-TABLE
026500           CF-T2-TABLE
026600         PERFORM OPENING-PROCEDURE
026700         GO TO GET-THE-QUERY.
026800
026900     IF PFKEY-16-PRESSED  AND
027000        FIRST-IN-FLAG = "Y"
027100         GO TO GET-THE-QUERY.
027200
000000     PERFORM CLOSE-CRT-FILE.
000000     CALL "BLNKSC".
000000     PERFORM OPEN-CRT-FILE.
000000
027300 INIT-FIRST-IN.
027400
027500     MOVE "Please enter your query using the field names defined i
027600-        "n the CONTROL file." TO QUERY-1-FIELD.
027700     MOVE "To view the names which may be used as valid field name
027800-        "s, press (15).  You" TO QUERY-2-FIELD.
027900     MOVE "must use only the short form of the name.
028000-        " To receive an" TO QUERY-3-FIELD.
028100     MOVE "explanation of how to formulate your query, press (14).
028200-        "  When you have" TO QUERY-4-FIELD.
028300     MOVE "finished formulating your query, press (ENTER) to displ
028400-        "ay your edited query." TO QUERY-5-FIELD.
028500
028600     MOVE PASS-MESSAGE TO QUERY-7-FIELD.
028700
028800     MOVE UPPER-ENTRY-FAC TO FAC OF QUERY-12-ITEM
028900                             FAC OF QUERY-13-ITEM
029000                             FAC OF QUERY-14-ITEM
029100                             FAC OF QUERY-15-ITEM
029200                             FAC OF QUERY-16-ITEM
029300                             FAC OF QUERY-17-ITEM
029400                             FAC OF QUERY-18-ITEM
029500                             FAC OF QUERY-TITLE-ITEM.
029600
029700     MOVE "(ENTER) Continue or Select:" TO MSG-1-FIELD.
029800     MOVE "(1) Reselect query field  (2) Allow LOWER case letters"
029900         TO MSG-2-FIELD.
030000     MOVE "(14) Review instructions                       (15) Rev
030100-        "iew valid names" TO MSG-3-FIELD.
030200
030300 SET-UPLOW-ON.
030400
030500     MOVE UPLOW-ENTRY-FAC TO FAC OF QUERY-12-ITEM
030600                             FAC OF QUERY-13-ITEM
030700                             FAC OF QUERY-14-ITEM
030800                             FAC OF QUERY-15-ITEM
030900                             FAC OF QUERY-16-ITEM
031000                             FAC OF QUERY-17-ITEM
031100                             FAC OF QUERY-18-ITEM
031200                             FAC OF QUERY-TITLE-ITEM.
031300
031400 INIT-NOT-FIRST-IN.
031500
031600     MOVE "The QUERY which you have entered has been reduced to th
031700-        "e form displayed below." TO QUERY-1-FIELD.
031800     MOVE "Press (16) if this is equivalent to what you desire.  P
031900-        "ress (14) for query" TO QUERY-2-FIELD.
032000     MOVE "instructions.  Press (15) to view field names."
032100         TO QUERY-3-FIELD.
032200     MOVE SPACES TO QUERY-4-FIELD.
032300
032400     MOVE P-FMT-QUERY-LINE (1) TO QUERY-5-FIELD.
032500     MOVE P-FMT-QUERY-LINE (2) TO QUERY-6-FIELD.
032600     MOVE P-FMT-QUERY-LINE (3) TO QUERY-7-FIELD.
032700     MOVE P-FMT-QUERY-LINE (4) TO QUERY-8-FIELD.
032800     MOVE P-FMT-QUERY-LINE (5) TO QUERY-9-FIELD.
032900     MOVE P-FMT-QUERY-LINE (6) TO QUERY-10-FIELD.
033000     MOVE P-FMT-QUERY-LINE (7) TO QUERY-11-FIELD.
033100
033200     MOVE "(ENTER) Respecify or Select:" TO MSG-1-FIELD.
033300     MOVE "(1) Reselect query field  (2) Allow LOWER case letters
033400-        "  (16) Continue query" TO MSG-2-FIELD.
033500     MOVE "(14) Review instructions                       (15) Rev
033600-        "iew valid names" TO MSG-3-FIELD.
033700
033800 ENTER-THE-QUERY.
033900     MOVE SPACES TO RFL-RECORDS
034000                    DL-FIELDS
034100                    FMT-SCREEN
034200                    PASS-MESSAGE.
034300
034400     CALL "PARSEINQ" USING
034500         RFL-RECORDS
034600         DL-FIELDS
034700         FMT-SCREEN
034800         USER-SCREEN
034900         PASS-MESSAGE
035000         PASS-RC.
035100
035200     IF PASS-MESSAGE NOT = SPACES
035300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
035400     ELSE
035500         MOVE FMT-SCREEN TO P-FMT-QUERY-SCREEN
000000         MOVE USER-TITLE TO P-FMT-QUERY-TITLE.
035600
035700 VAL-THE-QUERY.
035800     IF NOT SCREEN-ENTRY-ERROR
035900         MOVE 0 TO RFL-IDX
036000                   WS-LIN-LEN
036100                   WS-FLD-SEQ
000000                   RHD-FIELD-COUNT
036200         PERFORM VAL-RFL-RECORD
036300             UNTIL SCREEN-ENTRY-ERROR  OR
036400                   RFL-IDX > 80.
000000     IF NOT SCREEN-ENTRY-ERROR
000000         PERFORM VAL-DLS.
036500
000000* THIS ALSO SETS THE RHD-FIELD-COUNT VALUE
036600 VAL-RFL-RECORD.
036700     ADD 1 TO RFL-IDX.
036800     IF RFL-IDX NOT > 80
036900         IF     RFL-NAME (RFL-IDX) = SPACES
000000            AND RFL-COL-HEAD(RFL-IDX) = SPACES
037000             MOVE 81 TO RFL-IDX
037100         ELSE
000000             MOVE RFL-IDX TO RHD-FIELD-COUNT
037200             PERFORM FIND-RFL-RECORD
037300             IF CF-RETURN-CODE NOT = "00"
037400                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
037500                 PERFORM FMT-ABSENT-MSG
037600             ELSE
037700             IF RFL-OCCURRENCE (RFL-IDX) > T1-OCCURRENCES (CF-IDX)
037800                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
037900                 PERFORM FMT-OCCURS-MSG
038000             ELSE
038100             IF T1-REPORT-CODE (CF-IDX) = 1
038200                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
038300                 PERFORM FMT-UPDATE-MSG
038400             ELSE
038500                 PERFORM FILL-IN-THE-RFL.
038600
000000* Correctly set the data limits flag.
000000 VAL-DLS.
000000     IF DL-FIELDS NOT = SPACE
000000         MOVE 1 TO RHD-DATA-LIMIT
000000     ELSE
000000         MOVE ZERO TO RHD-DATA-LIMIT.
000000
038700 FIND-RFL-RECORD.
000000     IF RFL-COL-HEAD(RFL-IDX) NOT = SPACE
000000         PERFORM FIND-RFL-BY-ALIAS
000000     ELSE
000000         PERFORM FIND-RFL-BY-NAME-OR-ALIAS.
000000
000000 FIND-RFL-BY-ALIAS.
000000     STRING RFL-COL-HEAD(RFL-IDX) DELIMITED BY SIZE
000000            RFL-COL-SUB-HEAD(RFL-IDX) DELIMITED BY SIZE
000000             INTO CX-ALIAS.
000000     PERFORM FIND-RFL-ALIAS.
000000
000000 FIND-RFL-ALIAS.
000000         MOVE "FNDALIAS" TO CF-ACTION-CODE
000000         CALL "CTRLARY" USING
000000              CALLED-RC
039100         COPY LKCTRL.
000000
000000 FIND-RFL-BY-NAME-OR-ALIAS.
038900     MOVE RFL-NAME (RFL-IDX) TO CX-NAME.
000000     PERFORM FIND-RFL-NAME.
000000     IF CF-RETURN-CODE NOT = "00"
000000         MOVE RFL-NAME(RFL-IDX) TO CX-ALIAS
000000         PERFORM FIND-RFL-ALIAS
000000         IF CF-RETURN-CODE = "00"
000000             MOVE RFL-NAME(RFL-IDX) TO
000000                  RFL-COL-HEAD(RFL-IDX)
000000             MOVE SPACE TO RFL-NAME(RFL-IDX).
000000 FIND-RFL-NAME.
038800     MOVE "FINDNAME" TO CF-ACTION-CODE
039000         CALL "CTRLARY" USING
000000              CALLED-RC
039100         COPY LKCTRL.
039200
039300 FMT-ABSENT-MSG.
000000     IF RFL-COL-HEAD(RFL-IDX) NOT = SPACE
000000         STRING RFL-COL-HEAD(RFL-IDX) DELIMITED BY SIZE
000000                RFL-COL-SUB-HEAD(RFL-IDX) DELIMITED BY SIZE
000000                 INTO FMT-ABSENT-NAME
000000     ELSE
039400         MOVE RFL-NAME (RFL-IDX) TO FMT-ABSENT-NAME.
039500     MOVE FMT-ABSENT-MSG-TEXT TO PASS-MESSAGE.
039600
039700 FMT-OCCURS-MSG.
039800     MOVE RFL-NAME (RFL-IDX) TO FMT-OCCURS-NAME.
039900     MOVE RFL-OCCURRENCE (RFL-IDX) TO FMT-OCCURS-NO.
040000     MOVE FMT-OCCURS-MSG-TEXT TO PASS-MESSAGE.
040100
040200 FMT-UPDATE-MSG.
040300     MOVE RFL-NAME (RFL-IDX) TO FMT-UPDATE-NAME.
040400     MOVE FMT-UPDATE-MSG-TEXT TO PASS-MESSAGE.
040500
040600 FILL-IN-THE-RFL.
000000     MOVE T2-NAME(CF-IDX) TO RFL-NAME(RFL-IDX).
000000* Working Length must be EXT-LEN or 8 (Length of the
000000* Field Name ) whichever is larger.
000000     COMPUTE WS-WRK-LEN = T1-EXT-LEN(CF-IDX).
000000     IF WS-WRK-LEN < 8
000000         MOVE 8 TO WS-WRK-LEN.
000000* For maximum packing in Inquiry, only one space is
000000* inserted before a field.
042100     MOVE 1 TO RFL-SPACES (RFL-IDX).
000000     ADD 1 TO WS-WRK-LEN.
000000     
040700     ADD WS-WRK-LEN TO WS-LIN-LEN.
040800     IF WS-LIN-LEN > 264
040900         MOVE "X" TO RFL-LINE-CODE (RFL-IDX)
041000     ELSE
041100     IF WS-LIN-LEN > 132
041200         MOVE "Y" TO RFL-LINE-CODE (RFL-IDX)
041300     ELSE
041400         MOVE "Z" TO RFL-LINE-CODE (RFL-IDX).
041500
041600     ADD 1 TO WS-FLD-SEQ.
041700     MOVE WS-FLD-SEQ TO RFL-SEQ (RFL-IDX).
041800
000000     IF RFL-COL-HEAD(RFL-IDX) = SPACE
041900         MOVE RFL-NAME (RFL-IDX) TO RFL-COL-HEAD (RFL-IDX).
042000     MOVE SPACES TO RFL-COL-SUB-HEAD (RFL-IDX).
042200     MOVE T1-EXT-LEN (CF-IDX) TO RFL-EXT-SIZE (RFL-IDX).
042300     MOVE SPACE TO RFL-ZERO-SUPPRESS (RFL-IDX)
042400                   RFL-DECIMAL-CARRY (RFL-IDX)
042500                   RFL-INSERTS (RFL-IDX)
042600                   RFL-TOTAL-CODE (RFL-IDX)
042700                   RFL-MAXIMUM-CODE (RFL-IDX)
042800                   RFL-MINIMUM-CODE (RFL-IDX)
042900                   RFL-AVERAGE-CODE (RFL-IDX).
000000
000000     IF T1-DECIMAL-POS (CF-IDX) > ZERO
000000         MOVE T1-DECIMAL-POS(CF-IDX) TO
000000             RFL-DECIMAL-CARRY(RFL-IDX).
043000
043100     IF T1-SIGN-CONTROL (CF-IDX) = 0
043200         MOVE SPACES TO RFL-SIGN-CONTROL (RFL-IDX)
043300     ELSE
043400     IF T1-SIGN-CONTROL (CF-IDX) = 1
043500         MOVE "9 " TO RFL-SIGN-CONTROL (RFL-IDX)
043600     ELSE
043700     IF T1-SIGN-CONTROL (CF-IDX) = 2
043800         MOVE "CR" TO RFL-SIGN-CONTROL (RFL-IDX)
043900     ELSE
044000     IF T1-SIGN-CONTROL (CF-IDX) = 3
044100         MOVE "DR" TO RFL-SIGN-CONTROL (RFL-IDX).
044200
044300     IF T1-DOLLAR-COMMA (CF-IDX) = 0
044400         MOVE 0 TO RFL-DOLLAR-SIGN (RFL-IDX)
044500                   RFL-COMMA-CODE (RFL-IDX)
044600     ELSE
044700     IF T1-DOLLAR-COMMA (CF-IDX) = 1
044800         MOVE 0 TO RFL-DOLLAR-SIGN (RFL-IDX)
044900         MOVE 1 TO RFL-COMMA-CODE (RFL-IDX)
045000     ELSE
045100     IF T1-DOLLAR-COMMA (CF-IDX) = 2
045200         MOVE 1 TO RFL-DOLLAR-SIGN (RFL-IDX)
045300         MOVE 0 TO RFL-COMMA-CODE (RFL-IDX)
045400     ELSE
045500     IF T1-DOLLAR-COMMA (CF-IDX) = 3
045600         MOVE 1 TO RFL-DOLLAR-SIGN (RFL-IDX)
045700                   RFL-COMMA-CODE (RFL-IDX).
045800
045900     MOVE 1 TO RFL-ORIGIN (RFL-IDX).
046000
000000     COPY PLCRT.
000000
000000     COPY PLKTRACE.
