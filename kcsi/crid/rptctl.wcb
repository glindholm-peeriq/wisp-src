000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTCTL.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/10/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE CONTROL FILE:  FILENAME LIRARY VOLUME
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800
001900 DATA DIVISION.
002000 FILE SECTION.
002100
002200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptctl.wcb 1.7 9/11/94".
002300     COPY WSGP.
002400     COPY WSINT.
002500     COPY WSCRTS.
000000     COPY LKVALNAM.
002600
002700 01  CONTROL-GETPARM-FIELDS.
002800     05  CONTROL-MSG-ID               PIC X(4) VALUE "2002".
002900     05  CONTROL-PRNAME               PIC X(8) VALUE "CONTROL".
003000     05  CONTROL-MSG-ISSUER           PIC X(6) VALUE "REPORT".
003100
003200 01  CONTROL-MSG-COUNT.
003300     05  FILLER                        BINARY   VALUE ZERO.
003400     05  FILLER                        BINARY   VALUE 2.
003500 01  CONTROL-MSG-TEXT-1               PIC X(1) VALUE
003600         " ".
003700
003800 01  CONTROL-MSG-TEXT-2               PIC X(55) VALUE
003900       "Please specify the Control File to use for this report.".
004000 01  CONTROL-FILE-KW              PIC X(8) VALUE "FILE".
004100 01  CONTROL-FILE-FIELD           PIC X(8) VALUE SPACE.
004200 01  CONTROL-FILE-DEFAULT         PIC X(8) VALUE SPACE.
004300 01  CONTROL-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
004400 01  CONTROL-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
004500 01  CONTROL-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
004600 01  CONTROL-VOLUME-KW              PIC X(8) VALUE "VOLUME".
004700 01  CONTROL-VOLUME-FIELD           PIC X(6) VALUE SPACE.
004800 01  CONTROL-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
004900 01  CONTROL-TEXT-01                  PIC X(50) VALUE
005000         "Note:  The Control File is created by the utility ".
005100 01  CONTROL-TEXT-02                  PIC X(7) VALUE
005200         "CONTROL".
005300 01  CONTROL-TEXT-03                  PIC X(1) VALUE
005400         ",".
005500 01  CONTROL-TEXT-04                  PIC X(73) VALUE
005600         "       and is a prerequisite to defining, modifying, or
005700-        "running a report.".
005800 01  CONTROL-TEXT-05                  PIC X(62) VALUE
005900         "       The Control File describes the attributes of the
006000-        "fields".
006100 01  CONTROL-TEXT-06                  PIC X(24) VALUE
006200         "       in the data file.".
006600
006700 01  MESSAGE-LITERALS.
006800     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
006900         "* Error * File Name cannot be blank".
007000     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
007100         "* Error * Invalid File Name".
007200     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
007300         "* Error * Invalid Library Name".
007400     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
007500         "* Error * Invalid Volume Name".
007600
           COPY LKPRC.

011200 LINKAGE SECTION.
011300 01  LOAD-SWITCH        PIC X(01).
011400     COPY WSRPTREC.
011500
011600 PROCEDURE DIVISION USING
011700     LOAD-SWITCH
011800     RPT-RECORDS.
011900 MAIN-LOGIC SECTION.
012000 PROGRAM-BEGIN.
000000     MOVE "RPTCTL BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012100     PERFORM OPENING-PROCEDURE.
012200     PERFORM MAIN-PROCESS.
012300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTCTL EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012400 MAIN-LOGIC-EXIT.
012500     EXIT PROGRAM.
012600 MAIN-LOGIC-STOP.
012700     STOP RUN.
012800 LEVEL-2 SECTION.
012900 OPENING-PROCEDURE.
013000     IF LOAD-SWITCH = "P"
013100         MOVE RCF-PRIM-FILE TO CONTROL-FILE-FIELD
013200         MOVE RCF-PRIM-LIBRARY TO CONTROL-LIBRARY-FIELD
013300         MOVE RCF-PRIM-VOLUME TO CONTROL-VOLUME-FIELD
013400     ELSE
013500         MOVE RCF-SEC-FILE TO CONTROL-FILE-FIELD
013600         MOVE RCF-SEC-LIBRARY TO CONTROL-LIBRARY-FIELD
013700         MOVE RCF-SEC-VOLUME TO CONTROL-VOLUME-FIELD.
013800
013900 CLOSING-PROCEDURE.
014100
014200 MAIN-PROCESS.
014300     MOVE SPACE TO SCREEN-ERROR-STATUS.
014400
014500     PERFORM CALL-CONTROL-GETPARM.
014600     MOVE SPACES TO CONTROL-MSG-TEXT-2.
014700     PERFORM VAL-FILE-NAME.
014800     IF NOT SCREEN-ENTRY-ERROR
014900         PERFORM VAL-LIB-NAME
015000             IF NOT SCREEN-ENTRY-ERROR
015100                PERFORM VAL-VOL-NAME.
015200
015300     IF NOT SCREEN-ENTRY-ERROR
015400         MOVE 0 TO CALLED-RC
015500         CALL "KEXISTS" USING
                   CALLED-RC
015600             CONTROL-FILE-FIELD
015700             CONTROL-LIBRARY-FIELD
015800             CONTROL-VOLUME-FIELD.
015900
016000     IF CALLED-RC NOT = 0
016100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016200         MOVE "Specified Control File does not exist."
016300             TO CONTROL-MSG-TEXT-2.
016400
016500     IF NOT SCREEN-ENTRY-ERROR
016600         IF LOAD-SWITCH = "P"
016700             MOVE CONTROL-FILE-FIELD TO RCF-PRIM-FILE
016800             MOVE CONTROL-LIBRARY-FIELD TO RCF-PRIM-LIBRARY
016900             MOVE CONTROL-VOLUME-FIELD TO RCF-PRIM-VOLUME
017000         ELSE
017100             MOVE CONTROL-FILE-FIELD TO RCF-SEC-FILE
017200             MOVE CONTROL-LIBRARY-FIELD TO RCF-SEC-LIBRARY
017300             MOVE CONTROL-VOLUME-FIELD TO RCF-SEC-VOLUME.
017400
017500     IF CALLED-RC = 0
017600         CALL "RPTCTLH" USING
017700             LOAD-SWITCH
017800             RPT-RECORDS.
017900
018000     IF SCREEN-ENTRY-ERROR
018100         GO TO MAIN-PROCESS.
018200
018300 CALL-CONTROL-GETPARM.
018400     CALL "GETPARM" USING
018500         GP-SPECIFY-TYPE, GP-REQUEST-FORM, CONTROL-PRNAME,
018600         GP-PFKEY-RECEIVER, CONTROL-MSG-ID,
018700         CONTROL-MSG-ISSUER,
018800
018900         CONTROL-MSG-COUNT,
019000         CONTROL-MSG-TEXT-1, INT01,
019100         CONTROL-MSG-TEXT-2, INT55,
019200
019300         GP-KW-TYPE, CONTROL-FILE-KW,
019400         CONTROL-FILE-FIELD, INT08,
019500         GP-ABS, INT15, GP-ABS, INT07,
019600         GP-UPPERCASE-DATA
019700
019800         GP-KW-TYPE, CONTROL-LIBRARY-KW,
019900         CONTROL-LIBRARY-FIELD, INT08,
020000         GP-ABS, INT15, GP-ABS, INT30,
020100         GP-UPPERCASE-DATA
020200
020300         GP-KW-TYPE, CONTROL-VOLUME-KW,
020400         CONTROL-VOLUME-FIELD, INT06,
020500         GP-ABS, INT15, GP-ABS, INT53,
020600         GP-UPPERCASE-DATA
020700
020800         GP-TEXT-TYPE, CONTROL-TEXT-01, INT50,
020900         GP-ABS, INT19, GP-ABS, INT02,
021000
021100         GP-TEXT-TYPE, CONTROL-TEXT-02, INT07,
021200         GP-ABS, INT19, GP-ABS, INT54,
021300
021400         GP-TEXT-TYPE, CONTROL-TEXT-03, INT01,
021500         GP-ABS, INT19, GP-ABS, INT63,
021600
021700         GP-TEXT-TYPE, CONTROL-TEXT-04, INT73,
021800         GP-ABS, INT20, GP-ABS, INT02,
021900
022000         GP-TEXT-TYPE, CONTROL-TEXT-05, INT62,
022100         GP-ABS, INT21, GP-ABS, INT02,
022200
022300         GP-TEXT-TYPE, CONTROL-TEXT-06, INT24,
022400         GP-ABS, INT22, GP-ABS, INT02,
022500         GP-ENABLE-ENTER-KEY.
022700
022800 VAL-FILE-NAME.
022900
023000     IF CONTROL-FILE-FIELD = SPACES
023100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
023200         MOVE FILE-ID-BLANK-LITERAL TO CONTROL-MSG-TEXT-2.
023300
023400     IF NOT SCREEN-ENTRY-ERROR
023500         MOVE CONTROL-FILE-FIELD TO VAL-NAME
023600         PERFORM TEST-THE-NAME
023700         IF SCREEN-ENTRY-ERROR
023800             MOVE INVALID-FILE-LITERAL TO CONTROL-MSG-TEXT-2.
023900
024000 VAL-LIB-NAME.
024100
024200     MOVE CONTROL-LIBRARY-FIELD TO VAL-NAME.
024300     PERFORM TEST-THE-NAME.
024400     IF SCREEN-ENTRY-ERROR
024500         MOVE INVALID-LIB-LITERAL TO CONTROL-MSG-TEXT-2.
024600
024700 VAL-VOL-NAME.
024800
024900     MOVE CONTROL-VOLUME-FIELD TO VAL-NAME.
025000     PERFORM TEST-THE-VOL.
025100     IF SCREEN-ENTRY-ERROR
025200         MOVE INVALID-VOL-LITERAL TO CONTROL-MSG-TEXT-2.
025300
037400 TEST-THE-NAME.
           CALL "VALNAM" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

037400 TEST-THE-VOL.
           CALL "VALVOL" USING
                VAL-NAME VAL-RC. 
           IF VAL-RC NOT = ZEROES
                MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.

000000     COPY PLKTRACE.
