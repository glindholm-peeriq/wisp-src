000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTDNW.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/14/89.
000500 DATE-COMPILED.
000600*----------------------------------------------------------------
000700* DEFINES THE PARAMETERS OF EACH NEW FIELD
000800*----------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptdnw.wcb 1.7 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002700
002800 01  EXPRESS-NEW-FIELD-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(10) ROW 2 COLUMN 35 VALUE
003100         "NEW FIELDS".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 3 COLUMN 3
003400         SOURCE UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(13) ROW 4 COLUMN 8 VALUE
003700         "Please define".
003800     05  DISPLAY-EXPFLD-ID-1-ITEM
003900         PIC X(8) ROW 4 COLUMN 22
004000         SOURCE DISPLAY-EXPFLD-ID-1-FIELD.
004100     05  FILLER
004200         PIC X(43) ROW 4 COLUMN 31 VALUE
004300         " in relation to previously selected fields.".
004400     05  FILLER
004500         PIC X(68) ROW 5 COLUMN 7 VALUE
004600         "The operands may be other fields, and/or literal constan
004700-        "ts delimited".
004800     05  FILLER
004900         PIC X(38) ROW 6 COLUMN 15 VALUE
005000         "by double quotes if alphabetic.   E.G.".
005100     05  FILLER
005200         PIC X(4) ROW 6 COLUMN 55 VALUE
005300         "ABCD".
005400     05  FILLER
005500         PIC X(9) ROW 6 COLUMN 61 VALUE
005600         ", 1234.56".
005700     05  FILLER
005800         PIC X(62) ROW 7 COLUMN 7 VALUE
005900         "Optional OP values include:  +, -, /, *   (for numeric f
006000-        "ields)".
006100     05  FILLER
006200         PIC X(40) ROW 8 COLUMN 36 VALUE
006300         "&  (concatenation, for character fields)".
006400     05  FILLER
006500         PIC X(13) ROW 9 COLUMN 2 VALUE
006600         "(for example)".
006700     05  FILLER
006800         PIC X(54) ROW 10 COLUMN 2 VALUE
006900         "NEWFIELD  =     FIELD1                    +     123.00".
007000     05  FILLER
007100         PIC X(14) ROW 12 COLUMN 21 VALUE
007200         "Field/Constant".
007300     05  FILLER
007400         PIC X(2) ROW 12 COLUMN 43 VALUE
007500         "OP".
007600     05  FILLER
007700         PIC X(14) ROW 12 COLUMN 53 VALUE
007800         "Field/Constant".
007900     05  FILLER
008000         PIC X(2) ROW 12 COLUMN 74 VALUE
008100         "OP".
008200     05  DISPLAY-EXPFLD-ID-2-ITEM
008300         PIC X(8) ROW 13 COLUMN 2
008400         SOURCE DISPLAY-EXPFLD-ID-2-FIELD.
008500     05  FILLER
008600         PIC X(1) ROW 13 COLUMN 12 VALUE
008700         "=".
008800     05  EXPFLD-CONSTANT-1-ITEM
008900         PIC X(20) ROW 13 COLUMN 18
009000         SOURCE EXPFLD-CONSTANT-1-FIELD
009100         OBJECT EXPFLD-CONSTANT-1-FIELD.
009200     05  EXPFLD-OP-1-ITEM
009300         PIC X ROW 13 COLUMN 44
009400         SOURCE EXPFLD-OP-1-FIELD
009500         OBJECT EXPFLD-OP-1-FIELD.
009600     05  EXPFLD-CONSTANT-2-ITEM
009700         PIC X(20) ROW 13 COLUMN 50
009800         SOURCE EXPFLD-CONSTANT-2-FIELD
009900         OBJECT EXPFLD-CONSTANT-2-FIELD.
010000     05  EXPFLD-OP-2-ITEM
010100         PIC X ROW 13 COLUMN 75
010200         SOURCE EXPFLD-OP-2-FIELD
010300         OBJECT EXPFLD-OP-2-FIELD.
010400     05  EXPFLD-CONSTANT-3-ITEM
010500         PIC X(20) ROW 14 COLUMN 18
010600         SOURCE EXPFLD-CONSTANT-3-FIELD
010700         OBJECT EXPFLD-CONSTANT-3-FIELD.
010800     05  EXPFLD-OP-3-ITEM
010900         PIC X ROW 14 COLUMN 44
011000         SOURCE EXPFLD-OP-3-FIELD
011100         OBJECT EXPFLD-OP-3-FIELD.
011200     05  EXPFLD-CONSTANT-4-ITEM
011300         PIC X(20) ROW 14 COLUMN 50
011400         SOURCE EXPFLD-CONSTANT-4-FIELD
011500         OBJECT EXPFLD-CONSTANT-4-FIELD.
011600     05  EXPFLD-OP-4-ITEM
011700         PIC X ROW 14 COLUMN 75
011800         SOURCE EXPFLD-OP-4-FIELD
011900         OBJECT EXPFLD-OP-4-FIELD.
012000     05  EXPFLD-CONSTANT-5-ITEM
012100         PIC X(20) ROW 15 COLUMN 18
012200         SOURCE EXPFLD-CONSTANT-5-FIELD
012300         OBJECT EXPFLD-CONSTANT-5-FIELD.
012400     05  EXPFLD-OP-5-ITEM
012500         PIC X ROW 15 COLUMN 44
012600         SOURCE EXPFLD-OP-5-FIELD
012700         OBJECT EXPFLD-OP-5-FIELD.
012800     05  EXPFLD-CONSTANT-6-ITEM
012900         PIC X(20) ROW 15 COLUMN 50
013000         SOURCE EXPFLD-CONSTANT-6-FIELD
013100         OBJECT EXPFLD-CONSTANT-6-FIELD.
013200     05  EXPFLD-OP-6-ITEM
013300         PIC X ROW 15 COLUMN 75
013400         SOURCE EXPFLD-OP-6-FIELD
013500         OBJECT EXPFLD-OP-6-FIELD.
013600     05  EXPFLD-CONSTANT-7-ITEM
013700         PIC X(20) ROW 16 COLUMN 18
013800         SOURCE EXPFLD-CONSTANT-7-FIELD
013900         OBJECT EXPFLD-CONSTANT-7-FIELD.
014000     05  EXPFLD-OP-7-ITEM
014100         PIC X ROW 16 COLUMN 44
014200         SOURCE EXPFLD-OP-7-FIELD
014300         OBJECT EXPFLD-OP-7-FIELD.
014400     05  EXPFLD-CONSTANT-8-ITEM
014500         PIC X(20) ROW 16 COLUMN 50
014600         SOURCE EXPFLD-CONSTANT-8-FIELD
014700         OBJECT EXPFLD-CONSTANT-8-FIELD.
014800     05  EXPFLD-OP-8-ITEM
014900         PIC X ROW 16 COLUMN 75
015000         SOURCE EXPFLD-OP-8-FIELD
015100         OBJECT EXPFLD-OP-8-FIELD.
015200     05  EXPFLD-CONSTANT-9-ITEM
015300         PIC X(20) ROW 17 COLUMN 18
015400         SOURCE EXPFLD-CONSTANT-9-FIELD
015500         OBJECT EXPFLD-CONSTANT-9-FIELD.
015600     05  EXPFLD-OP-9-ITEM
015700         PIC X ROW 17 COLUMN 44
015800         SOURCE EXPFLD-OP-9-FIELD
015900         OBJECT EXPFLD-OP-9-FIELD.
016000     05  EXPFLD-CONSTANT-10-ITEM
016100         PIC X(20) ROW 17 COLUMN 50
016200         SOURCE EXPFLD-CONSTANT-10-FIELD
016300         OBJECT EXPFLD-CONSTANT-10-FIELD.
016400     05  EXPFLD-OP-10-ITEM
016500         PIC X ROW 17 COLUMN 75
016600         SOURCE EXPFLD-OP-10-FIELD
016700         OBJECT EXPFLD-OP-10-FIELD.
016800     05  FILLER
016900         PIC X(62) ROW 19 COLUMN 10 VALUE
017000         "Note: The first blank 'OP', defines the end of the expre
017100-        "ssion.".
017200     05  FILLER
017300         PIC X(33) ROW 21 COLUMN 24 VALUE
017400         "**  Press (ENTER) to Continue  **".
017500
017600 01  EXPRESS-NEW-FIELD-TABLE.
017700     05  DISPLAY-EXPFLD-ID-1-FIELD         PIC X(8) VALUE SPACE.
017800     05  DISPLAY-EXPFLD-ID-2-FIELD         PIC X(8) VALUE SPACE.
017900     05  UPPER-MSG-FIELD  PIC X(78) VALUE SPACES.
018000     05  EXPRESS-NEW-FIELD-FIELDS.
018100         07  EXPFLD-CONSTANT-1-FIELD       PIC X(20) VALUE SPACE.
018200         07  EXPFLD-OP-1-FIELD             PIC X VALUE SPACE.
018300         07  EXPFLD-CONSTANT-2-FIELD       PIC X(20) VALUE SPACE.
018400         07  EXPFLD-OP-2-FIELD             PIC X VALUE SPACE.
018500         07  EXPFLD-CONSTANT-3-FIELD       PIC X(20) VALUE SPACE.
018600         07  EXPFLD-OP-3-FIELD             PIC X VALUE SPACE.
018700         07  EXPFLD-CONSTANT-4-FIELD       PIC X(20) VALUE SPACE.
018800         07  EXPFLD-OP-4-FIELD             PIC X VALUE SPACE.
018900         07  EXPFLD-CONSTANT-5-FIELD       PIC X(20) VALUE SPACE.
019000         07  EXPFLD-OP-5-FIELD             PIC X VALUE SPACE.
019100         07  EXPFLD-CONSTANT-6-FIELD       PIC X(20) VALUE SPACE.
019200         07  EXPFLD-OP-6-FIELD             PIC X VALUE SPACE.
019300         07  EXPFLD-CONSTANT-7-FIELD       PIC X(20) VALUE SPACE.
019400         07  EXPFLD-OP-7-FIELD             PIC X VALUE SPACE.
019500         07  EXPFLD-CONSTANT-8-FIELD       PIC X(20) VALUE SPACE.
019600         07  EXPFLD-OP-8-FIELD             PIC X VALUE SPACE.
019700         07  EXPFLD-CONSTANT-9-FIELD       PIC X(20) VALUE SPACE.
019800         07  EXPFLD-OP-9-FIELD             PIC X VALUE SPACE.
019900         07  EXPFLD-CONSTANT-10-FIELD      PIC X(20) VALUE SPACE.
020000         07  EXPFLD-OP-10-FIELD            PIC X VALUE SPACE.
020100     05  VAL-ENTRY-TABLE REDEFINES EXPRESS-NEW-FIELD-FIELDS.
020200         07  EXPRESS-NEW-FIELD-LINE OCCURS 10 TIMES.
020300             09  CONSTANT-FIELD      PIC X(20).
020400             09  ID-FIELD REDEFINES CONSTANT-FIELD.
020500                 11  NAME-FIELD      PIC X(08).
020600                 11  O-FIELD.
020700                     13  LEFT-PAREN      PIC X(01).
020800                     13  OCCURS-FIELD    PIC X(02).
020900                     13  RIGHT-PAREN     PIC X(01).
021000                     13 FILLER          PIC X(08).
021100             09  OP-FIELD            PIC X(01).
021200     05  LITERAL-FIELD               PIC X(01) OCCURS 10 TIMES.
021300     05  ORIGIN-FIELD                PIC 9(01) OCCURS 10 TIMES.
021400     05  IDX                         PIC 9(02) VALUE 0.
021500     05  TEST-NAME                   PIC X(08) VALUE SPACES.
021600
021700 01  MESSAGE-LITERALS.
021800     05  NINE-DECIMAL-LITERAL  PIC X(78) VALUE
021900         "* Error * More than 9 decimal positions are invalid".
022000     05  INVALID-NAME-LITERAL  PIC X(78) VALUE
022100         "* Error * Field has not been selected from processing".
022200     05  FIRST-FC-LITERAL  PIC X(78) VALUE
022300         "* Error * First F/C cannot be blank".
022400     05  MUST-HAVE-FC-LITERAL  PIC X(78) VALUE
022500         "* Error * F/C cannot be blank".
022600     05  DOUBLE-QUOTE-LITERAL  PIC X(78) VALUE
022700         "* Error * Literal must be delimited by double quotes".
022800     05  INVALID-CONSTANT-LITERAL  PIC X(78) VALUE
022900         "* Error * Field is not a character field".
023000     05  INVALID-NUMERIC-LITERAL   PIC X(78) VALUE
023100         "* Error * Field is not a numeric field".
023200     05  NUMERIC-LITERAL-LITERAL  PIC X(78) VALUE
023300         "* Error * Field must be a numeric literal".
023400     05  INVALID-OP-LITERAL  PIC X(78) VALUE
023500         "* Error * Invalid OP".
023600
023700 01  CONSTANT-FLAG               PIC X(01) VALUE "N".
023800     88  ITS-A-CONSTANT          VALUE "Y".
023900
024000 01  POSITION-THE-NAME-FIELDS.
024100     05  POSITION-NAME-IN        PIC X(12) VALUE SPACES.
024200     05  FILLER REDEFINES POSITION-NAME-IN.
024300         07  POS-IN-CHAR         PIC X(01) OCCURS 12 TIMES.
024400     05  POSITION-NAME-OUT       PIC X(12) VALUE SPACES.
024500     05  FILLER  REDEFINES POSITION-NAME-OUT.
024600         07  POS-OUT-CHAR        PIC X(01) OCCURS 8 TIMES.
024700         07  POS-OUT-OCCURS.
024800             09  POS-OUT-LT-PAREN PIC X(01).
024900             09  POS-OUT-OCC-1    PIC X(01).
025000             09  POS-OUT-OCC-2    PIC X(01).
025100             09  POS-OUT-RT-PAREN PIC X(01).
025200     05  POSITIONING-COUNTERS.
025300         07  PAREN-LOCATION      PIC 9(02) VALUE 0.
025400         07  POS-POINTER         PIC 9(02) VALUE 0.
025500         07  RIGHT-PAREN-COUNT   PIC 9(02) VALUE 0.
025600         07  LEFT-PAREN-COUNT    PIC 9(02) VALUE 0.
025700
025800 01  NEXIDX                      PIC 9(02) VALUE 0.
025900 01  CKIDX                       PIC 9(02) VALUE 0.
026000 01  TSTIDX                      PIC 9(02) VALUE 0.
026100 01  VALIDX                      PIC 9(02) VALUE 0.
026200 01  CONSTANT-LENGTH             PIC 9(02) VALUE 0.
026300 01  DEC-IDX                     PIC 9(02) VALUE 0.
026400 01  DECIMAL-COUNT               PIC 9(02) VALUE 0.
026500 01  LAST-IDX                    PIC 9(02) VALUE 0.
026600
026700 01  TEST-TABLE.
026800     05  TEST-SPACE              PIC X(20) VALUE SPACES.
026900     05  FILLER REDEFINES TEST-SPACE.
027000         07 TEST-CHAR            PIC X(01) OCCURS 20 TIMES.
027100     05  WHERE-ITS-AT            PIC X(01) VALUE SPACES.
027200         88  NAME-FOUND          VALUES "R", "N".
027300         88  ITS-RFL             VALUE "R".
027400         88  ITS-NFL             VALUE "N".
027500
027600 LINKAGE SECTION.
027700 01  RPT-PFKEY         PIC 9(02).
027800     COPY WSRPTCFL.
027900     COPY WSRPTREC.
028000
028100 PROCEDURE DIVISION USING
028200     RPT-PFKEY
028300     RPT-CTL-FIELDS
028400     RPT-RECORDS.
028500
028600 MAIN-LOGIC SECTION.
028700 PROGRAM-BEGIN.
000000     MOVE "RPTDNW BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
028800     PERFORM OPENING-PROCEDURE.
028900     PERFORM MAIN-PROCESS.
029000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTDNW EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
029100 MAIN-LOGIC-EXIT.
029200     EXIT PROGRAM.
029300 MAIN-LOGIC-STOP.
029400     STOP RUN.
029500 LEVEL-2 SECTION.
029600 OPENING-PROCEDURE.
029700     OPEN I-O  CRT-FILE.
029800     MOVE 0 TO NF-IDX.
029900 CLOSING-PROCEDURE.
030000     CLOSE CRT-FILE.
030100
030200 MAIN-PROCESS.
030300     ADD 1 TO NF-IDX.
030400     IF NF-IDX NOT > 10
030500         IF NF-NAME (NF-IDX) NOT = SPACES
030600             PERFORM INIT-THE-EXPRESSION
030700             PERFORM PROCESS-THE-EXPRESSION
030800             PERFORM RELOAD-THE-EXPRESSION
030900             GO TO MAIN-PROCESS.
031000
031100 INIT-THE-EXPRESSION.
031200     MOVE NF-NAME (NF-IDX) TO DISPLAY-EXPFLD-ID-1-FIELD
031300                              DISPLAY-EXPFLD-ID-2-FIELD.
031400     MOVE SPACES TO EXPRESS-NEW-FIELD-FIELDS.
031500     PERFORM JUST-THE-FACS.
031600     MOVE DISPLAY-HI-FAC TO FAC OF UPPER-MSG-ITEM.
031700     MOVE 0 TO NF-ENTRY-IDX.
031800     PERFORM LOAD-THE-ENTRY.
031900
032000 LOAD-THE-ENTRY.
032100     ADD 1 TO NF-ENTRY-IDX.
032200     IF NF-ENTRY-IDX NOT > 10
032300         IF NF-LIT-20 (NF-IDX, NF-ENTRY-IDX) NOT = SPACES
032400             PERFORM LOAD-THE-NF-ENTRY-DATA
032500             IF NF-OP-CODE (NF-IDX, NF-ENTRY-IDX) NOT = SPACE
032600                 GO TO LOAD-THE-ENTRY.
032700
032800 LOAD-THE-NF-ENTRY-DATA.
032900     IF NF-NOT-LIT-CODE (NF-IDX, NF-ENTRY-IDX) NOT = "X"
033000         MOVE NF-LIT-20 (NF-IDX, NF-ENTRY-IDX) TO
033100             CONSTANT-FIELD (NF-ENTRY-IDX)
033200         MOVE ZERO TO ORIGIN-FIELD (NF-ENTRY-IDX)
033300         MOVE NF-OP-CODE (NF-IDX, NF-ENTRY-IDX) TO
033400             OP-FIELD (NF-ENTRY-IDX)
033500         MOVE NF-NOT-LIT-CODE (NF-IDX, NF-ENTRY-IDX) TO
033600             LITERAL-FIELD (NF-ENTRY-IDX)
033700     ELSE
033800         MOVE SPACES TO CONSTANT-FIELD (NF-ENTRY-IDX)
033900         MOVE NF-OP-OCCURRENCE (NF-IDX, NF-ENTRY-IDX) TO
034000             O-FIELD (NF-ENTRY-IDX)
034100         MOVE NF-OP-NAME (NF-IDX, NF-ENTRY-IDX) TO
034200             NAME-FIELD (NF-ENTRY-IDX)
034300         MOVE "X" TO LITERAL-FIELD (NF-ENTRY-IDX)
034400         MOVE NF-OP-ORIGIN (NF-IDX, NF-ENTRY-IDX) TO
034500             ORIGIN-FIELD (NF-ENTRY-IDX)
034600         MOVE NF-OP-CODE (NF-IDX, NF-ENTRY-IDX) TO
034700             OP-FIELD (NF-ENTRY-IDX)
034800         IF NF-OP-ORIGIN (NF-IDX, NF-ENTRY-IDX) = 2
034900             MOVE 1 TO RFL-IDX
035000             PERFORM CHECK-FOR-AKA.
035100
035200 CHECK-FOR-AKA.
035300     IF NAME-FIELD (NF-ENTRY-IDX) = RFL-NAME (RFL-IDX)  AND
035400        RFL-ORIGIN (RFL-IDX) = 2
035500         IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
035600            RFL-OCCURRENCE-A (RFL-IDX) NOT > 99  AND
035700            OCCURS-FIELD (NF-ENTRY-IDX) =
035800                      RFL-OCCURRENCE-A (RFL-IDX)
035900             IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
036000                 MOVE CTL-FIELDS-AKA (RFL-IDX) TO
036100                             ID-FIELD (NF-ENTRY-IDX)
036200             ELSE
036300                 NEXT SENTENCE
036400         ELSE
036500         IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
036600             MOVE CTL-FIELDS-AKA (RFL-IDX) TO
036700                                 ID-FIELD (NF-ENTRY-IDX).
036800
036900     IF RFL-NAME (RFL-IDX) = SPACES
037000         MOVE 80 TO RFL-IDX.
037100
037200     ADD 1 TO RFL-IDX.
037300     IF RFL-IDX NOT > 80
037400         GO TO CHECK-FOR-AKA.
037500
037600 RELOAD-THE-EXPRESSION.
037700     MOVE 0 TO NF-ENTRY-IDX.
037800     PERFORM RELOAD-THE-ENTRY.
037900
038000 RELOAD-THE-ENTRY.
038100     ADD 1 TO NF-ENTRY-IDX.
038200     IF NF-ENTRY-IDX NOT > 10
038300         IF CONSTANT-FIELD (NF-ENTRY-IDX) NOT = SPACES
038400             PERFORM RELOAD-NF-ENTRY-DATA
038500             GO TO RELOAD-THE-ENTRY
038600         ELSE
038700             MOVE SPACES TO NF-ENTRY (NF-IDX, NF-ENTRY-IDX)
038800             GO TO RELOAD-THE-ENTRY.
038900
039000 RELOAD-NF-ENTRY-DATA.
039300     MOVE OP-FIELD (NF-ENTRY-IDX) TO
039400         NF-OP-CODE (NF-IDX, NF-ENTRY-IDX).
039500     IF LITERAL-FIELD (NF-ENTRY-IDX) NOT = "X"
039600         MOVE CONSTANT-FIELD (NF-ENTRY-IDX) TO
039700             NF-LIT-20 (NF-IDX, NF-ENTRY-IDX)
039800     ELSE
039900         MOVE ORIGIN-FIELD (NF-ENTRY-IDX) TO
040000             NF-OP-ORIGIN (NF-IDX, NF-ENTRY-IDX)
040100         MOVE 1 TO RFL-IDX
040200         PERFORM CHECK-RELOAD-AKA
040300         MOVE NAME-FIELD (NF-ENTRY-IDX) TO
040400             NF-OP-NAME (NF-IDX, NF-ENTRY-IDX)
000000         MOVE "X" TO NF-NOT-LIT-CODE(NF-IDX, NF-ENTRY-IDX)
040500         MOVE O-FIELD (NF-ENTRY-IDX) TO
040600             NF-OP-OCCURRENCE (NF-IDX, NF-ENTRY-IDX).
040700
040800 CHECK-RELOAD-AKA.
040900     IF ORIGIN-FIELD (NF-ENTRY-IDX) NOT = 2
041000         MOVE 80 TO RFL-IDX
041100     ELSE
041200     IF CTL-FIELDS-AKA (RFL-IDX) = NAME-FIELD (NF-ENTRY-IDX)
041300         MOVE RFL-NAME (RFL-IDX) TO NAME-FIELD (NF-ENTRY-IDX)
041400         IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
041500            RFL-OCCURRENCE-A (RFL-IDX) NOT > 99
041600             MOVE RFL-OCCURRENCE-A (RFL-IDX) TO
041700                             OCCURS-FIELD (NF-ENTRY-IDX)
041800             MOVE "(" TO LEFT-PAREN (NF-ENTRY-IDX)
041900             MOVE ")" TO RIGHT-PAREN (NF-ENTRY-IDX).
042000
042100     IF RFL-NAME (RFL-IDX) = SPACES
042200         MOVE 80 TO RFL-IDX.
042300
042400     ADD 1 TO RFL-IDX.
042500     IF RFL-IDX NOT > 80
042600         GO TO CHECK-RELOAD-AKA.
042700
042800 PROCESS-THE-EXPRESSION.
042900
043000     MOVE SPACES TO SCREEN-ERROR-STATUS.
043100
043200     DISPLAY AND READ ALTERED
043300         EXPRESS-NEW-FIELD-SCREEN ON CRT-FILE.
043400
043500     MOVE SPACE TO UPPER-MSG-FIELD.
043600
043700     PERFORM VAL-THE-ENTRY.
043800     IF SCREEN-ENTRY-ERROR
043900         GO TO PROCESS-THE-EXPRESSION.
044000
044100 VAL-THE-ENTRY.
044200     MOVE 0 TO VALIDX.
044300     PERFORM VAL-ONE-LINE.
044400
044500 VAL-ONE-LINE.
044600     ADD 1 TO VALIDX.
044700     IF VALIDX NOT > 10
044800         PERFORM VAL-FIELD
044900         IF NOT SCREEN-ENTRY-ERROR
045000             PERFORM VAL-OP
045100             IF NOT SCREEN-ENTRY-ERROR
045200                 GO TO VAL-ONE-LINE.
045300
045400 VAL-FIELD.
045500     IF VALIDX = 1  AND
045600        CONSTANT-FIELD (VALIDX) = SPACES
045700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
045800         MOVE FIRST-FC-LITERAL TO UPPER-MSG-FIELD.
045900
046000     IF NOT SCREEN-ENTRY-ERROR  AND
046100        CONSTANT-FIELD (VALIDX) NOT = SPACES
046200         MOVE CONSTANT-FIELD (VALIDX) TO TEST-SPACE
046300         PERFORM TEST-THE-FIELD.
046400
046500     IF NOT SCREEN-ENTRY-ERROR  AND
046600         VALIDX NOT = 1
046700         COMPUTE LAST-IDX = VALIDX - 1
046800         IF OP-FIELD (LAST-IDX) = SPACES
046900             MOVE SPACES TO CONSTANT-FIELD (VALIDX)
047000         ELSE
047100         IF CONSTANT-FIELD (VALIDX) = SPACES
047200             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
047300             MOVE MUST-HAVE-FC-LITERAL TO UPPER-MSG-FIELD.
047400
047500     IF SCREEN-ENTRY-ERROR
047600         IF VALIDX = 1
047700             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-1-ITEM ON
047800         ELSE
047900         IF VALIDX = 2
048000             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-2-ITEM ON
048100         ELSE
048200         IF VALIDX = 3
048300             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-3-ITEM ON
048400         ELSE
048500         IF VALIDX = 4
048600             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-4-ITEM ON
048700         ELSE
048800         IF VALIDX = 5
048900             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-5-ITEM ON
049000         ELSE
049100         IF VALIDX = 6
049200             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-6-ITEM ON
049300         ELSE
049400         IF VALIDX = 7
049500             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-7-ITEM ON
049600         ELSE
049700         IF VALIDX = 8
049800             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-8-ITEM ON
049900         ELSE
050000         IF VALIDX = 9
050100             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-9-ITEM ON
050200         ELSE
050300         IF VALIDX = 10
050400             SET ERROR-BIT IN FAC OF EXPFLD-CONSTANT-10-ITEM ON.
050500
050600 VAL-OP.
050700     IF NF-TYPE (NF-IDX) = "C"
050800         IF OP-FIELD (VALIDX) NOT = " "  AND
050900            OP-FIELD (VALIDX) NOT = "&"
051000             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
051100             MOVE INVALID-OP-LITERAL TO UPPER-MSG-FIELD.
051200
051300     IF NF-TYPE (NF-IDX) = "P"
051400         IF OP-FIELD (VALIDX) NOT = " "  AND
051500            OP-FIELD (VALIDX) NOT = "/"  AND
051600            OP-FIELD (VALIDX) NOT = "*"  AND
051700            OP-FIELD (VALIDX) NOT = "+"  AND
051800            OP-FIELD (VALIDX) NOT = "-"
051900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
052000             MOVE INVALID-OP-LITERAL TO UPPER-MSG-FIELD.
052100
052200     IF SCREEN-ENTRY-ERROR
052300         IF VALIDX = 1
052400             SET ERROR-BIT IN FAC OF EXPFLD-OP-1-ITEM ON
052500         ELSE
052600         IF VALIDX = 2
052700             SET ERROR-BIT IN FAC OF EXPFLD-OP-2-ITEM ON
052800         ELSE
052900         IF VALIDX = 3
053000             SET ERROR-BIT IN FAC OF EXPFLD-OP-3-ITEM ON
053100         ELSE
053200         IF VALIDX = 4
053300             SET ERROR-BIT IN FAC OF EXPFLD-OP-4-ITEM ON
053400         ELSE
053500         IF VALIDX = 5
053600             SET ERROR-BIT IN FAC OF EXPFLD-OP-5-ITEM ON
053700         ELSE
053800         IF VALIDX = 6
053900             SET ERROR-BIT IN FAC OF EXPFLD-OP-6-ITEM ON
054000         ELSE
054100         IF VALIDX = 7
054200             SET ERROR-BIT IN FAC OF EXPFLD-OP-7-ITEM ON
054300         ELSE
054400         IF VALIDX = 8
054500             SET ERROR-BIT IN FAC OF EXPFLD-OP-8-ITEM ON
054600         ELSE
054700         IF VALIDX = 9
054800             SET ERROR-BIT IN FAC OF EXPFLD-OP-9-ITEM ON
054900         ELSE
055000         IF VALIDX = 10
055100             SET ERROR-BIT IN FAC OF EXPFLD-OP-10-ITEM ON.
055200
055300 TEST-THE-FIELD.
055400     IF NF-TYPE (NF-IDX) = "C"
055500         PERFORM TEST-CHARACTER-FIELD.
055600
055700     IF NF-TYPE (NF-IDX) = "P"
055800         PERFORM TEST-NUMERIC-FIELD.
055900
056000 TEST-CHARACTER-FIELD.
056100     IF TEST-CHAR (1) = "'"
056200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
056300         MOVE DOUBLE-QUOTE-LITERAL TO UPPER-MSG-FIELD
056400     ELSE
056500     IF TEST-CHAR (1) = """"
056600         MOVE " " TO LITERAL-FIELD (VALIDX)
056700         PERFORM FIND-THE-LENGTH
056800         IF TEST-CHAR (TSTIDX) NOT = """"
056900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
057000             MOVE DOUBLE-QUOTE-LITERAL TO UPPER-MSG-FIELD
057100         ELSE
057200             NEXT SENTENCE
057300     ELSE
057400     IF TEST-CHAR (1) NOT = """"
057500         MOVE "X" TO LITERAL-FIELD (VALIDX)
057600         MOVE ID-FIELD (VALIDX) TO POSITION-NAME-IN
057700         PERFORM POSITION-THE-NAME
057800         MOVE POSITION-NAME-OUT TO ID-FIELD (VALIDX)
057900         MOVE ID-FIELD (VALIDX) TO TEST-NAME
058000         PERFORM IS-THE-NAME-THERE
058100         IF NOT SCREEN-ENTRY-ERROR
058200             IF (ITS-RFL  AND
058300                CTL-FIELDS-TYPE (RFL-IDX) NOT = "C")  OR
058400                (ITS-NFL  AND
058500                NF-TYPE (CKIDX) NOT = "C")
058600                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
058700                 MOVE INVALID-CONSTANT-LITERAL TO
058800                     UPPER-MSG-FIELD
058900             ELSE
059000             IF ITS-RFL  AND
059100                CTL-FIELDS-TYPE (RFL-IDX) =  "C"
059200                 MOVE RFL-ORIGIN (RFL-IDX) TO
059300                     ORIGIN-FIELD (VALIDX)
059400             ELSE
059500             IF ITS-NFL  AND
059600                NF-TYPE (CKIDX) = "C"
059700                 MOVE 3 TO ORIGIN-FIELD (VALIDX).
059800
059900 TEST-NUMERIC-FIELD.
060000     IF TEST-CHAR (1) = "'"  OR
060100        TEST-CHAR (1) = """"
060200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
060300         MOVE NUMERIC-LITERAL-LITERAL TO UPPER-MSG-FIELD
060400     ELSE
060500     IF    TEST-CHAR (1) IS NUMERIC
000000        OR TEST-CHAR(1) = "+"
000000        OR TEST-CHAR(1) = "-"
000000        OR TEST-CHAR(1) = "."
060600         MOVE " " TO LITERAL-FIELD (VALIDX)
060700         PERFORM FIND-THE-NUMERIC-LENGTH
060800         IF CONSTANT-LENGTH > 16
060900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
061000             MOVE NUMERIC-LITERAL-LITERAL TO UPPER-MSG-FIELD
061100         ELSE
061200             MOVE 0 TO DECIMAL-COUNT
061300             PERFORM DECIMAL-TEST
061400             IF DECIMAL-COUNT > 9
061500                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
061600                 MOVE NINE-DECIMAL-LITERAL TO UPPER-MSG-FIELD
061700             ELSE
061800                 NEXT SENTENCE
061900     ELSE
062000     IF TEST-CHAR (1) IS NOT NUMERIC
062100         MOVE "X" TO LITERAL-FIELD (VALIDX)
062200         MOVE ID-FIELD (VALIDX) TO POSITION-NAME-IN
062300         PERFORM POSITION-THE-NAME
062400         MOVE POSITION-NAME-OUT TO ID-FIELD (VALIDX)
062500         MOVE ID-FIELD (VALIDX) TO TEST-NAME
062600         PERFORM IS-THE-NAME-THERE
062700         IF NOT SCREEN-ENTRY-ERROR
062800              IF (ITS-RFL  AND
062900               CTL-FIELDS-TYPE (RFL-IDX) NOT = "N")  OR
063000               (ITS-NFL  AND
063100                NF-TYPE (CKIDX) NOT = "P")
063200                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
063300                 MOVE INVALID-NUMERIC-LITERAL TO
063400                     UPPER-MSG-FIELD
063500             ELSE
063600             IF ITS-RFL AND
063700                CTL-FIELDS-TYPE (RFL-IDX) = "N"
063800                 MOVE RFL-ORIGIN (RFL-IDX)
063900                     TO ORIGIN-FIELD (VALIDX)
064000             ELSE
064100             IF ITS-NFL  AND
064200                NF-TYPE (CKIDX) = "P"
064300                 MOVE 3 TO ORIGIN-FIELD (VALIDX).
064400
064500 IS-THE-NAME-THERE.
064600     MOVE SPACE TO WHERE-ITS-AT.
064700     MOVE 0 TO RFL-IDX.
064800     MOVE 0 TO CKIDX.
064900     PERFORM CHECK-RFL-NAME.
065000     PERFORM CHECK-NFL-NAME.
065100
065200     IF RFL-IDX > 80  AND
065300        CKIDX > 10
065400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
065500         MOVE INVALID-NAME-LITERAL TO UPPER-MSG-FIELD.
065600
065700 CHECK-RFL-NAME.
065800     ADD 1 TO RFL-IDX.
065900     IF RFL-IDX NOT > 80
066000         IF NAME-FIELD (VALIDX) NOT = RFL-NAME (RFL-IDX)
066100             IF NAME-FIELD (VALIDX) NOT = CTL-FIELDS-AKA (RFL-IDX)
066200                 GO TO CHECK-RFL-NAME
066300             ELSE
066400                 MOVE "R" TO WHERE-ITS-AT
066500         ELSE
066600         IF RFL-OCCURRENCE-A (RFL-IDX) NOT  = SPACES  AND
066700            RFL-OCCURRENCE-A (RFL-IDX) NOT  = ZEROES  AND
066800            OCCURS-FIELD (VALIDX) NOT =
066900                    RFL-OCCURRENCE-A (RFL-IDX)
067000             GO TO CHECK-RFL-NAME
067100         ELSE
067200             MOVE "R" TO WHERE-ITS-AT.
067300
067400 CHECK-NFL-NAME.
067500     ADD 1 TO CKIDX.
067600     IF CKIDX NOT > 10
067700         IF CKIDX = NF-IDX
067800             MOVE 11 TO CKIDX
067900         ELSE
068000         IF NAME-FIELD (VALIDX) NOT = NF-NAME (CKIDX)
068100             GO TO CHECK-NFL-NAME
068200         ELSE
068300             MOVE "N" TO WHERE-ITS-AT.
068400
068500 FIND-THE-LENGTH.
068600     MOVE 1 TO TSTIDX.
068800     PERFORM CHECK-THE-LENGTH.
068900
069000 CHECK-THE-LENGTH.
069100     ADD 1 TO TSTIDX.
069200     IF TSTIDX NOT > 20
069300         IF TEST-CHAR (TSTIDX) NOT = """"
069500             GO TO CHECK-THE-LENGTH.
069600
069700 FIND-THE-NUMERIC-LENGTH.
069800     MOVE 0 TO TSTIDX.
069900     MOVE 0 TO CONSTANT-LENGTH.
070000     PERFORM CHECK-NUM-LENGTH.
070100
070200 CHECK-NUM-LENGTH.
070300     ADD 1 TO TSTIDX.
070400     IF TSTIDX NOT > 20  AND
070500        TEST-CHAR (TSTIDX) NOT = SPACE
070600         IF    TEST-CHAR (TSTIDX) = "."
000000            OR TEST-CHAR(TSTIDX) = "+"
000000            OR TEST-CHAR(TSTIDX) = "-"
070700             GO TO CHECK-NUM-LENGTH
070800         ELSE
070900             IF TEST-CHAR (TSTIDX) IS NOT NUMERIC
071000                 MOVE 17 TO CONSTANT-LENGTH
071100             ELSE
071200                 ADD 1 TO CONSTANT-LENGTH
071300                 GO TO CHECK-NUM-LENGTH.
071400
071500 DECIMAL-TEST.
071600     MOVE 0 TO DEC-IDX.
071700     PERFORM LOOK-FOR-DEC.
071800
071900 LOOK-FOR-DEC.
072000     ADD 1 TO DEC-IDX.
072100     IF DEC-IDX NOT > 20
072200         IF TEST-CHAR (DEC-IDX) NOT = "."
072300             GO TO LOOK-FOR-DEC
072400         ELSE
072500             PERFORM COUNT-THE-DEC.
072600
072700 COUNT-THE-DEC.
072800     ADD 1 TO DEC-IDX.
072900     IF DEC-IDX NOT > 20
073000         IF TEST-CHAR (DEC-IDX) IS NUMERIC
073100             ADD 1 TO DECIMAL-COUNT
073200             GO TO COUNT-THE-DEC.
073300
073400 JUST-THE-FACS.
073500     MOVE UPPER-ENTRY-FAC TO
073600         FAC OF EXPFLD-CONSTANT-1-ITEM
073700         FAC OF EXPFLD-CONSTANT-2-ITEM
073800         FAC OF EXPFLD-CONSTANT-3-ITEM
073900         FAC OF EXPFLD-CONSTANT-4-ITEM
074000         FAC OF EXPFLD-CONSTANT-5-ITEM
074100         FAC OF EXPFLD-CONSTANT-6-ITEM
074200         FAC OF EXPFLD-CONSTANT-7-ITEM
074300         FAC OF EXPFLD-CONSTANT-8-ITEM
074400         FAC OF EXPFLD-CONSTANT-9-ITEM
074500         FAC OF EXPFLD-CONSTANT-10-ITEM
074600         FAC OF EXPFLD-OP-1-ITEM
074700         FAC OF EXPFLD-OP-2-ITEM
074800         FAC OF EXPFLD-OP-3-ITEM
074900         FAC OF EXPFLD-OP-4-ITEM
075000         FAC OF EXPFLD-OP-5-ITEM
075100         FAC OF EXPFLD-OP-6-ITEM
075200         FAC OF EXPFLD-OP-7-ITEM
075300         FAC OF EXPFLD-OP-8-ITEM
075400         FAC OF EXPFLD-OP-9-ITEM.
075500     MOVE HIDE-FAC TO
075600         FAC OF EXPFLD-OP-10-ITEM.
075700
075800 POSITION-THE-NAME.
075900     MOVE 0 TO LEFT-PAREN-COUNT.
076000     INSPECT POSITION-NAME-IN
076100         TALLYING LEFT-PAREN-COUNT
076200         FOR ALL "(".
076300
076400     MOVE 0 TO RIGHT-PAREN-COUNT.
076500     INSPECT POSITION-NAME-IN
076600         TALLYING RIGHT-PAREN-COUNT
076700         FOR ALL ")".
076800
076900     IF LEFT-PAREN-COUNT NOT = RIGHT-PAREN-COUNT
077000         MOVE POSITION-NAME-IN TO POSITION-NAME-OUT
077100     ELSE
077200     IF LEFT-PAREN-COUNT NOT = 1
077300         MOVE POSITION-NAME-IN TO POSITION-NAME-OUT
077400     ELSE
077500         MOVE SPACES TO POSITION-NAME-OUT
077600         MOVE 1 TO PAREN-LOCATION
077700         PERFORM LOCATE-THE-LEFT-PAREN
077800         MOVE 1 TO POS-POINTER
077900         PERFORM MOVE-IN-NAME-TO-OUT
078000         PERFORM MOVE-IN-OCCURS-TO-OUT.
078100
078200 LOCATE-THE-LEFT-PAREN.
078300     IF POS-IN-CHAR (PAREN-LOCATION) NOT = "("
078400         ADD 1 TO PAREN-LOCATION
078500         IF PAREN-LOCATION NOT > 12
078600             GO TO LOCATE-THE-LEFT-PAREN.
078700 MOVE-IN-NAME-TO-OUT.
078800     IF POS-POINTER < PAREN-LOCATION
078900         MOVE POS-IN-CHAR (POS-POINTER) TO
079000                         POS-OUT-CHAR (POS-POINTER)
079100     ELSE
079200         MOVE SPACE TO POS-OUT-CHAR (POS-POINTER).
079300     ADD 1 TO POS-POINTER.
079400     IF POS-POINTER NOT > 8
079500         GO TO MOVE-IN-NAME-TO-OUT.
079600 MOVE-IN-OCCURS-TO-OUT.
079700     IF PAREN-LOCATION > 12
079800         MOVE SPACES TO POS-OUT-OCCURS
079900     ELSE
080000         ADD 1 TO PAREN-LOCATION
080100         PERFORM POSITION-OCCURS-1-OUT.
080200     IF POS-OUT-OCCURS NOT = SPACES
080300         ADD 1 TO PAREN-LOCATION
080400         PERFORM POSITION-OCCURS-2-OUT.
080500     IF POS-OUT-OCCURS NOT = SPACES
080600         MOVE "(" TO POS-OUT-LT-PAREN
080700         MOVE ")" TO POS-OUT-RT-PAREN.
080800 POSITION-OCCURS-1-OUT.
080900     IF POS-IN-CHAR (PAREN-LOCATION) < 0  OR
081000        POS-IN-CHAR (PAREN-LOCATION) > 9
081100         MOVE SPACES TO POS-OUT-OCCURS.
081200     IF POS-IN-CHAR (PAREN-LOCATION) NOT < 0  AND
081300        POS-IN-CHAR (PAREN-LOCATION) NOT > 9
081400         MOVE POS-IN-CHAR (PAREN-LOCATION) TO
081500                 POS-OUT-OCC-1.
081600 POSITION-OCCURS-2-OUT.
081700     IF POS-IN-CHAR (PAREN-LOCATION) = ")"
081800         IF POS-OUT-OCC-1 = 0
081900             MOVE SPACES TO POS-OUT-OCCURS
082000         ELSE
082100             MOVE POS-OUT-OCC-1 TO POS-OUT-OCC-2
082200             MOVE 0 TO POS-OUT-OCC-1
082300     ELSE
082400     IF POS-IN-CHAR (PAREN-LOCATION) NOT > 9  AND
082500        POS-IN-CHAR (PAREN-LOCATION) NOT < 0
082600         MOVE POS-IN-CHAR (PAREN-LOCATION) TO POS-OUT-OCC-2
082700     ELSE
082800         MOVE SPACES TO POS-OUT-OCCURS.
082900
083000     IF POS-OUT-OCC-1 = 0  AND
083100        POS-OUT-OCC-2 = 0
083200         MOVE SPACES TO POS-OUT-OCCURS.
083300
083400
000000     COPY PLKTRACE.
