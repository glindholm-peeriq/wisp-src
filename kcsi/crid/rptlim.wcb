000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTLIM.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  11/17/89.
000500 DATE-COMPILED.
000600*----------------------------------------------------------------
000700* DEFINES THE DATA LIMITS
000800*----------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800     COPY SLCRT.
001900
002000 DATA DIVISION.
002100 FILE SECTION.
002200     COPY FDCRT.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptlim.wcb 1.10 9/11/94".
002500     COPY WSINT.
002600     COPY WSCRT.
002700
002800 01  DATA-LIMITS-SCREEN USAGE IS DISPLAY-WS.
002900     05  FILLER
003000         PIC X(11) ROW 1 COLUMN 35 VALUE
003100         "DATA LIMITS".
003200     05  UPPER-MSG-ITEM
003300         PIC X(78) ROW 2 COLUMN 3
003400         SOURCE  UPPER-MSG-FIELD.
003500     05  FILLER
003600         PIC X(71) ROW 3 COLUMN 2 VALUE
003700         "     This option allows the user to select the records t
003800-        "o be printed by".
003900     05  FILLER
004000         PIC X(69) ROW 4 COLUMN 2 VALUE
004100         "      defining limits on the values of certain fields in
004200-        " the records.".
004300     05  FILLER
004400         PIC X(34) ROW 5 COLUMN 2 VALUE
004500         "      The allowed 'OP' values are:".
004600     05  FILLER
004700         PIC X(64) ROW 6 COLUMN 2 VALUE
004800         "            GT = Greater Than      GE = Greater than or
004900-        "Equal to".
005000     05  FILLER
005100         PIC X(61) ROW 7 COLUMN 2 VALUE
005200         "            LT = Less Than         LE = Less than or Equ
005300-        "al to".
005400     05  FILLER
005500         PIC X(52) ROW 8 COLUMN 2 VALUE
005600         "            EQ = Equal to          NE = Not Equal to".
005700     05  FILLER
005800         PIC X(76) ROW 10 COLUMN 2 VALUE
005900         "  Field ID   OP     Field/Constant    And/Or  OP     Fie
006000-        "ld/Constant   And/Or".
006100     05  DATLIM-FIELD-ID-ITEM
006200         PIC X(12) ROW 11 COLUMN 2
006300         SOURCE DATLIM-FIELD-ID-FIELD
006400         OBJECT DATLIM-FIELD-ID-FIELD.
006500     05  DATLIM-OP-1-ITEM
006600         PIC XX ROW 11 COLUMN 15
006700         SOURCE DATLIM-OP-1-FIELD
006800         OBJECT DATLIM-OP-1-FIELD.
006900     05  DATLIM-CONSTANT-1-ITEM
007000         PIC X(20) ROW 11 COLUMN 19
007100         SOURCE DATLIM-CONSTANT-1-FIELD
007200         OBJECT DATLIM-CONSTANT-1-FIELD.
007300     05  DATLIM-CONNECT-1-ITEM
007400         PIC XXX ROW 11 COLUMN 42
007500         SOURCE DATLIM-CONNECT-1-FIELD
007600         OBJECT DATLIM-CONNECT-1-FIELD.
007700     05  DATLIM-OP-2-ITEM
007800         PIC XX ROW 11 COLUMN 48
007900         SOURCE DATLIM-OP-2-FIELD
008000         OBJECT DATLIM-OP-2-FIELD.
008100     05  DATLIM-CONSTANT-2-ITEM
008200         PIC X(20) ROW 11 COLUMN 52
008300         SOURCE DATLIM-CONSTANT-2-FIELD
008400         OBJECT DATLIM-CONSTANT-2-FIELD.
008500     05  DATLIM-CONNECT-2-ITEM
008600         PIC XXX ROW 11 COLUMN 74
008700         SOURCE DATLIM-CONNECT-2-FIELD
008800         OBJECT DATLIM-CONNECT-2-FIELD.
008900     05  DATLIM-OP-3-ITEM
009000         PIC XX ROW 12 COLUMN 15
009100         SOURCE DATLIM-OP-3-FIELD
009200         OBJECT DATLIM-OP-3-FIELD.
009300     05  DATLIM-CONSTANT-3-ITEM
009400         PIC X(20) ROW 12 COLUMN 19
009500         SOURCE DATLIM-CONSTANT-3-FIELD
009600         OBJECT DATLIM-CONSTANT-3-FIELD.
009700     05  DATLIM-CONNECT-3-ITEM
009800         PIC XXX ROW 12 COLUMN 42
009900         SOURCE DATLIM-CONNECT-3-FIELD
010000         OBJECT DATLIM-CONNECT-3-FIELD.
010100     05  DATLIM-OP-4-ITEM
010200         PIC XX ROW 12 COLUMN 48
010300         SOURCE DATLIM-OP-4-FIELD
010400         OBJECT DATLIM-OP-4-FIELD.
010500     05  DATLIM-CONSTANT-4-ITEM
010600         PIC X(20) ROW 12 COLUMN 52
010700         SOURCE DATLIM-CONSTANT-4-FIELD
010800         OBJECT DATLIM-CONSTANT-4-FIELD.
010900     05  DATLIM-CONNECT-4-ITEM
011000         PIC XXX ROW 12 COLUMN 74
011100         SOURCE DATLIM-CONNECT-4-FIELD
011200         OBJECT DATLIM-CONNECT-4-FIELD.
011300     05  DATLIM-OP-5-ITEM
011400         PIC XX ROW 13 COLUMN 15
011500         SOURCE DATLIM-OP-5-FIELD
011600         OBJECT DATLIM-OP-5-FIELD.
011700     05  DATLIM-CONSTANT-5-ITEM
011800         PIC X(20) ROW 13 COLUMN 19
011900         SOURCE DATLIM-CONSTANT-5-FIELD
012000         OBJECT DATLIM-CONSTANT-5-FIELD.
012100     05  DATLIM-CONNECT-5-ITEM
012200         PIC XXX ROW 13 COLUMN 42
012300         SOURCE DATLIM-CONNECT-5-FIELD
012400         OBJECT DATLIM-CONNECT-5-FIELD.
012500     05  DATLIM-OP-6-ITEM
012600         PIC XX ROW 13 COLUMN 48
012700         SOURCE DATLIM-OP-6-FIELD
012800         OBJECT DATLIM-OP-6-FIELD.
012900     05  DATLIM-CONSTANT-6-ITEM
013000         PIC X(20) ROW 13 COLUMN 52
013100         SOURCE DATLIM-CONSTANT-6-FIELD
013200         OBJECT DATLIM-CONSTANT-6-FIELD.
013300     05  DATLIM-CONNECT-6-ITEM
013400         PIC XXX ROW 13 COLUMN 74
013500         SOURCE DATLIM-CONNECT-6-FIELD
013600         OBJECT DATLIM-CONNECT-6-FIELD.
013700     05  DATLIM-OP-7-ITEM
013800         PIC XX ROW 14 COLUMN 15
013900         SOURCE DATLIM-OP-7-FIELD
014000         OBJECT DATLIM-OP-7-FIELD.
014100     05  DATLIM-CONSTANT-7-ITEM
014200         PIC X(20) ROW 14 COLUMN 19
014300         SOURCE DATLIM-CONSTANT-7-FIELD
014400         OBJECT DATLIM-CONSTANT-7-FIELD.
014500     05  DATLIM-CONNECT-7-ITEM
014600         PIC XXX ROW 14 COLUMN 42
014700         SOURCE DATLIM-CONNECT-7-FIELD
014800         OBJECT DATLIM-CONNECT-7-FIELD.
014900     05  DATLIM-OP-8-ITEM
015000         PIC XX ROW 14 COLUMN 48
015100         SOURCE DATLIM-OP-8-FIELD
015200         OBJECT DATLIM-OP-8-FIELD.
015300     05  DATLIM-CONSTANT-8-ITEM
015400         PIC X(20) ROW 14 COLUMN 52
015500         SOURCE DATLIM-CONSTANT-8-FIELD
015600         OBJECT DATLIM-CONSTANT-8-FIELD.
015700     05  DATLIM-CONNECT-8-ITEM
015800         PIC XXX ROW 14 COLUMN 74
015900         SOURCE DATLIM-CONNECT-8-FIELD
016000         OBJECT DATLIM-CONNECT-8-FIELD.
016100     05  DATLIM-OP-9-ITEM
016200         PIC XX ROW 15 COLUMN 15
016300         SOURCE DATLIM-OP-9-FIELD
016400         OBJECT DATLIM-OP-9-FIELD.
016500     05  DATLIM-CONSTANT-9-ITEM
016600         PIC X(20) ROW 15 COLUMN 19
016700         SOURCE DATLIM-CONSTANT-9-FIELD
016800         OBJECT DATLIM-CONSTANT-9-FIELD.
016900     05  DATLIM-CONNECT-9-ITEM
017000         PIC XXX ROW 15 COLUMN 42
017100         SOURCE DATLIM-CONNECT-9-FIELD
017200         OBJECT DATLIM-CONNECT-9-FIELD.
017300     05  DATLIM-OP-10-ITEM
017400         PIC XX ROW 15 COLUMN 48
017500         SOURCE DATLIM-OP-10-FIELD
017600         OBJECT DATLIM-OP-10-FIELD.
017700     05  DATLIM-CONSTANT-10-ITEM
017800         PIC X(20) ROW 15 COLUMN 52
017900         SOURCE DATLIM-CONSTANT-10-FIELD
018000         OBJECT DATLIM-CONSTANT-10-FIELD.
018100     05  FILLER
018200         PIC X(53) ROW 16 COLUMN 2 VALUE
018300         "    The Logical Connector to the Next Set of Limits =".
018400     05  DATLIM-CONNECT-10-ITEM
018500         PIC XXX ROW 16 COLUMN 56
018600         SOURCE DATLIM-CONNECT-10-FIELD
018700         OBJECT DATLIM-CONNECT-10-FIELD.
018800     05  FILLER
018900         PIC X(8) ROW 16 COLUMN 62 VALUE
019000         "(AND/OR)".
019100     05  FILLER
019200         PIC X(71) ROW 18 COLUMN 2 VALUE
019300         "    Notes:  The first blank 'AND/OR' defines the end of
019400-        "the expression.".
019500     05  FILLER
019600         PIC X(75) ROW 19 COLUMN 2 VALUE
019700         "            Press (ENTER) to define another set of limit
019800-        "s. (Maximum is ten)".
019900     05  FILLER
020000         PIC X(56) ROW 20 COLUMN 2 VALUE
020100         "            Press (13) to delete this set of limits.".
	   COPY RPTPFMNU.
021000
021100 01  DATA-LIMITS-TABLE.
021200     05  UPPER-MSG-FIELD    PIC X(78) VALUE SPACES.
021300     05  DATA-LIMITS-FIELDS.
021400         07  DATLIM-FIELD-ID-FIELD     PIC X(12) VALUE SPACE.
021500         07  DATLIM-OP-1-FIELD         PIC XX VALUE SPACE.
021600         07  DATLIM-CONSTANT-1-FIELD   PIC X(20) VALUE SPACE.
021700         07  DATLIM-CONNECT-1-FIELD    PIC XXX VALUE SPACE.
021800         07  DATLIM-OP-2-FIELD         PIC XX VALUE SPACE.
021900         07  DATLIM-CONSTANT-2-FIELD   PIC X(20) VALUE SPACE.
022000         07  DATLIM-CONNECT-2-FIELD    PIC XXX VALUE SPACE.
022100         07  DATLIM-OP-3-FIELD         PIC XX VALUE SPACE.
022200         07  DATLIM-CONSTANT-3-FIELD   PIC X(20) VALUE SPACE.
022300         07  DATLIM-CONNECT-3-FIELD    PIC XXX VALUE SPACE.
022400         07  DATLIM-OP-4-FIELD         PIC XX VALUE SPACE.
022500         07  DATLIM-CONSTANT-4-FIELD   PIC X(20) VALUE SPACE.
022600         07  DATLIM-CONNECT-4-FIELD    PIC XXX VALUE SPACE.
022700         07  DATLIM-OP-5-FIELD         PIC XX VALUE SPACE.
022800         07  DATLIM-CONSTANT-5-FIELD   PIC X(20) VALUE SPACE.
022900         07  DATLIM-CONNECT-5-FIELD    PIC XXX VALUE SPACE.
023000         07  DATLIM-OP-6-FIELD         PIC XX VALUE SPACE.
023100         07  DATLIM-CONSTANT-6-FIELD   PIC X(20) VALUE SPACE.
023200         07  DATLIM-CONNECT-6-FIELD    PIC XXX VALUE SPACE.
023300         07  DATLIM-OP-7-FIELD         PIC XX VALUE SPACE.
023400         07  DATLIM-CONSTANT-7-FIELD   PIC X(20) VALUE SPACE.
023500         07  DATLIM-CONNECT-7-FIELD    PIC XXX VALUE SPACE.
023600         07  DATLIM-OP-8-FIELD         PIC XX VALUE SPACE.
023700         07  DATLIM-CONSTANT-8-FIELD   PIC X(20) VALUE SPACE.
023800         07  DATLIM-CONNECT-8-FIELD    PIC XXX VALUE SPACE.
023900         07  DATLIM-OP-9-FIELD         PIC XX VALUE SPACE.
024000         07  DATLIM-CONSTANT-9-FIELD   PIC X(20) VALUE SPACE.
024100         07  DATLIM-CONNECT-9-FIELD    PIC XXX VALUE SPACE.
024200         07  DATLIM-OP-10-FIELD        PIC XX VALUE SPACE.
024300         07  DATLIM-CONSTANT-10-FIELD  PIC X(20) VALUE SPACE.
024400         07  DATLIM-CONNECT-10-FIELD   PIC XXX VALUE SPACE.
024500     05  VAL-ENTRY-TABLE REDEFINES DATA-LIMITS-FIELDS.
024600         07  MAIN-ID-FIELD.
024700             09  MAIN-NAME           PIC X(08).
024800             09  MAIN-O.
024900                 11  MAIN-LEFT-PAREN    PIC X(01).
025000                 11  MAIN-OCCURS         PIC X(02).
025100                 11  MAIN-RIGHT-PAREN     PIC X(01).
025200         07  EXPRESS-NEW-FIELD-LINE OCCURS 10 TIMES.
025300             09  OP-FIELD            PIC X(02).
025400             09  CONSTANT-FIELD      PIC X(20).
025500             09  ID-FIELD REDEFINES CONSTANT-FIELD.
025600                 11  NAME-FIELD      PIC X(08).
025700                 11  O-FIELD.
025800                     13  LEFT-PAREN      PIC X(01).
025900                     13  OCCURS-FIELD    PIC X(02).
026000                     13  RIGHT-PAREN     PIC X(01).
026100                     13  FILLER          PIC X(08).
026200             09  CONNECT-FIELD       PIC X(03).
026300     05  MAIN-IDX                    PIC 9(02) VALUE 0.
026400     05  SAVE-RFL-IDX                PIC 9(02) OCCURS 10 TIMES.
026500     05  LITERAL-FIELD               PIC X(01) OCCURS 10 TIMES.
026600     05  ORIGIN-FIELD                PIC 9(01) OCCURS 10 TIMES.
026700     05  MAIN-ORIGIN                 PIC 9(01).
026800     05  IDX                         PIC 9(02) VALUE 0.
026900     05  FIELD-TYPE                  PIC X(01) VALUE SPACES.
027000     05  LAST-OP-IDX                 PIC 9(01) VALUE 0.
027100     05  TEST-NAME                   PIC X(08) VALUE SPACES.
027200
027300 01  MESSAGE-LITERALS.
027400     05  NINE-DECIMAL-LITERAL  PIC X(78) VALUE
027500         "* Error * More than 9 decimal positions are invalid".
027600     05  INVALID-NAME-LITERAL  PIC X(78) VALUE
027700         "* Error * Field has not been selected from processing".
027800     05  FIRST-FC-LITERAL  PIC X(78) VALUE
027900         "* Error * First F/C cannot be blank".
028000     05  MUST-HAVE-FC-LITERAL  PIC X(78) VALUE
028100         "* Error * F/C cannot be blank".
028200     05  DOUBLE-QUOTE-LITERAL  PIC X(78) VALUE
028300         "* Error * Literal must be delimited by double quotes".
028400     05  INVALID-CONSTANT-LITERAL  PIC X(78) VALUE
028500         "* Error * Field is not a character field".
028600     05  INVALID-NUMERIC-LITERAL   PIC X(78) VALUE
028700         "* Error * Field is not a numeric field".
028800     05  NUMERIC-LITERAL-LITERAL  PIC X(78) VALUE
028900         "* Error * Field must be a numeric literal".
029000     05  INVALID-OP-LITERAL  PIC X(78) VALUE
029100         "* Error * Invalid OP".
029200     05  INVALID-CONNECT-LITERAL  PIC X(78) VALUE
029300         "* Error * Must be 'AND' or 'OR'".
029400
029500 01  CONSTANT-FLAG               PIC X(01) VALUE "N".
029600     88  ITS-A-CONSTANT          VALUE "Y".
029700
029800 01  DUPLICATE-LIMITS-SCREEN USAGE IS DISPLAY-WS.
029900     05  FILLER
030000         PIC X(42) ROW 5 COLUMN 2 VALUE
030100         "                                   Warning".
030200     05  FILLER
030300         PIC X(64) ROW 8 COLUMN 2 VALUE
030400         "                Limits for this fields have already been
030500-        " defined".
030600     05  FILLER
030700         PIC X(66) ROW 10 COLUMN 2 VALUE
030800         "            Press (1) if you desire the old limits to be
030900-        " replaced.".
031000     05  FILLER
031100         PIC X(65) ROW 11 COLUMN 2 VALUE
031200         "            Press (16) to discard this screen and define
031300-        " another.".
031400
031500 01  TSTIDX                      PIC 9(02) VALUE 0.
031600 01  VALIDX                      PIC 9(02) VALUE 0.
031700 01  CONSTANT-LENGTH             PIC 9(02) VALUE 0.
031800 01  DEC-IDX                     PIC 9(02) VALUE 0.
031900 01  EX-IDX                      PIC 9(02) VALUE 0.
032000 01  DECIMAL-COUNT               PIC 9(02) VALUE 0.
032100 01  LAST-IDX                    PIC 9(02) VALUE 0.
032200
032300 01  POSITION-THE-NAME-FIELDS.
032400     05  POSITION-NAME-IN        PIC X(12) VALUE SPACES.
032500     05  FILLER REDEFINES POSITION-NAME-IN.
032600         07  POS-IN-CHAR         PIC X(01) OCCURS 12 TIMES.
032700     05  POSITION-NAME-OUT       PIC X(12) VALUE SPACES.
032800     05  FILLER  REDEFINES POSITION-NAME-OUT.
032900         07  POS-OUT-CHAR        PIC X(01) OCCURS 8 TIMES.
033000         07  POS-OUT-OCCURS.
033100             09  POS-OUT-LT-PAREN PIC X(01).
033200             09  POS-OUT-OCC-1    PIC X(01).
033300             09  POS-OUT-OCC-2    PIC X(01).
033400             09  POS-OUT-RT-PAREN PIC X(01).
033500     05  POSITIONING-COUNTERS.
033600         07  PAREN-LOCATION      PIC 9(02) VALUE 0.
033700         07  POS-POINTER         PIC 9(02) VALUE 0.
033800         07  RIGHT-PAREN-COUNT   PIC 9(02) VALUE 0.
033900         07  LEFT-PAREN-COUNT    PIC 9(02) VALUE 0.
034000
034100 01  TEST-TABLE.
034200     05  TEST-SPACE              PIC X(20) VALUE SPACES.
034300     05  FILLER REDEFINES TEST-SPACE.
034400         07 TEST-CHAR            PIC X(01) OCCURS 20 TIMES.
034500     05  WHERE-ITS-AT            PIC X(01) VALUE SPACES.
034600         88  NAME-FOUND          VALUE "R".
034700         88  ITS-RFL             VALUE "R".
034800
034900 01  SQ-IDX-1        BINARY VALUE 0.
035000 01  SQ-IDX-2        BINARY VALUE 0.
035100
035200 LINKAGE SECTION.
035300 01  RPT-PFKEY         PIC 9(02).
035400     COPY WSRPTCFL.
035500     COPY WSRPTREC.
035600
035700 PROCEDURE DIVISION USING
035800     RPT-PFKEY
035900     RPT-CTL-FIELDS
036000     RPT-RECORDS.
036100
036200 MAIN-LOGIC SECTION.
036300 PROGRAM-BEGIN.
000000     MOVE "RPTLIM BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
036400     PERFORM OPENING-PROCEDURE.
036500     PERFORM MAIN-PROCESS.
036600     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTLIM EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
036700 MAIN-LOGIC-EXIT.
036800     EXIT PROGRAM.
036900 MAIN-LOGIC-STOP.
037000     STOP RUN.
037100 LEVEL-2 SECTION.
037200 OPENING-PROCEDURE.
037300     OPEN I-O  CRT-FILE.
037400     MOVE 0 TO DL-IDX.
037500 CLOSING-PROCEDURE.
037600     CLOSE CRT-FILE.
037700
037800 MAIN-PROCESS.
037900     PERFORM INIT-THE-LIMIT.
038000     PERFORM PROCESS-THE-LIMIT.
038100     PERFORM SQUEEZE-THE-LIMITS.
038200
038300 SQUEEZE-THE-LIMITS.
038400     MOVE 1 TO SQ-IDX-1.
038500     MOVE 2 TO SQ-IDX-2.
038600     PERFORM SQUEEZE-UP.
038700
038800 SQUEEZE-UP.
038900     IF DL-DATA (SQ-IDX-1) NOT = SPACES
039000         ADD 1 TO SQ-IDX-1
039100         ADD 1 TO SQ-IDX-2
039200         IF SQ-IDX-1 NOT  > 10
039300             GO TO SQUEEZE-UP.
039400
039500     IF SQ-IDX-1 NOT > 10
039600         IF DL-DATA (SQ-IDX-2) NOT = SPACES
039700             MOVE DL-DATA (SQ-IDX-2) TO DL-DATA (SQ-IDX-1)
039800             MOVE SPACES TO DL-DATA (SQ-IDX-2)
039900             GO TO SQUEEZE-UP
040000         ELSE
040100             ADD 1 TO SQ-IDX-2
040200             IF SQ-IDX-2 NOT > 10
040300                 GO TO  SQUEEZE-UP.
040400
040500 INIT-THE-LIMIT.
040600     ADD 1 TO DL-IDX.
040700     IF DL-NAME (DL-IDX) NOT = SPACES
040800         MOVE SPACES TO DATA-LIMITS-FIELDS
040900         PERFORM LOAD-THE-LIMIT
041000         MOVE SPACES TO DL-DATA (DL-IDX)
041100     ELSE
041200         MOVE SPACES TO DATA-LIMITS-FIELDS
041300         MOVE "AND" TO CONNECT-FIELD (10).
041400
041500     PERFORM JUST-THE-FACS.
041600     MOVE DISPLAY-HI-FAC TO FAC OF UPPER-MSG-ITEM.
041700
041800 LOAD-THE-LIMIT.
041900     MOVE DL-NAME (DL-IDX) TO MAIN-NAME.
042000     IF DL-OCCURRENCE-A (DL-IDX) NOT = SPACES
042100         MOVE DL-OCCURRENCE-A (DL-IDX) TO MAIN-OCCURS
042200         MOVE "(" TO MAIN-LEFT-PAREN
042300         MOVE ")" TO MAIN-RIGHT-PAREN
042400     ELSE
042500         MOVE SPACES TO MAIN-O.
042600     MOVE DL-ORIGIN (DL-IDX) TO MAIN-ORIGIN.
042700     IF  DL-ORIGIN (DL-IDX)  = 2
042800         MOVE  1 TO RFL-IDX
042900         PERFORM LOAD-MAIN-AKA.
043000     MOVE 0 TO DL-ENTRY-IDX.
043100     PERFORM LOAD-THE-DL-ENTRY-DATA.
043200
043300 LOAD-MAIN-AKA.
043400     IF DL-NAME (DL-IDX) =  RFL-NAME (RFL-IDX)  AND
043500        RFL-ORIGIN (RFL-IDX)  = 2
043600         IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
043700            RFL-OCCURRENCE-A (RFL-IDX) NOT > 99  AND
043800            RFL-OCCURRENCE-A (RFL-IDX) =
043900                         DL-OCCURRENCE-A (DL-IDX)
044000             IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
044100                 MOVE CTL-FIELDS-AKA (RFL-IDX) TO MAIN-ID-FIELD
044200             ELSE
044300                 NEXT SENTENCE
044400         ELSE
044500             IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
044600                 MOVE CTL-FIELDS-AKA (RFL-IDX) TO MAIN-ID-FIELD.
044700     IF RFL-NAME (RFL-IDX) = SPACES
044800         MOVE 80 TO RFL-IDX.
044900     ADD 1 TO RFL-IDX.
045000     IF RFL-IDX NOT > 80
045100         GO TO LOAD-MAIN-AKA.
045200
045300 LOAD-THE-DL-ENTRY-DATA.
045400     ADD 1 TO DL-ENTRY-IDX.
045500     IF DL-ENTRY-IDX NOT > 10
045600         IF DL-LIT-20 (DL-IDX, DL-ENTRY-IDX) NOT = SPACES
045700             PERFORM LOAD-ONE-DL-ENTRY-SET
045800             GO TO LOAD-THE-DL-ENTRY-DATA.
045900
046000     IF DL-SET-CONNECTOR (DL-IDX) = "O"
046100         MOVE "OR " TO CONNECT-FIELD (10)
046200     ELSE
046300         MOVE "AND" TO CONNECT-FIELD (10).
046400
046500 LOAD-ONE-DL-ENTRY-SET.
046600     MOVE DL-NOT-LIT-CODE (DL-IDX, DL-ENTRY-IDX) TO
046700         LITERAL-FIELD (DL-ENTRY-IDX).
046800     MOVE DL-OP-CODE (DL-IDX, DL-ENTRY-IDX) TO
046900         OP-FIELD (DL-ENTRY-IDX).
047000
047100     IF DL-CONNECTOR (DL-IDX, DL-ENTRY-IDX) = "A"
047200         MOVE "AND" TO CONNECT-FIELD (DL-ENTRY-IDX)
047300     ELSE
047400     IF DL-CONNECTOR (DL-IDX, DL-ENTRY-IDX) = "O"
047500         MOVE "OR " TO CONNECT-FIELD (DL-ENTRY-IDX).
047600
047700     IF DL-NOT-LIT-CODE (DL-IDX, DL-ENTRY-IDX) NOT = "X"
047800         MOVE DL-LIT-20 (DL-IDX, DL-ENTRY-IDX) TO
047900             CONSTANT-FIELD (DL-ENTRY-IDX)
048000         MOVE ZERO TO ORIGIN-FIELD (DL-ENTRY-IDX)
048100     ELSE
048200         MOVE SPACES TO CONSTANT-FIELD (DL-ENTRY-IDX)
048300         MOVE DL-OP-NAME (DL-IDX, DL-ENTRY-IDX) TO
048400             NAME-FIELD (DL-ENTRY-IDX)
048500         MOVE DL-OP-ORIGIN (DL-IDX, DL-ENTRY-IDX) TO
048600             ORIGIN-FIELD (DL-ENTRY-IDX)
048700         MOVE DL-OP-OCCURRENCE-A (DL-IDX, DL-ENTRY-IDX) TO
048800             O-FIELD (DL-ENTRY-IDX)
048900         MOVE 1 TO RFL-IDX
049000         PERFORM LOAD-DL-OP-AKA.
049100
049200 LOAD-DL-OP-AKA.
049300     IF DL-OP-ORIGIN (DL-IDX, DL-ENTRY-IDX) NOT = 2
049400         MOVE 80 TO RFL-IDX
049500     ELSE
049600     IF DL-OP-NAME (DL-IDX, DL-ENTRY-IDX) = RFL-NAME (RFL-IDX)
049700        AND RFL-ORIGIN (RFL-IDX) = 2
049800         IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
049900            RFL-OCCURRENCE-A (RFL-IDX) NOT > 99  AND
050000            RFL-OCCURRENCE-A (RFL-IDX) =
050100                             OCCURS-FIELD (DL-ENTRY-IDX)
050200             IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
050300                 MOVE CTL-FIELDS-AKA (RFL-IDX) TO
050400                         ID-FIELD (DL-ENTRY-IDX)
050500             ELSE
050600                 NEXT SENTENCE
050700         ELSE
050800             IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
050900                 MOVE CTL-FIELDS-AKA (RFL-IDX) TO
051000                         ID-FIELD (DL-ENTRY-IDX).
051100
051200     IF RFL-NAME (RFL-IDX) = SPACES
051300         MOVE 80 TO RFL-IDX.
051400     ADD 1 TO RFL-IDX.
051500     IF RFL-IDX NOT > 80
051600         GO TO LOAD-DL-OP-AKA.
051700
051800 RELOAD-THE-LIMIT.
051900     MOVE 0 TO DL-ENTRY-IDX.
052000     PERFORM RELOAD-A-LIMIT .
052100
052200 RELOAD-A-LIMIT.
052300     IF MAIN-NAME = SPACES
052400         PERFORM RELOAD-BLANK-LIMIT
052500     ELSE
052600         PERFORM RELOAD-ONE-LIMIT.
052700
052800 RELOAD-BLANK-LIMIT.
052900     MOVE SPACES TO DL-DATA (DL-IDX).
053000
053100 RELOAD-ONE-LIMIT.
053200     MOVE 1 TO RHD-DATA-LIMIT.
053300     IF CTL-FIELDS-AKA (MAIN-IDX) NOT = SPACES
053400         MOVE RFL-NAME (MAIN-IDX) TO DL-NAME (DL-IDX)
053500     ELSE
053600         MOVE MAIN-NAME TO DL-NAME (DL-IDX).
053700     IF MAIN-O NOT = SPACES
053800         MOVE MAIN-OCCURS TO DL-OCCURRENCE-A (DL-IDX)
053900     ELSE
054000         MOVE SPACES TO DL-OCCURRENCE-A (DL-IDX).
054100     MOVE MAIN-ORIGIN TO DL-ORIGIN (DL-IDX).
054200     PERFORM RELOAD-THE-DL-ENTRY-DATA.
054300
054400 RELOAD-THE-DL-ENTRY-DATA.
054500     ADD 1 TO DL-ENTRY-IDX.
054600     IF DL-ENTRY-IDX NOT > 10
054700         IF CONSTANT-FIELD (DL-ENTRY-IDX) NOT = SPACES
054800             PERFORM RELOAD-ONE-DL-ENTRY-SET
054900             GO TO RELOAD-THE-DL-ENTRY-DATA.
055000
055100     IF CONNECT-FIELD (10)  = "OR "
055200         MOVE "O" TO DL-SET-CONNECTOR (DL-IDX)
055300     ELSE
055400         MOVE "A" TO DL-SET-CONNECTOR (DL-IDX).
055500
055600 RELOAD-ONE-DL-ENTRY-SET.
055700     MOVE OP-FIELD (DL-ENTRY-IDX) TO
055800         DL-OP-CODE (DL-IDX, DL-ENTRY-IDX).
056100     IF DL-ENTRY-IDX NOT = 10
056200         MOVE CONNECT-FIELD (DL-ENTRY-IDX) TO
056300             DL-CONNECTOR (DL-IDX, DL-ENTRY-IDX).
056400
056500     IF LITERAL-FIELD (DL-ENTRY-IDX) NOT = "X"
056600         MOVE CONSTANT-FIELD (DL-ENTRY-IDX) TO
056700             DL-LIT-20 (DL-IDX, DL-ENTRY-IDX)
056800     ELSE
056900         MOVE SPACES TO DL-LIT-20 (DL-IDX, DL-ENTRY-IDX)
057000         MOVE ORIGIN-FIELD (DL-ENTRY-IDX) TO
057100             DL-OP-ORIGIN (DL-IDX, DL-ENTRY-IDX)
057200         MOVE NAME-FIELD (DL-ENTRY-IDX) TO
057300             DL-OP-NAME (DL-IDX, DL-ENTRY-IDX)
057400         PERFORM RELOAD-DL-OP-AKA
000000         MOVE "X" TO DL-NOT-LIT-CODE(DL-IDX, DL-ENTRY-IDX)
057500         MOVE O-FIELD (DL-ENTRY-IDX) TO
057600             DL-OP-OCCURRENCE-A (DL-IDX, DL-ENTRY-IDX).
000000
057700 RELOAD-DL-OP-AKA.
057800         MOVE SAVE-RFL-IDX (DL-ENTRY-IDX) TO RFL-IDX.
057900         IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
058000             MOVE RFL-NAME (RFL-IDX) TO
058100                    DL-OP-NAME (DL-IDX, DL-ENTRY-IDX)
058200             IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
058300                RFL-OCCURRENCE-A (RFL-IDX) NOT > 99
058400                 MOVE RFL-OCCURRENCE-A (RFL-IDX)
058500                         TO OCCURS-FIELD (DL-ENTRY-IDX)
058600                 MOVE "(" TO LEFT-PAREN (DL-ENTRY-IDX)
058700                 MOVE ")" TO RIGHT-PAREN (DL-ENTRY-IDX).
058800
058900 PROCESS-THE-LIMIT.
059000     MOVE SPACES TO SCREEN-ERROR-STATUS.
059100
059200     DISPLAY AND READ ALTERED DATA-LIMITS-SCREEN ON CRT-FILE
059300     PFKEYS 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 13, 16.
059400     MOVE SPACE TO UPPER-MSG-FIELD.
059500
059600     IF PFKEY-16-PRESSED
059700         MOVE 1 TO RPT-PFKEY
059800     ELSE
059900     IF PFKEY-13-PRESSED
060000         MOVE SPACES TO DL-DATA (DL-IDX)
060100                        DATA-LIMITS-FIELDS
060200         GO TO PROCESS-THE-LIMIT
060300     ELSE
060400     IF ENTER-KEY-PRESSED
060500         MOVE 9 TO RPT-PFKEY
060600     ELSE
060700         MOVE PFKEY-CODE TO RPT-PFKEY.
060800
060900     IF MAIN-NAME NOT = SPACES
061000         PERFORM VAL-THE-ENTRY
061100         IF SCREEN-ENTRY-ERROR
061200             GO TO PROCESS-THE-LIMIT
061300         ELSE
061400             PERFORM RELOAD-THE-LIMIT
061500             IF ENTER-KEY-PRESSED
000000                 IF DL-IDX NOT > 9
061600                     PERFORM INIT-THE-LIMIT
061700                     GO TO PROCESS-THE-LIMIT.
061800
061900 VAL-THE-ENTRY.
062000     IF MAIN-NAME NOT = SPACES
062100         PERFORM VAL-THE-MAIN.
062200     IF NOT SCREEN-ENTRY-ERROR AND
062300        MAIN-NAME NOT = SPACES
062400         MOVE 0 TO VALIDX
062500         PERFORM VAL-ONE-LINE.
062600
062700 VAL-THE-MAIN.
062800     MOVE SPACE TO WHERE-ITS-AT.
062900     MOVE 0 TO RFL-IDX.
063000     PERFORM CHECK-MAIN-NAME.
063100     IF RFL-IDX > 80
063200         SET ERROR-BIT IN FAC OF DATLIM-FIELD-ID-ITEM ON
063300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
063400         MOVE INVALID-NAME-LITERAL TO UPPER-MSG-FIELD.
063500
066200 CHECK-MAIN-NAME.
066300     ADD 1 TO RFL-IDX.
066400     IF RFL-IDX NOT > 80
066500         IF RFL-NAME (RFL-IDX) = SPACES
066600             MOVE 81 TO RFL-IDX.
066700
066800     IF RFL-IDX NOT > 80
066900         IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
067000             IF CTL-FIELDS-AKA (RFL-IDX) = MAIN-NAME
067100                 MOVE RFL-ORIGIN (RFL-IDX) TO MAIN-ORIGIN
067200                 MOVE RFL-IDX TO MAIN-IDX
067300                 MOVE "R" TO WHERE-ITS-AT
067400             ELSE
067500                 GO TO CHECK-MAIN-NAME.
067600
067700     IF RFL-IDX NOT > 80 AND
067800        WHERE-ITS-AT = SPACES
067900         IF RFL-NAME (RFL-IDX) =  MAIN-NAME
068000             IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01  AND
068100                RFL-OCCURRENCE-A (RFL-IDX) NOT > 99
068200                 IF RFL-OCCURRENCE-A (RFL-IDX) = MAIN-OCCURS
068300                     MOVE RFL-ORIGIN (RFL-IDX) TO MAIN-ORIGIN
068400                     MOVE RFL-IDX TO MAIN-IDX
068500                     MOVE "R" TO WHERE-ITS-AT
068600                 ELSE
068700                     GO TO CHECK-MAIN-NAME
068800             ELSE
068900                 MOVE RFL-IDX TO MAIN-IDX
069000                 MOVE RFL-ORIGIN (RFL-IDX) TO MAIN-ORIGIN
069100                 MOVE "R" TO WHERE-ITS-AT
069200         ELSE
069300             GO TO CHECK-MAIN-NAME.
069400
069500 VAL-ONE-LINE.
069600     ADD 1 TO VALIDX.
069700     IF VALIDX NOT > 10
069800         PERFORM VAL-THE-SET-OF-DATA
069900         IF NOT SCREEN-ENTRY-ERROR
070000             GO TO VAL-ONE-LINE.
070100
070200 VAL-THE-SET-OF-DATA.
070300     MOVE CTL-FIELDS-TYPE (MAIN-IDX) TO FIELD-TYPE.
070400     PERFORM VAL-OP.
070500     IF NOT SCREEN-ENTRY-ERROR
070600         PERFORM VAL-FIELD
070700         IF NOT SCREEN-ENTRY-ERROR
070800             IF CONNECT-FIELD (VALIDX) NOT = SPACES
070900                 PERFORM VAL-CONNECT.
071000
071100 FILL-IN-THE-BLANKS.
071200     MOVE SPACES TO CONSTANT-FIELD (VALIDX)
071300                    OP-FIELD (VALIDX)
071400                    CONNECT-FIELD (VALIDX).
071500
071600     IF VALIDX = 10
071700         MOVE "AND" TO CONNECT-FIELD (VALIDX).
071800
071900 VAL-FIELD.
072000     IF VALIDX = 1  AND
072100        CONSTANT-FIELD (VALIDX) = SPACES
072200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
072300         MOVE FIRST-FC-LITERAL TO UPPER-MSG-FIELD.
072400
072500     IF NOT SCREEN-ENTRY-ERROR  AND
072600        CONSTANT-FIELD (VALIDX) NOT = SPACES
072700         MOVE CONSTANT-FIELD (VALIDX) TO TEST-SPACE
072800         PERFORM TEST-THE-FIELD.
072900
073000     IF NOT SCREEN-ENTRY-ERROR  AND
073100         VALIDX NOT = 1
073200         COMPUTE LAST-IDX = VALIDX - 1
073300         IF CONNECT-FIELD (LAST-IDX) = SPACES
073400             MOVE SPACES TO CONSTANT-FIELD (VALIDX)
073500                            OP-FIELD (VALIDX)
073600         ELSE
073700         IF CONSTANT-FIELD (VALIDX) = SPACES OR
073800            OP-FIELD (VALIDX) = SPACES
073900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
074000             MOVE MUST-HAVE-FC-LITERAL TO UPPER-MSG-FIELD.
074100
074200     IF SCREEN-ENTRY-ERROR
074300         IF VALIDX = 1
074400             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-1-ITEM ON
074500         ELSE
074600         IF VALIDX = 2
074700             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-2-ITEM ON
074800         ELSE
074900         IF VALIDX = 3
075000             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-3-ITEM ON
075100         ELSE
075200         IF VALIDX = 4
075300             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-4-ITEM ON
075400         ELSE
075500         IF VALIDX = 5
075600             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-5-ITEM ON
075700         ELSE
075800         IF VALIDX = 6
075900             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-6-ITEM ON
076000         ELSE
076100         IF VALIDX = 7
076200             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-7-ITEM ON
076300         ELSE
076400         IF VALIDX = 8
076500             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-8-ITEM ON
076600         ELSE
076700         IF VALIDX = 9
076800             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-9-ITEM ON
076900         ELSE
077000         IF VALIDX = 10
077100             SET ERROR-BIT IN FAC OF DATLIM-CONSTANT-10-ITEM ON.
077200
077300 VAL-OP.
077400     IF OP-FIELD (VALIDX) NOT = "  "  AND
077500        OP-FIELD (VALIDX) NOT = "GT"  AND
077600        OP-FIELD (VALIDX) NOT = "GE"  AND
077700        OP-FIELD (VALIDX) NOT = "LT"  AND
077800        OP-FIELD (VALIDX) NOT = "LE"  AND
077900        OP-FIELD (VALIDX) NOT = "EQ"  AND
078000        OP-FIELD (VALIDX) NOT = "NE"
078100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
078200         MOVE INVALID-OP-LITERAL TO UPPER-MSG-FIELD.
078300
078400     IF OP-FIELD (VALIDX) = SPACES
078500         IF VALIDX = 1
078600             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
078700             MOVE INVALID-OP-LITERAL TO UPPER-MSG-FIELD
078800         ELSE
078900             COMPUTE LAST-OP-IDX = VALIDX - 1
079000             IF CONNECT-FIELD (LAST-OP-IDX) NOT = SPACES
079100                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
079200                 MOVE INVALID-OP-LITERAL TO UPPER-MSG-FIELD.
079300
079400     IF SCREEN-ENTRY-ERROR
079500         IF VALIDX = 1
079600             SET ERROR-BIT IN FAC OF DATLIM-OP-1-ITEM ON
079700         ELSE
079800         IF VALIDX = 2
079900             SET ERROR-BIT IN FAC OF DATLIM-OP-2-ITEM ON
080000         ELSE
080100         IF VALIDX = 3
080200             SET ERROR-BIT IN FAC OF DATLIM-OP-3-ITEM ON
080300         ELSE
080400         IF VALIDX = 4
080500             SET ERROR-BIT IN FAC OF DATLIM-OP-4-ITEM ON
080600         ELSE
080700         IF VALIDX = 5
080800             SET ERROR-BIT IN FAC OF DATLIM-OP-5-ITEM ON
080900         ELSE
081000         IF VALIDX = 6
081100             SET ERROR-BIT IN FAC OF DATLIM-OP-6-ITEM ON
081200         ELSE
081300         IF VALIDX = 7
081400             SET ERROR-BIT IN FAC OF DATLIM-OP-7-ITEM ON
081500         ELSE
081600         IF VALIDX = 8
081700             SET ERROR-BIT IN FAC OF DATLIM-OP-8-ITEM ON
081800         ELSE
081900         IF VALIDX = 9
082000             SET ERROR-BIT IN FAC OF DATLIM-OP-9-ITEM ON
082100         ELSE
082200         IF VALIDX = 10
082300             SET ERROR-BIT IN FAC OF DATLIM-OP-10-ITEM ON.
082400
082500 VAL-CONNECT.
082600     IF CONNECT-FIELD (VALIDX) NOT = "AND"  AND
082700        CONNECT-FIELD (VALIDX) NOT = "OR"
082800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
082900         MOVE INVALID-CONNECT-LITERAL TO UPPER-MSG-FIELD.
083000
083100     IF SCREEN-ENTRY-ERROR
083200         IF VALIDX = 1
083300             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-1-ITEM ON
083400         ELSE
083500         IF VALIDX = 2
083600             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-2-ITEM ON
083700         ELSE
083800         IF VALIDX = 3
083900             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-3-ITEM ON
084000         ELSE
084100         IF VALIDX = 4
084200             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-4-ITEM ON
084300         ELSE
084400         IF VALIDX = 5
084500             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-5-ITEM ON
084600         ELSE
084700         IF VALIDX = 6
084800             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-6-ITEM ON
084900         ELSE
085000         IF VALIDX = 7
085100             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-7-ITEM ON
085200         ELSE
085300         IF VALIDX = 8
085400             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-8-ITEM ON
085500         ELSE
085600         IF VALIDX = 9
085700             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-9-ITEM ON
085800         ELSE
085900         IF VALIDX = 10
086000             SET ERROR-BIT IN FAC OF DATLIM-CONNECT-10-ITEM ON.
086100
086200 TEST-THE-FIELD.
086300     IF FIELD-TYPE = "C"
086400         PERFORM TEST-CHARACTER-FIELD.
086500
086600     IF FIELD-TYPE = "N"
086700         PERFORM TEST-NUMERIC-FIELD.
086800
086900 TEST-CHARACTER-FIELD.
087000*     IF TEST-CHAR (1) = "'"
087100*         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
087200*         MOVE DOUBLE-QUOTE-LITERAL TO UPPER-MSG-FIELD
087300*     ELSE
087400     IF TEST-CHAR (1) = """"
087500         MOVE " " TO LITERAL-FIELD (VALIDX)
087600         PERFORM FIND-THE-LENGTH
087700         IF     TEST-CHAR (CONSTANT-LENGTH) NOT = """"
087700            AND TEST-CHAR (CONSTANT-LENGTH) NOT = "'"
087800             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
087900             MOVE DOUBLE-QUOTE-LITERAL TO UPPER-MSG-FIELD
088000         ELSE
088100             NEXT SENTENCE
088200     ELSE
088300     IF     TEST-CHAR (1) NOT = """"
088300       AND  TEST-CHAR (1) NOT = "'"
088400         MOVE "X" TO LITERAL-FIELD (VALIDX)
088500         MOVE ID-FIELD (VALIDX) TO POSITION-NAME-IN
088600         PERFORM POSITION-THE-NAME
088700         MOVE POSITION-NAME-OUT TO ID-FIELD (VALIDX)
088800         MOVE ID-FIELD (VALIDX) TO TEST-NAME
088900         PERFORM IS-THE-NAME-THERE
089000         IF NOT SCREEN-ENTRY-ERROR
089100             IF CTL-FIELDS-TYPE (RFL-IDX) NOT = "C"
089200                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
089300                 MOVE INVALID-CONSTANT-LITERAL TO
089400                     UPPER-MSG-FIELD
089500             ELSE
089600                 MOVE RFL-ORIGIN (RFL-IDX)
089700                     TO ORIGIN-FIELD (VALIDX).
089800
089900 TEST-NUMERIC-FIELD.
090000     IF TEST-CHAR (1) = "'"  OR
090100        TEST-CHAR (1) = """"
090200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
090300         MOVE NUMERIC-LITERAL-LITERAL TO UPPER-MSG-FIELD
090400     ELSE
090500     IF    TEST-CHAR (1) IS NUMERIC
000000        OR TEST-CHAR(1) = "+"
000000        OR TEST-CHAR(1) = "-"
000000        OR TEST-CHAR(1) = "."
090600         MOVE " " TO LITERAL-FIELD (VALIDX)
090700         PERFORM FIND-THE-NUMERIC-LENGTH
090800         IF CONSTANT-LENGTH > 16
090900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
091000             MOVE NUMERIC-LITERAL-LITERAL TO UPPER-MSG-FIELD
091100         ELSE
091200             MOVE 0 TO DECIMAL-COUNT
091300             PERFORM DECIMAL-TEST
091400             IF DECIMAL-COUNT > 9
091500                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
091600                 MOVE NINE-DECIMAL-LITERAL TO UPPER-MSG-FIELD
091700             ELSE
091800                 NEXT SENTENCE
091900     ELSE
092000     IF TEST-CHAR (1) IS NOT NUMERIC
092100         MOVE "X" TO LITERAL-FIELD (VALIDX)
092200         MOVE ID-FIELD (VALIDX) TO POSITION-NAME-IN
092300         PERFORM POSITION-THE-NAME
092400         MOVE POSITION-NAME-OUT TO ID-FIELD (VALIDX)
092500         MOVE ID-FIELD (VALIDX) TO TEST-NAME
092600         PERFORM IS-THE-NAME-THERE
092700         IF NOT SCREEN-ENTRY-ERROR
092800             IF CTL-FIELDS-TYPE (RFL-IDX) NOT = "N"
092900                 MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
093000                 MOVE INVALID-NUMERIC-LITERAL TO
093100                     UPPER-MSG-FIELD
093200             ELSE
093300                 MOVE RFL-ORIGIN (RFL-IDX)
093400                     TO ORIGIN-FIELD (VALIDX).
093500
093600 IS-THE-NAME-THERE.
093700     MOVE SPACE TO WHERE-ITS-AT.
093800     MOVE 0 TO RFL-IDX.
093900     PERFORM CHECK-RFL-NAME.
094000     IF RFL-IDX > 80
094100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
094200         MOVE INVALID-NAME-LITERAL TO UPPER-MSG-FIELD.
094300
094400 CHECK-RFL-NAME.
094500     ADD 1 TO RFL-IDX.
094600     IF RFL-IDX NOT > 80
094700         IF RFL-NAME (RFL-IDX) = SPACES
094800             MOVE 81 TO RFL-IDX.
094900
095000     IF RFL-IDX NOT > 80
095100         IF CTL-FIELDS-AKA (RFL-IDX) NOT = SPACES
095200             IF CTL-FIELDS-AKA (RFL-IDX) = NAME-FIELD (VALIDX)
095300                 MOVE RFL-ORIGIN (RFL-IDX)
095400                     TO ORIGIN-FIELD (VALIDX)
095500                 MOVE RFL-IDX
095600                     TO SAVE-RFL-IDX (VALIDX)
095700                 MOVE "R" TO WHERE-ITS-AT
095800             ELSE
095900                 GO TO CHECK-RFL-NAME.
096000
096100     IF RFL-IDX NOT > 80 AND
096200        WHERE-ITS-AT = SPACES
096300         IF RFL-NAME (RFL-IDX) =  NAME-FIELD (VALIDX)
096400             IF RFL-OCCURRENCE-A (RFL-IDX) NOT < 01 AND
096500                RFL-OCCURRENCE-A (RFL-IDX) NOT > 99
096600                 IF RFL-OCCURRENCE-A (RFL-IDX) =
096700                         OCCURS-FIELD (VALIDX)
096800                     MOVE RFL-ORIGIN (RFL-IDX)
096900                         TO ORIGIN-FIELD (VALIDX)
097000                     MOVE RFL-IDX
097100                         TO SAVE-RFL-IDX (VALIDX)
097200                     MOVE "R" TO WHERE-ITS-AT
097300                 ELSE
097400                     GO TO CHECK-RFL-NAME
097500             ELSE
097600                 MOVE RFL-ORIGIN (RFL-IDX)
097700                     TO ORIGIN-FIELD (VALIDX)
097800                 MOVE RFL-IDX
097900                     TO SAVE-RFL-IDX (VALIDX)
098000                 MOVE "R" TO WHERE-ITS-AT
098100         ELSE
098200             GO TO CHECK-RFL-NAME.
098300
098400 FIND-THE-LENGTH.
098500     MOVE 1 TO TSTIDX.
098600     MOVE 1 TO CONSTANT-LENGTH.
098700     PERFORM CHECK-THE-LENGTH.
098800
098900 CHECK-THE-LENGTH.
099000     ADD 1 TO TSTIDX.
099100     IF TSTIDX NOT > 20
099200         IF     TEST-CHAR (TSTIDX) NOT =  """"
000000            AND TEST-CHAR (TSTIDX) NOT = "'"
099300             ADD 1 TO CONSTANT-LENGTH
099400             GO TO CHECK-THE-LENGTH.
000000
000000     IF CONSTANT-LENGTH NOT > 19
000000         ADD 1 TO CONSTANT-LENGTH.
099500
099600 FIND-THE-NUMERIC-LENGTH.
099700     MOVE 0 TO TSTIDX.
099800     MOVE 0 TO CONSTANT-LENGTH.
099900     PERFORM CHECK-NUM-LENGTH.
100000
100100 CHECK-NUM-LENGTH.
100200     ADD 1 TO TSTIDX.
100300     IF TSTIDX NOT > 20  AND
100400        TEST-CHAR (TSTIDX) NOT = SPACE
100500         IF    TEST-CHAR (TSTIDX) = "."
100500            OR TEST-CHAR (TSTIDX) = "+"
100500            OR TEST-CHAR (TSTIDX) = "-"
100600             GO TO CHECK-NUM-LENGTH
100700         ELSE
100800             IF TEST-CHAR (TSTIDX) IS NOT NUMERIC
100900                 MOVE 17 TO CONSTANT-LENGTH
101000             ELSE
101100                 ADD 1 TO CONSTANT-LENGTH
101200                 GO TO CHECK-NUM-LENGTH.
101300
101400 DECIMAL-TEST.
101500     MOVE 0 TO DEC-IDX.
101600     PERFORM LOOK-FOR-DEC.
101700
101800 LOOK-FOR-DEC.
101900     ADD 1 TO DEC-IDX.
102000     IF DEC-IDX NOT > 20
102100         IF TEST-CHAR (DEC-IDX) NOT = "."
102200             GO TO LOOK-FOR-DEC
102300         ELSE
102400             PERFORM COUNT-THE-DEC.
102500
102600 COUNT-THE-DEC.
102700     ADD 1 TO DEC-IDX.
102800     IF DEC-IDX NOT > 20
102900         IF TEST-CHAR (DEC-IDX) IS NUMERIC
103000             ADD 1 TO DECIMAL-COUNT
103100             GO TO COUNT-THE-DEC.
103200
103300 JUST-THE-FACS.
103400     MOVE UPPER-ENTRY-FAC TO
103500         FAC OF DATLIM-CONSTANT-1-ITEM
103600         FAC OF DATLIM-CONSTANT-2-ITEM
103700         FAC OF DATLIM-CONSTANT-3-ITEM
103800         FAC OF DATLIM-CONSTANT-4-ITEM
103900         FAC OF DATLIM-CONSTANT-5-ITEM
104000         FAC OF DATLIM-CONSTANT-6-ITEM
104100         FAC OF DATLIM-CONSTANT-7-ITEM
104200         FAC OF DATLIM-CONSTANT-8-ITEM
104300         FAC OF DATLIM-CONSTANT-9-ITEM
104400         FAC OF DATLIM-CONSTANT-10-ITEM
104500         FAC OF DATLIM-OP-1-ITEM
104600         FAC OF DATLIM-OP-2-ITEM
104700         FAC OF DATLIM-OP-3-ITEM
104800         FAC OF DATLIM-OP-4-ITEM
104900         FAC OF DATLIM-OP-5-ITEM
105000         FAC OF DATLIM-OP-6-ITEM
105100         FAC OF DATLIM-OP-7-ITEM
105200         FAC OF DATLIM-OP-8-ITEM
105300         FAC OF DATLIM-OP-9-ITEM
105400         FAC OF DATLIM-OP-10-ITEM.
105500
105600 POSITION-THE-NAME.
105700     MOVE 0 TO LEFT-PAREN-COUNT.
105800     INSPECT POSITION-NAME-IN
105900         TALLYING LEFT-PAREN-COUNT
106000         FOR ALL "(".
106100
106200     MOVE 0 TO RIGHT-PAREN-COUNT.
106300     INSPECT POSITION-NAME-IN
106400         TALLYING RIGHT-PAREN-COUNT
106500         FOR ALL ")".
106600
106700     IF LEFT-PAREN-COUNT NOT = RIGHT-PAREN-COUNT
106800         MOVE POSITION-NAME-IN TO POSITION-NAME-OUT
106900     ELSE
107000     IF LEFT-PAREN-COUNT NOT = 1
107100         MOVE POSITION-NAME-IN TO POSITION-NAME-OUT
107200     ELSE
107300         MOVE SPACES TO POSITION-NAME-OUT
107400         MOVE 1 TO PAREN-LOCATION
107500         PERFORM LOCATE-THE-LEFT-PAREN
107600         MOVE 1 TO POS-POINTER
107700         PERFORM MOVE-IN-NAME-TO-OUT
107800         PERFORM MOVE-IN-OCCURS-TO-OUT.
107900
108000 LOCATE-THE-LEFT-PAREN.
108100     IF POS-IN-CHAR (PAREN-LOCATION) NOT = "("
108200         ADD 1 TO PAREN-LOCATION
108300         IF PAREN-LOCATION NOT > 12
108400             GO TO LOCATE-THE-LEFT-PAREN.
108500 MOVE-IN-NAME-TO-OUT.
108600     IF POS-POINTER < PAREN-LOCATION
108700         MOVE POS-IN-CHAR (POS-POINTER) TO
108800                         POS-OUT-CHAR (POS-POINTER)
108900     ELSE
109000         MOVE SPACE TO POS-OUT-CHAR (POS-POINTER).
109100     ADD 1 TO POS-POINTER.
109200     IF POS-POINTER NOT > 8
109300         GO TO MOVE-IN-NAME-TO-OUT.
109400 MOVE-IN-OCCURS-TO-OUT.
109500     IF PAREN-LOCATION > 12
109600         MOVE SPACES TO POS-OUT-OCCURS
109700     ELSE
109800         ADD 1 TO PAREN-LOCATION
109900         PERFORM POSITION-OCCURS-1-OUT.
110000     IF POS-OUT-OCCURS NOT = SPACES
110100         ADD 1 TO PAREN-LOCATION
110200         PERFORM POSITION-OCCURS-2-OUT.
110300     IF POS-OUT-OCCURS NOT = SPACES
110400         MOVE "(" TO POS-OUT-LT-PAREN
110500         MOVE ")" TO POS-OUT-RT-PAREN.
110600 POSITION-OCCURS-1-OUT.
110700     IF POS-IN-CHAR (PAREN-LOCATION) < 0  OR
110800        POS-IN-CHAR (PAREN-LOCATION) > 9
110900         MOVE SPACES TO POS-OUT-OCCURS.
111000     IF POS-IN-CHAR (PAREN-LOCATION) NOT < 0  AND
111100        POS-IN-CHAR (PAREN-LOCATION) NOT > 9
111200         MOVE POS-IN-CHAR (PAREN-LOCATION) TO
111300                 POS-OUT-OCC-1.
111400 POSITION-OCCURS-2-OUT.
111500     IF POS-IN-CHAR (PAREN-LOCATION) = ")"
111600         IF POS-OUT-OCC-1 = 0
111700             MOVE SPACES TO POS-OUT-OCCURS
111800         ELSE
111900             MOVE POS-OUT-OCC-1 TO POS-OUT-OCC-2
112000             MOVE 0 TO POS-OUT-OCC-1
112100     ELSE
112200     IF POS-IN-CHAR (PAREN-LOCATION) NOT > 9  AND
112300        POS-IN-CHAR (PAREN-LOCATION) NOT < 0
112400         MOVE POS-IN-CHAR (PAREN-LOCATION) TO POS-OUT-OCC-2
112500     ELSE
112600         MOVE SPACES TO POS-OUT-OCCURS.
112700
112800     IF POS-OUT-OCC-1 = 0  AND
112900        POS-OUT-OCC-2 = 0
113000         MOVE SPACES TO POS-OUT-OCCURS.
113100
113200
000000     COPY PLKTRACE.
