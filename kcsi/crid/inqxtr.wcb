000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQXTR.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/19/90.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE EXTRACT FILE:  FILENAME LIRARY VOLUME
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800
001900 DATA DIVISION.
002000 FILE SECTION.
002100
002200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqxtr.wcb 1.10 9/11/94".
002300     COPY WSGP.
002400     COPY WSINT.
002500     COPY WSCRTS.
002600
002700 01  EXTRACT-GETPARM-FIELDS.
002800     05  EXTRACT-MSG-ID               PIC X(4) VALUE "0001".
002900     05  EXTRACT-PRNAME               PIC X(8) VALUE "OUTPUT".
003000     05  EXTRACT-MSG-ISSUER           PIC X(6) VALUE "INQRY".
003100
003200 01  EXTRACT-MSG-COUNT.
003300     05  FILLER                        BINARY   VALUE ZERO.
003400     05  FILLER                        BINARY   VALUE 2.
003500 01  EXTRACT-MSG-TEXT-1               PIC X(1) VALUE
003600         " ".
003700
003800 01  EXTRACT-MSG-TEXT-2               PIC X(55) VALUE
003900     "Please specify the file name for the extracted data.".
004000 01  EXTRACT-FILE-KW              PIC X(8) VALUE "FILE".
004100 01  EXTRACT-FILE-FIELD           PIC X(8) VALUE SPACE.
004200 01  EXTRACT-FILE-DEFAULT         PIC X(8) VALUE SPACE.
004300 01  EXTRACT-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
004400 01  EXTRACT-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
004500 01  EXTRACT-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
004600 01  EXTRACT-VOLUME-KW              PIC X(8) VALUE "VOLUME".
004700 01  EXTRACT-VOLUME-FIELD           PIC X(6) VALUE SPACE.
004800 01  EXTRACT-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
000000 01  EXTRACT-CONSEC-KW              PIC X(8) VALUE "CONSEC".
000000 01  EXTRACT-CONSEC-FIELD           PIC X(3) VALUE "NO".
004900 01  EXTRACT-TEXT-01                  PIC X(50) VALUE
005000         "Note:  Data matching the query will be put in the ".
005100 01  EXTRACT-TEXT-02                  PIC X(50) VALUE
005600         "file defined above.                               ".
005300 01  EXTRACT-TEXT-03                  PIC X(50) VALUE
005400         "To create a consecutive file enter CONSEC = YES.".
005500 01  EXTRACT-TEXT-04                  PIC X(50) VALUE
005200         "To create a relative file enter CONSEC = REL.".
006200
006300     COPY WSUSAGE.
006400
006500 01  MESSAGE-LITERALS.
006600     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
006700         "* Error * File Name cannot be blank".
006800     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
006900         "* Error * Invalid File Name".
007000     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
007100         "* Error * Invalid Library Name".
007200     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
007300         "* Error * Invalid Volume Name".
007400
000000     COPY LKVALNAM.
000000     COPY LKPRC.
000000
011000 LINKAGE SECTION.
011100 01  EXTRACT-IO-BLOCK.
011200     COPY LKDIO.
011300     COPY WSRPTREC.
011400
011500 PROCEDURE DIVISION USING
011600     EXTRACT-IO-BLOCK
011700     RPT-RECORDS.
011800 MAIN-LOGIC SECTION.
011900 PROGRAM-BEGIN.
000000     MOVE "INQXTR BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012000     PERFORM EXTRACT-USAGE-CONSTANTS.
012100     PERFORM OPENING-PROCEDURE.
012200     PERFORM MAIN-PROCESS.
012300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQXTR EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
012400 MAIN-LOGIC-EXIT.
012500     EXIT PROGRAM.
012600 MAIN-LOGIC-STOP.
012700     STOP RUN.
012800 LEVEL-2 SECTION.
012900 OPENING-PROCEDURE.
013000     MOVE OUTLIB TO EXTRACT-LIBRARY-FIELD.
013100     MOVE OUTVOL TO EXTRACT-VOLUME-FIELD.
000000     MOVE "NO" TO EXTRACT-CONSEC-FIELD.
013200
013300 CLOSING-PROCEDURE.
013400     IF GP-PFKEY-1-PRESSED
013500         MOVE SPACES TO
013600                 FILE-NAME OF EXTRACT-IO-BLOCK.
013700
013900
014000 MAIN-PROCESS.
014100     MOVE SPACE TO SCREEN-ERROR-STATUS.
014300
014400     PERFORM CALL-EXTRACT-GETPARM.
014500     MOVE SPACES TO EXTRACT-MSG-TEXT-2.
014600     IF NOT GP-PFKEY-1-PRESSED
014700         PERFORM VAL-FILE-NAME
014800         IF NOT SCREEN-ENTRY-ERROR
014900             PERFORM VAL-LIB-NAME
015000                 IF NOT SCREEN-ENTRY-ERROR
015100                    PERFORM VAL-VOL-NAME
000000                    IF NOT SCREEN-ENTRY-ERROR
000000                        PERFORM VAL-CONSEC.
015200
015300     IF NOT SCREEN-ENTRY-ERROR  AND
015400        NOT GP-PFKEY-1-PRESSED
015500         MOVE 0 TO CALLED-RC
015600         CALL "KEXISTS" USING
000000             CALLED-RC
015700             EXTRACT-FILE-FIELD
015800             EXTRACT-LIBRARY-FIELD
015900             EXTRACT-VOLUME-FIELD.
016000
016100     IF CALLED-RC = 0
016200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
016300         MOVE "File exists.  Please re-specify."
016400             TO EXTRACT-MSG-TEXT-2.
016500
016600     IF NOT SCREEN-ENTRY-ERROR
016700         MOVE EXTRACT-FILE-FIELD TO
016800             FILE-NAME OF EXTRACT-IO-BLOCK
016900         MOVE EXTRACT-LIBRARY-FIELD TO
017000             FILE-LIBRARY OF EXTRACT-IO-BLOCK
017100         MOVE EXTRACT-VOLUME-FIELD TO
017200             FILE-VOLUME OF EXTRACT-IO-BLOCK.
017300
017400     IF SCREEN-ENTRY-ERROR
017500         GO TO MAIN-PROCESS.
017600
017700 CALL-EXTRACT-GETPARM.
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-01-VAL TO PFKEY-MASK-16.
017900
018000     CALL "GETPARM" USING
018100         GP-SPECIFY-TYPE, GP-REQUEST-FORM, EXTRACT-PRNAME,
018200         GP-PFKEY-RECEIVER, EXTRACT-MSG-ID,
018300         EXTRACT-MSG-ISSUER,
018400
018500         EXTRACT-MSG-COUNT,
018600         EXTRACT-MSG-TEXT-1, INT01,
018700         EXTRACT-MSG-TEXT-2, INT55,
018800
018900         GP-KW-TYPE, EXTRACT-FILE-KW,
019000         EXTRACT-FILE-FIELD, INT08,
019100         GP-ABS, INT15, GP-ABS, INT07,
019200         GP-UPPERCASE-DATA
019300
019400         GP-KW-TYPE, EXTRACT-LIBRARY-KW,
019500         EXTRACT-LIBRARY-FIELD, INT08,
019600         GP-ABS, INT15, GP-ABS, INT30,
019700         GP-UPPERCASE-DATA
019800
019900         GP-KW-TYPE, EXTRACT-VOLUME-KW,
020000         EXTRACT-VOLUME-FIELD, INT06,
020100         GP-ABS, INT15, GP-ABS, INT53,
020200         GP-UPPERCASE-DATA
020300
020400         GP-TEXT-TYPE, EXTRACT-TEXT-01, INT50,
020500         GP-ABS, INT17, GP-ABS, INT02,
020600
020700         GP-TEXT-TYPE, EXTRACT-TEXT-02, INT50,
020800         GP-ABS, INT18, GP-ABS, INT02,
020900
021000         GP-TEXT-TYPE, EXTRACT-TEXT-03, INT50,
021100         GP-ABS, INT19, GP-ABS, INT02,
021200
021300         GP-TEXT-TYPE, EXTRACT-TEXT-04, INT50,
021400         GP-ABS, INT20, GP-ABS, INT02,
021500
000000         GP-KW-TYPE, EXTRACT-CONSEC-KW,
000000         EXTRACT-CONSEC-FIELD, INT03,
000000         GP-ABS, INT22, GP-ABS, INT20,
000000         GP-UPPERCASE-DATA,
000000
021600         GP-ENABLE-ENTER-KEY
000000         GP-PFKEY-TYPE, PFKEY-MASK.
000000
000000     IF GP-PFKEY-RECEIVER = LOW-VALUES
000000         MOVE HEX40 TO GP-PFKEY-RECEIVER.
021800
021900     IF NOT GP-ENTER-KEY-PRESSED  AND
022000        NOT GP-PFKEY-1-PRESSED
022100         GO TO CALL-EXTRACT-GETPARM.
022200
022300 VAL-FILE-NAME.
022400
022500     IF EXTRACT-FILE-FIELD = SPACES
022600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
022700         MOVE FILE-ID-BLANK-LITERAL TO EXTRACT-MSG-TEXT-2.
022800
022900     IF NOT SCREEN-ENTRY-ERROR
023000         MOVE EXTRACT-FILE-FIELD TO VAL-NAME 
023100         PERFORM TEST-THE-NAME
023200         IF SCREEN-ENTRY-ERROR
023300             MOVE INVALID-FILE-LITERAL TO EXTRACT-MSG-TEXT-2.
023400
023500 VAL-LIB-NAME.
023600
023700     MOVE EXTRACT-LIBRARY-FIELD TO VAL-NAME.
023800     PERFORM TEST-THE-NAME.
023900     IF SCREEN-ENTRY-ERROR
024000         MOVE INVALID-LIB-LITERAL TO EXTRACT-MSG-TEXT-2.
024100
024200 VAL-VOL-NAME.
024300
024400     MOVE EXTRACT-VOLUME-FIELD TO VAL-NAME.
024500     PERFORM TEST-THE-VOL.
024600     IF SCREEN-ENTRY-ERROR
024700         MOVE INVALID-VOL-LITERAL TO EXTRACT-MSG-TEXT-2.
024800
000000 VAL-CONSEC.
000000     IF EXTRACT-CONSEC-FIELD NOT = "YES" AND "NO" AND "REL"
000000         MOVE "Must be YES, NO or REL" TO
000000             EXTRACT-MSG-TEXT-2
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
000000     IF EXTRACT-CONSEC-FIELD = "NO"
000000          MOVE "I" TO FILE-ORG OF EXTRACT-IO-BLOCK
000000     ELSE
000000     IF EXTRACT-CONSEC-FIELD = "REL"
000000          MOVE "R" TO FILE-ORG OF EXTRACT-IO-BLOCK
000000     ELSE
000000          MOVE "C" TO FILE-ORG OF EXTRACT-IO-BLOCK.
000000
031500 TEST-THE-NAME.
000000     CALL "VALNAM" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
031500 TEST-THE-VOL.
000000     CALL "VALVOL" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
028700*USAGE STUFF.
028800     COPY PLUSAGE.
028900
029000

000000     COPY PLKTRACE.
