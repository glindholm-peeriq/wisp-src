000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLTBL.
000300*--------------------------------------------------
000000* Mar 30, 1990
000000* Added set and unset KEEP mode around closing the
000000* the printer file to avoid the file being lost.
000400*--------------------------------------------------
000500 ENVIRONMENT DIVISION.
000600 CONFIGURATION SECTION.
000700 FIGURATIVE-CONSTANTS.
000800     COPY FIGS.
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100     COPY SLCRT.
001200     COPY SLPRT.
001300 DATA DIVISION.
001400 FILE SECTION.
001500     COPY FDCRT.
001600     COPY FDPRT.
001700 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrltbl.wcb 1.11 9/11/94".
001800*FIELDS ADDED FOR WISP CONVERSION.
001900 01  GENERAL-FIELD      PIC X(100).
002000 01  GENERAL-NUMERIC    PIC S9(10).
002100*GENERAL
002200     COPY WSCRT.
002300     COPY WSPRT.
002400     COPY WSINT.
002500     COPY WSDISPK.
002600     COPY WSSCRTCH.
002700     COPY WSIOCODE.
002800     COPY WSFILEIO.
000000     COPY WSKEEP.
000000
002900 01  WORK-FIELD                    PIC X(17) VALUE SPACE.
003000 01  FILLER REDEFINES WORK-FIELD.
003100     05 WORK-CHAR                  PIC X
003200        OCCURS 17 TIMES.
003300 01  WORK-INDEX                    BINARY VALUE ZERO.
003400
003500 01  MENU-PICK      PIC 99.
003600     88  CREATING-TABLE VALUE 2.
003700     88  ADDING-VALUES  VALUE 3.
003800     88  DELETING-VALUES VALUE 5.
003900     88  LISTING-VALUES VALUE 6.
004000*---------------------------------------------------------------
004100* SINCE THE TABLE RECORD COULD BE 1 TO 16 BYTES LONG,
004200* ONLY THE FIRST BYTE IS TESTED TO ESTABLISH FILE-TYPE.
004300* LOW VALUES = NUMERIC
004400* HIGH VALUES = ALPHA-NUMERIC.
004500*---------------------------------------------------------------
004600 01  TBL-FILE-STATUS PIC XX.
004700 01  TBL-RECORD  PIC X(16) VALUE SPACE.
004800 01  FILLER REDEFINES TBL-RECORD.
004900     05 TBL-RECORD-TEST-BYTE PIC X.
005000     05  FILLER PIC X(15).
005100 01  TBL-IO-BLOCK.
005200     COPY LKDIO.
005300* SCREENS
005400*---------------------------------------------------------------
005500* TABLE SCREENS
005600*---------------------------------------------------------------
005700 01  MENU-SCREEN USAGE IS DISPLAY-WS.
005800     05  FILLER PIC X(10) ROW 01 COLUMN 35
005900         VALUE "TABLE MENU".
006000     05  FILLER PIC X(70) ROW 03 COLUMN 08 VALUE
006100     "From this menu you can name and create tables for field vali
006200-    "dation".
006300     05  FILLER PIC X(70) ROW 04 COLUMN 08 VALUE
006400     "and maintain existing tables by displaying, adding, or delet
006500-    "ing".
006600     05  FILLER PIC X(70) ROW 05 COLUMN 08 VALUE
006700     "values.  DATENTRY cannot use a table to validate a field, ho
006800-    "wever,".
006900     05  FILLER PIC X(70) ROW 06 COLUMN 08 VALUE
007000     "unless the table file is specified by name from the Field In
007100-    "put".
007200     05  FILLER PIC X(70) ROW 07 COLUMN 08 VALUE
007300     "Validation Specifications screen for that field.".
007400     05  FILLER PIC X(55) ROW 11 COLUMN 14 VALUE
007500     "Select the appropriate PFkey for the desired option".
007600     05  FILLER PIC X(25) ROW 13 COLUMN 21 VALUE
007700     "PFkey             Option".
007800     05  FILLER PIC X(41) ROW 15 COLUMN 21 VALUE
007900     " (2)    Create a new table file".
008000     05  FILLER PIC X(41) ROW 16 COLUMN 21 VALUE
008100     " (3)    Add values to a table file".
008200     05  FILLER PIC X(41) ROW 17 COLUMN 21 VALUE
008300     " (5)    Delete values from a table file".
008400     05  FILLER PIC X(41) ROW 18 COLUMN 21 VALUE
008500     " (6)    List the contents of a table file".
008600     05  FILLER PIC X(41) ROW 22 COLUMN 21 VALUE
008700     "(16)    Exit to the CONTROL Central Menu".
008800
008900*---------------------------------------------------------------
009000* TABLE A FIELD ENTRY SCREEN
009100*---------------------------------------------------------------
009200 01  TABLE-SCREEN USAGE IS DISPLAY-WS.
009300     05  FILLER  PIC X(10) ROW 2 COLUMN 35 VALUE
009400     "TABLE FILE".
009500     05  TABLE-MODE-ITEM PIC X(14) ROW 3 COLUMN 33
009600         SOURCE TABLE-MODE-FIELD.
009700     05  FILLER PIC X(42) ROW 4 COLUMN 20 VALUE
009800     "Supply all information and press (ENTER)".
009900     05  TABLE-MESSAGE-ITEM PIC X(79) ROW 07 COLUMN 2
010000         SOURCE MESSAGE-FIELD.
010100     05  FILLER PIC X(14) ROW 10 COLUMN 22 VALUE
010200     "Table Name:".
010300     05  TABLE-NAME-ITEM PIC X(6) ROW 10 COLUMN 40
010400         SOURCE FILE-NAME-FIELD
010500         OBJECT FILE-NAME-FIELD.
010600     05  FILLER PIC X(14) ROW 12 COLUMN 22 VALUE
010700     "Record Size:".
010800     05  TABLE-SIZE-ITEM PIC ZZ ROW 12 COLUMN 40
010900         SOURCE RECORD-LEN-FIELD
011000         OBJECT RECORD-LEN-FIELD.
011100     05  FILLER PIC X(14) ROW 14 COLUMN 22 VALUE
011200     "Numeric only:".
011300     05  NUMERIC-ONLY-ITEM PIC X ROW 14 COLUMN 40
011400         SOURCE NUMERIC-ONLY-FIELD
011500         OBJECT NUMERIC-ONLY-FIELD.
000000     05  FILLER PIC X(31) ROW 14 COLUMN 42 VALUE
000000         "(Y - Numeric  N - Alphanumeric)".
011600     05  T1-ITEM PIC X(14) ROW 16 COLUMN 22 VALUE
011700     "Table Value:".
011800     05  TABLE-VALUE-ITEM PIC X(16) ROW 16 COLUMN 40
011900         SOURCE TBL-RECORD
012000         OBJECT TBL-RECORD.
012100     05  TABLE-FOOTER-1-ITEM PIC X(79) ROW 23 COLUMN 2
012200         SOURCE TABLE-FOOTER-1-FIELD.
012300     05  TABLE-FOOTER-2-ITEM PIC X(79) ROW 24 COLUMN 2
012400         SOURCE TABLE-FOOTER-2-FIELD.
012500
012600 01  FILE-NAME-FIELD         PIC X(8)  VALUE SPACE.
012700 01  RECORD-LEN-FIELD        PIC 99 VALUE ZEROES.
012800 01  TABLE-MODE-FIELD        PIC X(14) VALUE SPACE.
012900 01  NUMERIC-ONLY-FIELD      PIC X     VALUE SPACE.
013000 01  TABLE-FOOTER-1-FIELD    PIC X(79) VALUE SPACE.
013100 01  TABLE-FOOTER-2-FIELD    PIC X(79) VALUE SPACE.
013200
013300*---------------------------------------------------------------
013400* DETAIL LINES FOR PRINTING
013500*---------------------------------------------------------------
013600 01  DETAIL-LINE-1.
013700     05  FILLER          PIC X(21) VALUE SPACE.
013800     05  FILLER          PIC X(16) VALUE
013900         "Table Name:".
014000     05  PRT-TABLE-NAME  PIC X(6).
014100 01  DETAIL-LINE-2.
014200     05  FILLER          PIC X(21) VALUE SPACE.
014300     05  FILLER          PIC X(16) VALUE
014400         "Record Size:".
014500     05  PRT-TABLE-SIZE  PIC 99.
014600     05  FILLER          PIC X(9) VALUE
014700         " (1 - 16)".
014800 01  DETAIL-LINE-3.
014900     05  FILLER          PIC X(21) VALUE SPACE.
015000     05  FILLER          PIC X(16) VALUE
015100         "Numeric Only:".
015200     05  PRT-NUMERIC-ONLY PIC X.
015300     05  FILLER          PIC X(26) VALUE
015400         " (Y - numeric entries only".
015500 01  DETAIL-LINE-4.
015600     05  FILLER          PIC X(39) VALUE SPACE.
015700     05  FILLER          PIC X(20) VALUE
015800         "(N - alphanumeric)".
015900
016000 01  CTBL PIC X.
000000 01  FILTYP              PIC X.
016100 01  HOLDIT PIC X.
016200
016300 LINKAGE SECTION.
016400 01  CTRL-IO-BLOCK.
016500     COPY LKDIO.
016600 01  CTRL-HEADER-RECORDS.
016700     COPY RCCTRLH.
016800 01  CF-RECORD.
016900     COPY RCCTRLF.
017000     COPY LKCTRLAR.
017100     COPY RCCTRLT1.
017200     COPY RCCTRLT2.
017300
000000     COPY LKPRC.
000000
017400 PROCEDURE DIVISION USING
000000     CALLED-RC
017500     COPY LKCTRL.
017600 MAIN-LOGIC SECTION.
017700 PROGRAM-BEGIN.
000000     MOVE "CTRLTBL BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     MOVE ZEROES TO CALLED-RC.
017800     PERFORM PROGRAM-INIT.
017900     IF CF-ACTION-CODE = "EXISTS"
018000         PERFORM DOES-FILE-EXIST
018100     ELSE
018200         PERFORM MAIN-PROCESS.
018300
000000     MOVE "CTRLTBL EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
018400 MAIN-LOGIC-EXIT.
018500     EXIT PROGRAM.
018600 MAIN-LOGIC-STOP.
018700     STOP RUN.
018800 THE-OTHER SECTION.
018900 PROGRAM-INIT.
000000     CALL "INIDIO" USING TBL-IO-BLOCK.
000000     MOVE "I" TO FILE-ORG OF TBL-IO-BLOCK.
019000     MOVE ZERO TO FILE-OPEN-STATUS OF TBL-IO-BLOCK.
019100     MOVE CF-TABLE-NAME OF CF-RECORD TO FILE-NAME OF TBL-IO-BLOCK
019200                                        FILE-NAME-FIELD.
019300     MOVE FILE-LIBRARY OF CTRL-IO-BLOCK TO
019400          FILE-LIBRARY OF TBL-IO-BLOCK.
019500     MOVE FILE-VOLUME  OF CTRL-IO-BLOCK TO
019600          FILE-VOLUME  OF TBL-IO-BLOCK.
019700     MOVE CF-EXT-LEN OF CF-RECORD TO
019800                     FILE-RECORD-LEN OF TBL-IO-BLOCK
019900                     RECORD-LEN-FIELD.
000000     MOVE "I" TO FILE-ORG OF TBL-IO-BLOCK.
020000     MOVE CF-TYPE OF CF-RECORD TO GENERAL-FIELD.
020100     IF GENERAL-FIELD = "C"
020200          MOVE "N" TO NUMERIC-ONLY-FIELD
020300     ELSE
020400          MOVE "Y" TO NUMERIC-ONLY-FIELD.
020500 MAIN-PROCESS.
020600     PERFORM OPENING-PROCEDURE.
020700*     MOVE SPACE TO CF-TABLE-NAME OF CF-RECORD.
020800     IF CF-ACTION-CODE = "CREATE"
020900         PERFORM CREATE-TABLE
021000*         PERFORM NAMED-TABLE
021100     ELSE
021200     IF CF-ACTION-CODE = "MENU"
021300         PERFORM PROCESS-MENU
021400         .
021500     PERFORM CLOSING-PROCEDURE.
021600 OPENING-PROCEDURE.
021700     PERFORM OPEN-CRT-FILE.
021800 CLOSING-PROCEDURE.
021900     PERFORM CLOSE-CRT-FILE.
022000     MOVE PFKEY-CODE TO CALLED-RC.
022100
022200 PROCESS-MENU.
022300     PERFORM ENTER-MENU.
022400     IF NOT END-KEY-PRESSED
022500         PERFORM PROCESS-OPTION
022600          UNTIL END-KEY-PRESSED.
022700 PROCESS-OPTION.
022800     IF CREATING-TABLE
022900         PERFORM CREATE-TABLE
023000     ELSE
023100     IF ADDING-VALUES
023200         PERFORM ADD-VALUES
023300     ELSE
023400     IF DELETING-VALUES
023500         PERFORM DELETE-VALUES
023600     ELSE
023700     IF LISTING-VALUES
023800         PERFORM LIST-VALUES
023900         .
024000     PERFORM ENTER-MENU.
024100 ENTER-MENU.
024200     DISPLAY AND READ MENU-SCREEN ON CRT-FILE
024300         ONLY PFKEYS 2 3 5 6 16.
024400
024500     MOVE PFKEY-CODE TO MENU-PICK.
024600
024700 CREATE-TABLE.
024800     MOVE ZEROES TO PFKEY-CODE.
024900     IF CF-ACTION-CODE = "MENU"
025000         PERFORM ENTER-KEY.
025100     IF NOT END-KEY-PRESSED
025200         PERFORM CREATE-TABLE-FILE
025300         PERFORM OPEN-IO-AND-SET-TBL-FILE
025400         PERFORM ADD-RECORDS
025500          UNTIL END-KEY-PRESSED
025600         PERFORM CLOSE-TBL-FILE
025700          .
025800 CREATE-TABLE-FILE.
025900     MOVE 50 TO FILE-SPACE OF TBL-IO-BLOCK.
026000     PERFORM OPEN-OUTPUT-TBL-FILE.
026100     IF NUMERIC-ONLY-FIELD = "Y"
026200         MOVE LOW-VALUES TO TBL-RECORD
026300     ELSE
026400         MOVE HIGH-VALUES TO TBL-RECORD
026500         .
026600     PERFORM WRITE-TBL-RECORD.
026700     PERFORM CLOSE-TBL-FILE.
026800 ADD-VALUES.
026900     PERFORM ENTER-KEY.
027000     IF NOT END-KEY-PRESSED
027100         PERFORM OPEN-IO-AND-SET-TBL-FILE
027200         IF TBL-FILE-STATUS NOT = I-O-OK
027300             PERFORM FILE-TYPE-ERROR
027400         ELSE
027500         PERFORM DETERMINE-FILE-TYPE
027600         PERFORM ADD-RECORDS
027700          UNTIL END-KEY-PRESSED
027800         PERFORM CLOSE-TBL-FILE
027900          .
028000 DELETE-VALUES.
028100     PERFORM ENTER-KEY.
028200     IF NOT END-KEY-PRESSED
028300         PERFORM OPEN-IO-AND-SET-TBL-FILE
028400         PERFORM DETERMINE-FILE-TYPE
028500         PERFORM DELETE-RECORDS
028600          UNTIL END-KEY-PRESSED
028700         PERFORM CLOSE-TBL-FILE
028800          .
028900 LIST-VALUES.
029000     PERFORM ENTER-KEY.
029100     IF NOT END-KEY-PRESSED
029200         PERFORM OPEN-IO-AND-SET-TBL-FILE
029300         PERFORM DETERMINE-FILE-TYPE
029400         PERFORM LIST-FILE
029500          .
029600* FILE NAME AND LENGTH (TYPE REQUIRED FOR CREATE
029700 ENTER-KEY.
029800     PERFORM ENTER-KEY-INIT.
029900     PERFORM ENTER-KEY-ENTRY.
030000 ENTER-KEY-INIT.
030100     PERFORM INIT-ENTER-KEY-FIELDS.
030200     PERFORM INIT-ENTER-KEY-FACS.
030300     PERFORM INIT-ENTER-KEY-FOOTERS.
030400     PERFORM INIT-ENTER-KEY-PROMPTS.
030500     PERFORM INIT-ENTER-KEY-PFS.
030600 ENTER-KEY-ENTRY.
030700     MOVE SPACE TO SCREEN-ERROR-STATUS.
030800     DISPLAY AND READ TABLE-SCREEN ON CRT-FILE
030900       PFKEYS PF-3 16.
031000     MOVE SPACE TO MESSAGE-FIELD.
031100     IF NOT END-KEY-PRESSED
031200         PERFORM VAL-KEY-FIELDS
031300         IF SCREEN-ENTRY-ERROR
031400             GO TO ENTER-KEY-ENTRY.
031500
031600 INIT-ENTER-KEY-FIELDS.
031700     MOVE SPACE TO CF-TABLE-NAME OF CF-RECORD
031800                   TBL-RECORD
031900                   NUMERIC-ONLY-FIELD
032000                   .
032100     MOVE ZEROES TO FILE-RECORD-LEN OF TBL-IO-BLOCK
032200                    RECORD-LEN-FIELD.
032300 INIT-ENTER-KEY-FACS.
032400     PERFORM UNPROTECT-KEY-FIELDS.
032500     PERFORM HIDE-DATA-FIELDS.
032600 INIT-ENTER-KEY-FOOTERS.
032700     MOVE "(ENTER) to continue or"
032800      TO TABLE-FOOTER-1-FIELD.
032900     MOVE "(16) Return to the Table Menu"
033000      TO TABLE-FOOTER-2-FIELD.
033100 INIT-ENTER-KEY-PROMPTS.
033200     MOVE SPACE TO MESSAGE-FIELD.
033300     IF CREATING-TABLE
033400         MOVE "CREATE TABLE" TO TABLE-MODE-FIELD
033500     ELSE
033600     IF ADDING-VALUES
033700         MOVE "ADD VALUES" TO TABLE-MODE-FIELD
033800     ELSE
033900     IF DELETING-VALUES
034000         MOVE "DELETE VALUES" TO TABLE-MODE-FIELD
034100     ELSE
034200     IF LISTING-VALUES
034300         MOVE "LIST VALUES" TO TABLE-MODE-FIELD
034400         .
034500 INIT-ENTER-KEY-PFS.
034600     MOVE 16 TO PF-3.
034700
034800* KEY (FILENAME ETC) VALIDATION.
034900 VAL-KEY-FIELDS.
035000     PERFORM VAL-TABLE-SIZE.
035100     IF NOT SCREEN-ENTRY-ERROR
035200         PERFORM VAL-NUMERIC-ONLY.
035300     IF NOT SCREEN-ENTRY-ERROR
035400         PERFORM VAL-TABLE-NAME.
035500 VAL-TABLE-SIZE.
035600     IF CREATING-TABLE
035700         MOVE RECORD-LEN-FIELD TO FILE-RECORD-LEN OF TBL-IO-BLOCK
035800         IF RECORD-LEN-FIELD < 1 OR
035900            RECORD-LEN-FIELD > 16
036000             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
036100             MOVE "Record Size Must be 1-16"
036200               TO MESSAGE-FIELD
036300               .
036400 VAL-NUMERIC-ONLY.
036500     IF CREATING-TABLE
036600         IF NUMERIC-ONLY-FIELD NOT = "Y" AND "N"
036700             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
036800             MOVE "Numeric Only Must be 'Y' or 'N'"
036900               TO MESSAGE-FIELD
037000               .
037100
037200 VAL-TABLE-NAME.
037300     MOVE FILE-NAME-FIELD TO FILE-NAME OF TBL-IO-BLOCK.
037400     IF FILE-NAME-FIELD = SPACE
037500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
037600         MOVE "Table Name is Required"
037700           TO MESSAGE-FIELD
037800     ELSE
037900         PERFORM DOES-FILE-EXIST
038000         IF CREATING-TABLE
038100             PERFORM VAL-FOR-CREATE
038200         ELSE
038300             PERFORM VAL-FOR-FILE.
038400 VAL-FOR-CREATE.
038500     IF TBL-FILE-STATUS NOT = ZEROES
038600         NEXT SENTENCE
038700     ELSE
038800     IF PFKEY-3-PRESSED
038900         PERFORM SCRATCH-TABLE
039000     ELSE
039100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
039200         MOVE "Table Exists - Press (3) to Overwrite"
039300           TO MESSAGE-FIELD
039400         MOVE 3 TO PF-3
039500         .
039600 VAL-FOR-FILE.
039700     IF TBL-FILE-STATUS NOT = ZEROES
039800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
039900         MOVE "Table Not Found"
040000           TO MESSAGE-FIELD
040100           .
040200
040300*FAC ROUTINES.
040400 UNPROTECT-KEY-FIELDS.
040500     MOVE UPPER-ENTRY-FAC TO
040600        FAC OF TABLE-NAME-ITEM
040700        FAC OF NUMERIC-ONLY-ITEM
040800        .
040900     MOVE NUMERIC-ENTRY-FAC TO
041000        FAC OF TABLE-SIZE-ITEM
041100        .
041200
041300     IF NOT CREATING-TABLE
041400         MOVE DISPLAY-HI-FAC TO
041500            FAC OF TABLE-SIZE-ITEM
041600            FAC OF NUMERIC-ONLY-ITEM
041700            .
041800
041900 UNPROTECT-DATA-FIELDS.
042000     MOVE DIM-FAC TO
042100        FAC OF T1-ITEM
042200        .
042300     IF NUMERIC-ONLY-FIELD = "Y"
042400         MOVE NUMERIC-ENTRY-FAC TO
042500            FAC OF TABLE-VALUE-ITEM
042600     ELSE
042700         MOVE UPLOW-ENTRY-FAC TO
042800            FAC OF TABLE-VALUE-ITEM
042900        .
043000 PROTECT-KEY-FIELDS.
043100     MOVE DISPLAY-HI-FAC TO
043200        FAC OF TABLE-NAME-ITEM
043300        FAC OF NUMERIC-ONLY-ITEM
043400        FAC OF TABLE-SIZE-ITEM
043500        .
043600 HIDE-DATA-FIELDS.
043700     MOVE HIDE-FAC TO
043800        FAC OF T1-ITEM
043900        FAC OF TABLE-VALUE-ITEM
044000        .
044100*---------------------------------------------------------------
044200* THIS ROUTINE IS EXECUTED FROM THE RANGE OR TABLE SELECTION
044300* WITHIN A FIELD SPECIFICATION.
044400*---------------------------------------------------------------
044500* NAMED-TABLE.
044600*     PERFORM DOES-FILE-EXIST.
044700*     IF TBL-FILE-STATUS = ZEROES
044800*         PERFORM ADD-RECORDS
044900*          UNTIL END-KEY-PRESSED OR
045000*                PFKEY-1-PRESSED
045100*          .
045200*
045300*---------------------------------------------------------------
045400* ADD RECORDS BEHAVES TWO WAYS FROM THE MENU, PFKEY 16 ENDS
045500* THE ROUTINE CALLED FROM THE FRONT MENU, PF3 ENDS THE ROUTINE
045600* WHEN CALLED FROM WITH A FIELD SPECIFICATION
045700* 3 IS THE EQUIVALENT OF END OF AND A 16 IS MOVED IN SO THAT
045800* THE CALLING PROGRAM NEED ONLY TEST FOR THE ONE VALUE.
045900*---------------------------------------------------------------
046000 ADD-RECORDS.
046100     PERFORM ADD-RECORDS-INIT.
046200     PERFORM ADD-RECORDS-ENTRY.
046300 ADD-RECORDS-INIT.
046400     PERFORM INIT-ADD-RECORD-FIELDS.
046500     PERFORM INIT-ADD-RECORD-FACS.
046600     PERFORM INIT-ADD-RECORD-FOOTERS.
046700     PERFORM INIT-ADD-RECORD-PROMPTS.
046800     PERFORM INIT-ADD-RECORD-PFS.
046900 ADD-RECORDS-ENTRY.
047000     MOVE SPACE TO SCREEN-ERROR-STATUS.
047100     DISPLAY AND READ TABLE-SCREEN ON CRT-FILE
000000        PFKEYS 16.
047200*       PFKEYS PF-1 PF-3 PF-16.
047300     MOVE SPACE TO MESSAGE-FIELD.
047400*     IF PFKEY-3-PRESSED
047500*         MOVE 16 TO PFKEY-CODE.
047600     IF ENTER-KEY-PRESSED
047700         PERFORM VAL-ADD-FIELD
047800         IF SCREEN-ENTRY-ERROR
047900             GO TO ADD-RECORDS-ENTRY
048000         ELSE
048100             PERFORM ADD-TO-TABLE.
048200 INIT-ADD-RECORD-FIELDS.
048300     COMPUTE WORK-INDEX =
048400       FILE-RECORD-LEN OF TBL-IO-BLOCK + 1.
048500     MOVE SPACE TO WORK-FIELD.
048600     MOVE DIM-FAC TO WORK-CHAR(WORK-INDEX).
048700     MOVE WORK-FIELD TO TBL-RECORD.
048800 INIT-ADD-RECORD-FACS.
048900     PERFORM PROTECT-KEY-FIELDS.
049000     PERFORM UNPROTECT-DATA-FIELDS.
049100 INIT-ADD-RECORD-FOOTERS.
049200     MOVE "(ENTER) to Add this value to the table or"
049300       TO TABLE-FOOTER-1-FIELD.
049400     IF CF-ACTION-CODE = "MENU"
049500         MOVE "(16) Return to Table Menu" TO
049600          TABLE-FOOTER-2-FIELD
049700     ELSE
049500         MOVE "(16) Return" TO
049600          TABLE-FOOTER-2-FIELD
049800*         MOVE "(1)Field validation (3)Field specification" TO
049900*          TABLE-FOOTER-2-FIELD
050000          .
050100 INIT-ADD-RECORD-PROMPTS.
050200 INIT-ADD-RECORD-PFS.
050300*     IF CF-ACTION-CODE = "MENU"
050400*         MOVE 16 TO PF-1 PF-3 PF-16
050500*     ELSE
050600*         MOVE 1 TO PF-1
050700*         MOVE 3 TO PF-1 PF-16
050800*         .
050900 VAL-ADD-FIELD.
051000     PERFORM READ-FORMAT-TBL-RECORD.
051100     IF TBL-FILE-STATUS = I-O-OK
051200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
051300         MOVE "Table Value already on file"
051400          TO MESSAGE-FIELD.
051500 ADD-TO-TABLE.
051600     PERFORM WRITE-TBL-RECORD.
051700 DELETE-RECORDS.
051800     PERFORM DELETE-RECORDS-INIT.
051900     PERFORM DELETE-RECORDS-ENTRY.
052000 DELETE-RECORDS-INIT.
052100     PERFORM INIT-DELETE-RECORD-FIELDS.
052200     PERFORM INIT-DELETE-RECORD-FACS.
052300     PERFORM INIT-DELETE-RECORD-FOOTERS.
052400     PERFORM INIT-DELETE-RECORD-PROMPTS.
052500     PERFORM INIT-DELETE-RECORD-PFS.
052600 DELETE-RECORDS-ENTRY.
052700     MOVE SPACE TO SCREEN-ERROR-STATUS.
052800     DISPLAY AND READ TABLE-SCREEN ON CRT-FILE
052900       PFKEYS 16.
053000     MOVE SPACE TO MESSAGE-FIELD.
053100     IF ENTER-KEY-PRESSED
053200         PERFORM VAL-DELETE-FIELD
053300         IF SCREEN-ENTRY-ERROR
053400             GO TO DELETE-RECORDS-ENTRY
053500         ELSE
053600             PERFORM DELETE-FROM-TABLE.
053700 INIT-DELETE-RECORD-FIELDS.
053800     COMPUTE WORK-INDEX =
053900       FILE-RECORD-LEN OF TBL-IO-BLOCK + 1.
054000     MOVE SPACE TO WORK-FIELD.
054100     MOVE DIM-FAC TO WORK-CHAR(WORK-INDEX).
054200     MOVE WORK-FIELD TO TBL-RECORD.
054300 INIT-DELETE-RECORD-FACS.
054400     PERFORM PROTECT-KEY-FIELDS.
054500     PERFORM UNPROTECT-DATA-FIELDS.
054600 INIT-DELETE-RECORD-FOOTERS.
054700     MOVE "(ENTER) to Delete this value or"
054800       TO TABLE-FOOTER-1-FIELD.
054900     MOVE "(16) Return to Table Menu" TO
055000          TABLE-FOOTER-2-FIELD.
055100 INIT-DELETE-RECORD-PROMPTS.
055200 INIT-DELETE-RECORD-PFS.
055300 VAL-DELETE-FIELD.
055400     PERFORM READ-FORMAT-TBL-RECORD.
055500     IF TBL-FILE-STATUS NOT = I-O-OK
055600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
055700         MOVE "Table Value Not on file"
055800          TO MESSAGE-FIELD.
055900 DELETE-FROM-TABLE.
056000     PERFORM HOLD-TBL-RECORD.
056100     PERFORM DELETE-TBL-RECORD.
056200 LIST-FILE.
056300     PERFORM OPEN-LIST-FILE.
056400* RESTART THE FILE BY CLOSE AND OPEN
056500     PERFORM CLOSE-TBL-FILE.
056600     PERFORM OPEN-IO-AND-SET-TBL-FILE.
056700     PERFORM READ-NEXT-TBL-RECORD.
056800     MOVE ZEROES TO LINE-COUNT.
056900     PERFORM LIST-THE-FILE.
000000     PERFORM SET-KEEP-MODE.
057000     PERFORM CLOSE-LIST-FILE.
000000     PERFORM UNSET-KEEP-MODE.
057100     MOVE PRINTER-FILE-NAME TO DISPLAY-FILE.
057200     MOVE PRINTER-FILE-LIBRARY TO DISPLAY-LIBRARY.
057300     MOVE PRINTER-FILE-VOLUME TO DISPLAY-VOLUME.
057400     PERFORM CLOSE-CRT-FILE.
057500     PERFORM DISPLAY-PRINT-FILE.
057600     PERFORM OPEN-CRT-FILE.
057700
057800 OPEN-LIST-FILE.
057900     MOVE SPACE TO
058000                   PRINTER-FILE-LIBRARY
058100                   PRINTER-FILE-VOLUME
058200                   .
058300     MOVE 1000 TO PRINTER-FILE-SPACE.
058400     MOVE "##CTRL" TO PRINTER-FILE-NAME.
058500     OPEN OUTPUT PRINTER-FILE.
058600 CLOSE-LIST-FILE.
058700     CLOSE PRINTER-FILE.
058800 LIST-THE-FILE.
058900     PERFORM LIST-THE-HEADER.
059000     PERFORM LIST-ONE-RECORD
059100       UNTIL FILE-STATUS OF TBL-IO-BLOCK = AT-END.
059200 LIST-THE-HEADER.
059300     PERFORM LINE-FEED.
059400
059500     MOVE FILE-NAME OF TBL-IO-BLOCK TO
059600       PRT-TABLE-NAME.
059700     MOVE DETAIL-LINE-1 TO DETAIL-LINE.
059800     PERFORM PRINT-ONE-LINE.
059900
060000     MOVE FILE-RECORD-LEN OF TBL-IO-BLOCK TO
060100       PRT-TABLE-SIZE.
060200     MOVE DETAIL-LINE-2 TO DETAIL-LINE.
060300     PERFORM PRINT-ONE-LINE.
060400
060500     MOVE NUMERIC-ONLY-FIELD TO PRT-NUMERIC-ONLY.
060600     MOVE DETAIL-LINE-3 TO DETAIL-LINE.
060700     PERFORM PRINT-ONE-LINE.
060800
060900     MOVE DETAIL-LINE-4 TO DETAIL-LINE.
061000     PERFORM PRINT-ONE-LINE.
061100
061200     PERFORM LINE-FEED.
061300 LIST-ONE-RECORD.
061400     IF TBL-RECORD-TEST-BYTE NOT = HIGH-VALUES AND LOW-VALUES
061500         MOVE TBL-RECORD TO DETAIL-LINE
061600         PERFORM PRINT-ONE-LINE.
061700     PERFORM READ-NEXT-TBL-RECORD.
061800
061900 LINE-FEED.
062000     MOVE SPACE TO PRINTER-RECORD.
062100     WRITE PRINTER-RECORD AFTER ADVANCING 1.
062200     ADD 1 TO LINE-COUNT.
062300 FORM-FEED.
062400     MOVE SPACE TO PRINTER-RECORD.
062500     WRITE PRINTER-RECORD AFTER ADVANCING PAGE.
062600     MOVE 1 TO LINE-COUNT.
062700 PRINT-ONE-LINE.
062800     IF LINE-COUNT > 55
062900         PERFORM FORM-FEED.
063000     WRITE PRINTER-RECORD FROM DETAIL-LINE
063100         AFTER ADVANCING 1.
063200 DETERMINE-FILE-TYPE.
063300     MOVE LOW-VALUES TO TBL-RECORD.
063400     PERFORM READ-FORMAT-TBL-RECORD.
063500     IF TBL-FILE-STATUS = I-O-OK
063600         MOVE "Y" TO NUMERIC-ONLY-FIELD
063700     ELSE
063800         MOVE "N" TO NUMERIC-ONLY-FIELD
063900         .
064000 FILE-TYPE-ERROR.
064100     DISPLAY
064200     "                 "
064300     "FILE TYPE DOES NOT MATCH LENGTH SPECIFICATION"
064400     .
064500     PERFORM CLOSE-TBL-FILE.
064600 DOES-FILE-EXIST.
064700     MOVE TEST-FOR-FILE TO FILE-IO OF TBL-IO-BLOCK.
064800     PERFORM CALL-TABLEIO.
000000     MOVE FILE-STATUS OF TBL-IO-BLOCK TO CALLED-RC.
064900 SCRATCH-TABLE.
065000     MOVE FILE-NAME OF TBL-IO-BLOCK TO
065100         SCRATCH-FILE-NAME.
065200     MOVE FILE-LIBRARY OF TBL-IO-BLOCK TO
065300         SCRATCH-FILE-LIBRARY.
065400     MOVE FILE-VOLUME OF TBL-IO-BLOCK TO
065500         SCRATCH-FILE-VOLUME.
065600     PERFORM SCRATCH-A-FILE.
065700 OPEN-OUTPUT-TBL-FILE.
065800     MOVE OPEN-OUTPUT TO FILE-IO OF TBL-IO-BLOCK.
065900     PERFORM CALL-TABLEIO.
066000 OPEN-IO-AND-SET-TBL-FILE.
000000     CALL "KFILTYP" USING FILTYP.
000000     IF FILTYP = "1"
000000         PERFORM SET-TBL-FILE
000000         PERFORM OPEN-IO-TBL-FILE
000000     ELSE
066100         PERFORM OPEN-IO-TBL-FILE
066200         PERFORM SET-TBL-FILE.
066300     MOVE FILE-RECORD-LEN OF TBL-IO-BLOCK
066400        TO RECORD-LEN-FIELD.
066500 OPEN-IO-TBL-FILE.
066600     MOVE OPEN-IO TO FILE-IO OF TBL-IO-BLOCK.
066700     PERFORM CALL-TABLEIO.
066800*TEST-TBL-FILE.
066900*    MOVE FILE-INFO TO FILE-IO OF TBL-IO-BLOCK.
067000*    PERFORM CALL-TABLEIO.
067100 SET-TBL-FILE.
067200     MOVE FILE-SET TO FILE-IO OF TBL-IO-BLOCK.
067300     PERFORM CALL-TABLEIO.
067400 CLOSE-TBL-FILE.
067500     MOVE CLOSE-FILE TO FILE-IO OF TBL-IO-BLOCK.
067600     PERFORM CALL-TABLEIO.
067700 WRITE-TBL-RECORD.
067800     MOVE WRITE-RECORD TO FILE-IO OF TBL-IO-BLOCK.
067900     PERFORM CALL-TABLEIO.
068000 DELETE-TBL-RECORD.
068100     MOVE DELETE-RECORD TO FILE-IO OF TBL-IO-BLOCK.
068200     PERFORM CALL-TABLEIO.
068300 READ-FORMAT-TBL-RECORD.
068400     PERFORM READ-TBL-RECORD.
068500     COMPUTE WORK-INDEX =
068600       FILE-RECORD-LEN OF TBL-IO-BLOCK + 1.
068700     MOVE TBL-RECORD TO WORK-FIELD.
068800     MOVE DIM-FAC TO WORK-CHAR(WORK-INDEX).
068900     MOVE WORK-FIELD TO TBL-RECORD.
069000 READ-TBL-RECORD.
069100     MOVE READ-RECORD TO FILE-IO OF TBL-IO-BLOCK.
069200     PERFORM CALL-TABLEIO.
069300 READ-NEXT-TBL-RECORD.
000000     MOVE SPACE TO TBL-RECORD.
069400     MOVE READ-NEXT-RECORD TO FILE-IO OF TBL-IO-BLOCK.
069500     PERFORM CALL-TABLEIO.
069600 HOLD-TBL-RECORD.
069700     MOVE HOLD-RECORD TO FILE-IO OF TBL-IO-BLOCK.
069800     PERFORM CALL-TABLEIO.
069900 CALL-TABLEIO.
070000     CALL "TABLEIO"
070100          USING
070200          TBL-IO-BLOCK
070300          TBL-RECORD.
070400     MOVE FILE-STATUS OF TBL-IO-BLOCK TO
070500         TBL-FILE-STATUS.
070600     COPY PLCRT.
070700     COPY PLSCRTCH.
070800     COPY PLDISPK.
070900     COPY PLKEEP.
000000
000000     COPY PLKTRACE.
