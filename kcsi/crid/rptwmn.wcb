000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTWMN.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  11/04/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* Report writing menu.
000800* THIS PROGRAM USES A COMMON GETPARM FOR ALL FILES
000900* RPTDEF CONTROL CONTROL2 INPUT1 AND INPUT2
001000*---------------------------------------------------------------
001100
001200 ENVIRONMENT DIVISION.
001300 CONFIGURATION SECTION.
001400 FIGURATIVE-CONSTANTS.
001500     COPY FIGS.
001600
001700 INPUT-OUTPUT SECTION.
001800
001900 FILE-CONTROL.
002000
002100 DATA DIVISION.
002200 FILE SECTION.
002300
002400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptwmn.wcb 1.16 9/11/94".
002500     COPY WSFILEIO.
002600     COPY WSIOCODE.
002700     COPY WSCTRL.
002800     COPY WSPRT.
000000 01  GETPARM-TYPE               PIC XX VALUE "ID".
000000 01  CTRLDIF-PARENT    PIC X(8) VALUE "REPORT".
000000 01  CTRLDIF-IO-BLOCK.
000000     COPY LKDIO.
002900 01  CTRL1-HEADER-RECORDS.
003000     COPY RCCTRLH.
003100 01  CTRL2-HEADER-RECORDS.
003200     COPY RCCTRLH.
003300 01  CTRL-RECORD     PIC X(130).
003400     COPY WSUSAGE.
003500     COPY WSCRTS.
003600 01  CRF-RECORDS.
003700     03 CRF-RECORD OCCURS 81 TIMES.
003800     COPY RCCTRLF.
003900 01  RPT-IO-BLOCK.
004000     COPY LKDIO.
004100 01  CTRL-IO-BLOCK.
004200     COPY LKDIO.
004300 01  CTRL1-IO-BLOCK.
004400     COPY LKDIO.
004500 01  CTRL2-IO-BLOCK.
004600     COPY LKDIO.
004700 01  INPUT1-IO-BLOCK.
004800     COPY LKDIO.
004900 01  INPUT2-IO-BLOCK.
005000     COPY LKDIO.
005100 01  INPUT1-MODE  PIC X(6).
005200 01  INPUT2-MODE  PIC X(6).
005300 01  INPUT1-RECORD  PIC X(2040).
005400 01  INPUT2-RECORD  PIC X(2040).
005500 01  INPUT1-UFB PIC X.
005600 01  INPUT2-UFB PIC X.
005700 01  THE-MODE    PIC X(6).
005800 01  THIS-CALLER PIC X VALUE "R".
       01  STATUS-FILE-NAME PIC X.
005900* DATE STUFF
006000 01  THE-DATE  PIC 9(6).
006100 01  FORMATTED-DATE PIC 99/99/99.
006200 01  RPT-MESSAGE PIC X(79) VALUE SPACE.
006300     COPY WSRPTOPT.
006400
006500 01  THE-PRNAME   PIC X(8).
006600     COPY WSRPTREC.
006700 01  GENERAL-FIELD-1  PIC X(100).
006800 01  GENERAL-FIELD-2  PIC X(100).
000000 01  KMATCHRC         PIC 99.
000000 01  WORK-FILE-NAME   PIC X(8).
006900 01  A-FILE-OPEN-STATUS  PIC 9.
007000     88  THE-FILE-IS-OPEN VALUE 1.
000000     COPY LKPRC.
000000 01  KEY-TO-SEC-IDX       PIC 99.
007100 LINKAGE SECTION.
007200 01  PASSED-RPT-ID        PIC X(08).
007300 PROCEDURE DIVISION USING
007400     PASSED-RPT-ID.
007500 MAIN-LOGIC SECTION.
007600 PROGRAM-BEGIN.
000000     MOVE "RPTWMN BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
007700     PERFORM PROGRAM-INIT.
007800     PERFORM OPENING-PROCEDURE.
007900     PERFORM MAIN-PROCESS.
008000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTWMN EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
008100 MAIN-LOGIC-EXIT.
008200     EXIT PROGRAM.
008300 MAIN-LOGIC-STOP.
008400     STOP RUN.
008500 LEVEL-2 SECTION.
008600 PROGRAM-INIT.
008700     MOVE ZERO TO
008800        FILE-OPEN-STATUS OF RPT-IO-BLOCK
008900        FILE-OPEN-STATUS OF CTRL1-IO-BLOCK
009000        FILE-OPEN-STATUS OF CTRL2-IO-BLOCK
009100        FILE-OPEN-STATUS OF INPUT1-IO-BLOCK
009200        FILE-OPEN-STATUS OF INPUT2-IO-BLOCK
009300        .
009400     MOVE SPACE TO
009500        FILE-NAME OF RPT-IO-BLOCK
009600        FILE-LIBRARY OF RPT-IO-BLOCK
009700        FILE-VOLUME OF RPT-IO-BLOCK
009800        FILE-NAME OF CTRL1-IO-BLOCK
009900        FILE-LIBRARY OF CTRL1-IO-BLOCK
010000        FILE-VOLUME OF CTRL1-IO-BLOCK
010100        FILE-NAME OF CTRL2-IO-BLOCK
010200        FILE-LIBRARY OF CTRL2-IO-BLOCK
010300        FILE-VOLUME OF CTRL2-IO-BLOCK
010400        FILE-NAME OF INPUT1-IO-BLOCK
010500        FILE-LIBRARY OF INPUT1-IO-BLOCK
010600        FILE-VOLUME OF INPUT1-IO-BLOCK
010700        FILE-NAME OF INPUT2-IO-BLOCK
010800        FILE-LIBRARY OF INPUT2-IO-BLOCK
010900        FILE-VOLUME OF INPUT2-IO-BLOCK
011000         PRINTER-FILE-NAME
011100         PRINTER-FILE-LIBRARY
011200         PRINTER-FILE-VOLUME
011300        .
011400     PERFORM EXTRACT-USAGE-CONSTANTS.
011500     STRING
011600             USER-ID DELIMITED BY SPACE
011700             "CTL" DELIMITED BY SIZE
011800             INTO FILE-LIBRARY OF CTRL1-IO-BLOCK.
011900     MOVE FILE-LIBRARY OF CTRL1-IO-BLOCK TO
012000          FILE-LIBRARY OF CTRL2-IO-BLOCK.
012100     STRING
012200             USER-ID DELIMITED BY SPACE
012300             "RPT" DELIMITED BY SIZE
012400             INTO FILE-LIBRARY OF RPT-IO-BLOCK.
012500     MOVE INVOL TO
012600          FILE-VOLUME OF CTRL1-IO-BLOCK
012700          FILE-VOLUME OF CTRL2-IO-BLOCK
012800          FILE-VOLUME OF INPUT1-IO-BLOCK
012900          FILE-VOLUME OF INPUT2-IO-BLOCK
013000          FILE-VOLUME OF RPT-IO-BLOCK
013100          .
013200
000000     MOVE "OPENIN1" TO FILE-PRNAME OF INPUT1-IO-BLOCK.
000000     MOVE "OPENIN2" TO FILE-PRNAME OF INPUT2-IO-BLOCK.
000000
013300 OPENING-PROCEDURE.
013400 CLOSING-PROCEDURE.
013600 MAIN-PROCESS.
013700     PERFORM WRITE-OPTIONS.
013800     IF NOT END-KEY-PRESSED
013900         PERFORM OPEN-INPUT1-FILE
014000         PERFORM OPEN-INPUT2-FILE
014100         PERFORM WRITE-REPORT
014200         PERFORM CLOSE-INPUT1-FILE
014300         PERFORM CLOSE-INPUT2-FILE.
000000
014400 WRITE-OPTIONS.
014500     PERFORM WRITE-OPTIONS-INIT.
014600     PERFORM WRITE-OPTIONS-ENTRY.
000000
014700 WRITE-OPTIONS-INIT.
014800     IF  RPT-DEVICE NOT = "PRINTER"  AND
014900         RPT-DEVICE NOT = "DISPLAY"
015000         MOVE SPACE TO RPT-OPTIONS
015100         MOVE "PRINTER" TO RPT-DEVICE
015200         MOVE "NO" TO RPT-FILES
015300         MOVE "NO" TO RPT-OPTION
015400         MOVE "NO" TO RPT-ONLY
015500         MOVE 55 TO RPT-PAGE
015600         MOVE "123" TO RPT-LINES
015700         MOVE "000" TO RPT-SPACING
015800         MOVE SPACE TO RPT-MESSAGE
000000         MOVE "YES" TO RPT-EXT-SORT.
015900     PERFORM INIT-DATE.
016000     MOVE PASSED-RPT-ID TO RPT-ID.
016100
016200 INIT-DATE.
016300     ACCEPT THE-DATE FROM DATE.
016400     COMPUTE THE-DATE = THE-DATE * 100.0001
016500     MOVE THE-DATE TO FORMATTED-DATE.
016600     MOVE FORMATTED-DATE TO RPT-DATE.
016700
016800 WRITE-OPTIONS-ENTRY.
016900     CALL "RPTWOP" USING CALLED-RC RPT-OPTIONS RPT-MESSAGE.
017000     MOVE CALLED-RC TO PFKEY-CODE.
017100     MOVE SPACE TO RPT-MESSAGE.
017200     IF NOT END-KEY-PRESSED
017300         PERFORM VAL-FILES
017400         IF SCREEN-ENTRY-ERROR
017500             GO TO WRITE-OPTIONS-ENTRY.
017600
017700 VAL-FILES.
017800     PERFORM RPT-FILE-NAME.
017900     IF NOT END-KEY-PRESSED
018000         PERFORM CTRL1-FILE-NAME.
018100     IF NOT END-KEY-PRESSED
018200         PERFORM CTRL2-FILE-NAME.
018300     IF NOT END-KEY-PRESSED
018400         PERFORM LOAD-BOTH-CTRLS.
018500     IF NOT END-KEY-PRESSED
018600         PERFORM INPUT1-FILE-NAME.
018700     IF NOT END-KEY-PRESSED
018800         PERFORM INPUT2-FILE-NAME.
018900     IF END-KEY-PRESSED
019000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
019100
019200*---------------------------------------------------------------
019300* THE REPORT DEFINITION FILE.
019400*---------------------------------------------------------------
019500 RPT-FILE-NAME.
019600     MOVE "RPTDEF" TO THE-PRNAME.
019700     MOVE RPT-ID TO FILE-NAME OF RPT-IO-BLOCK.
019800     MOVE SPACE TO RPT-MESSAGE.
000000     MOVE "ID" TO GETPARM-TYPE.
019900     PERFORM GET-RPT-FILE-NAME.
020000 GET-RPT-FILE-NAME.
020100     MOVE RPT-IO-BLOCK TO CTRL-IO-BLOCK.
020200     PERFORM GET-FILE-NAME.
020300     MOVE CTRL-IO-BLOCK TO RPT-IO-BLOCK.
020400     IF NOT END-KEY-PRESSED
020500         PERFORM LOAD-RPT-FILE
020600         IF SCREEN-ENTRY-ERROR
020700             GO TO GET-RPT-FILE-NAME.
020800 LOAD-RPT-FILE.
020900     CALL "RPTLOD" USING
000000          CALLED-RC
021000          RPT-IO-BLOCK
021100          RPT-RECORDS.
021200     IF CALLED-RC NOT = ZEROES
021300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
000000         IF CALLED-RC = 95
000000             MOVE "RPTDEF Not Found" TO
000000               RPT-MESSAGE
000000         ELSE
021400             MOVE "Error in Report Definition File"
021500               TO RPT-MESSAGE.
021600
021700*---------------------------------------------------------------
021800* THE PRIMARY CONTROL FILE.
021900*---------------------------------------------------------------
022000
022100 CTRL1-FILE-NAME.
022200     MOVE "CONTROL" TO THE-PRNAME.
022300     MOVE SPACE TO RPT-MESSAGE.
000000     MOVE "ID" TO GETPARM-TYPE.
022400     PERFORM LOAD-CTRL1-FILE-NAME.
022500     PERFORM GET-CTRL1-FILE-NAME.
022600
022700 GET-CTRL1-FILE-NAME.
022800     MOVE CTRL1-IO-BLOCK TO CTRL-IO-BLOCK.
022900     PERFORM GET-FILE-NAME.
023000     MOVE CTRL-IO-BLOCK TO CTRL1-IO-BLOCK.
023100     IF NOT END-KEY-PRESSED
023200         PERFORM LOAD-CTRL1-FILE
023300         IF SCREEN-ENTRY-ERROR
023400             GO TO GET-CTRL1-FILE-NAME.
000000
023500 LOAD-CTRL1-FILE-NAME.
023600     IF RCF-PRIM-FILE NOT = SPACE
023700         MOVE RCF-PRIM-FILE TO FILE-NAME OF CTRL1-IO-BLOCK
023800     ELSE
023900     IF RDF-PRIM-FILE NOT = SPACE
024000         MOVE RDF-PRIM-FILE TO FILE-NAME OF CTRL1-IO-BLOCK.
024100     IF RCF-PRIM-LIBRARY NOT = SPACE
024200         MOVE RCF-PRIM-LIBRARY TO FILE-LIBRARY OF CTRL1-IO-BLOCK.
024300     IF RCF-PRIM-VOLUME NOT = SPACE
024400         MOVE RCF-PRIM-VOLUME TO FILE-VOLUME OF CTRL1-IO-BLOCK.
024500
024600 LOAD-CTRL1-FILE.
024700      MOVE CTRL1-IO-BLOCK TO CTRL-IO-BLOCK.
024800      PERFORM LOAD-CTRL-FILE.
024900      MOVE CTRL2-HEADER-RECORDS TO CTRL1-HEADER-RECORDS.
000000      MOVE SPACE TO CTRL2-HEADER-RECORDS.
025000      MOVE CTRL-IO-BLOCK TO CTRL1-IO-BLOCK.
025100
025200*---------------------------------------------------------------
025300* THE secondary CONTROL FILE.
025400* IS ONLY ENTERED IF A SECONDARY DATA FILE IS SPECIFIED.
025500*---------------------------------------------------------------
025600
025700 CTRL2-FILE-NAME.
025800     MOVE "CONTROL2" TO THE-PRNAME.
025900     MOVE SPACE TO RPT-MESSAGE.
000000     MOVE "ID" TO GETPARM-TYPE.
026000     IF RDF-SEC-FILE NOT = SPACE
026100         PERFORM LOAD-CTRL2-FILE-NAME
026200         PERFORM GET-CTRL2-FILE-NAME.
026300 GET-CTRL2-FILE-NAME.
026400     MOVE CTRL2-IO-BLOCK TO CTRL-IO-BLOCK.
026500     PERFORM GET-FILE-NAME.
026600     MOVE CTRL-IO-BLOCK TO CTRL2-IO-BLOCK.
026700     IF NOT END-KEY-PRESSED
026800         PERFORM LOAD-CTRL2-FILE
026900         IF SCREEN-ENTRY-ERROR
027000             GO TO GET-CTRL2-FILE-NAME.
027100 LOAD-CTRL2-FILE-NAME.
027200     IF RCF-SEC-FILE NOT = SPACE
027300         MOVE RCF-SEC-FILE TO FILE-NAME OF CTRL2-IO-BLOCK
027400     ELSE
027500     IF RDF-SEC-FILE NOT = SPACE
027600         MOVE RDF-SEC-FILE TO FILE-NAME OF CTRL2-IO-BLOCK.
027700     IF RCF-SEC-LIBRARY NOT = SPACE
027800         MOVE RCF-SEC-LIBRARY TO FILE-LIBRARY OF CTRL2-IO-BLOCK.
027900     IF RCF-SEC-VOLUME NOT = SPACE
028000         MOVE RCF-SEC-VOLUME TO FILE-VOLUME OF CTRL2-IO-BLOCK.
028100
028200 LOAD-CTRL2-FILE.
028300      MOVE CTRL2-IO-BLOCK TO CTRL-IO-BLOCK.
028400      PERFORM LOAD-CTRL-FILE.
028500      MOVE CTRL-IO-BLOCK TO CTRL2-IO-BLOCK.
028600
028700 LOAD-BOTH-CTRLS.
028800     CALL "RPTCLD" USING
000000          CALLED-RC
028900          RPT-RECORDS
029000          CRF-RECORDS
029100          CTRL1-IO-BLOCK
029200          CTRL2-IO-BLOCK
040800          CTRL2-HEADER-RECORDS
000000          KEY-TO-SEC-IDX.
029300     IF CALLED-RC NOT = ZEROES
029400         MOVE 16 TO PFKEY-CODE
000000         IF CALLED-RC = 96
000000             MOVE "Secondary File is not Keyed"
029600              TO RPT-MESSAGE
000000         ELSE
029500             MOVE "RPTDEF and CONTROL(S) do Not Match"
029600              TO RPT-MESSAGE.
029700
029800*---------------------------------------------------------------
029900* THE PRIMARY DATA FILE.
030000*---------------------------------------------------------------
030100
030200 INPUT1-FILE-NAME.
030300     MOVE "INPUT1" TO THE-PRNAME.
030400     MOVE SPACE TO RPT-MESSAGE.
000000     MOVE "ID" TO GETPARM-TYPE.
000000     IF RPT-FILES = "YES"
000000         MOVE "I " TO GETPARM-TYPE.
030500     PERFORM LOAD-INPUT1-FILE-NAME.
030600     PERFORM GET-INPUT1-FILE-NAME.
030700
030800 GET-INPUT1-FILE-NAME.
030900     MOVE INPUT1-IO-BLOCK TO CTRL-IO-BLOCK.
031000     PERFORM GET-FILE-NAME.
031100     MOVE CTRL-IO-BLOCK TO INPUT1-IO-BLOCK.
031200     MOVE THE-MODE TO INPUT1-MODE.
031300     IF NOT END-KEY-PRESSED
031400         PERFORM TEST-INPUT1-FILE
031500         IF CALLED-RC NOT = ZEROES
000000             MOVE "I" TO GETPARM-TYPE
031600             GO TO GET-INPUT1-FILE-NAME.
031700 LOAD-INPUT1-FILE-NAME.
031800     IF RDF-PRIM-FILE NOT = SPACE
031900         MOVE RDF-PRIM-FILE TO FILE-NAME OF INPUT1-IO-BLOCK.
032000     IF RDF-PRIM-LIBRARY NOT = SPACE
032100         MOVE RDF-PRIM-LIBRARY TO FILE-LIBRARY OF INPUT1-IO-BLOCK.
032200     IF RDF-PRIM-VOLUME NOT = SPACE
032300         MOVE RDF-PRIM-VOLUME TO FILE-VOLUME OF INPUT1-IO-BLOCK.
032400
032500*---------------------------------------------------------------
032600* THE SECONDARY DATA FILE.
032700*---------------------------------------------------------------
032800
032900 INPUT2-FILE-NAME.
033000     MOVE "INPUT2" TO THE-PRNAME.
033100     MOVE SPACE TO RPT-MESSAGE.
000000     MOVE "ID" TO GETPARM-TYPE.
000000     IF RPT-FILES = "YES"
000000         MOVE "I " TO GETPARM-TYPE.
033200     IF RDF-SEC-FILE NOT = SPACE
033300         PERFORM LOAD-INPUT2-FILE-NAME
033400         PERFORM GET-INPUT2-FILE-NAME.
033500
033600 GET-INPUT2-FILE-NAME.
033700     MOVE INPUT2-IO-BLOCK TO CTRL-IO-BLOCK.
033800     PERFORM GET-FILE-NAME.
033900     MOVE CTRL-IO-BLOCK TO INPUT2-IO-BLOCK.
034000     MOVE THE-MODE TO INPUT2-MODE.
034100     IF NOT END-KEY-PRESSED
034200         PERFORM TEST-INPUT2-FILE
034300         IF CALLED-RC NOT = ZEROES
000000             MOVE "I" TO GETPARM-TYPE
034400             GO TO GET-INPUT2-FILE-NAME.
034500 LOAD-INPUT2-FILE-NAME.
034600     MOVE RDF-SEC-FILE TO FILE-NAME OF INPUT2-IO-BLOCK.
034700     IF RDF-SEC-LIBRARY NOT = SPACE
034800         MOVE RDF-SEC-LIBRARY TO FILE-LIBRARY OF INPUT2-IO-BLOCK.
034900     IF RDF-SEC-VOLUME NOT = SPACE
035000         MOVE RDF-SEC-VOLUME TO FILE-VOLUME OF INPUT2-IO-BLOCK.
035100
035200*---------------------------------------------------------------
035300* LOAD A SUPPOSED FILE DEFINITION FROM THE CRTRL-FILE ITSELF.
035400* COMPARE THAT TO THE ACTUAL FILE DATA AND ERROR IF THEY DO NOT
035500* MATCH.
035600*---------------------------------------------------------------
035700 TEST-INPUT1-FILE.
035800     CALL "CTRLDIO" USING
000000           CALLED-RC
035900           CTRL1-IO-BLOCK
036000           INPUT1-IO-BLOCK
036100           CTRL1-HEADER-RECORDS.
036200     IF CALLED-RC = ZEROES
036300         MOVE ZEROES TO FILE-ALTKEY-DATA OF INPUT1-IO-BLOCK
036400         MOVE FILE-ID-DATA OF INPUT1-IO-BLOCK  TO
036500             GENERAL-FIELD-1
000000         MOVE INPUT1-IO-BLOCK TO CTRLDIF-IO-BLOCK
036600         PERFORM OPEN-INPUT1-FILE
036700         PERFORM EXTRACT-INPUT1-INFO
036800         PERFORM CLOSE-INPUT1-FILE
036900         MOVE ZEROES TO FILE-ALTKEY-DATA OF INPUT1-IO-BLOCK
037000         MOVE FILE-ID-DATA OF INPUT1-IO-BLOCK TO
037100             GENERAL-FIELD-2
000000         CALL "KMATCH" USING KMATCHRC 
000000                             GENERAL-FIELD-1
000000                             GENERAL-FIELD-2
000000         MOVE KMATCHRC TO CALLED-RC.
037200*         IF GENERAL-FIELD-1 NOT = GENERAL-FIELD-2
037300*             MOVE 95 TO CALLED-RC.
037400      IF CALLED-RC NOT = ZEROES
000000          CALL "CTRLDIF" USING CTRLDIF-IO-BLOCK INPUT1-IO-BLOCK
000000                               CTRLDIF-PARENT
037500          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
037600          MOVE
037700          "Format Conflict Between RPTDEF CONTROL and DATA"
037800            TO RPT-MESSAGE.
037900 TEST-INPUT2-FILE.
038000     CALL "CTRLDIO" USING
000000           CALLED-RC
038100           CTRL2-IO-BLOCK
038200           INPUT2-IO-BLOCK
038300           CTRL2-HEADER-RECORDS.
038400     IF CALLED-RC = ZEROES
038500         MOVE FILE-ID-DATA OF INPUT2-IO-BLOCK  TO
038600             GENERAL-FIELD-1
000000         MOVE INPUT2-IO-BLOCK TO CTRLDIF-IO-BLOCK
038700         PERFORM OPEN-INPUT2-FILE
038800         PERFORM EXTRACT-INPUT2-INFO
038900         PERFORM CLOSE-INPUT2-FILE
039000         MOVE FILE-ID-DATA OF INPUT2-IO-BLOCK TO
039100             GENERAL-FIELD-2
039200         IF GENERAL-FIELD-1 NOT = GENERAL-FIELD-2
039300             MOVE 95 TO CALLED-RC.
039400      IF CALLED-RC NOT = ZEROES
000000          CALL "CTRLDIF" USING CTRLDIF-IO-BLOCK INPUT2-IO-BLOCK
000000                               CTRLDIF-PARENT
039500          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
039600          MOVE
039700          "Format Conflict Between RPTDEF CONTROL and DATA"
039800            TO RPT-MESSAGE.
039900*---------------------------------------------------------------
040000* GENERAL PURPOSE CONTROL HEADER LOAD ROUTINE
040100* USES CTRL2-HEADERS FOR THE LOAD FOR CTRL1 THEY ARE MOVED
040200* AFTER THE LOAD.
040300*---------------------------------------------------------------
040400 LOAD-CTRL-FILE.
040500      PERFORM OPEN-INPUT-CTRL-FILE.
040600      CALL "CTRLHDL" USING
040700          CTRL-IO-BLOCK
040800          CTRL2-HEADER-RECORDS.
041400      MOVE CH-REPORT-ALLOWED OF CTRL2-HEADER-RECORDS
041500       TO GENERAL-FIELD-1
041600      IF GENERAL-FIELD-1 = "1"
041700          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
041800          MOVE "Reporting Not Allowed for this File"
041900            TO RPT-MESSAGE.
042000      PERFORM CLOSE-CTRL-FILE.
042100*--------------------------------------------------------------
042200* FILENAME GETPARM FOR ALL FILES.
042300* THE CTRL-IO-BLOCK IS USED AS A DUMMY FOR COLLECTING
042400* THE INPUT.
042500*--------------------------------------------------------------
042600
042700 GET-FILE-NAME.
042800     MOVE SPACE TO SCREEN-ERROR-STATUS.
042900     CALL "RPTFIL" USING
000000         CALLED-RC
043000         FILE-NAME OF CTRL-IO-BLOCK
043100         FILE-LIBRARY OF CTRL-IO-BLOCK
043200         FILE-VOLUME OF CTRL-IO-BLOCK
043300         THE-MODE
043400         THE-PRNAME
043500         RPT-MESSAGE
000000         GETPARM-TYPE.
043600     MOVE SPACE TO RPT-MESSAGE.
043700     MOVE CALLED-RC TO PFKEY-CODE.
043800     IF NOT END-KEY-PRESSED
      *$ACU_CODE  PROCESS
      *         CALL "ISDBFILE" USING FILE-NAME OF CTRL-IO-BLOCK,
      *                               STATUS-FILE-NAME
      *         IF STATUS-FILE-NAME = "N"
043900*             PERFORM VAL-THE-FILE-EXISTS
044000*             IF SCREEN-ENTRY-ERROR
000000*                 MOVE "I " TO GETPARM-TYPE
044100*                 GO TO GET-FILE-NAME.
      *$ACU_ELSE
043900         PERFORM VAL-THE-FILE-EXISTS
044000         IF SCREEN-ENTRY-ERROR
000000             MOVE "I " TO GETPARM-TYPE
044100             GO TO GET-FILE-NAME.
      *$ACU_END
044200 VAL-THE-FILE-EXISTS.
044300     CALL "KEXISTS" USING
000000         CALLED-RC
044400         FILE-NAME OF CTRL-IO-BLOCK
044500         FILE-LIBRARY OF CTRL-IO-BLOCK
044600         FILE-VOLUME OF CTRL-IO-BLOCK.
044700     IF CALLED-RC NOT = ZEROES
044800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
044900         MOVE "File Not Found"
045000            TO RPT-MESSAGE.
045100
045200* This is it
045300 WRITE-REPORT.
045400     CALL "PLSWAIT".
045500     CALL "RPTCALL" USING
045600         RPT-OPTIONS
045700         RHD-RECORD
045800         RDF-RECORD
045900         RCB-ENTRIES
046000         RCD-RECORDS
046100         RFL-RECORDS
046200         CRF-RECORDS
046300         RTT-RECORDS
046400         ST-SORTS
046500         DL-FIELDS
046600         NF-FIELDS
046700         INPUT1-IO-BLOCK
046800         INPUT2-IO-BLOCK
046900         PRINTER-FILE-NAME
047000         PRINTER-FILE-LIBRARY
047100         PRINTER-FILE-VOLUME
047200         THIS-CALLER
000000         KEY-TO-SEC-IDX.
047300*    CALL "BLNKSC".
047400* UTILITY ROUTINES
047500     COPY PLUSAGE.
047600 OPEN-INPUT-CTRL-FILE.
047700     MOVE OPEN-INPUT TO FILE-IO
047800          OF CTRL-IO-BLOCK.
047900     PERFORM IO-TO-CTRL-FILE.
048000 CLOSE-CTRL-FILE.
048100     MOVE CLOSE-FILE TO FILE-IO
048200          OF CTRL-IO-BLOCK.
048300     PERFORM IO-TO-CTRL-FILE.
048400 IO-TO-CTRL-FILE.
048500     CALL "CTRLIO" USING
048600          CTRL-IO-BLOCK
048700          CTRL-RECORD.
048800     MOVE FILE-STATUS OF CTRL-IO-BLOCK TO
048900          CTRL-FILE-STATUS.
049000 OPEN-INPUT1-FILE.
049100     IF INPUT1-MODE = "INPUT"
049200         MOVE OPEN-INPUT TO FILE-IO OF INPUT1-IO-BLOCK
049300     ELSE
049400         MOVE OPEN-SHARED TO FILE-IO OF INPUT1-IO-BLOCK.
049500     PERFORM IO-TO-INPUT1.
049600 CLOSE-INPUT1-FILE.
049700     MOVE FILE-OPEN-STATUS OF INPUT1-IO-BLOCK TO
049800        A-FILE-OPEN-STATUS.
049900     IF THE-FILE-IS-OPEN
050000         MOVE CLOSE-FILE TO FILE-IO OF INPUT1-IO-BLOCK
050100         PERFORM IO-TO-INPUT1.
050200 EXTRACT-INPUT1-INFO.
050300     MOVE FILE-INFO TO FILE-IO OF INPUT1-IO-BLOCK.
050400     PERFORM IO-TO-INPUT1.
050500 IO-TO-INPUT1.
050600     CALL "KCSIO" USING
050700         INPUT1-IO-BLOCK
050800         INPUT1-UFB
050900         INPUT1-RECORD.
051000 OPEN-INPUT2-FILE.
051100     MOVE FILE-NAME OF INPUT2-IO-BLOCK TO WORK-FILE-NAME.
051200     IF WORK-FILE-NAME NOT = SPACE
051300         IF INPUT2-MODE = "INPUT"
051400             MOVE OPEN-INPUT TO FILE-IO OF INPUT2-IO-BLOCK
051500         ELSE
051600             MOVE OPEN-SHARED TO FILE-IO OF INPUT2-IO-BLOCK.
051700     IF WORK-FILE-NAME NOT = SPACE
051800         PERFORM IO-TO-INPUT2.
051900 CLOSE-INPUT2-FILE.
052000     MOVE FILE-OPEN-STATUS OF INPUT2-IO-BLOCK TO
052100        A-FILE-OPEN-STATUS.
052200     IF THE-FILE-IS-OPEN
052300         MOVE CLOSE-FILE TO FILE-IO OF INPUT2-IO-BLOCK
052400         PERFORM IO-TO-INPUT2.
052500 EXTRACT-INPUT2-INFO.
052600     MOVE FILE-INFO TO FILE-IO OF INPUT2-IO-BLOCK.
052700     PERFORM IO-TO-INPUT2.
052800 IO-TO-INPUT2.
052900     CALL "KCSIO" USING
053000         INPUT2-IO-BLOCK
053100         INPUT2-UFB
053200         INPUT2-RECORD.
053300
000000     COPY PLKTRACE.
