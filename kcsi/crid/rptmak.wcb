000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. RPTMAK.
000300 AUTHOR. HEATHER MLODINOW.
000400 DATE-WRITTEN.  11/20/89.
000500 DATE-COMPILED.
000600*--------------------------------------------------------------
000700* WRITES THE RECORDS TO THE REPORT DEFINITION FILE
000800*--------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300       COPY FIGS.
001400 INPUT-OUTPUT SECTION.
001500 FILE-CONTROL.
001600 DATA DIVISION.
001700 FILE SECTION.
001800 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)rptmak.wcb 1.6 9/11/94".
001900 01  HEADER-KEY          PIC X(13) VALUE "  CONTROL    ".
002000 01  DATA-FILES-KEY      PIC X(13) VALUE "  FILES      ".
002100 01  CONTROL-FILE-KEY    PIC X(13) VALUE "  FILESCTL   ".
002200 01  SORT-1-KEY          PIC X(13) VALUE "  SORT1      ".
002300 01  SORT-2-KEY          PIC X(13) VALUE "  SORT2      ".
002400 01  CONTROL-FIELDS-KEY  PIC X(13) VALUE " CNTRL FLDS  ".
002500 01  TITLE-KEY-TABLE.
002600     05  TITLE-WORK-KEYS.
002700         07  TITLE-1-KEY         PIC X(13) VALUE " H1-TITLE    ".
002800         07  TITLE-2-KEY         PIC X(13) VALUE " H2-TITLE    ".
002900         07  TITLE-3-KEY         PIC X(13) VALUE " H3-TITLE    ".
003000         07  PAGE-1-KEY          PIC X(13) VALUE " H4-PAGEH    ".
003100         07  PAGE-2-KEY          PIC X(13) VALUE " H5-PAGEH    ".
003200         07  PAGE-3-KEY          PIC X(13) VALUE " H6-PAGEH    ".
003300     05  TITLE-KEY REDEFINES TITLE-WORK-KEYS
003400                                 PIC X(13) OCCURS 6 TIMES.
003500 01  BREAK-DESC-TABLE.
003600     05  BREAK-DESC-WORK-KEYS.
003700         07  BREAK-DESC-1-KEY    PIC X(13) VALUE " HC1CNTRL    ".
003800         07  BREAK-DESC-2-KEY    PIC X(13) VALUE " HC2CNTRL    ".
003900         07  BREAK-DESC-3-KEY    PIC X(13) VALUE " HC3CNTRL    ".
004000         07  BREAK-DESC-4-KEY    PIC X(13) VALUE " HC4CNTRL    ".
004100         07  BREAK-DESC-5-KEY    PIC X(13) VALUE " HC5CNTRL    ".
004200         07  BREAK-DESC-6-KEY    PIC X(13) VALUE " HC6CNTRL    ".
004300     05  BREAK-DESC-KEY REDEFINES BREAK-DESC-WORK-KEYS
004400                                 PIC X(13) OCCURS 5 TIMES.
004500 01  DATA-LIMIT-KEY.
004600     05  LIMIT-TYPE      PIC X(01) VALUE SPACE.
004700     05  LIMIT-NAME      PIC X(08) VALUE SPACE.
004800     05  LIMIT-OCCURS    PIC X(02) VALUE SPACE.
004900     05  LIMIT-ORIGIN    PIC 9(01) VALUE ZERO.
005000     05  LIMIT-REC-NO    PIC 9(01) VALUE ZERO.
005100 01  NEW-FIELDS-KEY.
005200     05  NEW-TYPE        PIC X(01) VALUE "R".
005300     05  NEW-NAME        PIC X(08) VALUE SPACE.
005400     05  NEW-REC-NO      PIC 9(02) VALUE ZERO.
005500     05  NEW-FLD-NO      PIC 9(01) VALUE ZERO COMP.
005600 01  FIELD-DESC-KEY.
005700     05  FIELD-TYPE      PIC X(01) VALUE SPACE.
005800     05  FIELD-SEQ       PIC 9(02) VALUE ZERO.
005900     05  FIELD-NAME      PIC X(08) VALUE SPACE.
006000     05  FIELD-OCCURS    PIC X(02) VALUE SPACE.
006100
006200 01  DATA-LIMITS-TABLE.
006300     05  VALID-LIMITS-TYPES.
006400         10  FILLER      PIC X(01) VALUE "C".
006500         10  FILLER      PIC X(01) VALUE "D".
006600         10  FILLER      PIC X(01) VALUE "E".
006700         10  FILLER      PIC X(01) VALUE "F".
006800         10  FILLER      PIC X(01) VALUE "G".
006900         10  FILLER      PIC X(01) VALUE "H".
007000         10  FILLER      PIC X(01) VALUE "I".
007100         10  FILLER      PIC X(01) VALUE "J".
007200         10  FILLER      PIC X(01) VALUE "K".
007300         10  FILLER      PIC X(01) VALUE "L".
007400     05  VALID-DATA-LIMIT REDEFINES VALID-LIMITS-TYPES
007500                         PIC X(01) OCCURS 10 TIMES.
007600
007700 01  RDL-IDX           BINARY.
007800 01  RDL-ENTRY-IDX     BINARY.
007900 01  RNF-IDX           BINARY.
008000 01  NEXT-IDX          BINARY.
008100 01  WORK-IDX          BINARY.
008200 01  ST-SEC-IDX        PIC 9(01) VALUE 0.
008300 01  SEQ-IDX           PIC 9(03) VALUE 0.
008400     COPY WSFILEIO.
008500     COPY WSIOCODE.
008600     COPY WSRPT.
008700     COPY RCRPT.
008800     COPY WSRPTWRK.
008900 LINKAGE SECTION.
009000     COPY WSRPTREC.
009100 01  RPT-IO-BLOCK.
009200     COPY LKDIO.
009300 01  LOAD-SWITCH     PIC X(01).
009400 PROCEDURE DIVISION USING  LOAD-SWITCH RPT-IO-BLOCK RPT-RECORDS.
009500
009600 MAIN-LOGIC SECTION.
009700 PROGRAM-BEGIN.
000000     MOVE "RPTMAK BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
009800     PERFORM OPENING-PROCEDURE.
009900     PERFORM MAIN-PROCESS.
010000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "RPTMAK EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
010100 MAIN-LOGIC-EXIT.
010200     EXIT PROGRAM.
010300 MAIN-LOGIC-STOP.
010400     STOP RUN.
010500
010600 LEVEL-2 SECTION.
010700 OPENING-PROCEDURE.
010800     IF LOAD-SWITCH = "C"
010900         PERFORM OPEN-OUTPUT-RPT-FILE
011000         PERFORM CLOSE-RPT-FILE
011100         PERFORM OPEN-IO-RPT-FILE.
011200     IF LOAD-SWITCH = "M"
011300         PERFORM OPEN-IO-RPT-FILE.
011400 CLOSING-PROCEDURE.
011500     PERFORM CLOSE-RPT-FILE.
011600 MAIN-PROCESS.
011700     PERFORM WRITE-HEADER-RECORD.
011800     PERFORM WRITE-RDF-RECORD.
011900     PERFORM WRITE-RCF-RECORD.
012000     PERFORM WRITE-SORT-RECORDS.
012100     PERFORM WRITE-RCB-RECORD.
012200     PERFORM WRITE-TITLE-RECORDS.
012300     PERFORM WRITE-RCD-RECORDS.
012400     PERFORM WRITE-LIMIT-RECORDS.
012500     PERFORM WRITE-NF-RECORDS.
012600     PERFORM WRITE-FIELD-RECORDS.
012700 WRITE-HEADER-RECORD.
012800     MOVE HEADER-KEY TO RHD-KEY.
000000     MOVE RHD-RECORD TO RPT-RECORD.
012900     PERFORM HOLD-RPT-RECORD.
013000     IF RPT-FILE-STATUS NOT = RECORD-NOT-FOUND
013100         MOVE RHD-RECORD TO RPT-RECORD
013200         PERFORM REWRITE-RPT-RECORD
013300     ELSE
013400         MOVE HEADER-KEY TO RHD-KEY
013500         MOVE RHD-RECORD TO RPT-RECORD
013600         PERFORM WRITE-RPT-RECORD.
013700 WRITE-RDF-RECORD.
013800     MOVE RDF-KEY-VALUE TO RDF-KEY.
000000     MOVE RDF-RECORD TO RPT-RECORD.
013900     PERFORM HOLD-RPT-RECORD.
014000     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
014100         MOVE RDF-KEY-VALUE TO RDF-KEY
014200         MOVE RDF-RECORD TO RPT-RECORD
014300         PERFORM WRITE-RPT-RECORD
014400     ELSE
014500         MOVE RDF-RECORD TO RPT-RECORD
014600         PERFORM REWRITE-RPT-RECORD.
014700 WRITE-RCF-RECORD.
014800     MOVE RCF-KEY-VALUE TO RCF-KEY.
000000     MOVE RCF-RECORD TO RPT-RECORD.
014900     PERFORM HOLD-RPT-RECORD.
015000     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
015100         MOVE RCF-KEY-VALUE TO RCF-KEY
015200         MOVE RCF-RECORD TO RPT-RECORD
015300         PERFORM WRITE-RPT-RECORD
015400     ELSE
015500         MOVE RCF-RECORD TO RPT-RECORD
015600         PERFORM REWRITE-RPT-RECORD.
015700 WRITE-SORT-RECORDS.
015800     MOVE 1 TO ST-IDX.
000000     MOVE SPACE TO RST-RECORD.
015900     MOVE SORT-1-KEY TO RST-KEY.
000000     MOVE RST-RECORD TO RPT-RECORD.
016000     PERFORM HOLD-RPT-RECORD.
016100     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
016300         IF ST-NAME (ST-IDX) NOT = SPACES
016400             PERFORM LOAD-A-SORT
016500                 VARYING ST-IDX FROM 1 BY 1
016600                 UNTIL ST-IDX > 6
016800             MOVE RST-RECORD TO RPT-RECORD
016900             PERFORM WRITE-RPT-RECORD
017000         ELSE
017100             NEXT SENTENCE
017200     ELSE
017300         IF ST-NAME (ST-IDX) NOT = SPACES
017400             PERFORM LOAD-A-SORT
017500                 VARYING ST-IDX FROM 1 BY 1
017600                 UNTIL ST-IDX > 6
017700             MOVE RST-RECORD TO RPT-RECORD
017800             PERFORM REWRITE-RPT-RECORD
017900         ELSE
018000             PERFORM DELETE-RPT-RECORD.
018100
018200     MOVE 7 TO ST-IDX.
018300     MOVE 0 TO ST-SEC-IDX.
000000     MOVE SPACE TO RST-RECORD.
018400     MOVE SORT-2-KEY TO RST-KEY.
000000     MOVE RST-RECORD TO RPT-RECORD.
018500     PERFORM HOLD-RPT-RECORD.
018600     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
018800         IF ST-NAME (ST-IDX) NOT = SPACES
018900             PERFORM LOAD-A-SORT
019000                 VARYING ST-IDX FROM 7 BY 1
019100                 UNTIL ST-IDX > 8
019300             MOVE RST-RECORD TO RPT-RECORD
019400             PERFORM WRITE-RPT-RECORD
019500         ELSE
019600             NEXT SENTENCE
019700     ELSE
019800         IF ST-NAME (ST-IDX) NOT = SPACES
019900             PERFORM LOAD-A-SORT
020000                 VARYING ST-IDX FROM 7 BY 1
020100                 UNTIL ST-IDX > 8
020200             MOVE RST-RECORD TO RPT-RECORD
020300             PERFORM REWRITE-RPT-RECORD
020400         ELSE
020500             PERFORM DELETE-RPT-RECORD.
020600 LOAD-A-SORT.
020700     ADD 1 TO ST-SEC-IDX.
020800     MOVE ST-SORT (ST-IDX) TO RST-SORT (ST-SEC-IDX).
020900
021000 WRITE-RCB-RECORD.
021100     MOVE RCB-KEY-VALUE TO RCB-KEY.
000000     MOVE RCB-RECORD TO RPT-RECORD.
021200     PERFORM HOLD-RPT-RECORD.
021300     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
021400         IF RCB-NAME (1) NOT = SPACES
021500             MOVE RCB-KEY-VALUE TO RCB-KEY
021600             MOVE RCB-RECORD TO RPT-RECORD
021700             PERFORM WRITE-RPT-RECORD
021800             ELSE
021900                 NEXT SENTENCE
022000     ELSE
022100         IF RCB-NAME (1) NOT = SPACES
022200             MOVE RCB-RECORD TO RPT-RECORD
022300             PERFORM REWRITE-RPT-RECORD
022400         ELSE
022500             PERFORM DELETE-RPT-RECORD.
022600 WRITE-TITLE-RECORDS.
022700     PERFORM WRITE-A-TITLE-RECORD
022800      VARYING RTT-IDX FROM 1 BY 1
022900       UNTIL RTT-IDX > 6.
023000 WRITE-A-TITLE-RECORD.
023100     MOVE TITLE-KEY (RTT-IDX) TO RTT-KEY(RTT-IDX).
000000     MOVE RTT-RECORD(RTT-IDX) TO RPT-RECORD.
023200     PERFORM HOLD-RPT-RECORD.
023300     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
023400         IF RTT-TITLE (RTT-IDX) NOT = SPACES
023500             MOVE TITLE-KEY (RTT-IDX) TO RTT-KEY (RTT-IDX)
023600             MOVE RTT-RECORD (RTT-IDX) TO RPT-RECORD
023700             PERFORM WRITE-RPT-RECORD
023800         ELSE
023900             NEXT SENTENCE
024000     ELSE
024100         IF RTT-TITLE (RTT-IDX) NOT = SPACES
024200             MOVE RTT-RECORD (RTT-IDX) TO RPT-RECORD
024300             PERFORM REWRITE-RPT-RECORD
024400         ELSE
024500             PERFORM DELETE-RPT-RECORD.
024600 WRITE-RCD-RECORDS.
024700     PERFORM WRITE-AN-RCD-RECORD
024800      VARYING RCD-IDX FROM 1 BY 1
024900       UNTIL RCD-IDX > 5.
025000 WRITE-AN-RCD-RECORD.
025100     MOVE BREAK-DESC-KEY (RCD-IDX) TO RCD-KEY(RCD-IDX).
000000     MOVE RCD-RECORD(RCD-IDX) TO RPT-RECORD.
025200     PERFORM HOLD-RPT-RECORD.
025300     IF RPT-FILE-STATUS = RECORD-NOT-FOUND
025400         IF RCD-BREAK (RCD-IDX) NOT = SPACES
025500             MOVE BREAK-DESC-KEY (RCD-IDX) TO RCD-KEY (RCD-IDX)
025600             MOVE RCD-RECORD (RCD-IDX) TO RPT-RECORD
025700             PERFORM WRITE-RPT-RECORD
025800         ELSE
025900             NEXT SENTENCE
026000     ELSE
026100         IF RCD-BREAK (RCD-IDX) NOT = SPACES
026200             MOVE RCD-RECORD (RCD-IDX) TO RPT-RECORD
026300             PERFORM REWRITE-RPT-RECORD
026400         ELSE
026500             PERFORM DELETE-RPT-RECORD.
026600 WRITE-LIMIT-RECORDS.
026700     PERFORM DELETE-LIMIT-RECORDS.
026800     MOVE 0 TO RDL-IDX.
026900     PERFORM PROCESS-ONE-LIMIT-SET
027000         VARYING DL-IDX FROM 1 BY 1
027100         UNTIL DL-IDX > 10.
027200 PROCESS-ONE-LIMIT-SET.
027300     MOVE SPACES TO RDL-RECORD.
027400     MOVE 0 TO DL-ENTRY-IDX.
027500     IF DL-NAME (DL-IDX) NOT = SPACES
027600         ADD 1 TO RDL-IDX
027700         MOVE 0 TO RDL-RECORD-NUMBER
027800         PERFORM LOAD-ONE-LIMIT.
027900 LOAD-ONE-LIMIT.
028000     MOVE VALID-DATA-LIMIT (RDL-IDX) TO RDL-RECORD-CODE.
028100     MOVE DL-NAME (DL-IDX) TO RDL-NAME.
028200     MOVE DL-OCCURRENCE-A (DL-IDX) TO RDL-OCCURRENCE-A.
028300     MOVE DL-ORIGIN (DL-IDX) TO RDL-ORIGIN.
028400     IF RDL-RECORD-NUMBER = 0
028500         MOVE DL-SET-CONNECTOR (DL-IDX) TO RDL-SET-CONNECTOR
028600     ELSE
028700         MOVE SPACE TO RDL-SET-CONNECTOR.
028800     MOVE 0 TO WORK-IDX.
028900     PERFORM LOAD-THE-LIMITS.
029000     MOVE RDL-RECORD TO RPT-RECORD.
029100     PERFORM WRITE-RPT-RECORD.
029200     IF DL-CONNECTOR (DL-IDX, DL-ENTRY-IDX) NOT = SPACE  AND
029300        DL-ENTRY-IDX NOT > 10
029400         ADD 1 TO RDL-RECORD-NUMBER
029500         GO TO LOAD-ONE-LIMIT.
029600 LOAD-THE-LIMITS.
029700     ADD 1 TO WORK-IDX.
029800     IF WORK-IDX NOT > 3
029900         ADD 1 TO DL-ENTRY-IDX
030000         IF DL-ENTRY-IDX NOT > 10
030100             MOVE DL-OP-CODE (DL-IDX, DL-ENTRY-IDX) TO
030200                 RDL-OP-CODE (WORK-IDX)
030300             MOVE DL-LIT-20 (DL-IDX, DL-ENTRY-IDX) TO
030400                 RDL-LIT-20 (WORK-IDX)
030500*             MOVE DL-LIT-CODE (DL-IDX, DL-ENTRY-IDX) TO
030600*                 RDL-LIT-CODE (WORK-IDX)
030700             MOVE DL-CONNECTOR (DL-IDX, DL-ENTRY-IDX) TO
030800                 RDL-CONNECTOR (WORK-IDX)
030900             IF DL-OP-OCCURRENCE-A (DL-IDX, DL-ENTRY-IDX) =
031000                                         ZEROES
031100                 MOVE SPACES TO RDL-OP-OCCURRENCE (WORK-IDX)
031200                 GO TO LOAD-THE-LIMITS
031300             ELSE
031400                 GO TO LOAD-THE-LIMITS
031500         ELSE
031600             MOVE SPACES TO RDL-ENTRY (WORK-IDX)
031700             GO TO LOAD-THE-LIMITS.
031800 DELETE-LIMIT-RECORDS.
031900     MOVE "C" TO RPT-KEY.
032000     PERFORM START-NLT-RPT-KEY.
032100     IF RPT-FILE-STATUS NOT = AT-END
032200         PERFORM HOLD-NEXT-RPT-RECORD
000000         IF RPT-FILE-STATUS NOT = AT-END
032300             MOVE RPT-RECORD TO RDL-RECORD
032400             IF RDL-RECORD-CODE NOT < "C"  AND
032500                RDL-RECORD-CODE NOT > "L"
032600                 PERFORM DELETE-RPT-RECORD
032700                 GO TO DELETE-LIMIT-RECORDS.
032800
032900 WRITE-NF-RECORDS.
033000     PERFORM DELETE-NF-RECORDS.
033100     MOVE 0 TO SEQ-IDX.
033200     PERFORM PROCESS-ONE-NEW-FIELD-SET
033300         VARYING NF-IDX FROM 1 BY 1
033400         UNTIL NF-IDX > 10.
033500 PROCESS-ONE-NEW-FIELD-SET.
033600     MOVE SPACES TO RNF-RECORD.
033700     MOVE 0 TO NF-ENTRY-IDX.
033800     IF NF-NAME (NF-IDX) NOT = SPACES
033900         MOVE 0 TO RNF-RECORD-NUMBER
034000         MOVE SEQ-IDX TO RNF-SEQ
034100         PERFORM LOAD-ONE-NEW-FIELD.
034200     ADD 1 TO SEQ-IDX.
034300 LOAD-ONE-NEW-FIELD.
034400     MOVE "R" TO RNF-RECORD-TYPE.
034500     MOVE NF-NAME (NF-IDX) TO RNF-NAME.
034600     IF RNF-RECORD-NUMBER = 0
034700         MOVE NF-TYPE (NF-IDX) TO RNF-TYPE
034800         MOVE NF-LEN (NF-IDX) TO RNF-LEN
034900         MOVE NF-DEC (NF-IDX) TO RNF-DEC
035000     ELSE
035100         MOVE SPACE TO RNF-TYPE
035200         MOVE 0 TO RNF-LEN  RNF-DEC.
035300     MOVE 0 TO WORK-IDX.
035400     PERFORM LOAD-THE-NEW-FIELDS.
035500     MOVE RNF-RECORD TO RPT-RECORD.
035600     PERFORM WRITE-RPT-RECORD.
035700     IF NF-OP-CODE (NF-IDX, NF-ENTRY-IDX) NOT = SPACE  AND
035800        NF-ENTRY-IDX NOT > 10
035900         ADD 1 TO RNF-RECORD-NUMBER
036000         GO TO LOAD-ONE-NEW-FIELD.
036100 LOAD-THE-NEW-FIELDS.
036200     ADD 1 TO WORK-IDX.
036300     IF WORK-IDX NOT > 3
036400         ADD 1 TO NF-ENTRY-IDX
036500         IF NF-ENTRY-IDX NOT > 10
036600             MOVE NF-LIT-20 (NF-IDX, NF-ENTRY-IDX) TO
036700                 RNF-LIT-20 (WORK-IDX)
036800*             MOVE NF-LIT-FLAG (NF-IDX, NF-ENTRY-IDX) TO
036900*                 RNF-LITERAL-FLAG (WORK-IDX)
037000             MOVE NF-OP-CODE (NF-IDX, NF-ENTRY-IDX) TO
037100                 RNF-OP-CODE (WORK-IDX)
037200             GO TO LOAD-THE-NEW-FIELDS
037300         ELSE
037400             MOVE SPACES TO RNF-ENTRY (WORK-IDX)
037500             GO TO LOAD-THE-NEW-FIELDS.
037600 DELETE-NF-RECORDS.
037700     MOVE "R" TO RPT-KEY.
037800     PERFORM START-NLT-RPT-KEY.
037900     IF RPT-FILE-STATUS NOT = AT-END
038000         PERFORM HOLD-NEXT-RPT-RECORD
000000         IF RPT-FILE-STATUS NOT = AT-END
038100             MOVE RPT-RECORD TO RNF-RECORD
038200             IF RNF-RECORD-TYPE = "R"
038300                 PERFORM DELETE-RPT-RECORD
038400                 GO TO DELETE-NF-RECORDS.
038500 WRITE-FIELD-RECORDS.
038600     PERFORM DELETE-FIELD-RECORDS.
038700     MOVE 1 TO RFL-IDX.
038800     PERFORM WRITE-THE-FIELD-RECORDS.
038900 DELETE-FIELD-RECORDS.
039000     MOVE "X" TO FIELD-TYPE.
039100     MOVE FIELD-DESC-KEY TO RPT-KEY.
039200     PERFORM START-NLT-RPT-KEY.
039300     IF RPT-FILE-STATUS NOT = AT-END
039400         PERFORM HOLD-NEXT-RPT-RECORD
039500         IF RPT-FILE-STATUS NOT = AT-END
039600            PERFORM DELETE-RPT-RECORD
039700            GO TO DELETE-FIELD-RECORDS.
039800 WRITE-THE-FIELD-RECORDS.
039900     IF RFL-NAME (RFL-IDX) NOT = SPACE
040000         IF RFL-OCCURRENCE-A (RFL-IDX) = SPACE
040100            MOVE ZEROES TO RFL-OCCURRENCE (RFL-IDX).
040200     IF RFL-NAME (RFL-IDX) NOT = SPACE
040300         MOVE RFL-RECORD (RFL-IDX) TO RPT-RECORD
040400         PERFORM WRITE-RPT-RECORD.
040500     IF RFL-NAME (RFL-IDX) NOT = SPACE
040600         ADD 1 TO RFL-IDX
040700         IF RFL-IDX NOT > 80
040800             GO TO WRITE-THE-FIELD-RECORDS.
040900
041000     COPY IORPT.
041100
000000     COPY PLKTRACE.
