000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLVAL.
000300*--------------------------------------------------
000400* THIS ROUTINE HANDLES FIELD VALIDATIONS
000500* STILL TO DO, CHECK FOR TABLE TYPE OCCURS AND UPDATE
000600* THE TABLE FIELDS
000700* GET THE RIGHT SEQUENCE NUMBER.
000800*--------------------------------------------------
000900 ENVIRONMENT DIVISION.
001000 CONFIGURATION SECTION.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300 DATA DIVISION.
001400 FILE SECTION.
001500 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrlval.wcb 1.6 9/22/94".
001600 01  PROGRAM-NAME PIC X(8) VALUE "CTRLVAL".
001800     COPY WSFILEIO.
001900 01  EXT-LEN                  PIC 999.
002000 01  SCREEN-ERROR-STATUS      PIC X VALUE SPACE.
002100     88  SCREEN-ENTRY-ERROR   VALUE "E".
002200 01  ENTRY-ERROR              PIC X VALUE "E".
002300 01  REQUESTED-ACTION-CODE    PIC X(10).
002400 01  SAVE-CF-RECORD           PIC X(130).
002500 01  SAVE-INT-LEN               PIC 999 VALUE ZERO.
002600 01  CHECK-FIELD                PIC X(80).
002700 01  FILLER REDEFINES CHECK-FIELD.
002800     05  CHECK-CHAR  PIC X OCCURS 80 TIMES.
002900 01  CHECK-IDX                BINARY.
003000 01  CHECK-IDX-2              BINARY.
003100 01  CHECK-SIZE                 BINARY.
003200 01  EDITED-LEN               BINARY.
003300 01  ACCUMULATOR-FLAG         PIC 9.
003400     88  FIELD-IS-AN-ACCUMULATOR VALUE 1.
003500 01  ADDIT PIC X VALUE SPACE.
003600 01  VALIT PIC X VALUE SPACE.
000000 01  VALFNM-RC         PIC 99 VALUE ZERO.
003700*01  MAPIT PIC X VALUE SPACE.
003800 LINKAGE SECTION.
003900 01  CTRL-IO-BLOCK.
004000     COPY LKDIO.
004100 01  CTRL-HEADER-RECORDS.
004200     COPY RCCTRLH.
004300 01  CF-RECORD.
004400     COPY RCCTRLF.
004500     COPY LKCTRLAR.
004600     COPY RCCTRLT1.
004700     COPY RCCTRLT2.
000000     COPY LKPRC.
004800 PROCEDURE DIVISION USING
000000     CALLED-RC
004900     COPY LKCTRL .
005000 MAIN-LOGIC SECTION.
005100 PROGRAM-BEGIN.
000000     MOVE "CTRLVAL BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
005300     PERFORM PROGRAM-INITIALIZATION.
005400     PERFORM MAIN-PROCESS.
005500     PERFORM PROGRAM-CLOSE.
000000     MOVE "CTRLVAL EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
005600 MAIN-LOGIC-EXIT.
005700     EXIT PROGRAM.
005800 MAIN-LOGIC-STOP.
005900     STOP RUN.
006000 THE-OTHER SECTION.
006100 PROGRAM-INITIALIZATION.
006200     MOVE SPACE TO CF-MSG
006300                   SCREEN-ERROR-STATUS.
006400 PROGRAM-CLOSE.
006500
006600 MAIN-PROCESS.
006700*-----------------------------------------------------
006800* CHANGE IS JUST AN ADD ROUTINE
006900*-----------------------------------------------------
000000     MOVE CF-ACTION-CODE TO REQUESTED-ACTION-CODE.
007000     PERFORM LOAD-PASSED-RECORD.
007100     PERFORM VAL-ADD-CONTROL.
007300*-----------------------------------------------------
007400* VALIDATION FOR ADDING A RECORD TO A CONTROL FILE
007500*-----------------------------------------------------
007600 VAL-ADD-CONTROL.
000000     MOVE "VAL-ADD-CONTROL" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
008000     PERFORM VAL-FIELD-NAME.
008100     IF NOT SCREEN-ENTRY-ERROR
008200         PERFORM VAL-KEY-FOR-ADD.
008300     IF NOT SCREEN-ENTRY-ERROR
008400         PERFORM TEST-FOR-ACCUMULATOR.
008500     IF NOT SCREEN-ENTRY-ERROR
008600         PERFORM VAL-FIELD-START-POS.
008700     IF NOT SCREEN-ENTRY-ERROR
008800         PERFORM VAL-FIELD-TYPE.
008900     IF NOT SCREEN-ENTRY-ERROR
009000         PERFORM VAL-FIELD-ACCUMULATOR.
009100     IF NOT SCREEN-ENTRY-ERROR
009200         PERFORM VAL-FIELD-INT-LEN.
009300     IF NOT SCREEN-ENTRY-ERROR
009400         PERFORM VAL-PACKED-DIGITS.
009500     IF NOT SCREEN-ENTRY-ERROR
009600         PERFORM VAL-FIELD-DECIMAL-POS.
009700     IF NOT SCREEN-ENTRY-ERROR
009800         PERFORM VAL-FIELD-OCCURRENCES.
009900     IF NOT SCREEN-ENTRY-ERROR
010000         PERFORM VAL-FIELD-REPORT-CODE.
010100     IF NOT SCREEN-ENTRY-ERROR
010200         PERFORM VAL-FIELD-UPDATE-CODE.
010300     IF NOT SCREEN-ENTRY-ERROR
010400         PERFORM VAL-FIELD-DISPLAY-CODE.
010500     IF NOT SCREEN-ENTRY-ERROR
010600         PERFORM VAL-FIELD-ZERO-SUPPRESS.
010700     IF NOT SCREEN-ENTRY-ERROR
010800         PERFORM VAL-FIELD-SIGN-CONTROL.
010900     IF NOT SCREEN-ENTRY-ERROR
011000         PERFORM VAL-FIELD-DOLLAR-COMMA.
011100     IF NOT SCREEN-ENTRY-ERROR
011200         PERFORM VAL-FIELD-BINARY-EDIT.
011300     IF NOT SCREEN-ENTRY-ERROR
011400         PERFORM VAL-FIELD-EXT-LEN.
011500     IF NOT SCREEN-ENTRY-ERROR
011600         PERFORM VAL-FIELD-DATE-STAMP.
011700     IF NOT SCREEN-ENTRY-ERROR
011800         IF REQUESTED-ACTION-CODE NOT = "ADDFIL"
011900             PERFORM VAL-FIELD-CUMMULATIVE-NAME.
012000     IF NOT SCREEN-ENTRY-ERROR
012100         PERFORM VAL-FIELD-ALIAS.
012200     IF NOT SCREEN-ENTRY-ERROR
012300         PERFORM VAL-EDITED-LEN.
012400     IF NOT SCREEN-ENTRY-ERROR
012500         PERFORM VAL-FOR-MAP.
012600
012700 VAL-FIELD-NAME.
000000     MOVE "VAL-FIELD-NAME" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013100     IF CX-NAME = SPACE
013200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
013300         MOVE "Field Name Expected" TO CF-MSG
013400     ELSE
000000         MOVE ZEROES TO VALFNM-RC
000000         CALL "VALFNM" USING CX-NAME VALFNM-RC
000000         IF VALFNM-RC NOT = ZEROES
000000             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
013900             MOVE "Field Name Contains Invalid Characters"
014000                 TO CF-MSG
014100                   .
018100 VAL-KEY-FOR-ADD.
000000     MOVE "VAL-KEY-FOR-ADD" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
018500*    PERFORM LOAD-PASSED-RECORD.
018600     PERFORM FIND-CF-NAME.
018700     IF CF-RETURN-CODE NOT = "23"
018800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
018900         MOVE "Field Already Defined." TO CF-MSG.
019000
019100 LOAD-PASSED-RECORD.
000000     MOVE "LOAD-PASSED-RECORD" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
019500     MOVE CX-INT-LEN TO T1-INT-LEN(CF-WRK-IDX).
019600     MOVE CX-EXT-LEN TO T1-EXT-LEN(CF-WRK-IDX).
019700     MOVE CX-EDITED-LEN TO T1-EDITED-LEN(CF-WRK-IDX).
019800     MOVE CX-START-POS TO T1-START-POS(CF-WRK-IDX).
019900     MOVE CX-OCCURRENCES TO T1-OCCURRENCES(CF-WRK-IDX).
020000     MOVE CX-ZERO-SUPPRESS TO T1-ZERO-SUPPRESS(CF-WRK-IDX).
020100     MOVE CX-DECIMAL-INSERT TO T1-DECIMAL-INSERT(CF-WRK-IDX).
020200     MOVE CX-SIGN-CONTROL TO T1-SIGN-CONTROL(CF-WRK-IDX).
020300     MOVE CX-DOLLAR-COMMA TO T1-DOLLAR-COMMA(CF-WRK-IDX).
020400     MOVE CX-REPORT-CODE TO T1-REPORT-CODE(CF-WRK-IDX).
020500     MOVE CX-UPDATE-CODE TO T1-UPDATE-CODE(CF-WRK-IDX).
020600     MOVE CX-DECIMAL-POS TO T1-DECIMAL-POS(CF-WRK-IDX).
020700     MOVE CX-BINARY-EDIT TO T1-BINARY-EDIT(CF-WRK-IDX).
020800     MOVE CX-UPDATE-SEQ TO T1-UPDATE-SEQ(CF-WRK-IDX).
020900     MOVE CX-PACKED-DIGITS TO T1-PACKED-DIGITS(CF-WRK-IDX).
021000     MOVE CX-GROUP-FIELDS TO T1-GROUP-FIELDS(CF-WRK-IDX).
021100     MOVE CX-DISPLAY-CODE TO T1-DISPLAY-CODE(CF-WRK-IDX).
021200     MOVE CX-NAME TO T2-NAME(CF-WRK-IDX).
021300     MOVE CX-TYPE TO T2-TYPE(CF-WRK-IDX).
021400     MOVE CX-VALIDATION TO T2-VALIDATION(CF-WRK-IDX).
021500     MOVE CX-TABLE-NAME TO T2-TABLE-NAME(CF-WRK-IDX).
021600     MOVE CX-LO-RANGE TO T2-LO-RANGE(CF-WRK-IDX).
021700     MOVE CX-HI-RANGE TO T2-HI-RANGE(CF-WRK-IDX).
021800     MOVE CX-CUMMULATIVE-NAME TO T2-CUMMULATIVE-NAME(CF-WRK-IDX).
021900     MOVE CX-ALIAS TO T2-ALIAS(CF-WRK-IDX).
022000     MOVE CX-DEFAULT-FAC TO T2-DEFAULT-FAC(CF-WRK-IDX).
022100 TEST-FOR-ACCUMULATOR.
000000     MOVE "TEST-FOR-ACCUMULATOR" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
022500     MOVE ZERO TO ACCUMULATOR-FLAG.
022600     PERFORM FIND-CUMM-NAME.
022700     IF CALLED-RC = ZEROES
022800         MOVE 1 TO ACCUMULATOR-FLAG.
022900 VAL-FIELD-START-POS.
000000     MOVE "VAL-FIELD-START-POS" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
023300     IF CX-START-POS < 1 OR
023400        CX-START-POS > CH-RECORD-LEN
023500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
023600         MOVE "Invalid Starting Position" TO CF-MSG.
023700
023800 VAL-FIELD-TYPE.
000000     MOVE "VAL-FIELD-TYPE" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
024200     IF CX-TYPE NOT = "C" AND "U"
024300                                  AND "P"
024400                                  AND "Z"
024500                                  AND "B"
024600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
024700         MOVE "Format Must Be C, U, P, Z or B"
024800             TO CF-MSG
024900             .
025000 VAL-FIELD-ACCUMULATOR.
000000     MOVE "VAL-FIELD-ACCUMULATOR" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
025400     IF FIELD-IS-AN-ACCUMULATOR
025500         IF CX-TYPE NOT = "Z" AND "P"
025600             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
025700             MOVE
025800          "This field is an accumulator and must be 'P' or 'Z'"
025900             TO CF-MSG
026000               .
026100 VAL-FIELD-INT-LEN.
000000     MOVE "VAL-FIELD-INT-LEN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
026500     IF CX-INT-LEN < 1 OR
026600        CX-INT-LEN > 132
026700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
026800     ELSE
026900     IF ( CX-START-POS +
027000          (CX-INT-LEN *
027100          CX-OCCURRENCES) - 1 ) >
027200          CH-RECORD-LEN
027300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
027400     ELSE
027500     IF CX-TYPE = "C"
027600         NEXT SENTENCE
027700     ELSE
027800     IF CX-INT-LEN > 15
027900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
028000     ELSE
028100     IF CX-TYPE = "Z" OR "U"
028200         NEXT SENTENCE
028300     ELSE
028400     IF CX-INT-LEN > 8
028500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
028600     ELSE
028700     IF CX-TYPE = "P"
028800         NEXT SENTENCE
028900     ELSE
029000     IF CX-INT-LEN > 4
029100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
029200         .
029300     IF SCREEN-ENTRY-ERROR
029400         MOVE "Invalid Internal Length" TO CF-MSG.
029500
029600 VAL-PACKED-DIGITS.
030000     COMPUTE CX-PACKED-DIGITS = ZEROES.
030100     IF CX-TYPE = "P"
030200         COMPUTE CX-PACKED-DIGITS =
030300         (CX-INT-LEN * 1 ) - 1.
030400 VAL-FIELD-DECIMAL-POS.
030800     IF CX-TYPE = "C" OR "B"
030900         MOVE ZEROES TO CX-DECIMAL-POS.
031000 VAL-FIELD-OCCURRENCES.
000000     MOVE "VAL-FIELD-OCCURRENCES" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
031400     IF CX-OCCURRENCES < 1
031500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
031600         MOVE
031700         "Must Occur At Least Once"
031800             TO CF-MSG.
031900 VAL-FIELD-REPORT-CODE.
000000     MOVE "VAL-FIELD-REPORT-CODE" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
032300     IF CX-REPORT-CODE NOT = 0 AND 1
032400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
032500         MOVE "Report Code Must be 0 or 1"
032600             TO CF-MSG.
032700
032800 VAL-FIELD-UPDATE-CODE.
000000     MOVE "VAL-FIELD-UPDATE-CODE" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
033200     IF CX-UPDATE-CODE NOT = 0 AND 1
033300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
033400         MOVE "Update Code Must be 0 or 1"
033500             TO CF-MSG
033600     ELSE
033700     IF CX-UPDATE-CODE = 0 AND
033800        CX-INT-LEN > 67
033900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
034000         MOVE "Update field longer than 67."
034100             TO CF-MSG.
034200 VAL-FIELD-DISPLAY-CODE.
000000     MOVE "VAL-FIELD-DISPLAY-CODE" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
034600     IF CX-DISPLAY-CODE NOT = 0 AND 1 AND 2
034700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
034800         MOVE "Display Code Must be 0, 1 or 2"
034900             TO CF-MSG.
035000
035100 VAL-FIELD-ZERO-SUPPRESS.
035500     IF CX-TYPE = "C"
035600         MOVE 0 TO CX-ZERO-SUPPRESS
035700     ELSE
035800     IF CX-ZERO-SUPPRESS NOT = 0 AND 1 AND 2
035900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
036000         MOVE "Zero Suppress Must be 0, 1 or 2"
036100             TO CF-MSG
036200     ELSE
036300     IF CX-ZERO-SUPPRESS NOT = ZERO AND
036400        CX-TYPE = "C"
036500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
036600         MOVE "Zero Suppress Not Valid for C Format"
036700             TO CF-MSG
036800             .
036900
037000 VAL-FIELD-SIGN-CONTROL.
037400     IF CX-TYPE = "C"
037500         MOVE 0 TO CX-SIGN-CONTROL
037600     ELSE
037700     IF CX-SIGN-CONTROL
037800                 NOT = 0 AND 1 AND 2 AND 3
037900         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
038000         MOVE "Sign Control Must be 0, 1, 2 or 3"
038100             TO CF-MSG
038200     ELSE
038300     IF CX-SIGN-CONTROL NOT = ZERO AND
038400        CX-TYPE = "C"
038500         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
038600         MOVE "Sign Control Not Valid for C Format"
038700             TO CF-MSG
038800             .
038900 VAL-FIELD-DOLLAR-COMMA.
039300     IF CX-TYPE = "C"
039400         MOVE 0 TO CX-DOLLAR-COMMA
039500     ELSE
039600     IF CX-DOLLAR-COMMA
039700          NOT = 0 AND 1 AND 2 AND 3
039800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
039900         MOVE "Dollar-Comma Control Must be 0, 1, 2 or 3"
040000             TO CF-MSG
040100     ELSE
040200     IF CX-DOLLAR-COMMA NOT = ZERO AND
040300        CX-TYPE = "C"
040400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
040500         MOVE "Dollar-Comma Control Not Valid for C Format"
040600             TO CF-MSG
040700             .
040800
040900 VAL-FIELD-BINARY-EDIT.
041300     IF CX-TYPE NOT = "B"
041400         MOVE ZEROES TO CX-BINARY-EDIT.
041500
041600     IF CX-BINARY-EDIT NOT = 0 AND 1
041700         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
041800         MOVE "Binary Edit Must be 0 or 1"
041900             TO CF-MSG
042000     ELSE
042100     IF CX-BINARY-EDIT NOT = ZERO AND
042200        CX-TYPE NOT = "B"
042300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
042400         MOVE "Binary Edit Only Valid for B Format"
042500             TO CF-MSG
042600             .
042700 VAL-FIELD-EXT-LEN.
000000     MOVE "VAL-FIELD-EXT-LEN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
043100     PERFORM CREATE-DEFAULT-EXT-LEN.
043200     IF CX-EXT-LEN = ZEROES
043300         MOVE EXT-LEN TO CX-EXT-LEN
043400     ELSE
043500     IF CX-EXT-LEN > EXT-LEN
043600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
043700         MOVE "External Length is too Long."
043800             TO CF-MSG
043900             .
044000
044100 CREATE-DEFAULT-EXT-LEN.
044500     IF CX-TYPE = "C"
044600         MOVE CX-INT-LEN TO
044700                 EXT-LEN
044800     ELSE
044900     IF CX-TYPE = "U"
045000         MOVE CX-INT-LEN TO EXT-LEN
045100         PERFORM ADD-DEFAULT-EXT-EDIT-LEN
045200     ELSE
045300     IF CX-TYPE = "Z"
045400         PERFORM CREATE-DEFAULT-ZONED-EXT-LEN
045500     ELSE
045600     IF CX-TYPE = "P"
045700         PERFORM CREATE-DEFAULT-PACKED-EXT-LEN
045800     ELSE
045900     IF CX-TYPE = "B"
046000         PERFORM CREATE-DEFAULT-BINARY-EXT-LEN
046100         .
046200*---------------------------------------------------------------
046300* BINARY FIELDS IN HEX ARE DOUBLE THEIR INTERNAL SIZE.
046400* IF THEY ARE REPRESENTED IN DECIMAL THEN ADD 2 TO THIS
046500* AND IF THE INTERNAL LEN IS 4 ADD 1 MORE
046600* FOR DECIMAL REPRESENTATION
046700* 1 INTERNAL =  4 EXTERNAL
046800* 2 INTERNAL =  6 EXTERNAL
046900* 3 INTERNAL =  8 EXTERNAL
047000* 4 INTERNAL = 11 EXTERNAL
047100*---------------------------------------------------------------
047200 CREATE-DEFAULT-BINARY-EXT-LEN.
047600     COMPUTE EXT-LEN =
047700             CX-INT-LEN * 2.
047800     IF CX-BINARY-EDIT = 1
047900         ADD 2 TO EXT-LEN
048000         IF CX-INT-LEN = 4
048100             ADD 1 TO EXT-LEN
048200             .
048300 CREATE-DEFAULT-PACKED-EXT-LEN.
048700     COMPUTE EXT-LEN =
048800             CX-INT-LEN * 2.
048900     PERFORM ADD-DEFAULT-EXT-EDIT-LEN.
049000 CREATE-DEFAULT-ZONED-EXT-LEN.
049400     COMPUTE EXT-LEN =
049500             CX-INT-LEN + 1.
049600     PERFORM ADD-DEFAULT-EXT-EDIT-LEN.
049700 ADD-DEFAULT-EXT-EDIT-LEN.
050100     IF CX-DECIMAL-POS > 0
050200         ADD 1 TO EXT-LEN.
050300
050400 VAL-FIELD-DATE-STAMP.
050800     IF CX-VALIDATION = SPACES OR "R " OR
050900                                     "CF" OR "T "
051000         NEXT SENTENCE
051100     ELSE
051200     IF CX-TYPE = "B" OR "Z"
051300         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
051400         MOVE
051500         "Date Stamp May Not be Used With B or Z Format"
051600             TO CF-MSG
051700     ELSE
051800     IF CX-VALIDATION = "DF" OR "GD"
051900         PERFORM VAL-DATE-STAMP
052000     ELSE
052100     IF CX-VALIDATION = "DS"
052200         PERFORM VAL-JUL-DATE-STAMP
052300     ELSE
052400     IF CX-VALIDATION = "TS"
052500         PERFORM VAL-TIME-STAMP
052600     ELSE
052700     IF CX-VALIDATION = "CS" OR "CM"
052800         PERFORM VAL-DATE-TIME-STAMP
052900     ELSE
053000         MOVE "Date Stamp Must be 0 - 6" TO CF-MSG
053100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
053200
053300     IF SCREEN-ENTRY-ERROR
053400         IF CF-MSG = SPACE
053500             MOVE "Invalid Date Stamp Option"
053600                 TO CF-MSG
053700                   .
053800 VAL-DATE-STAMP.
054200     IF CX-TYPE = "C" OR "U" AND
054300        CX-INT-LEN NOT = 6
054400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
054500     ELSE
054600     IF CX-TYPE = "P" AND
054700        CX-INT-LEN NOT = 4
054800         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
054900         .
055000     IF SCREEN-ENTRY-ERROR
055100         MOVE "Invalid Date Stamp Field Length"
055200             TO CF-MSG
055300              .
055400 VAL-JUL-DATE-STAMP.
055800     IF CX-TYPE = "C" OR "U" AND
055900        CX-INT-LEN NOT = 5
056000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
056100     ELSE
056200     IF CX-TYPE = "P" AND
056300        CX-INT-LEN NOT = 3
056400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
056500         .
056600     IF SCREEN-ENTRY-ERROR
056700         MOVE "Invalid Date Stamp Field Length"
056800             TO CF-MSG
056900             .
057000 VAL-TIME-STAMP.
057400     IF CX-TYPE = "C" OR "U" AND
057500        CX-INT-LEN NOT = 8
057600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
057700     ELSE
057800     IF CX-TYPE = "P" AND
057900        CX-INT-LEN NOT = 5
058000         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
058100         .
058200     IF SCREEN-ENTRY-ERROR
058300         MOVE "Invalid Date Stamp Field Length"
058400             TO CF-MSG
058500             .
058600 VAL-DATE-TIME-STAMP.
059000     IF CX-TYPE = "C" OR "U" AND
059100        CX-INT-LEN NOT = 14
059200         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
059300     ELSE
059400     IF CX-TYPE = "P" AND
059500        CX-INT-LEN NOT = 8
059600         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
059700         .
059800     IF SCREEN-ENTRY-ERROR
059900         MOVE "Invalid Date Stamp Field Length"
060000             TO CF-MSG
060100              .
060200 VAL-FIELD-CUMMULATIVE-NAME.
060600     IF CX-CUMMULATIVE-NAME NOT = SPACE
060700         IF CX-TYPE = "C"
060800             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
060900             MOVE "Format Must be Numeric to Accumulate"
061000                 TO CF-MSG
061100         ELSE
061200         MOVE CF-RECORD TO SAVE-CF-RECORD
061300         MOVE CX-CUMMULATIVE-NAME TO
061400            CX-NAME
061500         PERFORM FIND-CF-NAME
061600         MOVE SAVE-CF-RECORD TO CF-RECORD
061700         IF CF-RETURN-CODE = RECORD-NOT-FOUND
061800             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
061900             MOVE "Cummulative Field Not Defined"
062000                 TO CF-MSG
062100         ELSE
062200         IF T2-TYPE(CF-IDX) NOT = "Z" AND "P"
062300             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
062400             MOVE "Accumulator Must be 'Z'oned or 'P'acked"
062500                 TO CF-MSG
062600         ELSE
062700         IF CX-INT-LEN >
062800            T1-INT-LEN(CF-IDX)
062900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
063000             MOVE
063100             "Accumulator Too Small" TO CF-MSG
063200         ELSE
063300         MOVE CF-RECORD TO SAVE-CF-RECORD
063400         MOVE CX-CUMMULATIVE-NAME TO
063500            CX-NAME
063600         PERFORM FIND-CUMM-NAME
063700         MOVE SAVE-CF-RECORD TO CF-RECORD
063800         IF CF-RETURN-CODE = I-O-OK
063900             MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
064000             MOVE "This field is an accumulator"
064100                 TO CF-MSG
064200             .
064300
064400 VAL-FIELD-ALIAS.
064500 VAL-EDITED-LEN.
064900     COMPUTE EDITED-LEN = CX-EXT-LEN.
065000* ADD 1 FOR DECIMALS THIS IS A BUG
065100     IF CX-DECIMAL-POS > 0
065200         ADD 1 TO EDITED-LEN.
065300* ADD FOR COMMAS
065400     IF CX-DOLLAR-COMMA = 1 OR 3
065500         COMPUTE EDITED-LEN = EDITED-LEN +
065600           ( CX-EXT-LEN -
065700           CX-DECIMAL-POS  - 1 ) / 3.
065800* ADD FOR DOLLARS
065900     IF CX-DOLLAR-COMMA = 2 OR 3
066000         ADD 1 TO EDITED-LEN.
066100* ADD FOR CR/ DB
066200     IF CX-SIGN-CONTROL = 2 OR 3
066300         ADD 2 TO EDITED-LEN.
066400* AND SAVE IT
066500     COMPUTE CX-EDITED-LEN = EDITED-LEN.
066600
066700 VAL-FOR-MAP.
000000     MOVE "VAL-FOR-MAP" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
067100     MOVE "VAL" TO CF-ACTION-CODE.
067200     PERFORM CALL-CTRLMAP.
067300     IF CALLED-RC NOT = ZEROES
067400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
067500 FIND-CF-NAME.
000000     MOVE "FIND-CF-NAME" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
067900     MOVE "FINDNAME" TO CF-ACTION-CODE.
068000     PERFORM CALL-CTRLARY.
068100 FIND-CUMM-NAME.
068500     MOVE "FINDCUMM" TO CF-ACTION-CODE.
068600     PERFORM CALL-CTRLARY.
068700 CALL-CTRLARY.
000000     MOVE "CALL-CTRLARY" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
069100     CALL "CTRLARY" USING
000000     CALLED-RC
069200     COPY LKCTRL.
069300 CALL-CTRLMAP.
000000     MOVE "CALL-CTRMAP" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
069700     CALL "CTRLMAP" USING
000000     CALLED-RC
069800     COPY LKCTRL.
070000
000000     COPY PLKTRACE.
