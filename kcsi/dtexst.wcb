000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. DTEXST.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/03/90.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* CHECKS FOR DUPLICATE FILE DEFINITION AND PROMPTS USER
000800* FOR APPROPRIATE ACTION.  WILL NOT EXIT UNTIL EITHER A
000900* SUCCESSFUL SCRATCH OR (CORRECT AND NON-EXISTANT RE-
001000* SPECIFICATION)
001100*---------------------------------------------------------------
001200
001300 ENVIRONMENT DIVISION.
001400 CONFIGURATION SECTION.
001500 FIGURATIVE-CONSTANTS.
001600     COPY FIGS.
001700
001800 INPUT-OUTPUT SECTION.
001900
002000 FILE-CONTROL.
002100
002200 DATA DIVISION.
002300 FILE SECTION.
002400
002500 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)dtexst.wcb 1.8 9/11/94".
002600     COPY WSGP.
002700     COPY WSINT.
002800     COPY WSCRTS.
000000     COPY LKVALNAM.
002900
003000 01  CORRUPT-GETPARM-FIELDS.
003100     05  CORRUPT-MSG-ID               PIC X(4) VALUE "0001".
003200     05  CORRUPT-PRNAME               PIC X(8) VALUE "CORRUPT".
003300     05  CORRUPT-MSG-ISSUER           PIC X(6) VALUE "FILDUP".
003400
003500 01  CORRUPT-MSG-COUNT.
003600     05  FILLER                        BINARY   VALUE ZERO.
003700     05  FILLER                        BINARY   VALUE 2.
003800 01  CORRUPT-MSG-TEXT-1               PIC X(70) VALUE
003900         "A File Definition with the same File ID         ".
004000
004100 01  CORRUPT-MSG-TEXT-2               PIC X(30) VALUE
004200         "already exists on this volume.".
004300 01  CORRUPT-TEXT-01                  PIC X(58) VALUE
004400         "Use (3) to scratch the existing file and create a new on
004500-        "e.".
004600 01  CORRUPT-TEXT-02                  PIC X(42) VALUE
004700         "Otherwise please specify another file name".
004800 01  CORRUPT-TEXT-03                  PIC X(53) VALUE
004900         "(which will become the File ID), library or volume.  ".
005000 01  CORRUPT-FILE-KW              PIC X(8) VALUE "FILE".
005100 01  RPT-FILE-NAME           PIC X(8) VALUE SPACE.
005200 01  CORRUPT-FILE-DEFAULT         PIC X(8) VALUE SPACE.
005300 01  CORRUPT-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
005400 01  RPT-LIBRARY-NAME           PIC X(8) VALUE SPACE.
005500 01  CORRUPT-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
005600 01  CORRUPT-VOLUME-KW              PIC X(8) VALUE "VOLUME".
005700 01  RPT-VOLUME-NAME           PIC X(6) VALUE SPACE.
005800 01  CORRUPT-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
005900 01  CORRUPT-TEXT-04                  PIC X(66) VALUE
006000         " ".
006100 01  CORRUPT-TEXT-05                  PIC X(67) VALUE
006200         " ".
006300 01  CORRUPT-TEXT-06                  PIC X(62) VALUE
006400         " ".
010300
010400 01  MESSAGE-LITERALS.
010500     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
010600         "* Error * File Name cannot be blank".
010700     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
010800         "* Error * Invalid File Name".
010900     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
011000         "* Error * Invalid Library Name".
011100     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
011200         "* Error * Invalid Volume Name".
011300     05  CANNOT-SCRATCH-LITERAL       PIC X(78) VALUE
011400         "* Error * File cannot be scratched. Please respecify.".
011500
011600 01  SAVE-FILE-NAME              PIC X(08) VALUE SPACE.
011700 01  SAVE-FILE-LIB               PIC X(08) VALUE SPACE.
011800 01  SAVE-FILE-VOL               PIC X(06) VALUE SPACE.
011900
012000 01  SCRATCH-VALUES.
012100     05  SCRATCH-TYPE                    PIC X(01) VALUE "F".
012200     05  SCRATCH-FILE-NAME               PIC X(08) VALUE SPACES.
012300     05  SCRATCH-FILE-LIBRARY            PIC X(08) VALUE SPACES.
012400     05  SCRATCH-FILE-VOLUME             PIC X(06) VALUE SPACES.
012500 01  SCRATCH-RC.
012600     05  SCRATCH-RC-1       BINARY VALUE ZEROES.
012700     05  SCRATCH-RC-2       BINARY VALUE ZEROES.
012800
000000     COPY LKPRC.
000000 
012900 LINKAGE SECTION.
013000 01  RPT-IO-BLOCK.
013100     COPY LKDIO.
013200
013300 PROCEDURE DIVISION USING
013400     RPT-IO-BLOCK.
013500 MAIN-LOGIC SECTION.
013600 PROGRAM-BEGIN.
000000     MOVE "DTEXST BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013700     PERFORM OPENING-PROCEDURE.
013800     IF CALLED-RC = 0
013900         PERFORM MAIN-PROCESS.
014000     PERFORM CLOSING-PROCEDURE.
000000     MOVE "DTEXST EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
014100 MAIN-LOGIC-EXIT.
014200     EXIT PROGRAM.
014300 MAIN-LOGIC-STOP.
014400     STOP RUN.
014500 LEVEL-2 SECTION.
014600 OPENING-PROCEDURE.
014700     MOVE FILE-NAME OF RPT-IO-BLOCK TO RPT-FILE-NAME
014800                 SAVE-FILE-NAME.
014900     MOVE FILE-LIBRARY OF RPT-IO-BLOCK TO RPT-LIBRARY-NAME
015000                 SAVE-FILE-LIB.
015100     MOVE FILE-VOLUME OF RPT-IO-BLOCK TO RPT-VOLUME-NAME
015200                 SAVE-FILE-VOL.
015300     MOVE 0 TO CALLED-RC.
015400     CALL "KEXISTS" USING
000000             CALLED-RC
015500         RPT-FILE-NAME
015600         RPT-LIBRARY-NAME
015700         RPT-VOLUME-NAME.
015800
015900 CLOSING-PROCEDURE.
016000     MOVE RPT-FILE-NAME TO FILE-NAME OF RPT-IO-BLOCK.
016100     MOVE RPT-LIBRARY-NAME TO FILE-LIBRARY OF RPT-IO-BLOCK.
016200     MOVE RPT-VOLUME-NAME TO FILE-VOLUME OF RPT-IO-BLOCK.
016300
016400 MAIN-PROCESS.
016500     MOVE SPACE TO SCREEN-ERROR-STATUS.
016600     PERFORM CALL-CORRUPT-GETPARM.
016700     MOVE SPACES TO CORRUPT-MSG-TEXT-1  CORRUPT-MSG-TEXT-2.
016800     IF GP-ENTER-KEY-PRESSED
016900         PERFORM VAL-FILE-NAME
017000         IF NOT SCREEN-ENTRY-ERROR
017100             PERFORM VAL-LIB-NAME
017200                 IF NOT SCREEN-ENTRY-ERROR
017300                    PERFORM VAL-VOL-NAME.
017400
017500     IF NOT SCREEN-ENTRY-ERROR
017600         IF GP-PFKEY-3-PRESSED
017700             MOVE RPT-FILE-NAME TO SCRATCH-FILE-NAME
017800             MOVE RPT-LIBRARY-NAME TO SCRATCH-FILE-LIBRARY
017900             MOVE RPT-VOLUME-NAME TO SCRATCH-FILE-VOLUME
018000             MOVE 0 TO SCRATCH-RC
018100             PERFORM SCRATCH-A-FILE.
018200
018300     IF NOT SCREEN-ENTRY-ERROR
018400         MOVE 0 TO CALLED-RC
018500         CALL "KEXISTS" USING
000000             CALLED-RC
018600             RPT-FILE-NAME
018700             RPT-LIBRARY-NAME
018800             RPT-VOLUME-NAME.
018900
019000     IF CALLED-RC = 0
019100         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
019200         MOVE CANNOT-SCRATCH-LITERAL TO CORRUPT-MSG-TEXT-1.
019300
019400     IF SCREEN-ENTRY-ERROR
019500         GO TO MAIN-PROCESS.
019600
019700 CALL-CORRUPT-GETPARM.
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-03-VAL TO PFKEY-MASK-16.
000000
019800     CALL "GETPARM" USING
019900         GP-SPECIFY-TYPE, GP-REQUEST-FORM, CORRUPT-PRNAME,
020000         GP-PFKEY-RECEIVER, CORRUPT-MSG-ID,
020100         CORRUPT-MSG-ISSUER,
020200
020300         CORRUPT-MSG-COUNT,
020400         CORRUPT-MSG-TEXT-1, INT70,
020500         CORRUPT-MSG-TEXT-2, INT30,
020600
020700         GP-TEXT-TYPE, CORRUPT-TEXT-01, INT58,
020800         GP-ABS, INT10, GP-ABS, INT02,
020900
021000         GP-TEXT-TYPE, CORRUPT-TEXT-02, INT42,
021100         GP-ABS, INT11, GP-ABS, INT02,
021200
021300         GP-TEXT-TYPE, CORRUPT-TEXT-03, INT53,
021400         GP-ABS, INT12, GP-ABS, INT02,
021500
021600         GP-KW-TYPE, CORRUPT-FILE-KW,
021700         RPT-FILE-NAME, INT08,
021800         GP-ABS, INT15, GP-ABS, INT07,
021900         GP-UPPERCASE-DATA
022000
022100         GP-KW-TYPE, CORRUPT-LIBRARY-KW,
022200         RPT-LIBRARY-NAME, INT08,
022300         GP-ABS, INT15, GP-ABS, INT32,
022400         GP-UPPERCASE-DATA
022500
022600         GP-KW-TYPE, CORRUPT-VOLUME-KW,
022700         RPT-VOLUME-NAME, INT06,
022800         GP-ABS, INT15, GP-ABS, INT57,
022900         GP-UPPERCASE-DATA
023000
023100         GP-TEXT-TYPE, CORRUPT-TEXT-04, INT66,
023200         GP-ABS, INT19, GP-ABS, INT02,
023300
023400         GP-TEXT-TYPE, CORRUPT-TEXT-05, INT67,
023500         GP-ABS, INT20, GP-ABS, INT02,
023600
023700         GP-TEXT-TYPE, CORRUPT-TEXT-06, INT62,
023800         GP-ABS, INT21, GP-ABS, INT02,
023900         GP-ENABLE-ENTER-KEY
000000         GP-PFKEY-TYPE PFKEY-MASK.
024100
024200 VAL-FILE-NAME.
024300     IF RPT-FILE-NAME = SPACES
024400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
024500         MOVE FILE-ID-BLANK-LITERAL TO CORRUPT-MSG-TEXT-1.
024600
024700     IF NOT SCREEN-ENTRY-ERROR
024800         MOVE RPT-FILE-NAME TO VAL-NAME
024900         PERFORM TEST-THE-NAME
025000         IF SCREEN-ENTRY-ERROR
025100             MOVE INVALID-FILE-LITERAL TO CORRUPT-MSG-TEXT-1.
025200
025300 VAL-LIB-NAME.
025400     MOVE RPT-LIBRARY-NAME TO VAL-NAME.
025500     PERFORM TEST-THE-NAME.
025600     IF SCREEN-ENTRY-ERROR
025700         MOVE INVALID-LIB-LITERAL TO CORRUPT-MSG-TEXT-1.
025800
025900 VAL-VOL-NAME.
026000     MOVE RPT-VOLUME-NAME TO VAL-NAME.
026100     PERFORM TEST-THE-VOL.
026200     IF SCREEN-ENTRY-ERROR
026300         MOVE INVALID-VOL-LITERAL TO CORRUPT-MSG-TEXT-1.
026400
026500 TEST-THE-NAME.
000000     CALL "VALNAM" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
029900
026500 TEST-THE-VOL.
000000     CALL "VALVOL" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
029900
030000 SCRATCH-ROUTINE.
030100     MOVE ZEROES TO SCRATCH-RC-1
030200                    SCRATCH-RC-2.
030300     CALL "SCRATCH" USING  SCRATCH-TYPE,
030400                         SCRATCH-FILE-NAME
030500                         SCRATCH-FILE-LIBRARY
030600                         SCRATCH-FILE-VOLUME
030700                         SCRATCH-RC.
030800 SCRATCH-A-FILE.
030900     MOVE "F" TO SCRATCH-TYPE.
031000     PERFORM SCRATCH-ROUTINE.
031100
031200
031300
031400
000000     COPY PLKTRACE.
