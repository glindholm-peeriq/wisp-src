000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLGRP.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  09/30/89.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* THIS PROGRAM HANDLES THE GROUPING LOGIC FOR GROUP ENTRIES OR
000800* ADDING AN ELEMENT TO A GROUP
000900* IN CONTROL A GROUP CONSISTS OF A NON-UPDATEABLE FIELD THAT
001000* OCCURS MORE THAN ONCE AND ONE OR MORE UPDATEABLE FIELDS THAT
001100* FALL WITHIN IT
001200* POS
001300*          1         2         3         4         5         6
001400* 123456789012345678901234567890123456789012345678901234567890
001500* ^ FIRST ^^    GROUP(1)      ^^   GROUP(2)       ^^ ANOTHER ^
001600*  -------  ------------------  ------------------  ---------
001700*          ^GX(1)^^   GY(1)   ^^GX(2)^^  GY(2)    ^
001800*           -----  -----------  -----  -----------
001900* IN THE ABOVE EXAMPLE 'GROUP' WOULD BE DEFINED AS A 20
002000* CHARACTER NON-UPDATEABLE FIELD STARTING AT POSITION 10 AND
002100* AND OCCURRING TWICE. GX WOULD BE DEFINED AS A 7 CHARACTER
002200* UPDATEABLE FIELD AT 10 AND GY AS A 13 CHARACTER UPDATEABLE
002300* FIELD AT 17. 'FIRST' WOULD HAVE AN UPDATE SEQUENCE OF 1,
002400* 'GX' WOULD BE 2 AND 'GY' WOULD BE 3. BECAUSE 'GX(2)' AND
002500* 'GY(2)' BOTH OCCUPY IMPLICIT UPDATE SEQUENCES OF 4 AND 5 SO
002600* 'ANOTHER' WOULD BE GIVEN AN EXPLICIT UPDATE SEQUENCE NUMBER
002700* OF 6. 'GROUP' WHICH WOULD NORMALLY NOT HAVE A SEQUENCE NUMBER
002800* ACQUIRES AN IMPLIED UPDATE SEQUENCE OF 2 THRU 5.
002900* BECAUSE THE RECORDS FOR 'GX' AND 'GY' ONLY CONTAIN THE
003000* INITIAL SEQUENCE NUMBERS 2 AND 3, KEEPING TRACK OF GROUPS
003100* IS FAIRLY TRICKY.
003200* THE CTRL-RECORD HAS TWO FIELDS RESERVED FOR THE GROUP LEVEL
003300* AND FOR THE ELEMENTS WITHIN A GROUP WHICH ARE NOT USED EXCEPT
003400* WHEN THIS CONDITION OCCURS.
003500* IN RCCTRLF THEY ARE NAMED CF-GROUP-FIELDS AND ARE REDEFINED
003600* FOR USE FOR A GROUP ITEM OR AN ELEMENT ITEM.
003700*
003800* FOR GROUPS:
003900* CF-GROUP-COUNT PIC 99 FOR A GROUP ITEM CONTAINS THE NUMBER
004000* OF COMPONENT FIELDS WITHIN THE GROUP. IN THE ABOVE EXAMPLE
004100* THE VALUE FOR GROUP WOULD BE 02 (GX AND GY MAKE TWO ELEMENTS).
004200*
004300* CF-GROUP-SEQ PIC 999. WANG REFERENCES STATE THAT THIS FIELD
004400* CONTAINS THE STARTING LOCATION OF THE FIRST ELEMENT IN THE
004500* GROUP. THIS IS INCORRECT. FIRSTLY THE FIELD IS A PIC 999 AND
004600* COULD NOT HOLD A POSITION IN A RECORD IF ITS LENGTH WENT TO
004700* 1000 OR MORE. BY TEST, THIS FIELD HOLDS THE LOWEST UPDATE
004800* SEQUENCE OF ALL THE SEQUENCE NUMBERS WITHIN THE GROUP. IN OTHER
004900* WORDS, THE FIRST FIELD THAT WOULD APPEAR ON THE SCREEN. IN THE
005000* ABOVE EXAMPLE CF-GROUP-SEQ WOULD BE 02
005100*
005200* FOR ELEMENTS:
005300* CF-ELEMENT-OCCURRENCE PIC 99 (SAME POSITION AS CF-GROUP-COUNT)
005400* CONTAINS THE OCCURENCE COUNT OF THE GROUP ELEMENT.
005500* IN THE ABOVE EXAMPLE CF-OCCURRENCES FOR 'GX' AND 'GY' WOULD BE
005600* O1 BUT CF-ELEMENT-OCCURENCE WOULD BE 02 BECAUSE 'GROUP' OCCURS
005700* TWICE.
005800*
005900* CF-ELEMENT-LEN PIC 999 (SAME POSITION AS CF-GROUP-SEQ)
006000* CONTAINS THE LENGTH OF THE GROUP PARENT ITEM IN THIS CASE
006100* 10 FOR BOTH 'GX' AND 'GY' WHICH IS THE LENGTH OF 'GROUP'
006200*
006300* THIS IDENTIFYING SCHEME PRODUCES SOME PROBLEMS WHEN ADDING AND
006400* DELETING CONTROL RECORDS.  THIS MODULE IS SUPPOSED TO CARE
006500* FOR THOSE PROBLEMS (FINGERS CROSSED).
006600*
006700* FOR NOW, THIS PROCESSES THE WHOLE ARRAY AND GROUPS AND
006800* SEQUENCES IT.
006900* A CHANGE IS IMPLEMENTED BY DELETING AND ADDING (THE LOGIC IS
007000* EASIER TO HANDLE)
007100* IN A TRUE ADD, THE RECORD WILL BE PASSED IN WITH A ZERO
007200* SEQUENCE NUMBER AND THE NEXT ONE WILL BE ALLOCATED.
007300* IN A CHANGE (RE-ADDING A DELETED RECORD), THE RECORD WILL BE
007400* PASSED IN WITH A SEQUENCE NUMBER, AND THE RECORD WILL BE
007500* INSERTED AT THE POSITION OCCUPIED BY THAT SEQUENCE NUMBER
007600* AND THE REMAINDER WILL BE SHUFFLED UP.
007700*---------------------------------------------------------------
007800
007900 ENVIRONMENT DIVISION.
008000 CONFIGURATION SECTION.
008100 FIGURATIVE-CONSTANTS.
008200     COPY FIGS.
008300 INPUT-OUTPUT SECTION.
008400
008500 FILE-CONTROL.
008600
008700 DATA DIVISION.
008800 FILE SECTION.
008900
009000 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrlgrp.wcb 1.4 9/11/94".
009100 01  LAST-SEQ            PIC 999.
009200 01  LOWEST-SEQ          PIC 99.
009300 01  UPDATE-START        BINARY.
009400 01  SAVE-IDX            BINARY.
009500 01  GROUP-IDX           BINARY.
009600 01  GROUP-START         BINARY.
009700 01  GROUP-END           BINARY.
009800 01  GROUP-LEN           BINARY.
009900 01  GROUP-OCCURRENCES   BINARY.
010000 01  ELEMENT-START       BINARY.
010100 01  ELEMENT-COUNT       BINARY.
010200 01  LAST-IDX            BINARY.
010300 01  ELEMENT-FLAG        PIC 9.
010400     88 THIS-IS-AN-ELEMENT VALUE 1.
010500     COPY RCCTRL.
010600     COPY WSCTRL.
010700     COPY WSIOCODE.
010800     COPY WSFILEIO.
010900 01  SAVE-CF-RECORD      PIC X(130).
011000*DEBUG STUFF
011100 01  GROUPIT  PIC X.
011200 01  GROUP-T1.
011300         10  CF-INT-LEN            PIC 999.
011400         10  CF-EXT-LEN            PIC 999.
011500         10  CF-EDITED-LEN         PIC 999.
011600         10  CF-START-POS          PIC 9999.
011700         10  CF-OCCURRENCES        PIC 99.
011800         10  CF-ZERO-SUPPRESS      PIC 9.
011900         10  CF-DECIMAL-INSERT     PIC 9.
012000         10  CF-SIGN-CONTROL       PIC 9.
012100         10  CF-DOLLAR-COMMA       PIC 9.
012200         10  CF-REPORT-CODE        PIC 9.
012300         10  CF-UPDATE-CODE        PIC 9.
012400         10  CF-DECIMAL-POS        PIC 9.
012500         10  CF-BINARY-EDIT        PIC 9.
012600         10  CF-UPDATE-SEQ         PIC 99.
012700         10  CF-PACKED-DIGITS      PIC 99.
012800         10  CF-GROUP-FIELDS       PIC 9(5).
012900         10  FILLER REDEFINES CF-GROUP-FIELDS.
013000             15  CF-GROUP-COUNT        PIC 99.
013100             15  CF-GROUP-SEQ          PIC 999.
013200         10  FILLER REDEFINES CF-GROUP-FIELDS.
013300             15  CF-ELEMENT-OCCURRENCE PIC 99.
013400             15  CF-ELEMENT-LEN        PIC 999.
013500         10  CF-DISPLAY-CODE       PIC 9.
013600         10  FILLER                PIC X(31).
013700 01  GROUP-T2.
013800         10  CF-NAME               PIC X(8).
013900         10  CF-TYPE               PIC X.
014000         10  CF-VALIDATION         PIC XX.
014100         10  CF-TABLE-NAME         PIC X(6).
014200         10  CF-LO-RANGE           PIC X(16).
014300         10  CF-HI-RANGE           PIC X(16).
014400         10  CF-CUMMULATIVE-NAME   PIC X(8).
014500         10  CF-ALIAS              PIC X(31).
014600         10  CF-DEFAULT-FAC        PIC X.
014700         10  FILLER                PIC X(7).
014800 01  HOLD-T1.
014900         10  CF-INT-LEN            PIC 999.
015000         10  CF-EXT-LEN            PIC 999.
015100         10  CF-EDITED-LEN         PIC 999.
015200         10  CF-START-POS          PIC 9999.
015300         10  CF-OCCURRENCES        PIC 99.
015400         10  CF-ZERO-SUPPRESS      PIC 9.
015500         10  CF-DECIMAL-INSERT     PIC 9.
015600         10  CF-SIGN-CONTROL       PIC 9.
015700         10  CF-DOLLAR-COMMA       PIC 9.
015800         10  CF-REPORT-CODE        PIC 9.
015900         10  CF-UPDATE-CODE        PIC 9.
016000         10  CF-DECIMAL-POS        PIC 9.
016100         10  CF-BINARY-EDIT        PIC 9.
016200         10  CF-UPDATE-SEQ         PIC 99.
016300         10  CF-PACKED-DIGITS      PIC 99.
016400         10  CF-GROUP-FIELDS       PIC 9(5).
016500         10  FILLER REDEFINES CF-GROUP-FIELDS.
016600             15  CF-GROUP-COUNT        PIC 99.
016700             15  CF-GROUP-SEQ          PIC 999.
016800         10  FILLER REDEFINES CF-GROUP-FIELDS.
016900             15  CF-ELEMENT-OCCURRENCE PIC 99.
017000             15  CF-ELEMENT-LEN        PIC 999.
017100         10  CF-DISPLAY-CODE       PIC 9.
017200         10  FILLER                PIC X(31).
017300 01  HOLD-T2.
017400         10  CF-NAME               PIC X(8).
017500         10  CF-TYPE               PIC X.
017600         10  CF-VALIDATION         PIC XX.
017700         10  CF-TABLE-NAME         PIC X(6).
017800         10  CF-LO-RANGE           PIC X(16).
017900         10  CF-HI-RANGE           PIC X(16).
018000         10  CF-CUMMULATIVE-NAME   PIC X(8).
018100         10  CF-ALIAS              PIC X(31).
018200         10  CF-DEFAULT-FAC        PIC X.
018300         10  FILLER                PIC X(7).
018400* END DEBUG STUFF
018500 LINKAGE SECTION.
018600 01  CTRL-IO-BLOCK.
018700     COPY LKDIO.
018800 01  CTRL-HEADER-RECORDS.
018900     COPY RCCTRLH.
019000 01  CF-RECORD.
019100     COPY RCCTRLF.
019200     COPY LKCTRLAR.
019300     COPY RCCTRLT1.
019400     COPY RCCTRLT2.
000000     COPY LKPRC.
019500 PROCEDURE DIVISION USING
000000     CALLED-RC
019600     COPY LKCTRL.
019700 MAIN-LOGIC SECTION.
019800 PROGRAM-BEGIN.
000000     MOVE "CTRLGRP BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     MOVE ZEROES TO CALLED-RC.
019900     PERFORM REGROUP-AND-SEQUENCE.
000000     MOVE "CTRLGRP EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
020000 MAIN-LOGIC-STOP.
020100     STOP RUN.
020200 LEVEL-2 SECTION.
020300*---------------------------------------------------------------
020400* IF THERE ARE NO FIELDS AT ALL RETURN 1
020500* FIND THE BEGINING OF UPDATEABLE FIELDS
020600* IF THERE ARE NO UPDATEABLE FIELDS RETURN 99
020700* OTHERWISE GO FOR IT
020800*---------------------------------------------------------------
020900 REGROUP-AND-SEQUENCE.
021000     MOVE ZEROES TO LAST-SEQ.
021100     IF CF-NEXT-IDX = 1
021200         MOVE 1 TO CALLED-RC
021300     ELSE
021400         PERFORM FIND-UPDATES.
021500     IF UPDATE-START = ZEROES
021600         MOVE 99 TO CALLED-RC
021700     ELSE
021800         PERFORM UPDATE-ARRAY.
021900*---------------------------------------------------------------
022000* SEARCH THE ARRAY FROM 1 LOOKING FOR AN UPDATEABLE FIELD, OR
022100* A NON UPDATEABLE FIELD WITH AN OCCURS.
022200*---------------------------------------------------------------
022300 FIND-UPDATES.
022400     MOVE ZEROES TO UPDATE-START.
022500     IF CF-NEXT-IDX > 1
022600         COMPUTE LAST-IDX = CF-NEXT-IDX - 1
022700         PERFORM FIND-ONE-UPDATE
022800          VARYING CF-IDX FROM 1 BY 1
022900           UNTIL UPDATE-START NOT = ZEROES OR
023000                 CF-IDX > LAST-IDX.
023100 FIND-ONE-UPDATE.
023200     PERFORM SAVE-THE-ELEMENT.
023300     IF T1-UPDATE-CODE(CF-IDX) = 0
023400         MOVE CF-IDX TO UPDATE-START
023500     ELSE
023600     IF T1-OCCURRENCES(CF-IDX) > 1
023700         MOVE CF-IDX TO UPDATE-START
023800     ELSE
023900         COMPUTE T1-UPDATE-SEQ(CF-IDX) = ZEROES.
024000     PERFORM REWRITE-IF-NEEDED.
024100*---------------------------------------------------------------
024200* THIS ASSUMES A CORRECTLY SORTED ARRAY WHICH CONTAINS
024300* UPDATEABLE FIELDS IN SEQUENCE WITH NON-UPDATEABLE PARENTS
024400* OCCURRING BEFORE UPDATE CHILDREN
024500*---------------------------------------------------------------
024600 UPDATE-ARRAY.
024700     PERFORM UPDATE-ONE-ENTRY
024800      VARYING CF-IDX FROM UPDATE-START BY 1
024900         UNTIL CF-IDX > LAST-IDX.
025000 UPDATE-ONE-ENTRY.
025100     IF T1-UPDATE-CODE(CF-IDX) = 0
025200         PERFORM UPDATE-ONE-SINGLE
025300     ELSE
025400         PERFORM UPDATE-ONE-GROUP.
025500*---------------------------------------------------------------
025600* NON GROUPS GRAB LAST-SEQ + 1. LAST SEQ IS UPDATED
025700* BY THE OCCURENCES COUNT.
025800*---------------------------------------------------------------
025900 UPDATE-ONE-SINGLE.
026000     PERFORM SAVE-THE-ELEMENT.
026100     COMPUTE T1-UPDATE-SEQ(CF-IDX) = LAST-SEQ + 1.
026200     COMPUTE LAST-SEQ = LAST-SEQ +
026300      T1-OCCURRENCES(CF-IDX).
026400     MOVE ZEROES TO T1-ELEMENT-OCCURRENCE(CF-IDX).
026500     MOVE ZEROES TO T1-ELEMENT-LEN(CF-IDX).
026600     PERFORM REWRITE-IF-NEEDED.
026700
026800*---------------------------------------------------------------
026900* SET UP TO POINTER TO THIS GROUP ENTRY, AND THEN PROCESS ALL
027000* THE CHILDREN WHICH ARE ASSUMED TO START AT THE NEXT ENTRY.
027100* THE FIRST PASS DOES A GROUPING. IF ELEMENTS ARE FOUND, THEN
027200* A SECOND PASS DOES SEQUENCING.
027300*
027400* THE LAST SEQ NUMBER IS CALCULATED BASED ON THE NUMBER OF
027500* GROUP OCCURRENCES - 1 USED UP IN THE SEQUENCING STEP
027600* TIMES THE TOTAL ELEMENTS IN THE GROUP
027700* FOR EXAMPLE IF A GROUP OCCURED 3 TIMES AND CONTAINED
027800* FIVE ELEMENTS. ASSUMING THE FIRST ELEMENT WAS SEQUENCE
027900* NUMBER 4 THE GROUP WOULD BREAK DOWN AS FOLLOWS
028000*       E1    E2    E3    E4    E5
028100* G(1)   4     5     6     7     8 <-LAST-SEQ = 8 AFTER
028200* G(2)   9    10    11    12    13    SEQUENCE-ONE-ELEMENT
028300* G(3)  14    15    16    17    18
028400*
028500* NEW LAST-SEQ = LAST-SEQ + ((OCCURENCES - 1) * ELEMENTS)
028600*              =   8      + (( 3 -1) * 5)
028700*              =   8      + (     2  * 5)
028800*              =  18
028900*---------------------------------------------------------------
029000 UPDATE-ONE-GROUP.
029100     MOVE CF-IDX TO GROUP-IDX.
029200     PERFORM SAVE-GROUP-ELEMENT.
029300     ADD 1 TO CF-IDX.
029400     MOVE CF-IDX TO SAVE-IDX.
029500     MOVE ZEROES TO ELEMENT-COUNT.
029600     MOVE 99 TO LOWEST-SEQ.
029700     PERFORM SET-GROUP-VALUES.
029800     PERFORM PROCESS-ONE-ELEMENT.
029900     IF ELEMENT-COUNT = ZEROES
030000         MOVE ZEROES TO T1-GROUP-COUNT(GROUP-IDX)
030100         MOVE ZEROES TO T1-GROUP-SEQ(GROUP-IDX)
030200     ELSE
030300         COMPUTE T1-GROUP-COUNT(GROUP-IDX) =
030400          ELEMENT-COUNT
030500         MOVE SAVE-IDX TO CF-IDX
030600         PERFORM SEQUENCE-ONE-ELEMENT
030700         COMPUTE T1-GROUP-SEQ(GROUP-IDX) =
030800          LOWEST-SEQ
030900         COMPUTE LAST-SEQ = LAST-SEQ +
031000                ((GROUP-OCCURRENCES - 1) * ELEMENT-COUNT).
031100     PERFORM REWRITE-GROUP-IF-NEEDED.
031200 SET-GROUP-VALUES.
031300     COMPUTE GROUP-START =
031400      T1-START-POS(GROUP-IDX).
031500     COMPUTE GROUP-LEN =
031600      T1-INT-LEN(GROUP-IDX).
031700     COMPUTE GROUP-END = GROUP-START +
031800      GROUP-LEN - 1.
031900     COMPUTE GROUP-OCCURRENCES =
032000      T1-OCCURRENCES(GROUP-IDX).
032100*---------------------------------------------------------------
032200* PROCESS EACH ELEMENT UNTIL EXHAUSTED
032300*---------------------------------------------------------------
032400 PROCESS-ONE-ELEMENT.
032500     COMPUTE ELEMENT-START =
032600      T1-START-POS(CF-IDX).
032700     PERFORM IS-THIS-AN-ELEMENT.
032800     IF THIS-IS-AN-ELEMENT
032900         PERFORM PROCESS-THIS-ELEMENT
033000         ADD 1 TO CF-IDX
033100         IF CF-IDX NOT > LAST-IDX
033200             GO TO PROCESS-ONE-ELEMENT.
033300*---------------------------------------------------------------
033400* UPDATE THE EXTRA BYTES WITH GROUP OCCURENCES AND LENGTH
033500*---------------------------------------------------------------
033600 PROCESS-THIS-ELEMENT.
033700     PERFORM SAVE-THE-ELEMENT
033800* COUNT IT
033900     ADD 1 TO ELEMENT-COUNT.
034000* UPDATE THE ELEMENTALS
034100     COMPUTE T1-ELEMENT-OCCURRENCE(CF-IDX) =
034200          GROUP-OCCURRENCES.
034300     COMPUTE T1-ELEMENT-LEN(CF-IDX) =
034400          GROUP-LEN.
034500     PERFORM REWRITE-IF-NEEDED.
034600
034700*---------------------------------------------------------------
034800* AN ELEMENTAL SEQUENCE IS THE NEXT SEQUENCE NUMBER
034900* LAST USED SEQUENCE IS UPDATED BY THE GROUP-OCCURENCE COUNT
035000* IE IF AN ELEMENT STARTS AS
035100*---------------------------------------------------------------
035200 SEQUENCE-ONE-ELEMENT.
035300     PERFORM SAVE-THE-ELEMENT.
035400     COMPUTE ELEMENT-START =
035500      T1-START-POS(CF-IDX).
035600     PERFORM IS-THIS-AN-ELEMENT.
035700     IF THIS-IS-AN-ELEMENT
035800         PERFORM SEQUENCE-THIS-ELEMENT
035900         PERFORM REWRITE-IF-NEEDED
036000         ADD 1 TO CF-IDX
036100         IF CF-IDX NOT > LAST-IDX
036200             GO TO SEQUENCE-ONE-ELEMENT.
036300 SEQUENCE-THIS-ELEMENT.
036400     COMPUTE LAST-SEQ = LAST-SEQ + 1.
036500     COMPUTE T1-UPDATE-SEQ(CF-IDX) = LAST-SEQ.
036600
036700* SAVE LOWEST SEQUENCE.
036800     IF LAST-SEQ < LOWEST-SEQ
036900         COMPUTE LOWEST-SEQ = LAST-SEQ.
037000
037100*---------------------------------------------------------------
037200* AN ELEMENT FALLS WITHIN A GROUP IF IT IS UPDATEABLE AND
037300* STARTS WITHIN THE GROUP LENGTH
037400*---------------------------------------------------------------
037500 IS-THIS-AN-ELEMENT.
037600     MOVE ZERO TO ELEMENT-FLAG.
037700     IF ELEMENT-START NOT < GROUP-START AND
037800        ELEMENT-START NOT > GROUP-END   AND
037900        T1-UPDATE-CODE(CF-IDX) = 0
038000         MOVE 1 TO ELEMENT-FLAG.
038100 SAVE-GROUP-ELEMENT.
038200     MOVE CF-T1(GROUP-IDX) TO GROUP-T1.
038300     MOVE CF-T2(GROUP-IDX) TO GROUP-T2.
038400 SAVE-THE-ELEMENT.
038500     MOVE CF-T1(CF-IDX) TO HOLD-T1.
038600     MOVE CF-T2(CF-IDX) TO HOLD-T2.
038700 REWRITE-IF-NEEDED.
038800     IF CF-T1(CF-IDX) = HOLD-T1 AND
038900        CF-T2(CF-IDX) = HOLD-T2
039000         NEXT SENTENCE
039100     ELSE
039200         MOVE CF-RECORD TO SAVE-CF-RECORD
039300         MOVE SPACE TO CF-RECORD
039400         PERFORM LOAD-FROM-CF-IDX
039500         MOVE CF-RECORD TO CTRL-RECORD
039600         PERFORM REWRITE-CTRL-RECORD
039700         MOVE SAVE-CF-RECORD TO CF-RECORD.
039800 LOAD-FROM-CF-IDX.
039900     MOVE T1-INT-LEN(CF-IDX) TO CX-INT-LEN.
040000     MOVE T1-EXT-LEN(CF-IDX) TO CX-EXT-LEN.
040100     MOVE T1-EDITED-LEN(CF-IDX) TO CX-EDITED-LEN.
040200     MOVE T1-START-POS(CF-IDX) TO CX-START-POS.
040300     MOVE T1-OCCURRENCES(CF-IDX) TO CX-OCCURRENCES.
040400     MOVE T1-ZERO-SUPPRESS(CF-IDX) TO CX-ZERO-SUPPRESS.
040500     MOVE T1-DECIMAL-INSERT(CF-IDX) TO CX-DECIMAL-INSERT.
040600     MOVE T1-SIGN-CONTROL(CF-IDX) TO CX-SIGN-CONTROL.
040700     MOVE T1-DOLLAR-COMMA(CF-IDX) TO CX-DOLLAR-COMMA.
040800     MOVE T1-REPORT-CODE(CF-IDX) TO CX-REPORT-CODE.
040900     MOVE T1-UPDATE-CODE(CF-IDX) TO CX-UPDATE-CODE.
041000     MOVE T1-DECIMAL-POS(CF-IDX) TO CX-DECIMAL-POS.
041100     MOVE T1-BINARY-EDIT(CF-IDX) TO CX-BINARY-EDIT.
041200     MOVE T1-UPDATE-SEQ(CF-IDX) TO CX-UPDATE-SEQ.
041300     MOVE T1-PACKED-DIGITS(CF-IDX) TO CX-PACKED-DIGITS.
041400     MOVE T1-GROUP-FIELDS(CF-IDX) TO CX-GROUP-FIELDS.
041500     MOVE T1-DISPLAY-CODE(CF-IDX) TO CX-DISPLAY-CODE.
041600     MOVE T2-NAME(CF-IDX) TO CX-NAME.
041700     MOVE T2-TYPE(CF-IDX) TO CX-TYPE.
041800     MOVE T2-VALIDATION(CF-IDX) TO CX-VALIDATION.
041900     MOVE T2-TABLE-NAME(CF-IDX) TO CX-TABLE-NAME.
042000     MOVE T2-LO-RANGE(CF-IDX) TO CX-LO-RANGE.
042100     MOVE T2-HI-RANGE(CF-IDX) TO CX-HI-RANGE.
042200     MOVE T2-CUMMULATIVE-NAME(CF-IDX) TO CX-CUMMULATIVE-NAME.
042300     MOVE T2-ALIAS(CF-IDX) TO CX-ALIAS.
042400     MOVE T2-DEFAULT-FAC(CF-IDX) TO CX-DEFAULT-FAC.
042500 REWRITE-GROUP-IF-NEEDED.
042600     IF CF-T1(GROUP-IDX) = GROUP-T1 AND
042700        CF-T2(GROUP-IDX) = GROUP-T2
042800         NEXT SENTENCE
042900     ELSE
043000         MOVE CF-RECORD TO SAVE-CF-RECORD
043100         MOVE SPACE TO CF-RECORD
043200         PERFORM LOAD-FROM-GROUP-IDX
043300         MOVE CF-RECORD TO CTRL-RECORD
043400         PERFORM REWRITE-CTRL-RECORD
043500         MOVE SAVE-CF-RECORD TO CF-RECORD.
043600 LOAD-FROM-GROUP-IDX.
043700     MOVE T1-INT-LEN(GROUP-IDX) TO CX-INT-LEN.
043800     MOVE T1-EXT-LEN(GROUP-IDX) TO CX-EXT-LEN.
043900     MOVE T1-EDITED-LEN(GROUP-IDX) TO CX-EDITED-LEN.
044000     MOVE T1-START-POS(GROUP-IDX) TO CX-START-POS.
044100     MOVE T1-OCCURRENCES(GROUP-IDX) TO CX-OCCURRENCES.
044200     MOVE T1-ZERO-SUPPRESS(GROUP-IDX) TO CX-ZERO-SUPPRESS.
044300     MOVE T1-DECIMAL-INSERT(GROUP-IDX) TO CX-DECIMAL-INSERT.
044400     MOVE T1-SIGN-CONTROL(GROUP-IDX) TO CX-SIGN-CONTROL.
044500     MOVE T1-DOLLAR-COMMA(GROUP-IDX) TO CX-DOLLAR-COMMA.
044600     MOVE T1-REPORT-CODE(GROUP-IDX) TO CX-REPORT-CODE.
044700     MOVE T1-UPDATE-CODE(GROUP-IDX) TO CX-UPDATE-CODE.
044800     MOVE T1-DECIMAL-POS(GROUP-IDX) TO CX-DECIMAL-POS.
044900     MOVE T1-BINARY-EDIT(GROUP-IDX) TO CX-BINARY-EDIT.
045000     MOVE T1-UPDATE-SEQ(GROUP-IDX) TO CX-UPDATE-SEQ.
045100     MOVE T1-PACKED-DIGITS(GROUP-IDX) TO CX-PACKED-DIGITS.
045200     MOVE T1-GROUP-FIELDS(GROUP-IDX) TO CX-GROUP-FIELDS.
045300     MOVE T1-DISPLAY-CODE(GROUP-IDX) TO CX-DISPLAY-CODE.
045400     MOVE T2-NAME(GROUP-IDX) TO CX-NAME.
045500     MOVE T2-TYPE(GROUP-IDX) TO CX-TYPE.
045600     MOVE T2-VALIDATION(GROUP-IDX) TO CX-VALIDATION.
045700     MOVE T2-TABLE-NAME(GROUP-IDX) TO CX-TABLE-NAME.
045800     MOVE T2-LO-RANGE(GROUP-IDX) TO CX-LO-RANGE.
045900     MOVE T2-HI-RANGE(GROUP-IDX) TO CX-HI-RANGE.
046000     MOVE T2-CUMMULATIVE-NAME(GROUP-IDX) TO CX-CUMMULATIVE-NAME.
046100     MOVE T2-ALIAS(GROUP-IDX) TO CX-ALIAS.
046200     MOVE T2-DEFAULT-FAC(GROUP-IDX) TO CX-DEFAULT-FAC.
046300
046400     COPY IOCTRL.
046500
000000     COPY PLKTRACE.
