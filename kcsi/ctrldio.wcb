000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. CTRLDIO.
000300*--------------------------------------------------
000400* LOAD A DIO BLOCK WITH DATA FROM A CONTROL HEADER
000500* AND FIELDS.
000600* RETURNS 95 IF AN ERROR OCCURS.
000700* THE ONLY ERRORS ARE CONSISTENCY, IE A PRIMARY OR
000800* ALTKEY IS DECLARED, BUT NO MATCHING RECORD COULD
000900* BE FOUND IN THE CONTROL FILE.
001000*--------------------------------------------------
001100 ENVIRONMENT DIVISION.
001200 CONFIGURATION SECTION.
001300 INPUT-OUTPUT SECTION.
001400 FILE-CONTROL.
001500 DATA DIVISION.
001600 FILE SECTION.
001700 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)ctrldio.wcb 1.6 9/11/94".
001800 01  ALTKEY-IDX    BINARY.
001900 01  ALTKEY2-IDX   BINARY.
002000 01  DUP-FLAG      PIC X.
002100     COPY RCCTRL.
002200     COPY WSFILEIO.
002300     COPY WSIOCODE.
002400* CTRL-FILE-HEADER FIELDS AND GENERAL WS
002500     COPY WSCTRL.
002600 01  CF-RECORD.
002700     COPY RCCTRLF.
002800 LINKAGE SECTION.
002900 01  CTRL-IO-BLOCK.
003000     COPY LKDIO.
003100 01  WORK-IO-BLOCK.
003200     COPY LKDIO.
003300 01  CTRL-HEADER-RECORDS.
003400     COPY RCCTRLH.
000000     COPY LKPRC.
003500 PROCEDURE DIVISION USING
000000     CALLED-RC
003600     CTRL-IO-BLOCK
003700     WORK-IO-BLOCK
003800     CTRL-HEADER-RECORDS.
003900 MAIN-LOGIC SECTION.
004000 PROGRAM-BEGIN.
000000     MOVE "CTRLDIO BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
004100     PERFORM OPENING-PROCEDURE.
004200     PERFORM MAIN-PROCESS.
004300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "CTRLDIO EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
004400 MAIN-LOGIC-EXIT.
004500     EXIT PROGRAM.
004600 MAIN-LOGIC-STOP.
004700     STOP RUN.
004800 THE-OTHER SECTION.
004900 OPENING-PROCEDURE.
005000     PERFORM OPEN-INPUT-CTRL-FILE.
005100 CLOSING-PROCEDURE.
005200     PERFORM CLOSE-CTRL-FILE.
005300 MAIN-PROCESS.
005400     MOVE ZEROES TO CALLED-RC.
005500     MOVE ZEROES TO FILE-ID-DATA OF WORK-IO-BLOCK.
000000     MOVE CH-FILE-ORG TO FILE-ORG OF WORK-IO-BLOCK.
005600     MOVE CH-RECORD-LEN
005700        TO FILE-RECORD-LEN OF WORK-IO-BLOCK.
005800     IF     (CH-PRIMARY-KEY NOT = SPACE)
000000        AND (CH-FILE-ORG = "I")
005900         PERFORM SET-UP-KEYS.
006000 SET-UP-KEYS.
006100     MOVE CH-PRIMARY-KEY TO CTRL-KEY.
006200     PERFORM READ-CTRL-RECORD.
006300     IF CTRL-FILE-STATUS = RECORD-NOT-FOUND
006400         MOVE 95 TO CALLED-RC
006500     ELSE
006600         PERFORM LOAD-PRIMARY-VALUES
006700         IF CA1-RECORD NOT = SPACES
006800             PERFORM SET-UP-ALTKEYS
006900             .
007000 LOAD-PRIMARY-VALUES.
007100     MOVE CTRL-RECORD TO CF-RECORD.
007200     MOVE CF-START-POS TO FILE-KEY-POS OF WORK-IO-BLOCK.
007300     MOVE CF-INT-LEN TO FILE-KEY-LEN OF WORK-IO-BLOCK.
007400 SET-UP-ALTKEYS.
007500     MOVE 1 TO ALTKEY-IDX.
007600     PERFORM SET-UP-ONE-ALTKEY.
007700     COMPUTE FILE-ALTKEY-COUNT OF WORK-IO-BLOCK =
007800         ALTKEY-IDX - 1.
007900 SET-UP-ONE-ALTKEY.
008000     COMPUTE ALTKEY2-IDX = ALTKEY-IDX - 8.
008100     IF ALTKEY-IDX > 8
008200         MOVE CA2-KEYNAME(ALTKEY2-IDX) TO CTRL-KEY
008300         MOVE CA2-DUPS-ALLOWED(ALTKEY2-IDX) TO DUP-FLAG
008400     ELSE
008500         MOVE CA1-KEYNAME(ALTKEY-IDX) TO CTRL-KEY
008600         MOVE CA1-DUPS-ALLOWED(ALTKEY-IDX) TO DUP-FLAG
008700         .
008800     IF CTRL-KEY NOT = SPACE
008900         PERFORM READ-CTRL-RECORD
009000         IF CTRL-FILE-STATUS NOT = ZEROES
009100             MOVE 95 TO CALLED-RC
009200         ELSE
009300             PERFORM SET-UP-THE-ALTKEY
009400             ADD 1 TO ALTKEY-IDX
009500             IF ALTKEY-IDX NOT > 16
009600                 GO TO SET-UP-ONE-ALTKEY.
009700 SET-UP-THE-ALTKEY.
009800     MOVE CTRL-RECORD TO CF-RECORD.
009900     COMPUTE FILE-ALTKEY-NUMBER OF WORK-IO-BLOCK(ALTKEY-IDX) =
010000         ALTKEY-IDX.
010100     COMPUTE FILE-ALTKEY-POS OF WORK-IO-BLOCK(ALTKEY-IDX) =
010200         CF-START-POS.
010300     COMPUTE FILE-ALTKEY-LEN OF WORK-IO-BLOCK(ALTKEY-IDX) =
010400         CF-INT-LEN.
010500     IF  DUP-FLAG = "Y"
010600         MOVE 1 TO FILE-ALTKEY-DUP OF WORK-IO-BLOCK(ALTKEY-IDX).
010700
010800* IO TO THE CTRL-FILE
010900     COPY IOCTRL.
011000
011100
000000     COPY PLKTRACE.
