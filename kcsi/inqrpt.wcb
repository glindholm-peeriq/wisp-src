000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. INQRPT.
000300 AUTHOR. HLM.
000400 DATE-WRITTEN.  01/31/90.
000500 DATE-COMPILED.
000600*---------------------------------------------------------------
000700* ASKS FOR THE DEFAULTS:  FILE LIBRARY VOLUME
000800*---------------------------------------------------------------
000900
001000 ENVIRONMENT DIVISION.
001100 CONFIGURATION SECTION.
001200 FIGURATIVE-CONSTANTS.
001300     COPY FIGS.
001400
001500 INPUT-OUTPUT SECTION.
001600
001700 FILE-CONTROL.
001800
001900 DATA DIVISION.
002000 FILE SECTION.
002100
002200 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)inqrpt.wcb 1.8 9/11/94".
002300 01  VERSION-COPYRIGHT         PIC X(79) VALUE
002400     " ".
002500     COPY WSGP.
002600     COPY WSINT.
002700     COPY WSCRTS.
002800
002900 01  SAVRPT-GETPARM-FIELDS.
003000     05  SAVRPT-MSG-ID               PIC X(4) VALUE "1016".
003100     05  SAVRPT-PRNAME               PIC X(8) VALUE "RPTDEF".
003200     05  SAVRPT-MSG-ISSUER           PIC X(6) VALUE "INQUIR".
003300
003400 01  SAVRPT-MSG-COUNT.
003500     05  FILLER                        BINARY   VALUE ZERO.
003600     05  FILLER                        BINARY   VALUE 1.
003700 01  SAVRPT-MSG-TEXT-1               PIC X(68) VALUE
003800         "  Please enter the name of the REPORT DEFINITION file.
003900-        "            ".
004000
004100 01  SAVRPT-TEXT-01                  PIC X(69) VALUE
004200         "  Please fill in the necessary information for the file
004300-        "to be created".
004400 01  SAVRPT-TEXT-02                  PIC X(20) VALUE
004500         "  and press (ENTER).".
004600 01  SAVRPT-FILE-KW              PIC X(8) VALUE "FILE".
004700 01  SAVRPT-FILE-FIELD           PIC X(8) VALUE SPACE.
004800 01  SAVRPT-FILE-DEFAULT         PIC X(8) VALUE SPACE.
004900 01  SAVRPT-LIBRARY-KW              PIC X(8) VALUE "LIBRARY".
005000 01  SAVRPT-LIBRARY-FIELD           PIC X(8) VALUE SPACE.
005100 01  SAVRPT-LIBRARY-DEFAULT         PIC X(8) VALUE SPACE.
005200 01  SAVRPT-VOLUME-KW              PIC X(8) VALUE "VOLUME".
005300 01  SAVRPT-VOLUME-FIELD           PIC X(6) VALUE SPACE.
005400 01  SAVRPT-VOLUME-DEFAULT         PIC X(6) VALUE SPACE.
005500 01  SAVRPT-TEXT-03                  PIC X(67) VALUE
005600         "  ".
005700 01  SAVRPT-TEXT-04                  PIC X(66) VALUE
005800         "  ".
005900 01  SAVRPT-TEXT-05                  PIC X(22) VALUE
006000         "  (ENTER) Continue  or".
006100 01  SAVRPT-TEXT-06                  PIC X(67) VALUE
006200         "                                 (1) Return without crea
006300-        "ting a file".
006900
007000     COPY WSUSAGE.
007100
007200 01  RPT-IO-BLOCK.
007300     COPY LKDIO.
007400
007500 01  LOAD-SWITCH             PIC X(01) VALUE "C".
007600 01  SAV-SEC-FILE            PIC 9(01) VALUE 0.
007700 01  SAV-CONTROL-FIELDS      PIC 9(01) VALUE 1.
007800
007900 01  MESSAGE-LITERALS.
008000     05  FILE-ID-BLANK-LITERAL        PIC X(78) VALUE
008100         "* Error * File Name cannot be blank".
008200     05  INVALID-FILE-LITERAL         PIC X(78) VALUE
008300         "* Error * Invalid File Name".
008400     05  INVALID-LIB-LITERAL       PIC X(78) VALUE
008500         "* Error * Invalid Library Name".
008600     05  INVALID-VOL-LITERAL          PIC X(78) VALUE
008700         "* Error * Invalid Volume Name".
008800
000000     COPY LKVALNAM.
000000
012400 LINKAGE SECTION.
012500     COPY WSRPTREC.
012600
012700 PROCEDURE DIVISION USING
012800     RPT-RECORDS.
012900 MAIN-LOGIC SECTION.
013000 PROGRAM-BEGIN.
000000     MOVE "INQRPT BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000     CALL "INIDIO" USING RPT-IO-BLOCK.
000000     MOVE "I" TO FILE-ORG OF RPT-IO-BLOCK.
013100     PERFORM OPENING-PROCEDURE.
013200     PERFORM MAIN-PROCESS.
013300     PERFORM CLOSING-PROCEDURE.
000000     MOVE "INQRPT EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
013400 MAIN-LOGIC-EXIT.
013500     EXIT PROGRAM.
013600 MAIN-LOGIC-STOP.
013700     STOP RUN.
013800 LEVEL-2 SECTION.
013900 OPENING-PROCEDURE.
014000     PERFORM EXTRACT-USAGE-CONSTANTS.
000000     MOVE SPACE TO SAVRPT-LIBRARY-FIELD.
000000     STRING USER-ID DELIMITED BY SPACE
000000            "RPT" DELIMITED BY SIZE
000000            INTO SAVRPT-LIBRARY-FIELD.
014200     MOVE OUTVOL TO SAVRPT-VOLUME-FIELD.
014300
014400 CLOSING-PROCEDURE.
014500
014600 MAIN-PROCESS.
014700     MOVE SPACE TO SCREEN-ERROR-STATUS.
014800     PERFORM CALL-SAVRPT-GETPARM.
014900
015000     MOVE SPACE TO SAVRPT-MSG-TEXT-1.
015100
015200     IF GP-ENTER-KEY-PRESSED
015300         PERFORM VAL-FILE-NAME
015400         IF NOT SCREEN-ENTRY-ERROR
015500             PERFORM VAL-LIB-NAME
015600             IF NOT SCREEN-ENTRY-ERROR
015700                 PERFORM VAL-VOL-NAME.
015800
015900     IF SCREEN-ENTRY-ERROR
016000         GO TO MAIN-PROCESS.
016100
016200     IF GP-ENTER-KEY-PRESSED
016300         MOVE SAVRPT-FILE-FIELD TO FILE-NAME OF RPT-IO-BLOCK
016400         MOVE SAVRPT-LIBRARY-FIELD TO FILE-LIBRARY OF RPT-IO-BLOCK
016500         MOVE SAVRPT-VOLUME-FIELD TO FILE-VOLUME OF RPT-IO-BLOCK
016600         CALL "INQXST" USING
016700             RPT-IO-BLOCK
016800         PERFORM MAKE-THE-REPORT-FILE.
016900
017000 CALL-SAVRPT-GETPARM.
017100
000000     MOVE ZEROES TO PFKEY-MASK-16.
000000     MOVE ZEROES TO PFKEY-MASK-32.
000000     ADD GP-PF-01-VAL TO PFKEY-MASK-16.
017300
017400     CALL "GETPARM" USING
017500         GP-SPECIFY-TYPE, GP-REQUEST-FORM, SAVRPT-PRNAME,
017600         GP-PFKEY-RECEIVER, SAVRPT-MSG-ID,
017700         SAVRPT-MSG-ISSUER,
017800
017900         SAVRPT-MSG-COUNT,
018000         SAVRPT-MSG-TEXT-1, INT68,
018100
018200         GP-TEXT-TYPE, SAVRPT-TEXT-01, INT69,
018300         GP-ABS, INT09, GP-ABS, INT02,
018400
018500         GP-TEXT-TYPE, SAVRPT-TEXT-02, INT20,
018600         GP-ABS, INT10, GP-ABS, INT02,
018700
018800         GP-KW-TYPE, SAVRPT-FILE-KW,
018900         SAVRPT-FILE-FIELD, INT08,
019000         GP-ABS, INT12, GP-ABS, INT09,
019100         GP-ALPHANUMERIC-DATA
019200
019300         GP-KW-TYPE, SAVRPT-LIBRARY-KW,
019400         SAVRPT-LIBRARY-FIELD, INT08,
019500         GP-ABS, INT12, GP-ABS, INT30,
019600         GP-ALPHANUMERIC-DATA
019700
019800         GP-KW-TYPE, SAVRPT-VOLUME-KW,
019900         SAVRPT-VOLUME-FIELD, INT06,
020000         GP-ABS, INT12, GP-ABS, INT51,
020100         GP-ALPHANUMERIC-DATA
020200
020300         GP-TEXT-TYPE, SAVRPT-TEXT-03, INT67,
020400         GP-ABS, INT14, GP-ABS, INT02,
020500
020600         GP-TEXT-TYPE, SAVRPT-TEXT-04, INT66,
020700         GP-ABS, INT17, GP-ABS, INT02,
020800
020900         GP-TEXT-TYPE, SAVRPT-TEXT-05, INT22,
021000         GP-ABS, INT23, GP-ABS, INT02,
021100
021200         GP-TEXT-TYPE, SAVRPT-TEXT-06, INT67,
021300         GP-ABS, INT24, GP-ABS, INT02,
021400         GP-ENABLE-ENTER-KEY
021500         GP-PFKEY-TYPE, PFKEY-MASK.
000000
000000     IF GP-PFKEY-RECEIVER = LOW-VALUES
000000         MOVE HEX40 TO GP-PFKEY-RECEIVER.
021600
021700     IF NOT GP-PFKEY-1-PRESSED  AND
021800        NOT GP-ENTER-KEY-PRESSED
021900         GO TO CALL-SAVRPT-GETPARM.
022000
022100 VAL-FILE-NAME.
022200
022300     IF SAVRPT-FILE-FIELD = SPACES
022400         MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS
022500         MOVE FILE-ID-BLANK-LITERAL TO SAVRPT-MSG-TEXT-1.
022600
022700     IF NOT SCREEN-ENTRY-ERROR
022800         MOVE SAVRPT-FILE-FIELD TO VAL-NAME
022900         PERFORM TEST-THE-NAME
023000         IF SCREEN-ENTRY-ERROR
023100             MOVE INVALID-FILE-LITERAL TO SAVRPT-MSG-TEXT-1.
023200
023300 VAL-LIB-NAME.
023400
023500     MOVE SAVRPT-LIBRARY-FIELD TO VAL-NAME.
023600     PERFORM TEST-THE-NAME.
023700     IF SCREEN-ENTRY-ERROR
023800         MOVE INVALID-LIB-LITERAL TO SAVRPT-MSG-TEXT-1.
023900
024000 VAL-VOL-NAME.
024100
024200     MOVE SAVRPT-VOLUME-FIELD TO VAL-NAME.
024300     PERFORM TEST-THE-VOL.
024400     IF SCREEN-ENTRY-ERROR
024500         MOVE INVALID-VOL-LITERAL TO SAVRPT-MSG-TEXT-1.
024600
024700 MAKE-THE-REPORT-FILE.
024800     MOVE "C" TO LOAD-SWITCH.
024900     PERFORM CLOSING-PROCEDURE.
025000     CALL "RPTMAK" USING
000000         LOAD-SWITCH
000000         RPT-IO-BLOCK
025100         RPT-RECORDS.
025400     PERFORM OPENING-PROCEDURE.
025500
031500 TEST-THE-NAME.
000000     CALL "VALNAM" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
031500 TEST-THE-VOL.
000000     CALL "VALVOL" USING
000000          VAL-NAME VAL-RC.
000000     IF VAL-RC NOT = ZEROES
000000          MOVE ENTRY-ERROR TO SCREEN-ERROR-STATUS.
035400
029400* GENERAL ROUTINES.
029500     COPY PLUSAGE.
029600

000000     COPY PLKTRACE.
