000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. TABLEIO.
000300 AUTHOR. MJB.
000400 DATE-WRITTEN.  09/24/89.
000500*---------------------------------------------------------------
000600* THIS PROGRAM EXECUTES IO TO THE TABLE FILE.
000700* AT THE TIME THIS WAS WRITTEN, THE WISP TRANSLATOR ON UNIX
000800* HAD A BUG ON 'IF' TESTS IN QUALIFIED DATA NAMES
000900* IF XXX OF YYY = ZZZ
001000* WAS INTERPRETED AS A WANG BIT-TEST WHICH HAS A SIMILAR SOUNDING
001100* SYNTAX
001200* IF BIT OF BYTE IS ON
001300* IN ORDER TO GET THESE THROUGH THE TRANSLATOR I HAD TO MOVE
001400* THE VARIABLES OUT TO NON-QUALIFIED DATA NAMES AND TEST-THEM
001500* THERE
001600* SO THEY READ A LITTLE STRANGELY.
001700*---------------------------------------------------------------
001800
001900 ENVIRONMENT DIVISION.
002000 CONFIGURATION SECTION.
002100 FIGURATIVE-CONSTANTS.
002200     COPY FIGS.
002300
002400 INPUT-OUTPUT SECTION.
002500
005600 DATA DIVISION.
005700 FILE SECTION.
008300
008400 WORKING-STORAGE SECTION.
000000     COPY WSKTRACE.
000000 01  SCCS-WHAT PIC X(50) VALUE
000000     "@(#)tableio.wcb 1.8 9/11/94".
000000 01  PROGRAM-NAME PIC X(8)  VALUE "TABLEIO".
008500     COPY WSFILEIO.
008600     COPY WSIOCODE.
000000* A DUMMY TO PASS TO KCSIO
000000 01  TBL-FILE  PIC X.
000000* This one is not a dummy.
000000 01  TBL-RECORD PIC X(16).
000000 01  GENERAL-FIELD         PIC X(100).
000000 01  GENERAL-FIELD-2       PIC X(100).
008700 01  TBL-FILE-OPEN-STATUS  PIC 9 VALUE ZEROES.
008800     88  TBL-FILE-IS-CLOSED VALUE ZEROES.
008900     88  TBL-FILE-IS-OPEN   VALUE 1.
009000* ADDED TO HANDLE WISP ANOMALIES
009100 01  IO-RQST               PIC XX.
009200 01  TBL-FILE-STATUS       PIC XX.
009500 01  TIO  PIC X.
009600
000000 01  KEXISTS-RC PIC 999 VALUE 0.
000000
009700 LINKAGE SECTION.
009800 01  TBL-IO-BLOCK.
009900     COPY LKDIO.
010000 01  PASSED-RECORD    PIC X(16).
010100 PROCEDURE DIVISION USING TBL-IO-BLOCK PASSED-RECORD.
010200 MAIN-LOGIC SECTION.
010300 PROGRAM-BEGIN.
000000     MOVE "TABLEIO BEGIN" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
010400     PERFORM MAIN-PROCESS.
000000     MOVE "TABLEIO EXIT" TO KTRACE-RECORD.
000000     PERFORM CALL-KTRACE.
000000 MAIN-LOGIC-EXIT.
000000     EXIT PROGRAM.
010700 MAIN-LOGIC-STOP.
010800     STOP RUN.
010900 LEVEL-2 SECTION.
011000 MAIN-PROCESS.
011100     MOVE FILE-IO OF TBL-IO-BLOCK TO IO-RQST.
011200     IF IO-RQST = READ-RECORD
011300         PERFORM READ-TBL-RECORD
011400     ELSE
011200     IF IO-RQST = HOLD-RECORD
011300         PERFORM HOLD-TBL-RECORD
011400     ELSE
011500     IF IO-RQST = READ-NEXT-RECORD
011600         PERFORM READ-NEXT-TBL-RECORD
011700     ELSE
011800     IF IO-RQST = WRITE-RECORD
011900         PERFORM WRITE-TBL-RECORD
012000     ELSE
012100     IF IO-RQST = DELETE-RECORD
012200         PERFORM DELETE-TBL-RECORD
012300     ELSE
012400     IF IO-RQST = OPEN-IO
012500         PERFORM OPEN-IO-TBL-FILE
012600     ELSE
012700     IF IO-RQST = OPEN-OUTPUT
012800         PERFORM OPEN-OUTPUT-TBL-FILE
012900     ELSE
013000     IF IO-RQST = OPEN-SHARED
013100         PERFORM OPEN-SHARED-TBL-FILE
013200     ELSE
013300     IF IO-RQST = OPEN-INPUT
013400         PERFORM OPEN-INPUT-TBL-FILE
013500     ELSE
013600     IF IO-RQST = CLOSE-FILE
013700         PERFORM CLOSE-TBL-FILE
013800     ELSE
013900     IF IO-RQST = TEST-FOR-FILE
014000         PERFORM DOES-TBL-FILE-EXIST
014100     ELSE
014200     IF IO-RQST = FILE-INFO
014300         PERFORM TEST-TBL-FILE
014400     ELSE
014200     IF IO-RQST = TABLE-INFO
014300         PERFORM TEST-TBL-FILE
014400     ELSE
014500     IF IO-RQST = FILE-SET
014600         PERFORM SET-TBL-FILE
000000     ELSE
000000         DISPLAY "UNKNOWN IO REQUEST IN TABLEIO " IO-RQST
014700         .
014800
014900 WRITE-TBL-RECORD.
015000     MOVE PASSED-RECORD TO TBL-RECORD.
015100     PERFORM CALL-TBL-KCSIO.
015200 READ-TBL-RECORD.
015300     MOVE PASSED-RECORD TO TBL-RECORD.
015400     MOVE ZEROES TO FILE-IO-KEY OF TBL-IO-BLOCK.
015500     PERFORM CALL-TBL-KCSIO.
015600     MOVE TBL-RECORD TO PASSED-RECORD.
015700 HOLD-TBL-RECORD.
015800     MOVE PASSED-RECORD TO TBL-RECORD.
015900     MOVE ZEROES TO FILE-IO-KEY OF TBL-IO-BLOCK.
016000     PERFORM CALL-TBL-KCSIO.
016100     MOVE TBL-RECORD TO PASSED-RECORD.
016200 READ-NEXT-TBL-RECORD.
016300     MOVE READ-NEXT-RECORD TO FILE-IO OF TBL-IO-BLOCK.
016400     PERFORM CALL-TBL-KCSIO.
           MOVE FILE-STATUS OF TBL-IO-BLOCK TO TBL-FILE-STATUS.
016500     IF TBL-FILE-STATUS NOT = AT-END
016600         MOVE TBL-RECORD TO PASSED-RECORD.
016700 OPEN-IO-TBL-FILE.
016800     MOVE OPEN-IO TO FILE-IO OF TBL-IO-BLOCK.
016900     PERFORM SET-FOR-OPEN.
017000     PERFORM CALL-TBL-KCSIO.
017100     MOVE FILE-IS-OPEN TO TBL-FILE-OPEN-STATUS.
017200 OPEN-INPUT-TBL-FILE.
017300     MOVE OPEN-INPUT TO FILE-IO OF TBL-IO-BLOCK.
017400     PERFORM SET-FOR-OPEN.
017500     PERFORM CALL-TBL-KCSIO.
017600     MOVE FILE-IS-OPEN TO TBL-FILE-OPEN-STATUS.
017700 OPEN-OUTPUT-TBL-FILE.
017800     MOVE OPEN-OUTPUT TO FILE-IO OF TBL-IO-BLOCK.
017900     PERFORM SET-FOR-OPEN.
018000     PERFORM CALL-TBL-KCSIO.
018100     MOVE FILE-IS-OPEN TO TBL-FILE-OPEN-STATUS.
018200 OPEN-SHARED-TBL-FILE.
018300     MOVE OPEN-SHARED TO FILE-IO OF TBL-IO-BLOCK.
018400     PERFORM SET-FOR-OPEN.
018500     PERFORM CALL-TBL-KCSIO.
018600     MOVE FILE-IS-OPEN TO TBL-FILE-OPEN-STATUS.
018700 CLOSE-TBL-FILE.
018800     MOVE CLOSE-FILE TO FILE-IO OF TBL-IO-BLOCK.
018900     PERFORM CALL-TBL-KCSIO.
019000     MOVE FILE-IS-CLOSED TO TBL-FILE-OPEN-STATUS.
019100 DELETE-TBL-RECORD.
019200     MOVE DELETE-RECORD TO FILE-IO OF TBL-IO-BLOCK.
019300     PERFORM CALL-TBL-KCSIO.
019400 DOES-TBL-FILE-EXIST.
000000     MOVE "00" TO FILE-STATUS OF TBL-IO-BLOCK.
019500     CALL "KEXISTS" USING
000000         KEXISTS-RC
019600         FILE-NAME    OF TBL-IO-BLOCK
019700         FILE-LIBRARY OF TBL-IO-BLOCK
019800         FILE-VOLUME  OF TBL-IO-BLOCK
019900         .
020000     IF KEXISTS-RC NOT = ZEROES
020100         MOVE FILE-NOT-FOUND TO FILE-STATUS
020200            OF TBL-IO-BLOCK
020300            .
020400 SET-FOR-OPEN.
020500     MOVE 100 TO FILE-SPACE OF TBL-IO-BLOCK.
020600     MOVE FILE-RECORD-LEN OF TBL-IO-BLOCK
020700          TO FILE-KEY-LEN OF TBL-IO-BLOCK.
020800     MOVE 1 TO FILE-KEY-POS OF TBL-IO-BLOCK.
020900     MOVE "N" TO FILE-COMPRESSED-FLAG OF TBL-IO-BLOCK.
000000     MOVE "I" TO FILE-ORG OF TBL-IO-BLOCK.
021000     MOVE ZEROES TO FILE-ALTKEY-DATA OF TBL-IO-BLOCK
021100                    FILE-IO-KEY OF TBL-IO-BLOCK
000000                    FILE-REL-KEY OF TBL-IO-BLOCK
000000                    FILE-IO-CHANNEL OF TBL-IO-BLOCK
000000                    FILE-OPEN-STATUS OF TBL-IO-BLOCK.
000000     MOVE "00" TO FILE-STATUS OF TBL-IO-BLOCK.
000000     MOVE "000" TO FILE-STATUS-EXT OF TBL-IO-BLOCK.
000000     MOVE LOW-VALUES TO C-DATA OF TBL-IO-BLOCK.
000000     MOVE "N" TO FILE-VARIABLE-FLAG OF TBL-IO-BLOCK.
000000     MOVE "TABLE" TO FILE-PRNAME OF TBL-IO-BLOCK.
021200
021300 SET-TBL-FILE.
021400     PERFORM TEST-TBL-FILE.
021500     MOVE "00" TO FILE-STATUS OF TBL-IO-BLOCK.
021600 TEST-TBL-FILE.
021700     MOVE TABLE-INFO TO FILE-IO OF TBL-IO-BLOCK.
000000     MOVE FILE-ID-DATA OF TBL-IO-BLOCK TO GENERAL-FIELD-2.
021800       CALL "KCSIO" USING
021900            TBL-IO-BLOCK
022000            TBL-FILE
022100            TBL-RECORD
022200            .
000000     MOVE FILE-ID-DATA OF TBL-IO-BLOCK TO GENERAL-FIELD.
023000     IF GENERAL-FIELD-2 NOT = GENERAL-FIELD
023100         MOVE "95" TO FILE-STATUS OF TBL-IO-BLOCK
023200     ELSE
023300         MOVE "00" TO FILE-STATUS OF TBL-IO-BLOCK
023400         .
023500 CALL-TBL-KCSIO.
023600       CALL "KCSIO" USING
023700            TBL-IO-BLOCK
023800            TBL-FILE
023900            TBL-RECORD.
000000     COPY PLKTRACE.
