000100**   Copyright (c) 2003 NeoMedia Technologies, All rights reserved.
000200**   $Id:$
000300**
000400**   File:       WL0030.wcb
000500**
000600**   Project:    WISP/TEST
000700**
000800**   Purpose:    Test SUBMIT routine 
000900**
001000**   Example:
001100**
006500**
001100 Identification Division.
001200 Program-Id.     WL0030.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT CRT
               ASSIGN TO "CRT",  "DISPLAY".

           SELECT BASE-FILE-UFB
               ASSIGN TO "BASEFILE", "DISK",  NODISPLAY.

001300 Data Division.
       FILE SECTION.
       FD  CRT
           LABEL RECORDS ARE OMITTED.
       01  CRTREC.
           03  CRTREC-OA.
               05  CRTREC-OA-1             PIC X.
               05  CRTREC-OA-2             PIC X.
               05  CRTREC-OA-3             PIC X.
               05  CRTREC-OA-4             PIC X.
           03  CRTREC-MAPPING-AREA.
               05  CRTREC-ROW  OCCURS 24 TIMES.
                   10 CRTREC-X OCCURS 80 TIMES PIC X.

       FD  BASE-FILE-UFB
           VALUE OF FILENAME IS BASE-FIL,
                    LIBRARY  IS BASE-LIB,
                    VOLUME   IS BASE-VOL.

       01  BASE-FIL-RECORD PIC X(80).


001400 Working-Storage Section.
001500
       01  failed-cnt	   pic 99 value 0.

       01  base-fil	   PIC X(8) VALUE "WL0030".
       01  base-lib	   pic x(8) VALUE "WL0030".
       01  base-vol	   pic x(6).

       01  sub-fil	   PIC X(8).
       01  sub-lib	   pic x(8).
       01  sub-vol	   pic x(6).
       01  job-name	   pic x(8).
       01  sub-status	   PIC X.
       
       01  tst-fil	   PIC X(8).
       01  tst-lib	   pic x(8).
       01  tst-vol	   pic x(6).

       01  rc-int4.
           03 rc-high     BINARY  VALUE 0.
           03 rc-bin      BINARY  value 0.
       01  rc-num	   pic 999.

       01  expected-error  pic 999.
       
       01  IL	   PIC X(2) VALUE "IL".
       01  IV	   PIC X(2) VALUE "IV".
       01  INLIB   PIC X(8).
       01  INVOL   PIC X(6).
       
       01  TEST-NAME PIC X(8).

       01  READFDR4-MODE.
	   03 FILLER BINARY VALUE 0.
	   03 FILLER BINARY VALUE 0.

       01  CD-keyword PIC XX VALUE "CD".
       01  CREATE-DATE PIC 9(8).
       01  L-TYPE PIC X VALUE "L".
       01  F-TYPE PIC X VALUE "F".

       01  spaces-fil   pic x(8) values spaces.
       01  find-start-cnt-int4.
	   05  find-start-cnt-high binary value 0.
	   05  find-start-cnt-bin  binary value 0.
       01  find-cnt-int4.
	   05  find-cnt-high binary value 0.
	   05  find-cnt-bin  binary value 0.
       01  find-receiver   pic x(22).

       01  PAUSE-2-SECONDS.
	   05 filler binary value 0.
	   05 filler binary value 200.
	   
001600 Procedure Division.
       
      *============================================================	  
       0000-init.
	   move 0 to failed-cnt.

	   CALL "EXTRACT" USING
	       IL, INLIB,
	       IV, INVOL.

      * Ensure Usage constants are set.	       
	   if INLIB = " " or INVOL = " " then
	       DISPLAY "INLIB and INVOL must be set to run test"
	       go to 9999-exit.

	   MOVE INVOL    TO BASE-VOL.

      * Remove the lib from previous runs.
	   perform cleanup-files.

	   MOVE "SETUP" TO TEST-NAME.
	   move "WL0030"   TO base-fil.
	   move "WL0030"   TO base-lib.
	   move INVOL	   TO base-vol.
	   perform create-base-file.

      *============================================================	  
      * TEST: submit 6 args
       test001.
	   MOVE "TEST001" TO TEST-NAME.

      *
      *	   PROC WLOO30P will copy file WL0030 in WL0030 on INVOL
      *	   to file named COPY.
      *
	   MOVE "WL0030P"  TO SUB-FIL.
	   MOVE SPACES	   TO SUB-LIB.
	   MOVE SPACES	   TO SUB-VOL.
	   MOVE TEST-NAME  TO JOB-NAME.
	   MOVE "R"	   TO SUB-STATUS.
	   
	   perform CALL-SUBMIT-6ARGS.
	   PERFORM CHECK-SUBMIT-SUCCESS.

      *
      *	   WAIT 2 seconds for the submitted proc to run
      *
	   CALL "PAUSE" USING PAUSE-2-SECONDS.	
 
	   move "COPY"	   to tst-fil.
	   move "WL0030"   to tst-lib.
	   move invol	   to tst-vol.
	   perform TEST-FILE-EXISTS.

      * TEST: proc missing
       test002.
	   MOVE "TEST002" TO TEST-NAME.

	   MOVE "MISSING"  TO SUB-FIL.
	   MOVE "WL0030"   TO SUB-LIB.
	   MOVE invol	   TO SUB-VOL.
	   
	   PERFORM CALL-SUBMIT-4ARGS.
	   move 20 to expected-error.
	   PERFORM CHECK-SUBMIT-FAILURE.

      *============================================================	  
       9999-done.
           IF failed-cnt = 0   THEN
                DISPLAY "SUBMIT completion successful."
           ELSE 
                DISPLAY "SUBMIT failed "
	   END-IF.
	   
       9999-exit.
            EXIT PROGRAM.
       9999-stop.
            STOP RUN.

      *============================================================
       CLEANUP-FILES.
	   move spaces	   TO base-fil.
	   move "WL0030"   TO base-lib.
	   move INVOL	   TO base-vol.
	   CALL "SCRATCH" USING 
	       L-TYPE, BASE-FIL, BASE-LIB, BASE-VOL, RC-int4.
       
      *============================================================
       CHECK-SUBMIT-SUCCESS.
	   if rc-bin NOT = 0 THEN
	       add 1 to failed-cnt
	       display "SUBMIT FAILED ", TEST-NAME,
		   " RC=", RC-NUM
	   END-IF.
	       
      *============================================================
       CHECK-SUBMIT-FAILURE.	        	   
	   if rc-num NOT = expected-error THEN
	       add 1 to failed-cnt
	       display "SUBMIT FAILED ", TEST-NAME,
		   " Expecting RC=", expected-error,
		   " Got RC=", rc-num.

      *============================================================
       CALL-SUBMIT-6ARGS.
	   CALL "SUBMIT" USING
	       SUB-FIL, SUB-LIB, SUB-VOL
	       JOB-NAME, SUB-STATUS, rc-int4.
	   move rc-bin to rc-num.

      *============================================================
       CALL-SUBMIT-4ARGS.
	   CALL "SUBMIT" USING
	       SUB-FIL, SUB-LIB, SUB-VOL
	       rc-int4.
	   move rc-bin to rc-num.
       
      *============================================================	  
       TEST-FILE-EXISTS.
	   CALL "READFDR4" USING
	       tst-fil, tst-lib, tst-vol,
	       readfdr4-mode, cd-keyword, create-date,
	       rc-int4.
	   move rc-bin to rc-num.
	   if rc-bin NOT = 0  then
	       add 1 to failed-cnt
	       display "TEST-FILE-EXISTS FAILED ",
		   TEST-NAME,
		   " RC=", rc-num,
		   " FIL=", tst-fil,
		   " LIB=", tst-lib,
		   " VOL=", tst-vol.
	       
      *============================================================
       CREATE-BASE-FILE.
      * Create a file to copy.
	   OPEN OUTPUT BASE-FILE-UFB.
	   WRITE BASE-FIL-RECORD.
	   CLOSE BASE-FILE-UFB.

      * Ensure file exists.
	   move base-fil to tst-fil.
	   move base-lib to tst-lib.
	   move base-vol to tst-vol.
	   perform TEST-FILE-EXISTS.
	   if rc-bin NOT = 0 THEN GO TO 9999-exit.
  
006600**   End of WL0030.wcb 
